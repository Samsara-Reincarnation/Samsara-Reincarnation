// The Replacements
Actor TimeBombWeapon_Samsara : SamsaraSlotTwoPickup replaces BFG9000 {}
Actor ComputerMap_Samsara : SamsaraSlotOnePickup replaces AllMap {}
Actor ShotgunDropHarmony : ShotgunDrop {Scale 0.30}
Actor ChingunDropHarmony : ChingunDrop {Scale 0.30}

// The Monsters
actor Beastling_Samsara : SamsaraMonsterBeastMediumTotalGib replaces ZombieMan
{
	scale .30
	health 150
	speed 12
	radius 30
	height 56
	reactiontime 8
	painchance 180
	mass 400
	seesound "demon/sight"
	attacksound "demon/melee"
	painsound "demon/pain"
	deathsound "demon/death"
	activesound "demon/active"
	BloodColor "ff 40 00"
	DamageFactor "CorvusCrossbow", 1.15
	DamageFactor "DragonClaw", 1.5
	DamageFactor "Pistol", 1.25
	DamageFactor "DukePistol", 1.25
	DamageFactor "ZorchPistol", 1.25
	DamageFactor "MarathonPistol", 0.85
	DamageFactor "Zorch", 1.25
	DamageFactor "Normal", 1.25
    DamageFactor "Fire", 1.15
    DamageFactor "Ice", 1.15
    DamageFactor "Disintegrate", 1.15
    DamageFactor "DukeDamage", 1.2
    DamageFactor "DukeExplosive", 1.1
    DamageFactor "DukeIce", 1.1
    DamageFactor "Explosive", 1.1
    DamageFactor "DoomPlasma", 1.1
    DamageFactor "CorvusHellstaff", 1.1
    DamageFactor "CorvusHellstaffTomed", 1.1
	DamageFactor "PhasingZorch", 1.1
	DamageFactor "MarathonPlasma", 1.1
	DamageFactor "WolfenFlamethrower", 1.1
	DamageFactor "FireExplosive", 1.1
	DamageFactor "MightyBoot", 1.5
	PainChance "MightyBoot", 255
	MONSTER
	Tag "Beastling"
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
	states
	{
		Spawn:
			SARG AB 10 A_Look
			loop
		See:
			SARG AABBCCDD 2 A_Chase
			loop
		Pain:
			SARG H 2
			SARG H 2 A_Pain
			goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			SARG H 2 A_FaceTarget
			TNT1 A 0 A_Recoil(12)
			SARG H 16 A_Pain
			Goto See
		Melee:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			SARG EF 8 A_FaceTarget
			SARG G 8 A_SargAttack
			goto See
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			SARG E 10 A_FaceTarget
			SARG F 4 A_SkullAttack
			SARG EF 4
			loop
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			TNT1 A 0 A_DropItem("Flies")
			SARG I 8
			SARG J 8 A_Scream
			SARG K 4
			SARG L 4 A_Fall
			SARG M 4
			SARG N -1
			stop
		Death.PainkillerStake:
			TNT1 A 0 ACS_NamedExecuteAlways("Painkiller_MonsterKill")
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_IsItGibbed")==1,"PainkillerGib")
			TNT1 A 0 A_GiveInventory("Painkiller_RunStake",1)
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_FaceTarget
		StakeDeath:
			SARG H 2
			Loop
	}
}

actor Follower_Samsara : SamsaraMonsterHumanoidMedium replaces ShotgunGuy
{
	scale .33
	health 60
	radius 10
	height 56
	mass 75
	speed 8
	painchance 170
	seesound "shotgun/sight"
	painsound "shotguy/pain"
	deathsound "shotguy/death"
	activesound "shotguy/active"
	dropitem "ShotgunDrop"
	MONSTER
	BloodColor "ff 40 00"
	DamageFactor "CorvusCrossbow", 1.15
	DamageFactor "DragonClaw", 1.5
	DamageFactor "Pistol", 1.1
	DamageFactor "DukePistol", 1.1
	DamageFactor "ZorchPistol", 1.1
	DamageFactor "MarathonPistol", 0.75
	DamageFactor "Zorch", 1.1
	DamageFactor "Normal", 1.1
    DamageFactor "Disintegrate", 1.1
    DamageFactor "DukeDamage", 1.1
    DamageFactor "DukeIce", 1.1
	DamageFactor "MightyBoot", 1.5
	PainChance "MightyBoot", 255
	Tag "Follower"
	+FLOORCLIP
	+NOTDMATCH
    +NOICEDEATH
	states
	{
		Spawn:
			SPOS AB 10 A_Look
			loop
		See:
			SPOS AABBCCDD 3 A_Chase
			loop
		Pain:
			SPOS G 3
			SPOS G 3 A_Pain
			goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			SPOS G 2 A_FaceTarget
			TNT1 A 0 A_Recoil(12)
			SPOS G 16 A_Pain
			Goto See
		Pain.Shock:
		Pain.Tesla:
			CBGS A 4 A_Pain
			CBGS A 4
			TNT1 A -1 A_Jump(256,"See")
			wait
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			SPOS E 10 A_FaceTarget
			SPOS F 10 bright A_SPosAttack
			SPOS E 10
			goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			SPOS H 7
			SPOS I 7 A_Scream
			SPOS J 6 A_Fall
			SPOS KL 5
			VILE Z -1
			stop
		Death.CalebFire:
		Death.LifeLeech:
			TNT1 A 0 A_Jump(218, 2)
			TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
			TNT1 A 0 A_SpawnItemEx("BloodBurningCorpse_Generic", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER)
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 5 A_NoBlocking
			stop
		Death.PainkillerStake:
			TNT1 A 0 ACS_NamedExecuteAlways("Painkiller_MonsterKill")
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_IsItGibbed")==1,"PainkillerGib")
			TNT1 A 0 A_GiveInventory("Painkiller_RunStake",1)
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_FaceTarget
		StakeDeath:
			SPOS G 2
			Loop
		XDeath:
		    TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			SPOS M 5
			SPOS N 5 A_XScream
			SPOS O 5 A_Fall
			SPOS PQRST 5
			SPOS U -1
			stop
	}
}

actor FallingFollower_Samsara : SamsaraMonsterHumanoidMedium replaces Archvile
{
	scale .30
	health 60
	speed 8
	radius 20
	height 56
	reactiontime 8
	painchance 170
	mass 75
	seesound "shotguy/sight"
	attacksound "shotguy/attack"
	painsound "shotguy/pain"
	deathsound "shotguy/raise"
	activesound "shotgun/active"
	MONSTER
	BloodColor "ff 40 00"
	DamageFactor "CorvusCrossbow", 1.15
	DamageFactor "DragonClaw", 1.5
	DamageFactor "Pistol", 1.1
	DamageFactor "DukePistol", 1.1
	DamageFactor "ZorchPistol", 1.1
	DamageFactor "MarathonPistol", 0.75
	DamageFactor "Zorch", 1.1
	DamageFactor "Normal", 1.1
    DamageFactor "Disintegrate", 1.1
    DamageFactor "DukeDamage", 1.1
    DamageFactor "DukeIce", 1.1
	DamageFactor "MightyBoot", 1.5
	PainChance "MightyBoot", 255
	Tag "Follower"
	+NOTDMATCH
    +NOICEDEATH
	+FLOORCLIP
	+NOTARGET
	+QUICKTORETALIATE
	states
	{
		Spawn:
			FCAN A 10 A_Look
			loop
		See:
			FCAN B 2 A_Chase
			FCAN B 1 A_Chase
			FCAN C 2 A_Chase
			FCAN C 1 A_Chase
			BON1 A 2 A_Chase
			BON1 A 1 A_Chase
			BON1 B 2 A_Chase
			BON1 B 1 A_Chase
			BON1 C 2 A_Chase
			BON1 CC 6 A_Chase
			BON1 D 2 A_Chase
			BON1 D 6 A_Chase
		See2:
			SPOS AABBCCDD 3 A_Chase
			loop
		Pain:
			SPOS G 3
			SPOS G 3 A_Pain
			goto See2
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			SPOS G 2 A_FaceTarget
			TNT1 A 0 A_Recoil(12)
			SPOS G 16 A_Pain
			Goto See2
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			SPOS E 10 A_FaceTarget
			SPOS F 10 bright A_SPosAttack
			SPOS E 10
			goto See2
		Pain.Shock:
		Pain.Tesla:
			CBGS A 4 A_Pain
			CBGS A 4
			TNT1 A -1 A_Jump(256,"See")
			wait
		Death:
		    TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			VILE R 5 A_XScream
			VILE S 5 A_Fall
			VILE TUVWX 5
			VILE Y -1
			stop
		Death.CalebFire:
		Death.LifeLeech:
			TNT1 A 0 A_Jump(218, 2)
			TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
			TNT1 A 0 A_SpawnItemEx("BloodBurningCorpse_Generic", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER)
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 5 A_NoBlocking
			stop
		Death.PainkillerStake:
			TNT1 A 0 ACS_NamedExecuteAlways("Painkiller_MonsterKill")
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_IsItGibbed")==1,"PainkillerGib")
			TNT1 A 0 A_GiveInventory("Painkiller_RunStake",1)
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_FaceTarget
		StakeDeath:
			SPOS G 2
			Loop
		XDeath:
		    TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			SPOS M 5
			SPOS N 5 A_XScream
			SPOS O 5 A_Fall
			SPOS PQRST 5
			SPOS U -1
			stop
	}
}

actor WalkingCentaur_Samsara : SamsaraMonsterBeastLargeTotalGib replaces Revenant
{
	scale 0.30
	health 300
	speed 10
	radius 20
	height 56
	reactiontime 8
	painchance 100
	mass 500
	seesound "skeleton/sight"
	painsound "cyber/hoof"
	deathsound "skeleton/death"
	MONSTER
	DamageFactor "CorvusCrossbow", 1.15
	DamageFactor "DragonClaw", 1.5
	PainChance "MightyBoot", 255
	BloodColor "ff 40 00"
	+FLOORCLIP
	+LONGMELEERANGE
	+MISSILEMORE
	+NOTDMATCH
    +NOICEDEATH
	Tag "Centuar"
	states
	{
		Spawn:
			CYBR AB 10 A_Look
			loop
		See:
			CYBR AABBCCDD 3 A_Chase
			loop
		Pain:
			CYBR G 10 A_Pain
			goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			CYBR G 2 A_FaceTarget
			TNT1 A 0 A_Recoil(12)
			CYBR G 16 A_Pain
			Goto See
		Missile:
		Melee:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			SKEL G 10 A_FaceTarget
			SKEL H 10 bright A_FaceTarget
			SKEL I 10 bright A_SkelMissile
			SKEL G 10 A_FaceTarget
			goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			CYBR H 5
			CYBR I 5 A_Scream
			CYBR JKL 5
			CYBR M -1 A_Fall
			stop
		Death.PainkillerStake:
			TNT1 A 0 ACS_NamedExecuteAlways("Painkiller_MonsterKill")
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_IsItGibbed")==1,"PainkillerGib")
			TNT1 A 0 A_GiveInventory("Painkiller_RunStake",1)
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_FaceTarget
		StakeDeath:
			CYBR G 2
			Loop
	}
}

actor GuidedChaos replaces RevenantTracer
{
	scale 0.30
	radius 11
	height 8
	speed 10
	damage 10
	deathsound "skeleton/active"
	PROJECTILE
	+SEEKERMISSILE 
	+RANDOMIZE
	seesound "skeleton/attack"
	renderstyle Add
	states
	{
		Spawn:
			FATB AB 2 bright A_Tracer
			loop
		Death:
			FBXP A 8 bright
			FBXP B 6 bright
			FBXP C 4 bright
			stop
	}
}

actor HarmonyWhiteSpark replaces RevenantTracerSmoke
{
	renderstyle Translucent
	alpha 0.5
	+NOBLOCKMAP
	states
	{
		Spawn:
			TRE1 A 300 bright
			stop
	}
}

actor Obstacle1 replaces Fatso
{
	health 600
	speed 8
	radius 48
	height 64
	painchance 80
	mass 1000
	MONSTER
	-SHOOTABLE
	+FLOORCLIP
	states
	{
		Spawn:
			HEAD A 1 A_Look
			loop
		See:
		Death:
			HEAD H 8 A_Scream
			HEAD IJ 8
			HEAD K 8 A_Fall
			HEAD L -1
			stop
	}
}

actor Soldier_Samsara : SamsaraMonsterHumanoidMedium replaces ChaingunGuy
{
	scale 0.30
	health 150
	speed 8
	radius 20
	height 56
	reactiontime 8
	painchance 170
	mass 100
	seesound "imp/sight"
	painsound "vile/pain"
	attacksound "chainguy/attack"
	deathsound "chainguy/death"
	activesound "chainguy/active"
	dropitem "ChingunDrop"
	MONSTER
	Tag "Soldier"
	BloodColor "ff 40 00"
	DamageFactor "CorvusCrossbow", 1.15
	DamageFactor "DragonClaw", 1.5
	DamageFactor "Pistol", 1.25
	DamageFactor "DukePistol", 1.25
	DamageFactor "ZorchPistol", 1.25
	DamageFactor "MarathonPistol", 0.85
	DamageFactor "Zorch", 1.25
	DamageFactor "Normal", 1.25
    DamageFactor "Fire", 1.15
    DamageFactor "Ice", 1.15
    DamageFactor "Disintegrate", 1.15
    DamageFactor "DukeDamage", 1.2
    DamageFactor "DukeExplosive", 1.1
    DamageFactor "DukeIce", 1.1
    DamageFactor "Explosive", 1.1
    DamageFactor "DoomPlasma", 1.1
    DamageFactor "CorvusHellstaff", 1.1
    DamageFactor "CorvusHellstaffTomed", 1.1
	DamageFactor "MightyBoot", 1.5
	PainChance "MightyBoot", 255
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
	states
	{
		Spawn:
			CPOS AB 10 A_Look
			loop
		See:
			CPOS AABBCCDD 3 A_Chase
			loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			CPOS E 10 A_FaceTarget
			CPOS FE 4 bright A_CPosAttack
			CPOS F 1 A_CPosRefire
			goto Missile+2
		Pain:
			CPOS G 3
			CPOS G 3 A_Pain
			goto See
	    Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			CPOS G 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			CPOS G 16 A_Pain
			Goto See
		Pain.Shock:
		Pain.Tesla:
			CBGS A 4 A_Pain
			CBGS A 4
			TNT1 A -1 A_Jump(256,"See")
			wait
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			CPOS H 5
			CPOS I 5 A_Scream
			CPOS J 5 A_Fall
			CPOS K 5
			CPOS L -1
			stop
		Death.CalebFire:
		Death.LifeLeech:
			TNT1 A 0 A_Jump(218, 2)
			TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
			TNT1 A 0 A_SpawnItemEx("BloodBurningCorpse_Generic", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER)
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 5 A_NoBlocking
			stop
		Death.PainkillerStake:
			TNT1 A 0 ACS_NamedExecuteAlways("Painkiller_MonsterKill")
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_IsItGibbed")==1,"PainkillerGib")
			TNT1 A 0 A_GiveInventory("Painkiller_RunStake",1)
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_FaceTarget
		StakeDeath:
			CPOS G 2
			Loop
		XDeath:
		    TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			SPOS M 5
			SPOS N 5 A_XScream
			SPOS O 5 A_Fall
			SPOS PQRST 5
			SPOS U -1
			stop
	}
}

actor Unknown1 replaces DoomImp
{
	health 60
	speed 8
	radius 20
	height 56
	reactiontime 8
	painchance 200
	mass 100
	seesound "imp/sight"
	painsound "imp/pain"
	deathsound "imp/death"
	MONSTER
	+FLOORCLIP
	states
	{
		Spawn:
			TROO AB 10 A_Look
			loop
		See:
			TROO AABBCC 3 A_Chase
			loop
		Pain:
			TROO G 1 bright
			loop
		Melee:
		Missile:
			TROO E 8 A_FaceTarget
			TROO F 8 A_SargAttack
		See2:
			BOSS AABBCCDD 3 A_Chase
			loop
		Death:
			TROO I 8
			TROO J 8 A_Scream
			TROO K 6
			TROO L 6 A_Fall
			TROO M -1
			stop
		XDeath:
			TROO N 5
			TROO O 5 A_XScream
			TROO P 5
			TROO Q 5 A_Fall
			TROO RST 5
			TROO U -1
			stop
		Raise:
			TROO ML 8
			TROO KJI 6
			goto See
	}
}

actor Unknown2 replaces Demon
{
	health 1000
	speed 10
	radius 30
	height 56
	reactiontime 0
	painchance 180
	mass 400
	seesound "weapons/sawfull"
	states
	{
		Spawn:
			HEAD A 1 A_Look
			loop
		See:
		Death:
			HEAD H 8 A_Scream
			HEAD IJ 8
			HEAD K 8 A_Fall
			HEAD L -1
			stop
	}
}

//The way this guy works in vanilla Harmony is that whenever
//it goes to its pain state, it will go into a pseudo "death"
//state where depending on the amount of damage recieved, it
//will fake its death and attack the player after a short time.
//I couldn't implement this neat trick into Samsara without
//messing up the kill counter so I simplified it so that it's
//pain state is almost non-existant.
actor Critter_Samsara : SamsaraMonsterBeastTinyTotalGib replaces BaronOfHell
{
	scale 0.33
	health 40
	speed 12
	radius 24
	height 22
	reactiontime 8
	painchance 50
	mass 1000
	seesound "baron/sight"
	painsound "baron/pain"
	deathsound "baron/death"
	DamageFactor "CorvusCrossbow", 1.15
	DamageFactor "DragonClaw", 1.5
	PainChance "MightyBoot", 255
	BloodColor "ff 40 00"
	MONSTER
	Tag "Critter"
	-SOLID
    +NOTDMATCH
    +NOICEDEATH
	states
	{
		Spawn:
			BOSS AB 10 A_Look
			loop
		See:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			BOSS AABBCCDD 3 A_Chase
			loop
		Melee:
			TROO E 8 A_FaceTarget
			TROO F 8 A_SargAttack
			Goto See
		Pain:
			BOSS A -1 A_Jump(256,"See")
			wait
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			BOSS I 8
			BOSS J 8 A_Scream
			BOSS K 8
			BOSS L 8 A_Fall
			BOSS M -1 A_BossDeath
			stop
	}
}

actor Mine_Samsara replaces LostSoul
{
	scale 0.30
	health 30
	speed 0
	radius 16
	height 8
	damage 3
	painchance 256
	mass 100
	MONSTER
	-SOLID
	+NOICEDEATH
	+DONTFALL
	+NOBLOOD
	-COUNTKILL
	+DONTMORPH
	Tag "Mine"
	DamageFactor "CorvusCrossbow", 1.15
	DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonFusion", 2.0
	states
	{
		Spawn:
			PAIN A 10 A_Look
			loop
		See:
			PAIN AABB 3 A_Chase
			loop
		Melee:
		Death:
			BAL2 D 0 bright A_PlaySound("weapons/sshotc")
			MISL B 8 bright A_Explode
			MISL C 6 bright
			MISL D 4 bright
			BAL2 D 4 bright
			BAL2 E 4 bright
			stop
	}
}

actor Echida_Samsara : SamsaraMonsterBeastHuge replaces SpiderMastermind
{
	scale 0.33
	health 5000
	speed 12
	radius 128
	height 110
	reactiontime 8
	painchance 40
	mass 1000
	seesound "spider/sight"
	attacksound "grunt/attack"
	painsound "skull/melee"
	deathsound "spider/death"
	activesound "spider/active"
	BloodColor "ff 40 00"
	DamageFactor "CorvusCrossbow", 1.15
	DamageFactor "DragonClaw", 1.6
    DamageFactor "WMCNade", 0.75
    DamageFactor "CorvusHellstaffTomed", 0.2
	Tag "Echidna"
	MONSTER
	+BOSS
	+FLOORCLIP
	+NORADIUSDMG
	+DONTMORPH
	+MISSILEMORE
    +NOTDMATCH
    +NOICEDEATH
    +DONTRIP
    +BOSSDEATH
	states
	{
		Spawn:
			SPID AB 10 A_Look
			loop
	    See:
			TNT1 A 0 A_JumpIfInventory("DukeBossSee",1,"See2")
			TNT1 A 0 A_JumpIfInventory("CalebBossSee",1,"See2")		
			TNT1 A 0 A_JumpIfInventory("EleenaBossSee",1,"See2")		
			TNT1 A 0 A_JumpIfInventory("LoWangBossSee",1,"See2")	
			TNT1 A 0 A_JumpIfInventory("LeonardBossSee",1,"See2")						
			TNT1 A 0 A_JumpIfInventory("HLBossSee",1,"See2")
			TNT1 A 0 A_GiveInventory("DukeBossSee",1)
			TNT1 A 0 A_GiveInventory("CalebBossSee",1)		
			TNT1 A 0 A_GiveInventory("EleenaBossSee",1)	
			TNT1 A 0 A_GiveInventory("LoWangBossSee",1)
			TNT1 A 0 A_GiveInventory("LeonardBossSee",1)
			TNT1 A 0 A_GiveInventory("HLBossSee",1)
			TNT1 A 0 A_GiveToTarget("DukeBossSightTaunt")
			TNT1 A 0 A_GiveToTarget("CalebBossSightTaunt")
			TNT1 A 0 A_GiveToTarget("EleenaBossSightTaunt")
			TNT1 A 0 A_GiveToTarget("LoWangBossSightTaunt")
			TNT1 A 0 A_GiveToTarget("LeonardBossSightTaunt")		
			TNT1 A 0 A_GiveToTarget("HLBossSightMusicQueue")
		See2:
			SPID AABB 3 A_Chase
			SPID C 3 A_Metal
			SPID C 3 A_Chase
			loop
		Missile:
			POSS A 10 A_FaceTarget
			POSS B 10 bright A_SkelMissile
			POSS B 10 bright A_FaceTarget
			POSS C 10 A_FaceTarget
			POSS D 10 bright A_SkelMissile
			POSS D 10 bright A_FaceTarget
			SPID A 20 bright A_FaceTarget
			SPID GH 4 bright A_SPosAttack
			SPID H 1 bright A_SpidRefire
			goto Missile+7
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			SPID J 20 A_Scream
			SPID K 10 A_Fall
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)	
			SPID LMNOPQR 10
			SPID S 30
			SPID S -1 A_KeenDie
			stop
		Death.DukeIce:
			"####" "#" 0 A_PlaySound("duke/enemyfreeze")
			"####" "#" 0 A_GiveInventory("FreezethrowerWeakness")
			"####" "#" 0 A_Jump(218,"Ice")
			"####" "#" 0 A_GiveToTarget("DukeKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.WTMeleeIce:
		Death.WTRangedIce:
		Death.Ice:
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_Scream
			"####" "#" 0 A_GenericFreezeDeath
			"####" "#" 0 A_UnsetShootable
			"####" "#" 140 A_UnsetSolid
			"####" "#" 0 A_PlaySoundEx("freeze/break","soundslot6",0,0)
			"####" "##############################" 0 A_SpawnItemEx("IceChunk2",random(-1,1),random(-1,1),random(16,96),random(-2,2),random(-2,2),random(5,15))
			"####" "#" 0 A_IceGuyDie
			"####" "#" 1 A_IceGuyDie
			TNT1 A -1 A_KeenDie
			stop
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_Stop
			"####" "#" 0 A_NoGravity
			"####" "#" 0 A_ChangeFlag("DONTGIB",1)
			"####" "#" 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			"####" "#" 3 A_SpawnItem("ZorchEffectHuge")
			"####" "#" 0 A_GiveInventory("EnemyZorched",1)
			"####" "#" 3 A_SetTranslucent(0.9,0)
			"####" "#" 0 A_Scream
			"####" "#" 3 A_SetTranslucent(0.8,0)
			"####" "#" 0 A_NoBlocking
			"####" "#" 3 A_SetTranslucent(0.7,0)
			"####" "#" 3 A_SetTranslucent(0.6,0)
			"####" "#" 3 A_SetTranslucent(0.5,0)
			"####" "#" 3 A_SetTranslucent(0.4,0)
			"####" "#" 3 A_SetTranslucent(0.3,0)
			"####" "#" 3 A_SetTranslucent(0.2,0)
			"####" "#" 3 A_SetTranslucent(0.1,0)
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A 0 A_Gravity
			TNT1 A -1 A_KeenDie
			stop
		Death.RottDisintegrate:
			"####" "#" 0 A_ChangeFlag("SOLID", 0)
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_Scream
			"####" "#" 0 A_NoBlocking	
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 0 A_Stop
			"####" "#" 0 A_ChangeFlag("SHOOTABLE",FALSE)
			"####" "#" 0 A_ChangeFlag("NOGRAVITY",TRUE)
			"####" "#" 0 A_ChangeFlag("FLOORCLIP",TRUE)
			"####" "#" 0 A_SpawnItem("RGodModeDeathHuge")
			"####" "#" 3 A_SetTranslucent(0.9,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.8,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.7,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.6,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.5,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.4,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.3,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.2,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.1,0)
			TNT1 A -1 A_KeenDie
			stop
		Disintegrate:
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_NoBlocking
			"####" "#" 0 A_ChangeFlag("DONTGIB",1)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 0 A_Scream
			"####" "#" 0 A_ChangeFlag("SOLID",FALSE)
			"####" "#" 0 A_ChangeFlag("SHOOTABLE",FALSE)
			"####" "#" 0 A_ChangeFlag("NOGRAVITY",TRUE)
			"####" "#" 0 A_ChangeFlag("FLOORCLIP",TRUE)
			"####" "#" 0 A_Stop
			"####" "#" 3 A_SpawnItem("DisintEffectLarge",0,10)
			"####" "#" 3 A_SetTranslucent(0.9,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.8,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.7,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.6,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.5,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.4,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.3,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.2,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.1,0)
			TNT1 A -1 A_KeenDie
			stop
		Death.Nuke:
			"####" "#" 0 A_ChangeFlag("SOLID", 0)
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_Scream
			"####" "#" 0 A_NoBlocking		
			"####" "#" 0 A_SpawnItem("WTNukeDeathHuge")
			"####" "#################" 3 A_Fadeout(0.05)
			TNT1 A -1 A_KeenDie
			stop	
	}
}

actor WalkingPhage_Samsara : SamsaraMonsterBeastLargeTotalGib replaces Arachnotron
{
	scale 0.30
	health 500
	speed 12
	radius 48
	height 64
	reactiontime 8
	painchance 128
	mass 600
	seesound "baby/sight"
	painsound "spider/pain"
	MONSTER
	BloodColor "ff 40 00"
	DamageFactor "CorvusCrossbow", 1.15
	DamageFactor "DragonClaw", 1.5
	PainChance "MightyBoot", 255
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
	Tag "Phage"
	states
	{
		Spawn:
			SSWV AB 10 A_Look
			loop
		See:
			SSWV AAABBCC 3 A_Chase
			loop
		Pain:
			BOSS G 8 A_Pain
			goto See
	    Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			BOSS G 2 A_FaceTarget
			TNT1 A 0 A_Recoil(8)
			BOSS G 16 A_Pain
			Goto See
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			BOSS E 20 A_FaceTarget
			BOSS F 4 bright A_BspiAttack
			BOSS F 4 bright
			BOSS F 1 A_SpidRefire
			goto Missile+2
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)	
			SSWV N 7
			SSWV O 7 A_PlaySound("weapons/sshotl")
			SSWV P 6 A_Fall
			SSWV QR 5
			SSWV S -1
			stop
	}
}


//This guy's real fun on MAP06, isn't he?
actor WalkingPredator_Samsara : SamsaraMonsterBeastHuge replaces Cyberdemon
{
	scale 0.30
	health 3000
	speed 16
	radius 40
	height 110
	reactiontime 8
	painchance 20
	mass 1000
	seesound "cyber/sight"
	painsound "skull/melee"
	deathsound "cyber/death"
	activesound "cyber/active"
	MONSTER
	+BOSS
	+FLOORCLIP
	+NORADIUSDMG
	+DONTMORPH
	+MISSILEMORE
    +NOTDMATCH
    +NOICEDEATH
	BloodColor "ff 40 00"
	DamageFactor "CorvusCrossbow", 1.15
	DamageFactor "DragonClaw", 1.6
	DamageFactor "PhasingZorch", 1.15
	DamageFactor "DukeIce", 1.15
	DamageFactor "DoomPlasma", 1.15
	DamageFactor "MarathonPlasma", 1.15
	DamageFactor "CorvusHellstaff", 1.15
	DamageFactor "CorvusHellstaffTomed", 1.15
	DamageFactor "WolfenFlamethrower", 1.15
	DamageFactor "FireExplosive", 1.15
	PainChance "MightyBoot", 255
	Tag "Predator"
	states
	{
		Spawn:
			TROO AB 10 A_Look
			loop
		See:
			TROO AABBCC 3 A_Chase
			loop
		Pain:
			TROO D 3 A_Pain
			goto See
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			CYBR E 6 A_FaceTarget
			CYBR F 12 bright A_PainAttack
			CYBR E 12 A_FaceTarget
			CYBR F 12 bright A_CyberAttack
			CYBR E 12 A_FaceTarget
			CYBR F 12 bright A_CyberAttack
			goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			SKEL M 7
			SKEL N 7 A_Scream
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)	
			SKEL O 7 A_Fall
			SKEL P 7
			SKEL Q -1
			Stop
	}
}

// Misc. Actors
actor Unknown4 replaces BossEye
{
	health 1000
	speed 0
	radius 20
	height 32
	mass 100
	+NOBLOCKMAP
	+NOSECTOR
	+NOBLOOD
	states
	{
		Spawn:
			SSWV A 10 A_Look
			loop
		See:
			SSWV A 181 A_BrainAwake
			SSWV A 150 A_BrainSpit
			wait
	}
}

actor SecurityBeams replaces SpawnShot
{
	scale 0.30
	health 1000
	speed 40
	radius 6
	height 32
	damage 3
	mass 100
	seesound "weapons/plasmaf"
	deathsound "weapons/plasmax"
	+NOGRAVITY
	+NOCLIP
	+NOBLOOD
	-ACTIVATEPCROSS
	PROJECTILE
	states
	{
		Spawn:
			MISL A 1 bright
			loop
		Death:
			PLSE A 6 bright
			PLSE BCD 4 bright
			stop
	}
}

actor RedBarrel_Samsara replaces ExplosiveBarrel
{
	scale 0.30
	health 20
	radius 10
	height 42
	mass 100
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	+ACTIVATEMCROSS
	+DONTGIB
	+NOICEDEATH
	+OLDRADIUSDMG
	-COUNTKILL
	Tag "Red Barrel"
	states
	{
		Spawn:
			BAR1 AB 6
			loop
		Death:
			BAL2 D 0 bright A_PlaySound("weapons/sshotc")
			MISL B 8 bright A_Explode
			MISL C 6 bright
			MISL D 4 bright
			BAL2 D 4 bright
			BAL2 E 4 bright
			stop
	}
}

actor GrenadeExplosion replaces CacodemonBall
{
	scale 0.33
	health 100
	speed 8
	radius 6
	height 8
	damage 2
	mass 100
	seesound "caco/attack"
	deathsound "caco/shotx"
	PROJECTILE
	-NOGRAVITY
	states
	{
		Spawn:
			BAL2 AB 4 bright
			loop
		Death:
			BAL2 C 100 bright
			stop
	}
}

actor Neutron replaces Rocket
{
	scale 0.30
	health 1000
	speed 30
	radius 11
	height 8
	damage 20
	seesound "weapons/rocklf"
	deathsound "weapons/rocklx"
	PROJECTILE
	states
	{
		Spawn:
			PLSS AB 6 bright
			loop
		Death:
			MISL B 8 bright A_Explode
			MISL C 6 bright
			MISL D 4 bright
			BAL2 D 4 bright
			BAL2 E 4 bright
			stop
	}
}

actor HarmonyGrenade replaces PlasmaBall
{
	scale 0.30
	health 1000
	speed 40
	radius 13
	height 8
	damage 5
	mass 100
	seesound "weapons/plasmaf"
	PROJECTILE
	states
	{
		Spawn:
			MISL A 1 bright
			loop
		Death:
			PLSE A 6 bright
			PLSE BCD 4 bright
			stop
	}
}

actor TimeBomb replaces BFGBall
{
	scale 0.30
	health 1000
	speed 25
	radius 13
	height 8
	damage 100
	reactiontime 1
	mass 100
	deathsound "weapons/rocklx"
	PROJECTILE
	-NOGRAVITY
	states
	{
		Spawn:
			PLSS AB 6 bright
			loop
		Death:
			MISL B 8 bright A_Explode
			MISL C 6 bright
			MISL D 4 bright
			BAL2 D 4 bright
			BAL2 E 4 bright
			stop
	}
}

actor PhageNeutron replaces ArachnotronPlasma
{
	scale 0.30
	health 1000
	speed 40
	radius 13
	height 8
	damage 5
	mass 100
	seesound "weapons/plasmaf"
	PROJECTILE
	states
	{
		Spawn:
			TROO G 1 bright
			loop
		Death:
			APBX A 6 bright
			APBX BCD 4 bright
			stop
	}
}

actor HarmonyPuff : BulletPuff replaces BulletPuff
{
	scale 0.30
}

actor Idem : SamsaraBloodyHellBlood replaces SamsaraBloodyHellBlood
{
	scale 0.30
}

// The Keys
actor GreenKeycard : BlueCard2 replaces BlueCard
{
	scale 0.30
	radius 20
	height 16
	inventory.pickupmessage "$GOTBLUECARD"
	inventory.icon "STKEYS0"
	+NOTDMATCH
	states
	{
		Spawn:
			BKEY A 6 bright
			TLMP A 6 bright
			BKEY B 6 bright
			TLMP B 6 bright
			loop
		Pickup:
			TNT1 A 0 ACS_NamedExecuteAlways("SamsaraKeyBroadcast",0,2)
			TNT1 A 1 A_GiveInventory("BlueCard")
			stop
	}
}

actor PurpleKeycard : RedCard2 replaces RedCard
{
	scale 0.30
	radius 20
	height 16
	inventory.pickupmessage "$GOTREDCARD"
	inventory.icon "STKEYS2"
	states
	{
		Spawn:
			RKEY A 6 bright
			TLP2 C 6 bright
			RKEY B 6 bright
			TLP2 D 6 bright
			loop
		Pickup:
			TNT1 A 0 ACS_NamedExecuteAlways("SamsaraKeyBroadcast",0,0)
			TNT1 A 1 A_GiveInventory("RedCard")
			stop
	}
}

actor YellowKeycard : YellowCard2 replaces YellowCard
{
	scale 0.30
	radius 20
	height 16
	inventory.pickupmessage "$GOTYELWCARD"
	inventory.icon "STKEYS1"
	states
	{
		Spawn:
			YKEY A 6 bright
			TLP2 A 6 bright
			YKEY B 6 bright
			TLP2 B 6 bright
			loop
		Pickup:
			TNT1 A 0 ACS_NamedExecuteAlways("SamsaraKeyBroadcast",0,1)
			TNT1 A 1 A_GiveInventory("YellowCard")
			stop
	}
}

actor YellowPPKey : YellowSkull2 replaces YellowSkull
{
	scale 0.30
	radius 20
	height 16
	inventory.pickupmessage "$GOTYELWSKUL"
	inventory.icon "STKEYS4"
	states
	{
		Spawn:
			YSKU A 6 bright
			TBLU A 6 bright
			YSKU B 6 bright
			TBLU B 6 bright
			loop
		Pickup:
			TNT1 A 0 ACS_NamedExecuteAlways("SamsaraKeyBroadcast",0,4)
			TNT1 A 1 A_GiveInventory("YellowSkull")
			stop
	}
}

actor PurplePPKey : RedSkull2 replaces RedSkull
{
	scale 0.30
	radius 20
	height 16
	inventory.pickupmessage "$GOTREDSKUL"
	inventory.icon "STKEYS5"
	states
	{
		Spawn:
			RSKU A 6 bright
			TBLU C 6 bright
			RSKU B 6 bright
			TBLU D 6 bright
			loop
		Pickup:
			TNT1 A 0 ACS_NamedExecuteAlways("SamsaraKeyBroadcast",0,3)
			TNT1 A 1 A_GiveInventory("RedSkull")
			stop
	}
}

actor GreenPPKey : BlueSkull2 replaces BlueSkull
{
	scale 0.30
	radius 20
	height 16
	inventory.pickupmessage "$GOTBLUESKUL"
	inventory.icon "STKEYS3"
	states
	{
		Spawn:
			BSKU A 6 bright
			TLMP C 6 bright
			BSKU B 6 bright
			TLMP D 6 bright
			loop
		Pickup:
			TNT1 A 0 ACS_NamedExecuteAlways("SamsaraKeyBroadcast",0,5)
			TNT1 A 1 A_GiveInventory("BlueSkull")
			stop
	}
}

// The Decorations
actor Flies
{
	scale 0.30
	states
	{
		Spawn:
			CEYE A 200
		Idle:
			CEYE BC 4
			COL5 A 4
			loop
	}
}

actor LaserLamp : Column replaces Column
{
	scale 0.30
}

actor BigMushroom : TallGreenColumn replaces TallGreenColumn
{
	scale 0.30
}

actor TruckPipe : ShortGreenColumn replaces ShortGreenColumn
{
	scale 0.30
}

actor AbductedAmira : TallRedColumn replaces TallRedColumn
{
	scale 0.33
}

actor ArtySphereWithHeads : ShortRedColumn replaces ShortRedColumn
{
	scale 0.30
}

actor LoopingExplosion : HeartColumn replaces HeartColumn
{
	scale 0.33
	states
	{
		Spawn:
			MISL B 8 bright
			MISL C 6 bright
			MISL D 4 bright
			BAL2 DE 4 bright
			CEYE A 14 bright
			loop
	}
}

actor DrippingWater : FloatingSkull replaces FloatingSkull
{
	-SOLID
	states
	{
		Spawn:
			FSKU ABC 6
			COL5 B 6
			loop
	}
}

actor DeadBeastlingWithFlies : BlueTorch replaces BlueTorch
{
	scale 0.30
	states
	{
		Spawn:
			TBLU A 6 bright
			YSKU B 6 bright
			TBLU B 6 bright
			YSKU A 6 bright
			loop
	}
}

actor Dopefish : GreenTorch replaces GreenTorch
{
	scale 0.50
	states
	{
		Spawn:
			TGRN ABCD 8
			loop
	}
}

actor GreenWaterSplash : RedTorch replaces RedTorch
{
	-SOLID
	states
	{
		Spawn:
			TRED ABCD 4
			loop
	}
}

actor Fire : ShortBlueTorch replaces ShortBlueTorch
{
	scale 0.30
}

actor CeilingSpark : ShortGreenTorch replaces ShortGreenTorch
{
	height 16
	+SPAWNCEILING
	+NOGRAVITY
	-SOLID
}

actor Firebox : ShortRedTorch replaces ShortRedTorch
{
	scale 0.30
	states
	{
		Spawn:
			SMRT ABCD 4
			loop
	}
}

actor Unknown5 : Stalagtite replaces Stalagtite
{
	radius 8
}

actor TallLamp : TechPillar replaces TechPillar
{
	scale 0.30
}

actor WhiteCandle : Candlestick replaces Candlestick
{
	scale 0.30
	states
	{
		Spawn:
			CAND A 8 bright
			CBRA A 8 bright
			loop
	}
}

actor WhiteCandle2 : Candelabra replaces Candelabra
{
	+SHOOTABLE
	states
	{
		Spawn:
			CBRA A 8 bright
			CAND A 8 bright
			loop
	}
}

actor NeutronbeamBlocker replaces Meat3
{
	health 9900
	mass 9000
	radius 16
	height 84
	+SHOOTABLE
	+NOBLOOD
	states
	{
		Spawn:
			GOR3 A -1
			stop
	}
}

actor HangingFire : BloodyTwitch replaces BloodyTwitch
{
	states
	{
		Spawn:
			GOR1 ABCB 4
			loop
	}
}

actor HangingFire2 : HangingFire replaces NonsolidTwitch
{
	scale 0.30
	-SOLID
}

actor DeadAmazone replaces DeadMarine
{
	scale 0.30
	states
	{
		Spawn:
			AMZN A -1
			Stop
	}
}

actor DeadFollower : CustomInventory replaces DeadShotgunGuy
{
    Inventory.PickupMessage "Ate for 1 Health."
    Inventory.PickupSound "wolfen/slurpie"
	scale 0.30
	states
	{
		Spawn:
			DDFL A -1
			Stop
		Pickup:
			TNT1 A 0 A_JumpIfInventory("WolfenClass",1,"WolfenCheck")
			DDFL A 1
			fail
		WolfenCheck:
			TNT1 A 0 A_JumpIfHealthLower(51,"DrinkUp")
			DDFL A 1
			fail
		DrinkUp:
			TNT1 A 0 A_GiveInventory("HealthBonus",1)
			TNT1 A 1
			stop
	}
}

actor DeadBeastling : Beastling_Samsara replaces DeadDemon
{
	skip_super
	scale 0.30
	states
	{
		Spawn:
			goto Super::Death+7
	}
}

actor Tentacle : HeadsOnAStick replaces HeadsOnAStick
{
	scale 0.30
}

actor SilverRocket : HeadOnAStick replaces HeadOnAStick
{
	scale 0.30
	states
	{
		Spawn:
			SMT2 A -1
			stop
	}
}

actor SilverRocketLaunched : HeadCandles replaces HeadCandles
{
	scale 0.30
}

actor ShootableCrate : LiveStick replaces LiveStick
{
	scale 0.30
}

actor LargeTree : BigTree replaces BigTree
{
	scale 0.30
}

actor HangingChain : HangNoGuts replaces HangNoGuts
{
	-SOLID
}

actor StoredMiniguns : HangBNoBrain replaces HangBNoBrain
{
	scale 0.30
	-NOGRAVITY
	-SPAWNCEILING
}

actor StoredShotguns : HangTLookingDown replaces HangTLookingDown
{
	scale 0.30
	-NOGRAVITY
	-SPAWNCEILING
}

actor Unknown6 replaces HangTSkull
{
	speed 12
	radius 16
	height 64
	mass 100
	reactiontime 0
	+FLOAT
	+SPAWNCEILING
	states
	{
		Spawn:
			HEAD A 1 A_Look
			loop
		See:
			SPOS AABBCCDD 3 A_Chase
			loop
	}
}

actor CrateExplosion replaces ColonGibs
{
	scale 0.33
	speed 0
	radius 20
	height 16
	reactiontime 0
	mass 100
	seesound "weapons/rocklx"
	states
	{
		Spawn:
			HEAD A 1 A_Look
			loop
		See:
			MISL B 8 bright A_Explode
			MISL C 6 bright
			MISL D 4 bright
			BAL2 D 4 bright
			BAL2 E 4 bright
			stop
	}
}

actor HangingLight : BrainStem replaces BrainStem
{
	scale 0.25
	+SPAWNCEILING
	+NOGRAVITY
}
