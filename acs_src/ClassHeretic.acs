script "TomeOfPowerStartCheck" (void)
{
	if (!isFreeForAll() && !isTeamGame()) { GiveInventory("PortTomeCoOp",1); }
}

script "TomeOfPowerCooldownReduction" (void)
{
	TakeInventory("TomeOfPowerCooldown",CheckInventory("TomeOfPowerCooldown")/2);
	SetResultValue(1);
}

script "LesserTomeCooldown" (void) //202
{
	while (CheckInventory("LesserTomeCooldown") > 0)
	{
		Delay(35);

		if (CheckInventory("LesserTomeCooldown") > 85)
		{
			TakeInventory("LesserTomeCooldown",1);
		}
		else
		{
			TakeInventory("LesserTomeCooldown",1);
			//TakeInventory("RainTracker",1);
		}
	}
}

script "HereticTomeChargeRate" (void)
{
	int weaponCount = CheckInventory("GotWeapon0") + CheckInventory("GotWeapon2") + CheckInventory("GotWeapon3") + CheckInventory("GotWeapon4") + CheckInventory("GotWeapon5") + CheckInventory("GotWeapon6") + CheckInventory("GotWeapon7");
	int result = 1800;

	until(weaponCount == 0)
	{
		weaponCount--;
		result = (result * 2)/3;
	}

	SetResultValue(result);
}

script "CorvusFaceTarget" (void)
{
	SetActivator(0, AAPTR_MASTER);
	SetResultValue(GetActorAngle(0));
}

script "HereticCloneSpawner" (int powered, int clonetype)
{
	int corvTid = UniqueTID();
	int z = GetActorZ(0);
	str cloneClassname;
	int minionCounter;

	if (powered)
	{
		cloneClassname = clonetype > 0 ? "HereticCorvusCloneCoop" : "HereticCorvusClone";
		z -= 28.0;
	}
	else
	{
		cloneClassname = clonetype > 0 ? "FWizardCoop" : "FWizard";
		z -= 34.0;
	}
	
	SpawnForced(cloneClassname, GetActorX(0), GetActorY(0), z, corvTid);
	SetActorAngle(corvTid, GetActorAngle(0));
	SetActivator(0, AAPTR_TARGET);
	GiveInventory(powered ? "DSparilStaffCloneCount" : "DSparilStaffWizardCount",1);
	minionCounter = CheckInventory(powered ? "DSparilStaffCloneCount" : "DSparilStaffWizardCount");
	SetPointerExt(AAPTR_MASTER, 0, corvTid);
	Thing_SetTranslation(corvTid, -1);
	GiveActorInventory(corvTid, "SamsaraHereticCloneCopyFriendliness", 1);
	SetActivator(corvTid);
	ACS_NamedExecuteWithResult("HereticCloneThink",powered,minionCounter);
	Thing_ChangeTID(corvTid, 0);
}

script "HereticCloneThink" (int type, int minionCounter)
{
	int queueRemoval = 0;
	SetInventory(type ? "DSparilStaffCloneCount" : "DSparilStaffWizardCount",minionCounter);
	While(ClassifyActor(0) > 1 && (ClassifyActor(0) & ACTOR_ALIVE))
	{
		GiveInventory(type ? "DSparilStaffCloneThinker" : "DSparilStaffWizardThinker",1);
		Delay(1);
	}
	if(!(ClassifyActor(0) & ACTOR_ALIVE) && !CheckInventory("DSparilStaffMinionDeath"))
		GiveInventory(type ? "DSparilStaffCloneACSDecrementer" : "DSparilStaffWizardACSDecrementer",1);
	SetActivator(0, AAPTR_MASTER);
}

script "HereticCloneDecrementHelper" (int type)
{
	SetResultValue(CheckInventory(type ? "DSparilStaffCloneCount" : "DSparilStaffWizardCount"));
}

script "HereticTurnTowardTarget" (int invertPitch)
{
	int corvTid = UniqueTID();

	Thing_ChangeTID(0, corvTid);
	SetActivator(0, AAPTR_MASTER);

	int playAngle = GetActorAngle(0);
	int playPitch = GetActorPitch(0);
	int adjHypRatio = cos(playPitch);
	int playX = GetActorX(0) + FixedMul(512 * cos(playAngle), adjHypRatio);
	int playY = GetActorY(0) + FixedMul(512 * sin(playAngle), adjHypRatio);
	int playZ = GetActorZ(0) + 768 * sin(playPitch);

	SetActivator(corvTid);
	Thing_ChangeTID(corvTid, 0);

	int vX = GetActorX(0) - playX;
	int vY = GetActorY(0) - playY;
	int vZ = GetActorZ(0) - playZ;

	int vAngle = VectorAngle(vX, vY) - 0.5;
	int vPitch = VectorAngle(VectorLength(vX, vY), vZ);

	SetActorAngle(0, vAngle);
	SetActorPitch(0, invertPitch ? -vPitch : vPitch);
}
