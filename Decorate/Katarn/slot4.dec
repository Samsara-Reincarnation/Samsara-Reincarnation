actor "Imperial Repeater": Weapon
{
  Weapon.SelectionOrder 600
  Weapon.SlotNumber     4
  Weapon.BobStyle		Inverse
  Weapon.BobSpeed		1.05
  Weapon.BobRangeX	  	1.0
  Weapon.BobRangeY	  	0.55  
  Weapon.AmmoUse1       1
  Weapon.AmmoUse2       1
  Weapon.AmmoGive       20
  Weapon.AmmoType1      "Cell" //"Shell" Why?
  Weapon.AmmoType2      "Cell" //"Shell"
  Weapon.UpSound        "katarn/wepslct"
  // Inventory.PickupMessage "You picked up an Imperial Repeater Rifle."
  Obituary              "%k drilled %o with the Repeater."
  +THRUGHOST
  +INVENTORY.UNDROPPABLE
  +WEAPON.NOAUTOAIM
  Inventory.RestrictedTo "KatarnPlayer"
  Tag "Imperial Repeater Gun"
  States
  {
    Spawn:
      IREP A -1
      Loop

    Ready:
      KREP A 2 A_WeaponReady
      loop

    Select:
      TNT1 A 0 A_Raise
      KREP A 1 A_Raise
      Loop

    Deselect:
      TNT1 A 0 A_Lower
      KREP A 1 A_Lower
      Loop

    Fire:
      TNT1 A 0 A_JumpIfInventory("KatarnFireRateUp",1,"FastFire")
      TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
      KREP B 1 BRIGHT 
      KREP B 1 BRIGHT A_FireCustomMissile("repeater_shot",0,1,6,0,0,0)
      KREP B 0 A_PlayWeaponSound("katarn/repshot")
      KREP B 0 A_GunFlash
      KREP C 5
      KREP CCCC 1 A_Refire
      Goto Ready

    CoopFire:
      KREP B 1 BRIGHT 
      KREP B 1 BRIGHT A_FireCustomMissile("repeater_shot2",0,1,6,0,0,0)
      KREP B 0 A_PlayWeaponSound("katarn/repshot")
      KREP B 0 A_GunFlash
      KREP C 5
      KREP CCCC 1 A_Refire
      Goto Ready

    AltFire:
      TNT1 A 0 A_JumpIfInventory("KatarnFireRateUp",1,"FastAltFire")
      TNT1 A 0 A_JumpIfInventory("Cell", 3, "AltFire2")
      KREP A 12 A_PlayWeaponSound("katarn/repempt")
      Goto Ready

    AltFire2:
      TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopAltfire")
      KREP B 1 BRIGHT //The main firing sprite.
      KREP B 0 A_FireCustomMissile("repeater_shot",0,1, 5, 0,0,0) //Top center
      KREP B 0 A_FireCustomMissile("repeater_shot",0,1,10,-3,0,0) //Bottom right
      KREP B 0 A_FireCustomMissile("repeater_shot",0,1, 0,-3,0,0) //Bottom left
      KREP B 0 A_PlayWeaponSound("katarn/repburs")
      KREP B 1 BRIGHT //The main firing sprite.
      KREP B 0 A_GunFlash
      KREP A 10
      KREP AAAAA 1 A_Refire
      Goto Ready

    CoopAltfire:
      KREP B 1 BRIGHT //The main firing sprite.
      KREP B 0 A_FireCustomMissile("repeater_shot2",0,1, 5, 0,0,0) //Top center
      KREP B 0 A_FireCustomMissile("repeater_shot2",0,1,10,-3,0,0) //Bottom right
      KREP B 0 A_FireCustomMissile("repeater_shot2",0,1, 0,-3,0,0) //Bottom left
      KREP B 0 A_PlayWeaponSound("katarn/repburs")
      KREP B 1 BRIGHT //The main firing sprite.
      KREP B 0 A_GunFlash
      KREP A 10
      KREP AAAAA 1 A_Refire
      Goto Ready

    FastFire:
      TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFastFire")
      KREP B 1 BRIGHT A_FireCustomMissile("repeater_shot",0,1,6,0,0,0)
      KREP B 0 A_PlayWeaponSound("katarn/repshot")
      KREP B 1 BRIGHT
      KREP B 0 A_GunFlash
      KREP C 1
      KREP C 1 A_Refire
      Goto Ready

    CoopFastFire:
      KREP B 1 BRIGHT A_FireCustomMissile("repeater_shot2",0,1,6,0,0,0)
      KREP B 0 A_PlayWeaponSound("katarn/repshot")
      KREP B 1 BRIGHT
      KREP B 0 A_GunFlash
      KREP C 1
      KREP C 1 A_Refire
      Goto Ready

    FastAltFire:
      TNT1 A 0 A_JumpIfInventory("Cell", 3, "FastAltFire2")
      KREP A 6 A_PlayWeaponSound("katarn/repempt")
      Goto Ready

    FastAltFire2:
      TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFastAltfire")
      KREP B 1 BRIGHT //The main firing sprite.
      KREP B 0 A_FireCustomMissile("repeater_shot",0,1, 5, 0,0,0) //Top center
      KREP B 0 A_FireCustomMissile("repeater_shot",0,1,10,-3,0,0) //Bottom right
      KREP B 0 A_FireCustomMissile("repeater_shot",0,1, 0,-3,0,0) //Bottom left
      KREP B 0 A_PlayWeaponSound("katarn/repburs")
      KREP B 1 BRIGHT
      KREP B 0 A_GunFlash
      KREP A 4
      KREP AAA 1 A_Refire
      Goto Ready

    CoopFastAltfire:
      KREP B 1 BRIGHT //The main firing sprite.
      KREP B 0 A_FireCustomMissile("repeater_shot2",0,1, 5, 0,0,0) //Top center
      KREP B 0 A_FireCustomMissile("repeater_shot2",0,1,10,-3,0,0) //Bottom right
      KREP B 0 A_FireCustomMissile("repeater_shot2",0,1, 0,-3,0,0) //Bottom left
      KREP B 0 A_PlayWeaponSound("katarn/repburs")
      KREP B 1 
      KREP B 0 A_GunFlash
      KREP A 4
      KREP AAA 1 A_Refire
      Goto Ready

    Flash:
      TNT1 A 3 BRIGHT A_Light(5)
      TNT1 A 2 A_Light(3)
	   TNT1 A 0 A_Light0
      Stop
  }
}

ACTOR repeater_shot : FastProjectile
{
  Radius  2
  Height  2
  Speed   120
  Damage  (random(9,13) * 2)
  Obituary              "%k drilled %o with the Repeater."
  +NOEXTREMEDEATH 
  +HITTRACER
  Scale   1.0
  Renderstyle ADD
  PROJECTILE
  States
  {
    Spawn:
      PREP A 2 BRIGHT
      Loop
    
    Death:
      TNT1 A 0
      TNT1 A 1 BRIGHT A_PlaySound("katarn/extiny",6,1.0,0,ATTN_NORM)
      TNT1 A 0 A_SpawnItem("repeater_puff")
      Stop
	XDeath:
	  TNT1 A 0
      TNT1 A 1 BRIGHT A_PlaySound("katarn/extiny",6,1.0,0,ATTN_NORM)
      TNT1 A 0 A_SpawnItem("repeater_puff")
	  TNT1 A 0 A_ReArrangePointers(AAPTR_TRACER,AAPTR_NULL,AAPTR_NULL)
	  TNT1 A 0 A_SpawnItemEx("Switchable_FlyingBlood_NonViolent",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),33,SXF_TRANSFERPOINTERS)
	  Stop
  }
}

ACTOR repeater_shot2 : repeater_shot
{
  Species "Player"
  +THRUSPECIES
}

ACTOR repeater_puff : spark_puff
{
  States
  {
    Spawn:
      EXBL A 2 BRIGHT
      EXBL BC 2 BRIGHT
      EXBL DEFG 2
      Stop
  }
}