actor "Sapphire Wand" : MWeapWand
{
    Weapon.SelectionOrder 3600
    Weapon.SlotNumber 1
	+WEAPON.NOALERT
    +INVENTORY.UNDROPPABLE
    Weapon.KickBack 0
    Weapon.YAdjust 9
	Inventory.RestrictedTo "HexenPlayer"	
    States
    {
      Select:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "SelectFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "SelectMage")
		H116 A 1 A_Raise
		Loop
	  SelectMage:	
        MWND A 1 A_Raise
        Loop
	  SelectFighter:
		H100 E 1 A_Raise
		Loop
      Deselect:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "DeselectFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "DeselectMage")
		H116 A 1 A_Lower
		Loop
	  DeselectMage:	
        MWND A 1 A_Lower
        Loop
	  DeselectFighter:
		H100 E 1 A_Lower
		Loop
      Ready:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "ReadyFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "ReadyMage")
		H116 A 1 A_WeaponReady
		Loop
	  ReadyMage:	
        MWND A 1 A_WeaponReady
        Goto Ready
	  ReadyFighter:
		H100 E 0 A_JumpIfNoAmmo("FakeReadyFighter")
		H100 EF 5
	  RealReadyFighter:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, 1)
		Goto Ready
		H100 A 1 A_WeaponReady
		Loop
	  FakeReadyFighter:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, 1)
		Goto Ready
		H100 E 1 A_WeaponReady
		Loop
      Fire:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "FireFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "FireMage")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")	  
		H116 A 2 Bright
		H116 B 0 A_PlaySoundEx("hexen/jadefire","auto")
		H116 B 4 Bright A_FireBullets(4,4, 4, 3, "JadePuff")
		H116 B 2
		H116 A 5
		H116 A 0 A_Refire
		Goto Ready		
	  CoopFire:
		H116 A 2 Bright
		H116 B 0 A_PlaySoundEx("hexen/jadefire","auto")
		H116 B 4 Bright A_FireCustomMissile("HexenJadeBulletCoop", random(-4,4), 0, 0, random(-4,4))
		H116 B 0 Bright A_FireCustomMissile("HexenJadeBulletCoop", random(-4,4), 0, 0, random(-4,4))
		H116 B 0 Bright A_FireCustomMissile("HexenJadeBulletCoop", random(-4,4), 0, 0, random(-4,4))
		H116 B 2
		H116 A 5
		H116 A 0 A_Refire
		Goto Ready		  
	  FireMage:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFireMage")	  
		MWND A 2
		MWND J 2
		MWND K 2 Bright
		MWND B 4 Bright Offset (0, 48) A_FireCustomMissile ("MageWandMissile2")
		MWND J 3 Offset (0, 40)
		MWND A 2 Offset (0, 36)
		MWND A 3 Offset (0, 32) A_ReFire
		Goto Ready
      CoopFireMage:
		MWND A 2
		MWND J 2
		MWND K 2 Bright
		MWND B 4 Bright Offset (0, 48) A_FireCustomMissile ("MageWandMissile3")
		MWND J 3 Offset (0, 40)
		MWND A 2 Offset (0, 36)
		MWND A 3 Offset (0, 32) A_ReFire
		Goto Ready
	  FireFighter:
		TNT1 A 0 A_AlertMonsters
		H100 B 0 A_JumpIfCloser(64, "FireFighterMelee")
		H100 B 6 A_FireCustomMissile("HexenImpalerMissile",0,1)
		H100 B 0 A_Recoil(1)
		H100 C 8 A_PlaySound("Hexen/Weapon/XBowLoad", CHAN_WEAPON)
		H100 D 5
		H100 E 5 A_CheckReload
		H100 F 5
		H100 A 12
		Goto RealReadyFighter
	  FireFighterMelee:
		H100 A 2 Offset(0,24)
		H100 A 2 Offset(0,14)
		H100 A 0 A_PlaySound("FighterPunchMiss", CHAN_6)
		H100 A 0 A_CustomPunch(random(10,30),1,1,"HexenImpalerPuff",64)
		H100 A 2 Offset(0,12)
		H100 A 2 Offset(0,14)
		H100 A 2 Offset(0,24)
		H100 A 10 Offset(0,32)
		Goto RealReadyFighter
    }
}

actor MageWandMissile2 : MageWandMissile replaces MageWandMissile
{
    Radius 3
    Height 2
	Damage 2
    Obituary "$SAMSARA_HEXEN_OB_PISTOL"
    Decal RailScorchLower
	+STRIFEDAMAGE
    +SPAWNSOUNDSOURCE
    +NOBOSSRIP
    DamageType "Pistol"
}

actor MageWandMissile3 : MageWandMissile2 { Species "Player" +THRUSPECIES }

ACTOR HexenImpalerMissile : FastProjectile
{
	Projectile
	+STRIFEDAMAGE
	+BLOODSPLATTER
	+NOEXTREMEDEATH
	Radius 4
	Height 8
	Damage 10
	Speed 80
	SeeSound "Hexen/Weapon/XBowFire"
	States
	{
		Spawn:
			H100 H 1
			Loop
		Death:
		Crash:
			H100 I 3 A_PlaySound("Hexen/Weapon/XBowHit")
			H100 JKLMN 3
			Stop
		XDeath:
			TNT1 A 10 A_PlaySound("Hexen/Weapon/XBowSplatter")
			Stop
	}
}

actor HexenImpalerPuff
{
	Radius 4
	Height 8
	+NOINTERACTION
	States
	{
		Spawn:
			H100 I 3 A_PlaySound("Hexen/Weapon/XBowHit")
			H100 JKLMN 3
			Stop
		Melee:
			TNT1 A 10 A_PlaySound("Hexen/Weapon/XBowSplatter")
			Stop
	}
}

actor JadePuff
{
	  renderstyle Add
	  +NOBLOCKMAP
	  +NOGRAVITY
	  +PUFFONACTORS
	  +PAINLESS
	  Scale .5
	  Alpha .5
	  DamageType "Electric"
	  states
	  {
		  Spawn:
			H116 IJKLMNO 2 bright
			stop
		  Crash:
			H116 IJKLMNO 2 bright
			stop
	  }
}

actor HexenJadeBulletCoop : FastProjectile
{
	Speed 480
	Damage (random(1,3)*3)
	Species "Player"
	+THRUSPECIES
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Death:
			TNT1 A 0
			TNT1 A 1 A_SpawnItemEX("JadePuff", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION | SXF_CLIENTSIDE)
			Stop
	}
}	