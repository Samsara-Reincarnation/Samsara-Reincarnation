actor "Mace of Contrition" : CWeapMace
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3500
    Weapon.KickBack 150
    Weapon.YAdjust -8
    +BLOODSPLATTER
    +WEAPON.WIMPY_WEAPON
    +WEAPON.MELEEWEAPON
    +WEAPON.NOALERT
    +INVENTORY.UNDROPPABLE
    Obituary "$SAMSARA_HEXEN_OB_FIST"
	Inventory.RestrictedTo "HexenPlayer"
    Tag "Mace of Contrition"
    States
    {
      Select:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "SelectFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "SelectMage")
        CMCE A 1 A_Raise
        Loop
	
	  SelectMage:
		TNT1 A 0 A_TakeInventory("Mace of Contrition",1)
		TNT1 A 1 A_SelectWeapon("Sapphire Wand")
		Stop
	
	  SelectFighter:
        FPCH A 1 A_Raise
        Loop
        
      Deselect:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "DeselectFighter")
        CMCE A 1 A_Lower
        Loop
		
	  DeselectFighter:
        FPCH A 1 A_Lower
        Loop

      Ready:
		TNT1 A 0 A_TakeInventory("Hexen_Fist_HitCounter",3)
		TNT1 A 0 A_TakeInventory("Hexen_Fist_Missed",1)
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "ReadyFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "SelectMage")
        CMCE A 1 A_WeaponReady
        Loop
		
	  ReadyFighter:
		FPCH A 1 A_WeaponReady
		Goto Ready

      Fire:
        TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "FireFighter")
        TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "SelectMage")
      FireCleric:
        CMCE B 2 Offset (60, 20)
        CMCE B 1 Offset (30, 33)
        CMCE B 2 Offset (8, 45)
        CMCE CDE 1 Offset (8, 45)
        TNT1 A 0 A_JumpIfCloser(129,"MacePuffClericMonster")
      MacePuffClericNull:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
        TNT1 A 0 A_CustomPunch(random(25,40),1,CPF_PULLIN,"HammerPuff",64)
        Goto MaceClericExit
        TNT1 A 0 A_CustomPunch(random(25,40),1,CPF_PULLIN,"HammerPuffCoop",64)
        Goto MaceClericExit
      MacePuffClericMonster:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
        TNT1 A 0 A_CustomPunch(random(25,40),1,CPF_PULLIN,"HammerPuff",128)
        Goto MaceClericExit
        TNT1 A 0 A_CustomPunch(random(25,40),1,CPF_PULLIN,"HammerPuffCoop",128)
        Goto MaceClericExit
      MaceClericExit:
        CMCE E 1 Offset (-11, 58)
        CMCE F 1 Offset (8, 45)
        CMCE F 2 Offset (-8, 74)
        CMCE F 1 Offset (-20, 96)
        CMCE F 8 Offset (-33, 160)
        CMCE A 2 Offset (8, 75) A_ReFire
        CMCE A 1 Offset (8, 65)
        CMCE A 2 Offset (8, 60)
        CMCE A 1 Offset (8, 55)
        CMCE A 2 Offset (8, 50)
        CMCE A 1 Offset (8, 45)
        Goto Ready

      FireFighter:
        FPCH B 5 Offset(5, 40)
        FPCH C 4 Offset(5, 40)
        TNT1 A 0 A_JumpIfCloser(129,2)
        TNT1 A 0 A_TakeInventory("Hexen_Fist_HitCounter",3)
        TNT1 A 0 A_JumpIfInventory("Hexen_Fist_HitCounter",2,"FireFighter2")
        TNT1 A 0 A_JumpIfCloser(129,"PunchPuffFighterMonster")
      PunchPuffFighterNull:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
        TNT1 A 0 A_CustomPunch(random(40,55),1,CPF_PULLIN,"Hexen_Baratus_PunchPuff",64)
        Goto PunchPuffFighterMonsterEnd
        TNT1 A 0 A_CustomPunch(random(40,55),1,CPF_PULLIN,"Hexen_Baratus_PunchPuffCoop",64)
        Goto PunchPuffFighterMonsterEnd
      PunchPuffFighterMonster:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
        TNT1 A 0 A_CustomPunch(random(40,55),1,CPF_PULLIN,"Hexen_Baratus_PunchPuffMonster",128)
        Goto PunchPuffFighterMonsterEnd
        TNT1 A 0 A_CustomPunch(random(40,55),1,CPF_PULLIN,"Hexen_Baratus_PunchPuffMonsterCoop",128)
        Goto PunchPuffFighterMonsterEnd
      PunchPuffFighterMonsterEnd:
        FPCH DC 4 Offset(5, 40) 
        TNT1 A 0 A_JumpIfInventory("Hexen_Fist_Missed",1,1)
        Goto PunchFighterExit
        TNT1 A 0 A_TakeInventory("Hexen_Fist_HitCounter",3)
      PunchFighterExit:
        FPCH B 5 Offset(5, 40) A_ReFire
        Goto Ready
      FireFighter2:
        TNT1 A 0 A_JumpIfCloser(129,"PunchPuffFighterMonster2")
      PunchPuffFighterNull2:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
        TNT1 A 0 A_CustomPunch(random(80,110),1,CPF_PULLIN,"Hexen_Baratus_PunchPuff",64)
        Goto PunchPuffFighterMonster2End
        TNT1 A 0 A_CustomPunch(random(80,110),1,CPF_PULLIN,"Hexen_Baratus_PunchPuffCoop",64)
        Goto PunchPuffFighterMonster2End
      PunchPuffFighterMonster2:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
        TNT1 A 0 A_CustomPunch(random(80,110),1,CPF_PULLIN,"Hexen_Baratus_HammerPuffMonster",128)
        Goto PunchPuffFighterMonster2End
        TNT1 A 0 A_CustomPunch(random(80,110),1,CPF_PULLIN,"Hexen_Baratus_HammerPuffMonsterCoop",128)
        Goto PunchPuffFighterMonster2End
      PunchPuffFighterMonster2End:
        TNT1 A 0 A_TakeInventory("Hexen_Fist_HitCounter",3)
        TNT1 A 0 A_TakeInventory("Hexen_Fist_Missed",1)
        TNT1 A 0 A_PlaySound("FighterGrunt",CHAN_VOICE)
        FPCH DE 4 Offset(5, 40)
        FPCH E 1 Offset(15, 50)
        FPCH E 1 Offset(25, 60)
        FPCH E 1 Offset(35, 70)
        FPCH E 1 Offset(45, 80)
        FPCH E 1 Offset(55, 90)
        FPCH E 1 Offset(65, 90)
        FPCH E 10 Offset(0, 150)
        Goto Ready
    }
}

Actor Hexen_Fist_Missed : Boolean
{
	Inventory.MaxAmount 1
} 

Actor Hexen_Fist_HitCounter : Counter
{
	Inventory.MaxAmount 3
} 

Actor Hexen_Baratus_PunchPuff : PunchPuff
{
	+PUFFGETSOWNER
	-PUFFONACTORS
	AttackSound "FighterPunchHitWall"
	SeeSound "FighterPunchHitThing"
	ActiveSound "FighterPunchMiss"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_TakeFromTarget("Hexen_Fist_HitCounter",3)
			FHFX STUVW 4
			Stop
	}
}

Actor Hexen_Baratus_PunchPuffMonster : Hexen_Baratus_PunchPuff
{
	+PUFFONACTORS
	AttackSound "FighterPunchHitWall"
	SeeSound "FighterPunchHitThing"
	ActiveSound "FighterPunchMiss"
	States
	{
		Spawn:
		Melee:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("Hexen_Fist_HitCounter",1)
			FHFX STUVW 4
			Stop
		Crash:
			TNT1 A 0 A_GiveToTarget("Hexen_Fist_Missed",1)
			FHFX STUVW 4
			Stop
	}
}

Actor Hexen_Baratus_HammerPuff : Hexen_Baratus_PunchPuff
{
	SeeSound "FighterHammerHitThing"
	AttackSound "FighterHammerHitWall"
	ActiveSound "FighterHammerMiss"
}

Actor Hexen_Baratus_HammerPuffMonster : Hexen_Baratus_PunchPuffMonster
{
	+PUFFONACTORS
	SeeSound "FighterHammerHitThing"
	AttackSound "FighterHammerHitWall"
	ActiveSound "FighterHammerMiss"
}

Actor HammerPuffCoop : HammerPuff { Species "Player" +THRUSPECIES +MTHRUSPECIES }
Actor Hexen_Baratus_PunchPuffCoop : Hexen_Baratus_PunchPuff { Species "Player" +THRUSPECIES +MTHRUSPECIES }
Actor Hexen_Baratus_PunchPuffMonsterCoop : Hexen_Baratus_PunchPuffMonster { Species "Player" +THRUSPECIES +MTHRUSPECIES }
Actor Hexen_Baratus_HammerPuffCoop : Hexen_Baratus_HammerPuff { Species "Player" +THRUSPECIES +MTHRUSPECIES }
Actor Hexen_Baratus_HammerPuffMonsterCoop : Hexen_Baratus_HammerPuffMonster { Species "Player" +THRUSPECIES +MTHRUSPECIES }

actor "Spiked Gauntlet" : FWeapFist
{
    Weapon.SelectionOrder 3400
    Weapon.KickBack 150
    +WIMPY_WEAPON
    +MELEEWEAPON
    +NOALERT
    +INVENTORY.UNDROPPABLE
    +CHEATNOTWEAPON
    Inventory.PickupMessage "You got the Spiked Gauntlet of Baratus!"
    Inventory.PickupSound "hexen/weaponget"
    Obituary "$SAMSARA_HEXEN_OB_PUNCHDRUNKB"
    Tag "Spiked Gauntlet"
    States
    {
      Select:
        FPCH A 1 A_Raise
        Loop

      Deselect:
        FPCH A 1 A_Lower
        Loop

      Ready:
        FPCH A 1 A_WeaponReady
        Loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopFire")
        FPCH B 5 Offset(5, 40)
        FPCH C 4 Offset(5, 40)
        TNT1 A 0 A_FPunchAttack
        FPCH D 4 Offset(5, 40) A_CustomPunch(random(10,15),1,0,"BlankPuff4",96)
        FPCH C 4 Offset(5, 40)
        FPCH B 5 Offset(5, 40) A_ReFire
        Goto Ready
        
      CoopFire:
        FPCH B 5 Offset(5, 40)
        FPCH C 4 Offset(5, 40)
        TNT1 A 0 A_FPunchAttack
        FPCH D 4 Offset(5, 40) A_CustomPunch(random(40,55),1,0,"BlankPuff4",96)
        FPCH C 4 Offset(5, 40)
        FPCH B 5 Offset(5, 40) A_ReFire
        Goto Ready

      Fire2:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire2")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopFire2")
        TNT1 A 0 A_CustomPunch(random(40,50),1,0,"BlankPuff4",96)
        FPCH DE 4 Offset(5, 40)
        FPCH E 1 Offset(15, 50)
        FPCH E 1 Offset(25, 60)
        FPCH E 1 Offset(35, 70)
        FPCH E 1 Offset(45, 80)
        FPCH E 1 Offset(55, 90)
        FPCH E 1 Offset(65, 90)
        FPCH E 10 Offset(0, 150)
        Goto Ready
        
      CoopFire2:
        TNT1 A 0 A_CustomPunch(random(80,110),1,0,"BlankPuff4",96)
        FPCH DE 4 Offset(5, 40)
        FPCH E 1 Offset(15, 50)
        FPCH E 1 Offset(25, 60)
        FPCH E 1 Offset(35, 70)
        FPCH E 1 Offset(45, 80)
        FPCH E 1 Offset(55, 90)
        FPCH E 1 Offset(65, 90)
        FPCH E 10 Offset(0, 150)
        Goto Ready
    }
}
