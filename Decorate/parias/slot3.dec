actor "Timon's Axe" : Weapon // FWeapAxe
{
    Weapon.SelectionOrder 1500
    Weapon.SlotNumber 3
    Weapon.AmmoUse1 0//3
    Weapon.AmmoGive1 25
    Weapon.KickBack 150
    Weapon.YAdjust -12
    DamageType "TimonsAxe"
    Weapon.AmmoType1 "Clip"
    Inventory.PickupMessage "You got Timon's Axe!"
    Inventory.PickupSound "hexen/weaponget"
    Obituary "$SAMSARA_HEXEN_OB_SLOT3"
	Inventory.RestrictedTo "HexenPlayer"
    +WEAPON.AMMO_OPTIONAL
    +WEAPON.MELEEWEAPON
    +INVENTORY.UNDROPPABLE
    +PIERCEARMOR
    States
    {
      Spawn:
        WFAX A -1
        Stop

      Select:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "SelectFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "SelectMage")
		H119 A 0 A_JumpIfInventory("Clip", 4, "SelectClericGlow")
		H119 I 1 A_Raise
		Loop
		
	  SelectFighter:	
		FAXE A 0 A_JumpIfInventory("Clip", 3, "SelectFighterGlow")
        FAXE A 1 A_Raise
        Loop
		
	 SelectMage:
		H113 AAAABBBBCCCC 1 A_Raise
		Loop
	 DeselectMage:
		H113 AAAABBBBCCCC 1 A_Lower
		Loop			

      Deselect:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "DeselectFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "DeselectMage")
		H119 A 0 A_JumpIfInventory("Clip", 4, "DeselectClericGlow")
		H119 I 1 A_Lower
		Loop
		
	 DeselectFighter:	
		FAXE A 0 A_JumpIfInventory("Clip", 3, "DeselectFighterGlow")
        FAXE A 1 A_Lower
        Loop
		

      Ready:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "ReadyFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "ReadyMage")
		H119 A 0 A_JumpIfInventory("Clip", 4, "ReadyClericGlow")
		H119 I 1 A_WeaponReady
		Loop
		
	  ReadyFighter:	
		FAXE A 0 A_JumpIfInventory ("Clip", 3, "ReadyFighterGlow")
		FAXE A 1 A_WeaponReady
		Goto Ready
		
	  ReadyMage:
		H113 AAAABBBBCCCC 1 A_WeaponReady
		Goto Ready		

	  SelectClericGlow:
		H119 AAABBB 1 A_Raise
		Loop

	  DeselectClericGlow:
		H119 AAABBB 1 A_Lower
		Loop
		
	  ReadyClericGlow:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "ReadyFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "ReadyMage")
		H119 A 0 A_JumpIfNoAmmo ("Ready")
		H119 A 3 A_WeaponReady
		H119 B 0 A_JumpIfNoAmmo ("Ready")
		H119 B 3 A_WeaponReady
		Loop

      SelectFighterGlow:
		FAXE L 0 A_JumpIfNoAmmo ("Select")
		FAXE L 1 A_Raise
		FAXE L 0 A_JumpIfNoAmmo ("Select")
		FAXE L 1 A_Raise
		FAXE L 0 A_JumpIfNoAmmo ("Select")
		FAXE L 1 A_Raise
		FAXE M 0 A_JumpIfNoAmmo ("Select")
		FAXE M 1 A_Raise
		FAXE M 0 A_JumpIfNoAmmo ("Select")
		FAXE M 1 A_Raise
		FAXE M 0 A_JumpIfNoAmmo ("Select")
		FAXE M 1 A_Raise
		loop

      DeselectFighterGlow:
        FAXE L 1 A_Lower
        Loop

      ReadyFighterGlow:
		FAXE L 1 A_WeaponReady
		FAXE L 0 A_JumpIfNoAmmo ("Ready")
		FAXE L 1 A_WeaponReady
		FAXE L 0 A_JumpIfNoAmmo ("Ready")
		FAXE L 1 A_WeaponReady
		FAXE L 0 A_JumpIfNoAmmo ("Ready")
		FAXE M 1 A_WeaponReady
		FAXE M 0 A_JumpIfNoAmmo ("Ready")
		FAXE M 1 A_WeaponReady
		FAXE M 0 A_JumpIfNoAmmo ("Ready")
		FAXE M 1 A_WeaponReady
		FAXE M 0 A_JumpIfNoAmmo ("Ready")
		Goto Ready
		

      Fire:
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 2, "FireFighter")
		TNT1 A 0 A_JumpIfInventory("HexenClassMode", 1, "FireMage")
		TNT1 A 0 A_JumpIfInventory("Clip", 4, "FireClericGlow")
		H119 J 5
		H119 K 0 A_PlaySound("Hexen/BishopSword/Swing",CHAN_WEAPON)  
		H119 KL 5
		H119 M 0 A_FireBullets(-32,-16,-1,5,"ClericSwordPuffNormal",FBF_NORANDOMPUFFZ|FBF_EXPLICITANGLE,64)
		H119 M 0 A_FireBullets(-16,-12,-1,5,"ClericSwordPuffNormal",FBF_NORANDOMPUFFZ|FBF_EXPLICITANGLE,64)
		H119 M 0 A_FireBullets(0,-8,-1,5,"ClericSwordPuffNormal",FBF_NORANDOMPUFFZ|FBF_EXPLICITANGLE,64)
		H119 M 0 A_FireBullets(16,-12,-1,5,"ClericSwordPuffNormal",FBF_NORANDOMPUFFZ|FBF_EXPLICITANGLE,64)
		H119 M 0 A_FireBullets(32,0,-1,5,"ClericSwordPuffNormal",FBF_NORANDOMPUFFZ|FBF_EXPLICITANGLE,64)
		H119 MN 5
		Goto Ready
	FireClericGlow:
		H119 C 5 A_PlaySound("Hexen/BishopSword/SwingHoly",CHAN_WEAPON)
		H119 DE 5
		H119 F 0 A_FireBullets(-32,-16,-1,10,"ClericSwordPuff",FBF_NORANDOMPUFFZ|FBF_EXPLICITANGLE,64)
		H119 F 0 A_FireBullets(-16,-12,-1,10,"ClericSwordPuff",FBF_NORANDOMPUFFZ|FBF_EXPLICITANGLE,64)
		H119 F 0 A_FireBullets(0,-8,-1,10,"ClericSwordWallPuff",FBF_NORANDOMPUFFZ|FBF_EXPLICITANGLE,64)
		H119 F 0 A_FireBullets(16,-12,-1,10,"ClericSwordPuff",FBF_NORANDOMPUFFZ|FBF_EXPLICITANGLE,64)
		H119 F 0 A_FireBullets(32,0,-1,10,"ClericSwordPuff",FBF_NORANDOMPUFFZ|FBF_EXPLICITANGLE,64)
		H119 F 0 A_JumpIfCloser(64,3)
		H119 F 0 A_TakeInventory("Clip", 4, TIF_NOTAKEINFINITE)
		H119 F 0 A_JumpIfInventory("CoopModeOn",1,3)
		H119 F 0 A_FireCustomMissile("ClericSwordSlash",0,0)
		H119 F 0 A_FireCustomMissile("ClericSwordSlash",0,0,-16)
		H119 F 0 A_FireCustomMissile("ClericSwordSlash",0,0,16)
		H119 F 0 A_Jump(256,2)
		H119 F 0 A_FireCustomMissile("ClericSwordSlashCoop",0,0)
		H119 FG 5
		Goto Ready
		
	  FireMage:
		H113 D 3
		H113 E 3
		H113 F 3 A_JumpIfInventory("Clip", 3, 1)
		Goto SwitchWeapon
		H113 G 5
		H113 H 3 A_PlayWeaponSound("FireDemonAttack")
		H113 H 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFireMage")
		H113 H 0 A_FireCustomMissile("MWeapFire_Missile", 0, false ,-4, 8, 0) // This was removed for damage reasons.
		H113 H 0 A_FireCustomMissile("MWeapFire_Missile", 3, false ,-4, 8,-1) // TODO: make these tighter to compensate(?)
		H113 H 0 A_FireCustomMissile("MWeapFire_Missile", 0, false ,-4, 8, -3)
		H113 H 0 A_FireCustomMissile("MWeapFire_Missile",-3, false ,-4, 8, -1)
		H113 H 0 A_FireCustomMissile("MWeapFire_Missile", 1, false ,-4, 8, 2)
		H113 H 0 A_FireCustomMissile("MWeapFire_Missile",-1, false,-4, 8, 2)
		H113 H 0 A_TakeInventory("Clip", 3, TIF_NOTAKEINFINITE)
		H113 F 3
		H113 E 3
		H113 D 3
		H113 A 2 A_ReFire("FireMage")
		Goto Ready				

	  CoopFireMage:
		H113 H 0 A_FireCustomMissile("MWeapFire_MissileCoop", 0, false ,-4, 8, 0)
		H113 H 0 A_FireCustomMissile("MWeapFire_MissileCoop", 3, false ,-4, 8,-1) // TODO: make these tighter to compensate(?)
		H113 H 0 A_FireCustomMissile("MWeapFire_MissileCoop", 0, false ,-4, 8, -3)
		H113 H 0 A_FireCustomMissile("MWeapFire_MissileCoop",-3, false ,-4, 8, -1)
		H113 H 0 A_FireCustomMissile("MWeapFire_MissileCoop", 1, false ,-4, 8, 2)
		H113 H 0 A_FireCustomMissile("MWeapFire_MissileCoop",-1, TRUE,-4, 8, 2)
		H113 F 3
		H113 E 3
		H113 D 3
		H113 A 2 A_ReFire("FireMage")
		Goto Ready	

 /*     SPFire:
		H119 E 0 A_PlayWeaponSound("Hexen/WandOfEmbers/Fire")
		H119 F 0 A_FireCustomMissile("WandOfEmbersBall_2", Random(-7, -4), 0, 0, Random(-3,8))
		H119 F 0 A_FireCustomMissile("WandOfEmbersBall_2", Random(8, 3), 0, 0, Random(-2, 9))
		H119 F 0 A_FireCustomMissile("WandOfEmbersBall_2", Random(-10, -2), 0, 0, Random(-1,12))
		H119 F 0 A_FireCustomMissile("WandOfEmbersBall_2", Random(10, 2), 0, 0, Random(-1, 12))		
		H119 F 0 A_FireCustomMissile("WandOfEmbersBall_2", 0, 0, 0, 0)
		H119 F 0 A_TakeInventory("Clip", 4, TIF_NOTAKEINFINITE)
		H119 FG 6 Bright
		H119 D 7
		H119 C 5 A_ReFire
		H119 B 6
		Goto Ready*/
		
	  FireFighter:
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"SPFireFighter")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"SPFireFighter")
        H104 B 4 Offset (15, 32) A_JumpIfInventory("Clip",3,"FireFighterGlow")//A_FAxeCheckAtk
        H104 C 3 Offset (15, 32)
        H104 D 2 Offset (15, 32)
        H104 D 1 Offset (-5, 70) A_CustomPunch(random(50,78),1,1,"AxePuff2",144)//A_FAxeAttack - Deals 40-62 damage
        H104 D 2 Offset (-25, 90)

      FireFighter2:
        FAXE E 1 Offset (15, 32)
        FAXE E 2 Offset (10, 54)
        FAXE E 7 Offset (10, 150)
        FAXE A 1 Offset (0, 60) A_ReFire
        FAXE A 1 Offset (0, 52)
        FAXE A 1 Offset (0, 44)
        FAXE A 1 Offset (0, 36)
        FAXE A 1
        Goto Ready
		
	  FireFighterGlow:
        H104 N 4 BRIGHT Offset (15, 32)
        H104 O 3 BRIGHT Offset (15, 32)
        H104 P 2 BRIGHT Offset (15, 32)
        H104 P 1 BRIGHT Offset (-5, 70) A_CustomPunch(random(120,195),1,1,"AxePuffGlow2",144)//A_FAxeAttack - Deals 80 to 124 damage
        H104 P 2 BRIGHT Offset (-25, 90)
        TNT1 A 0 A_JumpIfNoAmmo("FireFighter2")
        FAXE Q 1 BRIGHT Offset (15, 32)
        FAXE Q 2 BRIGHT Offset (10, 54)
        FAXE Q 7 BRIGHT Offset (10, 150)
        FAXE A 1 BRIGHT Offset (0, 60) A_ReFire
        FAXE A 1 BRIGHT Offset (0, 52)
        FAXE A 1 BRIGHT Offset (0, 44)
        FAXE A 1 BRIGHT Offset (0, 36)
        FAXE A 1 BRIGHT
        Goto ReadyFighterGlow
		
	  SPFireFighter:
        H104 B 4 Offset (15, 32) A_JumpIfInventory("Clip",3,"SPFireFighterGlow")//A_FAxeCheckAtk
        H104 C 3 Offset (15, 32)
        H104 D 2 Offset (15, 32)
        H104 D 1 Offset (-5, 70) A_CustomPunch(random(50,78),1,1,"AxePuff2",144)//
        TNT1 A 0 A_CustomPunch(2,0,0,"BlankPuff4",144)
        TNT1 A 0 A_CustomPunch(3,0,0,"BlankPuff4",84)//A_Saw("silence", "silence", 4, "BlankPuff")
        H104 D 2 Offset (-25, 90)

      SPFireFighter2:
        FAXE E 1 Offset (15, 32)
        FAXE E 2 Offset (10, 54)
        FAXE E 7 Offset (10, 150)
        FAXE A 1 Offset (0, 60) A_ReFire
        FAXE A 1 Offset (0, 52)
        FAXE A 1 Offset (0, 44)
        FAXE A 1 Offset (0, 36)
        FAXE A 1
        Goto Ready

      SPFireFighterGlow:
        H104 N 4 BRIGHT Offset (15, 32)
        H104 O 3 BRIGHT Offset (15, 32)
        H104 P 2 BRIGHT Offset (15, 32)
        H104 P 1 BRIGHT Offset (-5, 70) A_CustomPunch(random(120,195),1,1,"AxePuffGlow2",144)//A_FAxeAttack
        TNT1 A 0 A_CustomPunch(3,0,0,"BlankPuff4",144)
        TNT1 AA 0 A_CustomPunch(5,0,0,"BlankPuff4",84)//A_Saw("silence", "silence", 6, "BlankPuff")
        FAXE P 2 BRIGHT Offset (-25, 90)
        TNT1 A 0 A_JumpIfNoAmmo("SPFireFighter2")
        FAXE Q 1 BRIGHT Offset (15, 32)
        FAXE Q 2 BRIGHT Offset (10, 54)
        FAXE Q 7 BRIGHT Offset (10, 150)
        FAXE A 1 BRIGHT Offset (0, 60) A_ReFire
        FAXE A 1 BRIGHT Offset (0, 52)
        FAXE A 1 BRIGHT Offset (0, 44)
        FAXE A 1 BRIGHT Offset (0, 36)
        FAXE A 1 BRIGHT
        Goto ReadyFighterGlow
		
	  SwitchWeapon:
		TNT1 A 0 A_JumpIf(CallACS("Hexen_SwitchPriority")==1,1)
		TNT1 A 0
		Goto Ready		
    }
}

actor ClericSwordSlash
{
	Height 16
	Radius 16
	Damage (8)
	Speed 24
	Projectile
    Alpha 0.75
	DamageType Fire
	//Because this has to work on hellknights
	+THRUACTORS
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	States
	{
		Spawn:
			//Gonna have to do this the ugly way, huh?
			SPIR P 0 A_SpawnItemEx("ClericSwordSlashTrail2",cos(momx)*-8,0,sin(momz)*-8,0,0,0,0,SXF_NOCHECKPOSITION)
			SPIR P 0 A_Explode(8,24,!XF_HURTSOURCE,0,24)
			SPIR P 1 Bright A_Fadeout(0.075)
			SPIR P 0 A_SpawnItemEx("ClericSwordSlashTrail3",cos(momx)*-8,0,sin(momz)*-8,0,0,0,0,SXF_NOCHECKPOSITION)
			SPIR P 0 A_Explode(8,24,!XF_HURTSOURCE,0,24)
			SPIR P 1 Bright A_Fadeout(0.075)
			SPIR P 0 A_SpawnItemEx("ClericSwordSlashTrail4",cos(momx)*-8,0,sin(momz)*-8,0,0,0,0,SXF_NOCHECKPOSITION)
			SPIR P 0 A_Explode(8,24,!XF_HURTSOURCE,0,24)
			SPIR P 1 Bright A_Fadeout(0.075)
			SPIR P 0 A_SpawnItemEx("ClericSwordSlashTrail5",cos(momx)*-8,0,sin(momz)*-8,0,0,0,0,SXF_NOCHECKPOSITION)
			SPIR P 0 A_Explode(8,24,!XF_HURTSOURCE,0,24)
			SPIR P 1 Bright A_Fadeout(0.075)
			SPIR P 0 A_SpawnItemEx("ClericSwordSlashTrail6",cos(momx)*-8,0,sin(momz)*-8,0,0,0,0,SXF_NOCHECKPOSITION)
			SPIR P 0 A_Explode(8,24,!XF_HURTSOURCE,0,24)
			SPIR P 1 Bright A_Fadeout(0.075)
			SPIR P 0 A_SpawnItemEx("ClericSwordSlashTrail7",cos(momx)*-8,0,sin(momz)*-8,0,0,0,0,SXF_NOCHECKPOSITION)
			SPIR P 0 A_Explode(8,24,!XF_HURTSOURCE,0,24)
			SPIR P 1 Bright A_Fadeout(0.075)
			SPIR P 0 A_SpawnItemEx("ClericSwordSlashTrail8",cos(momx)*-8,0,sin(momz)*-8,0,0,0,0,SXF_NOCHECKPOSITION)
			SPIR P 0 A_Explode(8,24,!XF_HURTSOURCE,0,24)
			SPIR P 1 Bright A_Fadeout(0.075)
			SPIR P 0 A_SpawnItemEx("ClericSwordSlashTrail9",cos(momx)*-8,0,sin(momz)*-8,0,0,0,0,SXF_NOCHECKPOSITION)
			SPIR P 0 A_Explode(8,24,!XF_HURTSOURCE,0,24)
			SPIR P 1 Bright A_Fadeout(0.075)
			SPIR P 0 A_SpawnItemEx("ClericSwordSlashTrail10",cos(momx)*-8,0,sin(momz)*-8,0,0,0,0,SXF_NOCHECKPOSITION)
			SPIR P 0 A_Explode(8,24,!XF_HURTSOURCE,0,24)
			SPIR P 1 Bright A_Fadeout(0.075)
			SPIR P 0 A_SpawnItemEx("ClericSwordSlashTrail11",cos(momx)*-8,0,sin(momz)*-8,0,0,0,0,SXF_NOCHECKPOSITION)
			SPIR P 0 A_Explode(8,24,!XF_HURTSOURCE,0,24)
			SPIR P 1 Bright A_Fadeout(0.075)
			SPIR P 0 A_SpawnItemEx("ClericSwordSlashTrail11",cos(momx)*-8,0,sin(momz)*-8,0,0,0,0,SXF_NOCHECKPOSITION)
			SPIR P 0 A_Explode(8,24,!XF_HURTSOURCE,0,24)
			SPIR P 1 Bright A_Fadeout(0.075)
			SPIR P 0 A_SpawnItemEx("ClericSwordSlashTrail11",cos(momx)*-8,0,sin(momz)*-8,0,0,0,0,SXF_NOCHECKPOSITION)
			SPIR P 0 A_Explode(8,24,!XF_HURTSOURCE,0,24)
			SPIR P 1 Bright A_Fadeout(0.075)
			SPIR P 0 A_SpawnItemEx("ClericSwordSlashTrail11",cos(momx)*-8,0,sin(momz)*-8,0,0,0,0,SXF_NOCHECKPOSITION)
			SPIR P 0 A_Explode(8,24,!XF_HURTSOURCE,0,24)
			SPIR P 1 Bright A_Fadeout(0.075)
			SPIR P 0 A_SpawnItemEx("ClericSwordSlashTrail11",cos(momx)*-8,0,sin(momz)*-8,0,0,0,0,SXF_NOCHECKPOSITION)
			SPIR P 0 A_Explode(8,24,!XF_HURTSOURCE,0,24)
			SPIR P 1 Bright A_Fadeout(0.075)
			SPIR P 0 A_SpawnItemEx("ClericSwordSlashTrail11",cos(momx)*-8,0,sin(momz)*-8,0,0,0,0,SXF_NOCHECKPOSITION)
			SPIR P 0 A_Explode(8,24,!XF_HURTSOURCE,0,24)
			SPIR P 1 Bright A_Fadeout(0.075)
			Stop
		Death:
			SPIR P 0 A_SpawnItemEx("ClericSwordWallPuff")
			SPIR P 0 A_PlaySound("SpiritAttack",CHAN_BODY)
			SPIR P 1 Bright A_Fadeout(0.15)
			wait
	}
}

actor ClericSwordSlashCoop { Species "Player" +THRUSPECIES +DONTHARMSPECIES }

actor ClericSwordSlashTrail
{
	Height 16
	Radius 16
    Alpha 0.5
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			SPIR P 1 Bright A_Fadeout(0.05)
			wait
	}
}

actor ClericSwordWallPuff
{
	Height 16
	Radius 16
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			SPIR KLMNO 2 Bright
			Stop
	}
}

actor ClericSwordPuff
{
	Height 16
	Radius 16
    Alpha 0.5
	+PUFFGETSOWNER
	+NOINTERACTION
	+CLIENTSIDEONLY
	+PUFFONACTORS
	States
	{
		Spawn:
			SPIR P 0 A_FaceTarget
			SPIR P 1 A_Fadeout(0.05)
			wait
		Melee:
			SPIR P 0 A_PlaySound("SpiritAttack",CHAN_BODY)
			SPIR P 1 A_Fadeout(0.05)
			wait
		XDeath:
			SPIR P 0 A_PlaySound("FighterAxeHitThing",CHAN_BODY)
			SPIR P 1 A_Fadeout(0.05)
			wait
	}
}

actor ClericSwordSlashTrail2 : ClericSwordSlashTrail { Alpha 0.45 }
actor ClericSwordSlashTrail3 : ClericSwordSlashTrail { Alpha 0.4 }
actor ClericSwordSlashTrail4 : ClericSwordSlashTrail { Alpha 0.4 }
actor ClericSwordSlashTrail5 : ClericSwordSlashTrail { Alpha 0.35 }
actor ClericSwordSlashTrail6 : ClericSwordSlashTrail { Alpha 0.3 }
actor ClericSwordSlashTrail7 : ClericSwordSlashTrail { Alpha 0.25 }
actor ClericSwordSlashTrail8 : ClericSwordSlashTrail { Alpha 0.2 }
actor ClericSwordSlashTrail9 : ClericSwordSlashTrail { Alpha 0.15 }
actor ClericSwordSlashTrail10 : ClericSwordSlashTrail { Alpha 0.1 }
actor ClericSwordSlashTrail11 : ClericSwordSlashTrail { Alpha 0.05 }

actor AxePuff2 : AxePuff replaces AxePuff
{
    DamageType "TimonsAxe"
}

actor AxePuffGlow2 : AxePuffGlow replaces AxePuffGlow
{
    DamageType "TimonsAxe"
    +PUFFGETSOWNER
    States
    {
    Spawn:
         TNT1 A 0
         Stop
    Melee:
         TNT1 A 1
         TNT1 A 1 A_TakeFromTarget("Clip",3,TIF_NOTAKEINFINITE)
         FAXE RSTUVWX 4 Bright
         goto Spawn
    Crash:
         FHFX STUVW 4
         stop
    }
}

actor ClericSwordPuffNormal : AxePuff2 { ActiveSound "" }

actor BlankPuff4 : BlankPuff
{
    DamageType "TimonsAxe"
}

ACTOR MWeapFire_Missile : FireBall {
	Speed 32
	DamageType "Fire"
	Damage 4
	Projectile
	DeathSound "ClericFlameCircle"
	Obituary "%k toasted %o with the Fire Blast!"
}

Actor MWeapFire_MissileCoop : MWeapFire_Missile { Species "Player" +THRUSPECIES }