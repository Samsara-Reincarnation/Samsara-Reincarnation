Actor Unreal_Stinger : UnrealWeapon 
{
	Weapon.SelectionOrder 1800
	Weapon.SlotNumber 4
	Weapon.SlotPriority 1
	Weapon.AmmoGive 40
	Weapon.AmmoUse1 1
	Weapon.AmmoType1 Clip
	Weapon.AmmoUse2 5
	Weapon.AmmoType2 Clip
	Tag "Stinger"
	+THRUGHOST
	Inventory.PickupMessage "You picked up the Stinger"
	Obituary "%o was perforated by %k's Stinger."
	States
	{
	Spawn:
		UN00 A 10 BRIGHT
		UN00 A 10
		Loop
	Select:
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
        TNT1 A 1 A_Raise
        loop
	Deselect:
		UN02 ABCDEFG 1 //A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
		UN02 HI 1 //A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
	InstantDeselect:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait			
	Ready:
		UN01 A 0 A_PlaySound("Unreal/Stinger/Load", CHAN_WEAPON)
		UN01 ABCDEFG 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
		UN01 HIJKLMNOP 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
		UN01 QRSTUV 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
		Goto WIdle
	WIdle:
		UN01 W 3 A_WeaponReady
		Loop
	Fire:
		UN03 A 0 BRIGHT A_PlaySound("Unreal/Stinger/Fire", CHAN_WEAPON)
		UN03 A 4 BRIGHT 
		UN03 B 3 BRIGHT A_FireCustomMissile("UnrealStingerShot", random(-1,1), 1, 5, 0, random(-1,1))
		UN03 C 2 BRIGHT A_Refire
		Goto EndFire
	Hold:
		UN03 A 0 BRIGHT A_PlaySound("Unreal/Stinger/Fire2", CHAN_WEAPON, 1.0, true)
	Hold2:	
		UN03 A 4 BRIGHT 
		UN03 B 3 BRIGHT A_FireCustomMissile("UnrealStingerShot", random(-1,1), 1, 5, 0, random(-1,1))
		UN03 C 2 BRIGHT A_Refire("Hold2")
		Goto EndFire
	EndFire:
		UN01 W 0 A_StopSound(CHAN_WEAPON)
		UN01 W 3 A_PlaySound("Unreal/Stinger/EndFire", CHAN_WEAPON)
		Goto WIdle
	Altfire:
		UN03 A 0 BRIGHT A_PlaySound("Unreal/Stinger/AltFire", CHAN_WEAPON)
		UN03 A 4 BRIGHT A_FireCustomMissile("UnrealStingerShot", random(-10,10), 1, 5, random(-10,10)*2, random(-10,10)*2)
		UN03 BBBB 0 BRIGHT A_FireCustomMissile("UnrealStingerShot", random(-10,10), 0, 5, random(-10,10)*2, random(-10,10)*2)
		UN03 A 2 BRIGHT
		UN03 B 2 BRIGHT
		UN03 C 8 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
		UN03 DD 8 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
		Goto WIdle
		}
}		

Actor UnrealStingerShot : FastProjectile
{
	Radius 1
	Height 1
	Projectile
	Damage (21)
	Scale 0.10
	+HITTRACER
	+NODAMAGETHRUST
	Speed 32
	Obituary "%o was perforated by %k's Stinger."	
	States
	{
	Spawn:
		UN00 A 2 BRIGHT 
		Loop
	XDeath:
		TNT1 A 0 A_JumpIf(CallACS("Unreal_BloodGenerator_Offsets")==1,1)
		TNT1 A 1
		TNT1 A 0 A_GiveInventory("UnrealBloodyPuffGenerator",1,AAPTR_TRACER)
		TNT1 A 1 A_PlaySound("Unreal/RazorJack/HitFlesh", CHAN_BODY)
		Stop
	Death:
	Crash:
		TNT1 A 0
		TNT1 A 0 A_Jump(77, "Crash2")
		TNT1 A 0 A_PlaySound("Unreal/Stinger/Ricochet", CHAN_BODY)
		TNT1 A 1 A_SpawnItemEX("UnrealStingerBurst", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		Stop
	Crash2:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("Unreal/RazorJack/HitWall", CHAN_BODY)	
		TNT1 A 1 A_SpawnItemEX("UnrealStingerBurst", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		Stop		
		}
}		

Actor UnrealStingerBurst
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOTONAUTOMAP
	Radius 1
	Height 1
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256, "Type1", "Type2", "Type3", "Type4", "Type5", "Type6", "Type7", "Type8")
		Goto Type1
	Type1:
		UN00 ABCDEF 2 BRIGHT
		Stop	
	Type2:
		UN01 ABCDEF 2 BRIGHT
		Stop		
	Type3:
		UN02 ABCDEF 2 BRIGHT
		Stop		
	Type4:
		UN03 ABCDEF 2 BRIGHT
		Stop		
	Type5:
		UN04 ABCDEF 2 BRIGHT
		Stop		
	Type6:
		UN05 ABCDEF 2 BRIGHT
		Stop		
	Type7:
		UN06 ABCDEF 2 BRIGHT
		Stop			
	Type8:
		UN07 ABCDEF 2 BRIGHT
		Stop			
		}
}	