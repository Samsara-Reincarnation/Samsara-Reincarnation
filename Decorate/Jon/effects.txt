/////// EFFECTS
actor PSBulletPuff : BulletPuff
{  
  Scale 0.55
  renderstyle Normal
  attacksound "PS/Ricoch"
  DamageType "PSBullet"
  +NOBLOCKMAP
  +NOGRAVITY
  +ALLOWPARTICLES
  +RANDOMIZE
  VSpeed 0
  states
  {
  Spawn:
    TNT1 A 0
  Melee:
    TNT1 A 0 A_SpawnitemEx("PSBulletPuff2",0,0,0,0,0,0,0,128,0)  
    MCAS NNNOOOPPPQQQRRRSSS 1
    stop
  }
}

Actor PSBulletPuffCoop : PSBulletPuff { +MTHRUSPECIES +THRUSPECIES Species "Player" }

Actor PSBulletPuff2
{ 
  Radius 3
  Height 4
  Speed 25
  //renderstyle Add  
  Scale 0.55
  +NOBLOCKMAP
  +NOGRAVITY
  +DONTSPLASH
  +RANDOMIZE
  states
  {
  Spawn:            
    MCAS TTUU 2
    stop
  }
}

actor PSMacheteSparkPuff : BulletPuff
{  
  Scale 0.55
  renderstyle Normal
  DamageType "PSBullet"
  +NOBLOCKMAP
  +NOGRAVITY
  +ALLOWPARTICLES
  +RANDOMIZE
  VSpeed 0
  ActiveSound "PS/SlashSwing" 
  states
  {
  Spawn:
  Crash:
    TNT1 A 0 A_PlaySound("PS/SlashHit")
	KNF_ KKJJ 1 
	KNF_ J 1 A_FadeOut
	Stop
  Melee:
	TNT1 A 1 A_PlaySound("PS/SlashFlesh")
	Stop
  }
}

Actor PSMacheteSparkPuffCoop : PSMacheteSparkPuff { Species "Player" +THRUSPECIES +MTHRUSPECIES }

Actor PSMacheteSparkPuff_UnderWater : PSMacheteSparkPuff { ActiveSound "PS/SlashWater" }

Actor PSMacheteSparkPuff_UnderWaterCoop : PSMacheteSparkPuff_UnderWater { Species "Player" +THRUSPECIES +MTHRUSPECIES }

Actor ExM60CaseSpawner 
{
	Speed 25
	Projectile
	+NOCLIP
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			NULL A 0
			NULL A 1 A_CustomMissile("ExM60BulletCase", 16, 30, random(-80, -100), CMF_AIMDIRECTION, random(40, 80))
			stop
	}
}

actor ExM60BulletCase 
{
	Height 4
	Radius 4
	Speed 5
	Scale 0.25
	ReactionTime 15
	Mass 1
	Projectile
	-NOGRAVITY
	+DONTSPLASH
	+MOVEWITHSECTOR
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
    +THRUACTORS
	+DROPOFF
	+MISSILE
	+NOTELEPORT
	-TELESTOMP
	+TOUCHY
	States 
	{
		Spawn:
		    M60B D 3 A_Countdown
			NULL A 0 A_ScaleVelocity(0.85)
			loop
		Death:
		    NULL A 1
			Stop
	}
}

Actor ExShellCasing : ExM60BulletCase
{
	BounceType "Doom"
	BounceFactor 0.5
	BounceCount 4
	MaxStepHeight 4	
	BounceSound ""	
	+BOUNCEONACTORS
	+ALLOWBOUNCEONACTORS
	+BOUNCEAUTOOFF
	+BOUNCEONWALLS
	-TOUCHY
	states
	{
		Spawn:
			CAS2 ABCDEFGH 3 A_CountDown
			NULL A 0 A_ScaleVelocity(0.85)
			loop
		Death:
			CAS2 A 0 A_Jump(256,"Rest1","Rest2","Rest3","Rest4","Rest5","Rest6")
			Goto Rest1			
		Rest1:
			CAS2 I 500
			CAS2 I 1 A_FadeOut
			Goto Rest1+1
		Rest2:
			CAS2 J 500
			CAS2 J 1 A_FadeOut
			Goto Rest2+1
		Rest3:
			CAS2 K 500
			CAS2 K 1 A_FadeOut
			Goto Rest3+1
		Rest4:
			CAS2 L 500
			CAS2 L 1 A_FadeOut
			Goto Rest4+1
		Rest5:
			CAS2 M 500
			CAS2 M 1 A_FadeOut
			Goto Rest5+1
		Rest6:
			CAS2 N 500
			CAS2 N 1 A_FadeOut
			Goto Rest6+1
	}
}

actor ExAbstractChunk_PS
{
	Radius 8
	Height 8
	Scale 0.7
	Mass 50
	+NOBLOCKMAP
	+NOTRIGGER
	+DONTSPLASH
	-SOLID
	+CLIENTSIDEONLY
	States 
	{
		Vanish:
			NULL A 1
			stop
	}
}

actor ExBloodParticle_PS : ExAbstractChunk_PS 
{
	Scale 0.1
	Gravity 0.8
	+FORCEXYBILLBOARD
	-SOLID
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			_EX2 RST 8
			stop
	}
}

actor ExBloodCloud_PS : ExBloodParticle_PS 
{
	Scale 1.3
	RenderStyle Translucent
	Alpha 0.5
	Gravity 0.1
	+CLIENTSIDEONLY
}

actor ExAbstractGoreChunk_PS 
{
	Radius 4
	Height 8
	Mass 20
	Damage 0
	BounceType "Doom"
	BounceCount 1
	Scale 0.6
	Gravity 0.6
	DeathSound "PS/GoreSplat"
	Projectile
	-NOGRAVITY
	-SOLID
	+CANBOUNCEWATER
	+BOUNCEAUTOOFF
	+MOVEWITHSECTOR
	+FORCEXYBILLBOARD
	+DONTSPLASH
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			"----" "#" 0 A_SpawnItemEx("ExBloodSplash_PS",0,0)
			"----" "#" 0			
			loop
		Death:
		Crash:
			"----" "#" 250
			"----" "#" 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",31) == 1, "Crash")
			"----" "#" 1 A_FadeOut
			Goto Crash+2
		Vanish:
			NULL A 1
			stop
	}
}

actor ExPlayerGoreHead_PS : ExAbstractGoreChunk_PS 
{
	Gravity 0.8
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			_EX2 B 1 A_SpawnItemEx("ExBloodParticle_PS", Random(-1, 1), Random(-1, 1), 0)
			_EX2 BBBBBBBBBBBBB 1 
			loop
		Crash:
			_EX2 A 500
			_EX2 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",31) == 1, "Crash")
			_EX2 A 1 A_FadeOut
			Goto Crash+2
	}
}

actor ExPlayerGoreFoot_PS : ExAbstractGoreChunk_PS
{
	Gravity 0.85
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			_EX1 A 1 A_SpawnItemEx("ExBloodParticle_PS", Random(-1, 1), Random(-1, 1), 0)
			_EX1 AAAAAAAAAAAAA 1 
			loop
		Crash:
			_EX1 A 500
			_EX1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",31) == 1, "Crash")
			_EX1 A 1 A_FadeOut
			Goto Crash+2
	}
}

//BLOOD
actor ExBloodSplash_PS
{
	+NOBLOCKMAP
	+NOGRAVITY
	+CLIENTSIDEONLY
	-SOLID
	States 
	{
		Spawn:
			NULL A 2
			NULL AAAAAAAAAAA 0 A_SpawnItemEx("ExBloodDrop_PS", Random(-1, 1), Random(-1, 1), 1, Random(-3, 3), Random(-3, 3), Random(1, 3), 0, SXF_TRANSFERTRANSLATION, 70)
			stop
	}
}

actor ExSmallBloodSplash_PS
{
	+NOBLOCKMAP
	+NOGRAVITY
	+CLIENTSIDEONLY
	-SOLID	
	States 
	{
		Spawn:
			NULL A 2
			NULL AAAAAA 0 A_SpawnItemEx("ExBloodParticle_PS", Random(-1, 1), Random(-1, 1), 1, Random(-2, 2), Random(-2, 2), Random(1, 3), 0, SXF_TRANSFERTRANSLATION, 70)
			stop
	}
}

actor ExBloodDrop_PS : ExAbstractGoreChunk_PS
{
	Gravity 0.4
	Scale 0.3
	Mass 1
	+THRUACTORS
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			_EX2 U 1
			loop
		Death:
			_EX2 VWX 3
			_EX2 X 80
			_EX2 X 1 A_FadeOut
			Goto Death+4
	}
}

//spawned when actor is hit
actor ExBloodHit_PS
{
	Mass 5
	Scale 0.3
	-SOLID
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	-ALLOWPARTICLES
	+FORCEXYBILLBOARD
	States 
	{
		Spawn:
			_EX2 Y 1
			_EX2 YYY 1 A_SpawnItemEx("ExBloodDrop_PS",0,0,0,Random(-2, 2),Random(-2, 2),Random(-1, 1),0,SXF_TRANSFERTRANSLATION,128)
			NULL AAA 0 A_SpawnItemEx("ExBloodDrop_PS",0,0,0,Random(-2, 2),Random(-2, 2),Random(-1, 1),0,SXF_TRANSFERTRANSLATION,128)
			_EX2 Z 3
			_EX3 A 3
		Vanish:
			_EX3 BBC 1
			_EX3 C 1 A_FadeOut
			Goto Vanish+3
	}
}

actor ExBloodSplatter_PS
{
	Radius 4
	Height 8
	Mass 2
	Scale 0.4
	-SOLID
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+MISSILE
	+DROPOFF
	+NOTELEPORT
	+CANNOTPUSH
	-ALLOWPARTICLES
	+FORCEXYBILLBOARD
	States 
	{
		Spawn:
			_EX2 Y 1
			_EX2 YYY 1 A_SpawnItemEx("ExBloodDrop_PS",0,0,0,Random(-2, 2),Random(-2, 2),Random(-1, 1),0,SXF_TRANSFERTRANSLATION,128)
			NULL AAA 0 A_SpawnItemEx("ExBloodDrop_PS",0,0,0,Random(-2, 2),Random(-2, 2),Random(-1, 1),0,SXF_TRANSFERTRANSLATION,128)
			_EX2 Y 3
			_EX3 A 3
			_EX3 B 2
			_EX3 C 2
		Death:
			_EX3 C 1 a_FadeOut
			loop
	}
}

//GORE
actor ExGore1_PS : ExAbstractGoreChunk_PS
{
	Gravity 0.7
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			_EX2 L 1 A_SpawnItemEx("ExBloodParticle_PS",Random(-1, 1),Random(-1, 1),0,0,0,0,0,SXF_TRANSFERTRANSLATION) //ExBloodParticle
			loop
		Crash:
			_EX2 M 250
			_EX2 M 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",31) == 1, "Crash")
			_EX2 M 1 A_FadeOut
			Goto Crash+2
	}
}

actor ExGore2_PS : ExAbstractGoreChunk_PS 
{
	Gravity 0.5
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			_EX2 N 1 A_SpawnItemEx("ExBloodParticle_PS",Random(-1, 1),Random(-1, 1),0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			loop
		Crash:
			_EX2 N 250
			_EX2 N 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",31) == 1, "Crash")
			_EX2 N 1 A_FadeOut
			Goto Crash+2
	}
}

actor ExGore3_PS : ExAbstractGoreChunk_PS 
{
	Gravity 0.4
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			_EX2 O 1 A_SpawnItemEx("ExBloodParticle_PS",Random(-1, 1),Random(-1, 1),0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			loop
		Crash:
			_EX2 O 250
			_EX2 O 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",31) == 1, "Crash")
			_EX2 O 1 A_FadeOut
			Goto Crash+2
	}
}

actor ExGore4_PS : ExAbstractGoreChunk_PS 
{
	Gravity 0.65
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			_EX2 P 1 A_SpawnItemEx("ExBloodParticle_PS",Random(-1, 1),Random(-1, 1),0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			loop
		Crash:
			_EX2 P 250
			_EX2 P 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",31) == 1, "Crash")
			_EX2 P 1 A_FadeOut
			Goto Crash+2
	}
}

actor ExGore5_PS : ExAbstractGoreChunk_PS 
{
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			_EX2 Q 1 A_SpawnItemEx("ExBloodParticle_PS",Random(-1, 1),Random(-1, 1),0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			loop
		Crash:
			_EX2 Q 250
			_EX2 Q 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",31) == 1, "Crash")
			_EX2 Q 1 A_FadeOut
			Goto Crash+2

	}
}

actor ExGoreGuts_PS : ExAbstractGoreChunk_PS 
{
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			_EX2 E 1 A_SpawnItemEx("ExBloodParticle_PS",Random(-1, 1),Random(-1, 1),0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			loop			
		Crash:
			_EX2 G 15
		Crash2:
			_EX2 H 235
			_EX2 H 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",31) == 1, "Crash2")
			_EX2 H 1 A_FadeOut
			Goto Crash+2
	}
}

actor ExGore7_PS : ExAbstractGoreChunk_PS
{
	Gravity 0.9
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			_EX2 F 1 A_SpawnItemEx("ExBloodParticle_PS",Random(-1, 1),Random(-1, 1),0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			loop
		Crash:
			_EX2 G 15
		Crash2:
			_EX2 H 235
			_EX2 H 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",31) == 1, "Crash2")
			_EX2 H 1 A_FadeOut
			Goto Crash+2
	}
}

actor ExGoreRibcage_PS : ExAbstractGoreChunk_PS 
{
	Gravity 0.95
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			_EX2 CCCCCCCCCCCCCCCC 1 A_SpawnItemEx("ExBloodParticle_PS",Random(-1, 1),Random(-1, 1),0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			loop
		Crash:
			_EX2 D 250
			_EX2 D 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraDecorate",31) == 1, "Crash")
			_EX2 D 1 A_FadeOut
			Goto Crash+2
	}
}

actor PSGibSpawner
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOCLIP
	-TELESTOMP
	-SOLID
	+NOGRAVITY
	States
	{
		Spawn:
		TNT1 A 0
		NULL A 0 A_SpawnItemEx("ExGore1_PS",0, 0, 32, random(3.0,5.0),random(3.0,5.0),random(0.0,10.0),random(0.0,360.0), SXF_ABSOLUTEANGLE|SXF_TRANSFERTRANSLATION, 0)
		NULL A 0 A_SpawnItemEx("ExGore2_PS",0, 0, 32, random(3.0,5.0),random(3.0,5.0),random(0.0,10.0),random(0.0,360.0), SXF_ABSOLUTEANGLE|SXF_TRANSFERTRANSLATION, 0)
		NULL A 0 A_SpawnItemEx("ExGore3_PS",0, 0, 32, random(3.0,5.0),random(3.0,5.0),random(0.0,10.0),random(0.0,360.0), SXF_ABSOLUTEANGLE|SXF_TRANSFERTRANSLATION, 0)
		NULL A 0 A_SpawnItemEx("ExGore4_PS",0, 0, 32, random(3.0,5.0),random(3.0,5.0),random(0.0,10.0),random(0.0,360.0), SXF_ABSOLUTEANGLE|SXF_TRANSFERTRANSLATION, 0)
		NULL A 0 A_SpawnItemEx("ExGore5_PS",0, 0, 32, random(3.0,5.0),random(3.0,5.0),random(0.0,10.0),random(0.0,360.0), SXF_ABSOLUTEANGLE|SXF_TRANSFERTRANSLATION, 0)
		NULL A 0 A_SpawnItemEx("ExGore7_PS",0, 0, 32, random(3.0,5.0),random(3.0,5.0),random(0.0,10.0),random(0.0,360.0), SXF_ABSOLUTEANGLE|SXF_TRANSFERTRANSLATION, 0)
		NULL A 0 A_SpawnItemEx("ExGoreRibcage_PS",0, 0, 32, random(3.0,5.0),random(3.0,5.0),random(0.0,10.0),random(0.0,360.0), SXF_ABSOLUTEANGLE|SXF_TRANSFERTRANSLATION, 0)
		NULL A 0 A_SpawnItemEx("ExGoreGuts_PS",0, 0, 32, random(3.0,5.0),random(3.0,5.0),random(0.0,10.0),random(0.0,360.0), SXF_ABSOLUTEANGLE|SXF_TRANSFERTRANSLATION, 0)
		NULL A 0 A_SpawnItemEx("ExBloodCloud_PS", 0, 0, 35, 0, 0, 0, 0, SXF_ABSOLUTEANGLE|SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_PlaySound("Jon/XGibs",CHAN_BODY,1.0)	
		Stop
	}
}