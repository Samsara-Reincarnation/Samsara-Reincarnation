Actor RTCW_Flamethrower : RTCW_Weapon
{
	Weapon.SlotNumber 9
	Weapon.SelectionOrder 900
	Weapon.AmmoUse 1
	Weapon.AmmoGive 50
	Weapon.AmmoType "Cell"
	Obituary "%o was killed (flamethrower) by %k."
	Inventory.Pickupmessage "Flamethrower"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Flamethrower"
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	States
	{
		Spawn:
			RW98 A -1
			Stop
		Deselect:
			RW00 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 A 0 A_StopSound(CHAN_WEAPON)
			RW00 A 0 A_GunFlash("NozzleStop")
			RW00 GHIJK 2
			Goto Super::Deselect
		Ready:
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",16)
			RW00 LMNO 2
			RW00 A 0 A_GunFlash("Nozzle")
		ReadyLoop:
			RW00 A 0 A_StopSound(CHAN_WEAPON)
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 1 A_WeaponReady
			Loop
		NoAmmo:
			RW00 A 0 A_GunFlash("NozzleStop")
			RW00 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			RW00 A 17
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect
		Nozzle:
			RW01 ABCDEFGH 3 Bright
			Loop
		NozzleStop:
			TNT1 A 1
			Stop
		Fire:
			RW00 A 0 A_JumpIfInventory("Cell",1,1)
			Goto NoAmmo
			RW00 A 0 A_PlaySound("RTCW/Flamethrower/Start",CHAN_5)
			RW00 A 0 A_PlaySound("RTCW/Flamethrower/Stream",CHAN_WEAPON,1.0,1)
		FireHold:
			RW00 A 0 A_JumpIfInventory("RTCW_FlamethrowerStreamAmmo",0,"FireAmmo")
			RW00 A 1 A_FireCustomMissile("RTCW_FlamethrowerFire",frandom(-0.25,0.25),0,4,7,0,frandom(-0.25,0.25))
			RW00 A 0 A_GiveInventory("RTCW_FlamethrowerStreamAmmo",1)
			RW00 A 0 A_Refire("FireHold")
			Goto ReadyLoop
		FireAmmo:
			RW00 A 0 A_JumpIfInventory("Cell",1,1)
			Goto NoAmmo
			RW00 A 1 A_FireCustomMissile("RTCW_FlamethrowerFire",frandom(-0.25,0.25),1,4,7,0,frandom(-0.25,0.25))
			RW00 A 0 A_TakeInventory("RTCW_FlamethrowerStreamAmmo",2)
			RW00 A 0 A_Refire("FireHold")
			Goto ReadyLoop
		Kicking:
			TNT1 A 0 A_Gunflash("KickingFoot")
		KickingDelay:
			RW00 A 18
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			RW00 A 0 A_GunFlash("Nozzle")
			Goto ReadyLoop
		KickingFoot:
			RW99 ABCD 2
			RW99 E 0 A_FireCustomMissile("RTCW_KickProjectile",0,0,0,-8)
			RW99 EFGHI 2
			Stop
	}
}

Actor RTCW_FlamethrowerFire : FastProjectile
{
	Speed 192
	Radius 2
	Height 4
	Damage (0)
	DamageType "RTCWFire"
	+SKYEXPLODE
	+DONTSPLASH
	+CANNOTPUSH
	+BLOODLESSIMPACT
	+HITTRACER
	States
	{
		Spawn:
			TNT1 A 2
			Goto Death	
		Death:
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_FlamethrowerStreamRotate",16,36)
			TNT1 A 1
			stop
	}
}

Actor RTCW_FlamethrowerEndPoint
{
	Height 8
	Radius 4
	Scale 0.25
	Damage (0)
	Renderstyle Translucent
	DamageType "RTCWFire"
	ExplosionDamage 0
	Projectile
	+THRUACTORS
	+NODAMAGETHRUST
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	States
	{
		Spawn:
		Death:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("RTCW_FlamethrowerFireScale")
			RW03 P 0 A_PlaySound("RTCW/Flamethrower/Fire",CHAN_BODY,0.5)
			RW03 CDEFGHIJKLMNOPQRSTUVWXYZ 2 Bright A_Explode(CallACS("RTCW_Decorate",41),24,!XF_HURTSOURCE,0,24)
			RW04 ABCDEFGHIJKLMNOPQRS 2 Bright A_Explode(CallACS("RTCW_Decorate",41),24,!XF_HURTSOURCE,0,24)
			RW04 S 0 A_StopSound(CHAN_BODY)
			Stop
	}
}

Actor RTCW_FlamethrowerEndPointHarmless : RTCW_FlamethrowerEndPoint
{
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("RTCW_FlamethrowerFireScale")
			RW03 CDEFGHIJKLMNOPQRSTUVWXYZ 2 Bright
			RW04 ABCDEFGHIJKLMNOPQRS 2 Bright
			Stop
	}
}

Actor RTCW_FlameStreamSegment
{
	+NOINTERACTION
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	Radius 1
	Height 1
	Renderstyle Add
	Alpha 1.0
	States
	{
		Spawn:
			RW00 A 2 Bright
			stop
	}
}

Actor RTCW_FlameStreamSegment1 : RTCW_FlameStreamSegment { Alpha 0.15 }
Actor RTCW_FlameStreamSegment2 : RTCW_FlameStreamSegment { Alpha 0.3 States { Spawn: RW00 A 1 Bright A_Stop Goto Super::Spawn } }
Actor RTCW_FlameStreamSegment3 : RTCW_FlameStreamSegment { Alpha 0.45 States { Spawn: RW00 A 2 Bright A_Stop Goto Super::Spawn } }
Actor RTCW_FlameStreamSegment4 : RTCW_FlameStreamSegment { Alpha 0.6 States { Spawn: RW00 A 3 Bright A_Stop Goto Super::Spawn } }
Actor RTCW_FlameStreamSegment5 : RTCW_FlameStreamSegment { Alpha 0.75 States { Spawn: RW00 A 4 Bright A_Stop Goto Super::Spawn } }
Actor RTCW_FlameStreamSegment6 : RTCW_FlameStreamSegment { States { Spawn: RW00 A 5 Bright A_Stop Goto Super::Spawn } }
Actor RTCW_FlameStreamSegment7 : RTCW_FlameStreamSegment { States { Spawn: RW00 A 6 Bright A_Stop Goto Super::Spawn }}
Actor RTCW_FlameStreamSegment8 : RTCW_FlameStreamSegment { States { Spawn: RW00 A 7 Bright A_Stop Goto Super::Spawn }}
Actor RTCW_FlameStreamSegmentBlue : RTCW_FlameStreamSegment {}

Actor RTCW_FlamethrowerStreamAmmo : Inventory { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 1 Inventory.InterHubAmount 0 }
Actor RTCW_FlamethrowerFireScript : Inventory { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 1 Inventory.InterHubAmount 0 }

Actor RTCW_FlamethrowerFireVisual
{
	Scale 0.33
	Renderstyle Translucent
	+NOGRAVITY
	+NOINTERACTION
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 3
			RW03 P 0 A_PlaySound("RTCW/Flamethrower/Fire",CHAN_BODY,0.5)
			RW03 PQRSTUVWXYZ 2 Bright
			RW04 ABCDEFGHIJKLMNOPQRS 2 Bright
			RW04 S 0 A_StopSound(CHAN_BODY)
			Stop
	}
}

Actor RTCW_AlliedCrate : CustomInventory
{
	Inventory.PickupMessage "Allied equipment drop"
	Inventory.PickupSound "RTCW/WeaponPickup"
	+INVENTORY.ALWAYSPICKUP
	States
	{
		Spawn:
			RW00 A 5
			Loop
		Pickup:
			TNT1 A 0 A_GiveInventory("RTCW_Thompson",1)
			TNT1 A 0 A_GiveInventory("RTCW_Colt",1)
			TNT1 A 0 A_GiveInventory("RTCW_Snooper",1)
			TNT1 A 0 A_GiveInventory("RTCW_Pineapple",1)
			stop
	}
}

Actor RTCW_Sten : RTCW_Weapon
{
	Weapon.SlotNumber 8
	Weapon.SelectionOrder 600
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.AmmoType1 "RTCW_StenMagazine"
	Weapon.AmmoType2 "Clip"
	Weapon.AmmoGive2 16
	Obituary "%o was killed (sten) by %k."
	Inventory.Pickupmessage "Sten"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Sten"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Spawn:
			RW98 A -1
			Stop
		Deselect:
			RW00 V 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 HIJKL 2
			Goto Super::Deselect
		Ready:
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",17)
			RW00 MNOP 2
		ReadyLoop:
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		NoAmmo:
			RW00 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			RW00 A 17
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect
		Fire:
			RW00 B 0 A_JumpIfInventory("RTCW_StenMagazine",1,1)
			Goto Reload
			RW00 B 0 A_JumpIfInventory("RTCW_StenHeat",0,"CoolDown")
			RW00 B 0 A_GiveInventory("RTCW_StenHeat",1)
			RW00 B 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),0,8,4,0,-4)
			RW00 B 0 A_TakeInventory("RTCW_StenMagazine",1)
			RW00 B 0 A_GiveInventory("RTCW_StenSpread",1)
			RW00 B 0 A_SpawnItemEx("RTCW_VenomSmoke",cos(pitch)*20,4,35-(sin(pitch)*20),(cos(pitch)*frandom(-0.125,0.125))+(sin(pitch)*0.33),frandom(-0.125,0.125),(cos(pitch)*0.33)-sin(pitch)*frandom(-0.125,0.125))
			RW00 B 0 A_FireCustomMissile("RTCW_StenTracer",CallACS("RTCW_Decorate",1,3)*frandom(-1.0,1.0),0,0,4,0,CallACS("RTCW_Decorate",1,3)*frandom(-1.0,1.0))
			RW00 B 0 A_PlaySound("RTCW/Sten/Fire",CHAN_Weapon)
			RW00 B 0 ACS_NamedExecuteWithResult("RTCW_Recoil",1)
			RW00 B 1
			RW00 C 2
			RW00 D 1
			RW00 B 0 A_Refire("Fire")
			RW00 B 0 A_TakeInventory("RTCW_StenSpread",4)
			Goto ReadyLoop
		CoolDown:
			RW00 A 0 A_PlaySound("RTCW/Sten/Overheat",CHAN_5)
			RW00 A 0 A_GiveInventory("RTCW_StenCooling",1)
			RW00 AAAAAAAAAAAAAAA 4 A_SpawnItemEx("RTCW_VenomSmoke",cos(pitch)*20,4,35-(sin(pitch)*20),(cos(pitch)*frandom(-0.125,0.125))+(sin(pitch)*0.33),frandom(-0.125,0.125),(cos(pitch)*0.33)-sin(pitch)*frandom(-0.125,0.125))
			RW00 A 0 A_TakeInventory("RTCW_StenCooling",1)
			RW00 AAA 4
			Goto ReadyLoop
		Reload:
			TNT1 A 0 A_JumpIfInventory("RTCW_StenMagazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("Clip",1,"ReloadSuccess")
			TNT1 A 0 A_JumpIfInventory("RTCW_StenMagazine",1,1)
			goto NoAmmo
			TNT1 A 0
			goto ReadyLoop
		ReloadSuccess:
			TNT1 A 0 A_JumpIfInventory("RTCW_StenMagazine",1,1)
			goto ReloadNoAmmo		
			RW00 A 0 A_PlaySound("RTCW/Sten/Reload",CHAN_6)			
			RW00 "RSTUVWXYZ[\]" 2
			RW01 ABCDEFGHIJKLMNOPQRSTU 2
			RW02 MNOPQ 2
			RW00 A 0
			goto ReloadAmmo
		ReloadNoAmmo:
			RW00 A 0 A_PlaySound("RTCW/Sten/Reload",CHAN_6)			
			RW00 "RSTUVWXYZ[\]" 2
			RW01 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 2
			RW02 A 0 A_PlaySound("RTCW/Sten/Reload2",CHAN_6)		
			RW02 ABCDEFGHIJLMNOPQ 2
			RW00 A 0
			goto ReloadAmmo		
		ReloadAmmo:
			TNT1 A 0 A_JumpIfInventory("RTCW_StenMagazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("Clip",1,1)
			goto ReadyLoop
			TNT1 A 0 A_GiveInventory("RTCW_StenMagazine",1)
			TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
			Loop
		Kicking:
			TNT1 A 0 A_Gunflash("KickingFoot")
		KickingDelay:
			RW00 A 18 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			Goto ReadyLoop
		KickingFoot:
			RW99 ABCD 2
			RW99 E 0 A_FireCustomMissile("RTCW_KickProjectile",0,0,0,-8)
			RW99 EFGHI 2
			Stop
	}
}

Actor RTCW_StenCooling : Inventory { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 1 Inventory.InterHubAmount 0 }
Actor RTCW_StenHeat : Inventory { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 10 Inventory.InterHubAmount 0 }
Actor RTCW_StenSpread : Inventory { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 3 Inventory.InterHubAmount 0  }

Actor RTCW_StenMagazine : Ammo
{
	Inventory.MaxAmount 32
	Ammo.BackpackMaxAmount 32
	States
	{
		Spawn:
			RW00 A -1
			Stop
	}
}

//Was 10 damage, now 20
Actor RTCW_StenTracer : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",20),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",20),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}