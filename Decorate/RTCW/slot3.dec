Actor RTCW_Mauser : RTCW_Weapon
{
	Weapon.SlotNumber 3
	Weapon.SlotPriority 0
	Weapon.SelectionOrder 1400
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 0
	Weapon.AmmoType1 "RTCW_MauserMagazine"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoGive2 5
	Obituary "%o was killed (mauser) by %k."
	Inventory.Pickupmessage "Mauser"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Mauser Rifle"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Spawn:
			RW98 A -1
			Stop
		Deselect:
			RW00 "]" 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 "]" 0 A_TakeInventory("RTCW_ZoomFactor",90)
			RW00 "]" 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 "]" 0 A_ZoomFactor(1.0)
			RW00 "]" 2
			RW01 ABCD 2
			Goto Super::Deselect
		Ready:
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",10)
			RW01 EFGHIJK 2
		ReadyLoop:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,1)
			Goto ReadyLoop+3
			RW00 A 0 A_JumpIf(CallACS("RTCW_Decorate",3)==0,2)
			RW00 A 1 A_ZoomFactor(90.0/CallACS("RTCW_Decorate",2)*1.0)
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		NoAmmo:
			RW00 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			RW00 A 17
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect
		NoAmmoScoped:
			TNT1 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			TNT1 A 17
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect
		Fire:
			RW00 B 0 A_JumpIfInventory("RTCW_MauserMagazine",1,1)
			Goto Reload
			RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_JumpIfInventory("RTCW_Scoped",1,"FireScoped")
			RW00 B 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),0,8,4,0,-4)
			RW00 B 0 A_TakeInventory("RTCW_MauserMagazine",1)
			RW00 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 B 0 A_GunFlash("FireFlash")
			RW00 B 0 ACS_NamedExecuteWithResult("RTCW_Recoil",2)
			RW00 B 0 A_FireCustomMissile("RTCW_SnooperTracer",FRandom(-2.5,2.5),0,0,4, 0, FRandom(-2.5,2.5))
			RW00 B 0 A_JumpIfInventory("RTCW_MauserMagazine",1,1)
			Goto FireLast
			RW00 B 0 A_PlaySound("RTCW/Mauser/Fire",CHAN_Weapon)
		FireFinish:
			RW00 BCDEFGHIJKLM 2
			RW00 A 15
			Goto ReadyLoop
		FireLast:
			RW00 B 0 A_PlaySound("RTCW/Mauser/FireLast",CHAN_Weapon)
			Goto FireFinish
		FireScoped:
			RW00 B 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),0,8,4,0,-4)
			RW00 B 0 A_GiveInventory("RTCW_AimSpread",16711680)
			RW00 B 0 A_TakeInventory("RTCW_MauserMagazine",1)
			RW00 B 0 A_GiveInventory("RTCW_BlazkoWeaponAlert",1)
			RW00 B 0 A_FireCustomMissile("RTCW_MauserTracer",0,0,0,8)
			RW00 B 0 A_SetAngle(angle+frandom(-2.0,2.0))
			RW00 B 0 ACS_NamedExecuteWithResult("RTCW_Recoil",3)
			RW00 B 6
			RW00 B 0 A_JumpIfInventory("RTCW_MauserMagazine",1,1)			
			Goto FireLast
			RW00 B 0 A_PlaySound("RTCW/Mauser/Fire",CHAN_Weapon)
			Goto FireFinish
		FireFlash:
			RW03 B 2 Bright
			Stop
		FireFlashLast:
			RW00 P 2 Bright
			Stop
		AltFire:
			RW00 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,"LowerScope")
			RW00 A 0 A_GiveInventory("RTCW_ZoomFactor",12)
			RW00 A 0 A_GiveInventory("RTCW_Scoped",1)
			RW00 A 0 ACS_NamedExecuteWithResult("RTCW_Scope")
			TNT1 A 10 A_ZoomFactor(90.0/12.0)
			Goto ReadyLoop
		LowerScope:
			RW00 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 A 0 A_TakeInventory("RTCW_ZoomFactor",90)
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 A 0 A_ZoomFactor(1.0)
			Goto Ready
		Reload:
			TNT1 A 0 A_JumpIfInventory("RTCW_Scoped",1,"ScopedNoAmmo")
			TNT1 A 0 A_JumpIfInventory("RTCW_MauserMagazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("Shell",1,"ReloadSuccess")
			TNT1 A 0 A_JumpIfInventory("RTCW_MauserMagazine",1,1)
			goto NoAmmo
			TNT1 A 0
			goto ReadyLoop
		ScopedNoAmmo:
			TNT1 A 0 A_JumpIfInventory("RTCW_MauserMagazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("Shell",1,1)
			Goto NoAmmoScoped
			TNT1 A 0 A_JumpIfInventory("RTCW_MauserMagazine",1,"ReloadLowerScope")
			TNT1 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_6)
			TNT1 A 17
		ReloadLowerScope:
			RW00 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			Goto ReloadSuccess+1
		ReloadSuccess:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,"ReloadLowerScope")
			RW00 A 0 A_TakeInventory("RTCW_ZoomFactor",90)
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 A 0 A_ZoomFactor(1.0)
			RW00 A 0 A_PlaySound("RTCW/Mauser/Reload",CHAN_6)
			RW01 "LMNOPQRSTUVWXYZ[\]" 2
			RW02 ABCDEF 2
			goto ReloadAmmo
		ReloadAmmo:
			TNT1 A 0 A_JumpIfInventory("RTCW_MauserMagazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("Shell",1,1)
			goto ReadyLoop
			TNT1 A 0 A_GiveInventory("RTCW_MauserMagazine",1)
			TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
			Loop
		Kicking:
			TNT1 A 0 A_Gunflash("KickingFoot")
		KickingDelay:
			RW00 A 18
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			Goto ReadyLoop
		KickingFoot:
			RW99 ABCD 2
			RW99 E 0 A_FireCustomMissile("RTCW_KickProjectile",0,0,0,-8)
			RW99 EFGHI 2
			Stop
	}
}

//Dunno what it was, but I think this is a fair value
Actor RTCW_MauserTracer : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",200),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",200),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_MauserTracerDM : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",140),8,!XF_HURTSOURCE,0,8) 
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",140),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_MauserMagazine : Ammo
{
	Inventory.MaxAmount 10
	Ammo.BackpackMaxAmount 10
}

Actor RTCW_Snooper : RTCW_Weapon
{
	Weapon.SlotNumber 3
	Weapon.SlotPriority 1
	Weapon.SelectionOrder 1500
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 0
	Weapon.AmmoType1 "RTCW_SnooperMagazine"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoGive2 5
	Obituary "%o was killed (snooper) by %k."
	Inventory.Pickupmessage "Snooper"
	Inventory.PickupSound "RTCW/WeaponPickup"
	Tag "Snooper"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Spawn:
			RW98 A -1
			Stop
		Deselect:
			RW00 G 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 G 0 A_TakeInventory("RTCW_ZoomFactor",90)
			RW00 G 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 G 0 A_ZoomFactor(1.0)
			RW00 GHIJKLM 2
			Goto Super::Deselect
		Ready:
			RW00 A 0 A_TakeInventory("RTCW_WeaponToken",256)
			RW00 A 0 A_GiveInventory("RTCW_WeaponToken",11)
			RW00 MNOPQ 2
		ReadyLoop:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,"ReadyLoopScoped")
			RW00 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			RW00 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		ReadyLoopScoped:
			TNT1 A 0 A_JumpIfInventory("RTCW_Scoped",1,1)
			Goto ReadyLoopScoped+3
			TNT1 A 0 A_JumpIf(CallACS("RTCW_Decorate",3)==0,2)
			TNT1 A 1 A_ZoomFactor(90.0/CallACS("RTCW_Decorate",2,2)*1.0)
			TNT1 A 0 A_JumpIfInventory("RTCW_Kicking",1,"Kicking")
			TNT1 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		NoAmmo:
			RW00 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			RW00 A 17
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect
		NoAmmoScoped:
			TNT1 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_5)
			TNT1 A 17
			TNT1 A 0 A_SelectWeapon("RTCW_Knife")
			Goto Deselect
		Fire:
			RW00 B 0 A_JumpIfInventory("RTCW_SnooperMagazine",1,1)
			Goto Reload
			//RW00 B 0 A_GiveInventory("RTCW_FiringToken",1)
			RW00 B 0 A_JumpIfInventory("RTCW_Scoped",1,"FireScoped")
			RW00 B 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),0,8,4,0,-4)
			RW00 B 0 A_TakeInventory("RTCW_SnooperMagazine",1)
			RW00 B 0 A_GunFlash("FireFlash")
			RW00 B 0 ACS_NamedExecuteWithResult("RTCW_Recoil",2)
			RW00 B 0 A_FireCustomMissile("RTCW_SnooperTracer",FRandom(-1.5,1.5),0,0,4, 0, FRandom(-1.5,1.5))
			RW00 B 0 A_PlaySound("RTCW/Snooper/Fire",CHAN_Weapon)
		FireFinish:
			RW00 BC 2
			RW00 A 33
			Goto ReadyLoop
		FireFlash:
			RW03 B 2 Bright
			Stop
		FireScoped:
			RW00 B 0 A_FireCustomMissile("RTCW_Casing",-random(85,105),0,8,4,0,-4)
			RW00 B 0 A_GiveInventory("RTCW_AimSpread",16711680)
			RW00 B 0 A_TakeInventory("RTCW_SnooperMagazine",1)
			RW00 B 0 A_FireCustomMissile("RTCW_SnooperTracer",0,0,0,8)
			RW00 B 0 A_SetAngle(angle+frandom(-1.0,1.0))
			RW00 B 0 ACS_NamedExecuteWithResult("RTCW_Recoil",4)
			RW00 B 0 A_PlaySound("RTCW/Snooper/Fire",CHAN_Weapon)
			Goto FireFinishScoped
		FireFinishScoped:
			TNT1 BC 2
			TNT1 A 33
			Goto ReadyLoop
		AltFire:
			RW00 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,"LowerScope")
			RW00 A 0 A_GiveInventory("RTCW_ZoomFactor",40)
			RW00 A 0 A_GiveInventory("RTCW_Scoped",1)
			RW00 A 0 ACS_NamedExecuteWithResult("RTCW_Scope",2)
			TNT1 A 10 A_ZoomFactor(90.0/40.0)
			Goto ReadyLoop
		LowerScope:
			RW00 A 0 A_TakeInventory("RTCW_ZoomFactor",90)
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 A 0 A_ZoomFactor(1.0)
			Goto Ready
		Reload:
			TNT1 A 0 A_JumpIfInventory("RTCW_Scoped",1,"ScopedNoAmmo")
			TNT1 A 0 A_JumpIfInventory("RTCW_SnooperMagazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("Shell",1,"ReloadSuccess")
			TNT1 A 0 A_JumpIfInventory("RTCW_SnooperMagazine",1,1)
			goto NoAmmo
			TNT1 A 0
			goto ReadyLoop
		ScopedNoAmmo:
			TNT1 A 0 A_JumpIfInventory("RTCW_SnooperMagazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("Shell",1,1)
			Goto NoAmmoScoped
			TNT1 A 0 A_JumpIfInventory("RTCW_SnooperMagazine",1,"ReloadLowerScope")
			TNT1 A 0 A_PlaySound("RTCW/NoAmmo",CHAN_6)
			TNT1 A 17
		ReloadLowerScope:
			RW00 A 0 A_PlaySound("RTCW/Change",CHAN_7)
			Goto ReloadSuccess+1
		ReloadSuccess:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,"ReloadLowerScope")
			RW00 A 0 A_TakeInventory("RTCW_ZoomFactor",90)
			RW00 A 0 A_TakeInventory("RTCW_Scoped",1)
			RW00 A 0 A_ZoomFactor(1.0)
			RW00 A 0 A_PlaySound("RTCW/Snooper/Reload",CHAN_6)
			RW00 "QRSTUVWXYZ[\]" 2
			RW01 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 2
			RW02 ABCDEFG 1
			goto ReloadAmmo
		ReloadAmmo:
			TNT1 A 0 A_JumpIfInventory("RTCW_SnooperMagazine",0,"ReadyLoop")
			TNT1 A 0 A_JumpIfInventory("Shell",1,1)
			goto ReadyLoop
			TNT1 A 0 A_GiveInventory("RTCW_SnooperMagazine",1)
			TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
			Loop
		Kicking:
			TNT1 A 0 A_Gunflash("KickingFoot")
		KickingDelay:
			RW00 A 0 A_JumpIfInventory("RTCW_Scoped",1,"KickingScopedDelay")
			RW00 A 18
			RW00 A 0 A_TakeInventory("RTCW_Kicking",1)
			Goto ReadyLoop
		KickingScopedDelay:
			RW00 M 18
			TNT1 A 0 A_TakeInventory("RTCW_Kicking",1)
			Goto ReadyLoop
		KickingFoot:
			RW99 ABCD 2
			RW99 E 0 A_FireCustomMissile("RTCW_KickProjectile",0,0,0,-8)
			RW99 EFGHI 2
			Stop
	}
}

actor RTCW_SnooperVision : PowerLightAmp
{
	Powerup.Color InverseMap
}

Actor RTCW_SnooperMagazine : Ammo
{
	Inventory.MaxAmount 5
	Ammo.BackpackMaxAmount 5
}

Actor RTCW_SnooperTracer : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",100),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",100),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}

Actor RTCW_SnooperTracerDM : RTCW_Tracer
{ 
	States
	{
		Crash:
		Death:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",70),8,!XF_HURTSOURCE,0,8)
			Goto Super::Death
		XDeath:
			TNT1 A 0 A_Explode(CallACS("RTCW_BulletDamage",70),8,!XF_HURTSOURCE,0,8)
			Goto Super::XDeath
	}
}