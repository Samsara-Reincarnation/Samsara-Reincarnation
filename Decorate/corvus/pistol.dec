actor "Elven Wand" : Weapon
{
    +BLOODSPLATTER
    +PUFFONactorS
    +WEAPON.WIMPY_WEAPON
    +INVENTORY.UNDROPPABLE
    Inventory.PickUpSound "heretic/weaponget"
    Weapon.SelectionOrder 2000
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Inventory.PickupMessage "You got the Elven Wand!"
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    Decal RailScorchLower
    Obituary "$SAMSARA_CORVUS_OB_PISTOL"
    Weapon.YAdjust 5
    Inventory.RestrictedTo "CorvusPlayer"
    Tag "Elven Wand"
    States
    {
      Ready:
        GWND A 1 A_WeaponReady
        Loop

      Deselect:
        GWND A 1 A_Lower
        Loop

      Select:
        GWND A 1 A_Raise
        Loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckForAmmo")
      Fire2:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FireTomed")
        GWND B 3 BRIGHT
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
		TNT1 A 0 A_JumpIfInventory("DMModeOn", 1, 3)
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireBullets(5.625, 0, 1,random(1,8)+6, "GoldWandPuff3", FBF_NORANDOM) // Previously random(1,8)+6
        goto FireAnim
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireBullets(5.625, 0, 1,random(1,6)+12, "GoldWandPuff3", FBF_NORANDOM) // Previously random(1,8)+6
        goto FireAnim		

      Hold:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckForAmmo")
      Hold2:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FireTomed")
        GWND B 3 BRIGHT
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopHold")
		TNT1 A 0 A_JumpIfInventory("DMModeOn", 1, 3)
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireBullets(5.625, 0, -1,random(1,8)+6, "GoldWandPuff3", FBF_NORANDOM) // Previously random(1,8)+6
        goto FireAnim
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireBullets(5.625, 0, -1,random(1,6)+12, "GoldWandPuff3", FBF_NORANDOM) // Previously random(1,8)+6
        goto FireAnim		

      FireTomed:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFireTomed")
        GWND B 3 BRIGHT
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireCustomMissile("GoldWandFX4",5.625,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX4",2.8125,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX4",0,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX4",-2.8125,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX4",-5.625,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX3",5.6,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX3",-5.6,0,0,1)
        goto FireAnimTomed

      CoopFire:
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireBullets(5.625, 0, -1,random(1,8)+6, "GoldWandPuff3Coop", FBF_NORANDOM) //A_FireCustomMissile("ElvenBullet",0,0,0,1)
        goto FireAnim

      CoopHold:
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireBullets(5.625, 0, -1,random(1,8)+6, "GoldWandPuff3Coop", FBF_NORANDOM) //A_FireCustomMissile("ElvenBullet",random(-560,560)/100.00,0,0,1)
        goto FireAnim

      CoopFireTomed:
        GWND B 3 BRIGHT
        GWND C 0 A_PlayWeaponSound("heretic/wand")
        GWND C 0 A_FireCustomMissile("GoldWandFX42",5.625,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX42",2.8125,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX42",0,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX42",-2.8125,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX42",-5.625,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX32",5.6,0,0,1)
        GWND C 0 A_FireCustomMissile("GoldWandFX32",-5.6,0,0,1)
        goto FireAnimTomed
        
      CheckForAmmo:
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire2")
        TNT1 A 0 A_SelectWeapon(" Staff ")
        goto Deselect
        
      CheckForAmmoHold:
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Hold2")
        TNT1 A 0 A_SelectWeapon(" Staff ")
        goto Deselect
        
      FireAnim:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"TakePistolAmmo")
      FireAnim2:
		GWND C 0 A_JumpIfInventory("PowerLesserTome", 1, "FireAnimLesserTomed")
		GWND C 0 A_JumpIfInventory("CoopModeOn",1,"FireAnim2Slower")
		GWND C 0 A_JumpIfInventory("SPModeOn",1,"FireAnim2Slower")
	  FireAnim2DM:
        GWND C 4 BRIGHT
        GWND D 2
        GWND D 0 A_ReFire
        GWND D 0 A_ClearRefire
        Goto Ready
	  FireAnim2Slower:
        GWND C 5 BRIGHT
        GWND D 3
        GWND D 0 A_ReFire
        GWND D 0 A_ClearRefire
        Goto Ready
		
	  FireAnimLesserTomed:
        GWND C 3 BRIGHT
        GWND D 1
        GWND D 0 A_ReFire
        GWND D 0 A_ClearRefire
        Goto Ready		
        
      FireAnimTomed:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"TakePistolAmmoTomed")
      FireAnimTomed2:
		GWND C 0 A_JumpIfInventory("PowerLesserTome", 1, "FireAnimLesserTomed")	  
        GWND C 4 BRIGHT
        GWND D 3
        GWND D 0 A_ClearRefire
        GWND D 0 A_ReFire
        Goto Ready
        
      TakePistolAmmo:
        TNT1 A 0 A_TakeInventory("Clip",1)
        goto FireAnim2
        
      TakePistolAmmoTomed:
        TNT1 A 0 A_TakeInventory("Clip",3)
        goto FireAnimTomed2
    }
}

actor ElvenBullet : FastProjectile
{
    Damage (random(1,8)+6)
    Radius 1
    Height 1
    Speed 320
    Projectile
    Renderstyle "Normal"
    Species "Player"
    DamageType "Pistol"
    Decal RailScorchLower
    +BLOODSPLATTER
    +THRUSPECIES
    +NOTIMEFREEZE
    Alpha 1.0
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1 A_SpawnItem("GoldWandPuff3")
        stop
    }
}

actor GoldWandPuff3 : GoldWandPuff1
{
    DamageType "Pistol"
    +CLIENTSIDEONLY
}

Actor GoldWandPuff3Coop : GoldWandPuff3
{
	Species "Player"
	+THRUSPECIES
	+MTHRUSPECIES
}	

actor GoldWandPuff4 : GoldWandPuff2
{
    DamageType "Pistol"
    +CLIENTSIDEONLY
}

Actor GoldWandPuff4Coop : GoldWandPuff4
{
	Species "Player"
	+THRUSPECIES
	+MTHRUSPECIES
}	

actor GoldWandFX3 : GoldWandFX2
{
    Decal HImpScorch
    DamageType "Pistol"
    Obituary "$SAMSARA_CORVUS_OB_PISTOL"
}

actor GoldWandFX32 : GoldWandFX3 { +THRUSPECIES Species "Player" }

actor GoldWandFX4 : FastProjectile
{
    Damage (Random (2, 12))
    Radius 2
    Height 2
    Speed 320
    Projectile
    DamageType "Pistol"
    +BLOODSPLATTER
    +NOTIMEFREEZE
    Obituary "$SAMSARA_CORVUS_OB_PISTOL"
    Decal RailScorchLower
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
      Crash:
        FX01 EFGH 3 BRIGHT
        Stop

      XDeath:
        TNT1 A 1
        stop
    }
}

actor GoldWandFX42 : GoldWandFX4 { +THRUSPECIES Species "Player" }