actor PermaRemoteBomb : CustomInventory
{
    tag "Detonator of the Primordials"
    +FLOATBOB
    Inventory.Amount 1
    Inventory.MaxAmount 1
    +INVBAR
    +INVENTORY.UNDROPPABLE
    Inventory.Icon "ARTIRBMB"
    Inventory.PickupSound "heretic/itemget"
    Inventory.UseSound "heretic/artiuse"
    Inventory.PickupMessage "You picked up the Detonator of the Primordials!"
	Inventory.RestrictedTo "CorvusPlayer"
    States
    {
      Spawn:
        FBMB L 1 Bright
        Loop

      Use:
        TNT1 A 1 A_TakeInventory("CorvusRemoteDetonate",1)
        TNT1 A 0 A_JumpIfInventory("CorvusRemoteActive",1,"Use2")
        TNT1 A 0 A_PlaySound("heretic/artiuse")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"SPUse")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"SPUse")
	DMUse:
		TNT1 A 0 A_FireCustomMissile("RemoteOfTheAncients",0,0,0,-24,0,0)
		goto PostUse
	SPUse:
		TNT1 A 0 A_FireCustomMissile("RemoteOfTheAncientsSP",0,0,0,-24,0,0)
		goto PostUse
	  PostUse:
        TNT1 A 1 A_GiveInventory("CorvusRemoteActive",1)
        fail
      Use2:
        TNT1 A 1 A_GiveInventory("CorvusRemoteDetonate",1)
        TNT1 A 1 A_PlaySound("heretic/remoteancient")
        TNT1 A 1 A_TakeInventory("CorvusRemoteActive",1)
        fail
    }
}

actor TimeBombPause: Boolean {}
actor TimeBombCount: Counter { Inventory.MaxAmount 3 }

actor PermaTimeBomb : CustomInventory
{
    tag "Time Bomb of the Ancients"
    +FLOATBOB
    Inventory.Amount 1
    Inventory.MaxAmount 1
    +INVBAR
    +INVENTORY.UNDROPPABLE
    Inventory.Icon "ARTIFBMB"
    Inventory.PickupSound "heretic/itemget"
    Inventory.UseSound "heretic/artiuse"
    Inventory.PickupMessage "You picked up the Time Bomb of the Ancients!"
	Inventory.RestrictedTo "CorvusPlayer"
    States
    {
      Spawn:
        FBMB E 1 Bright
        Loop

      Use:
        TNT1 A 0 A_JumpIfInventory("TimeBombCount", 0, "UseFail")
        TNT1 A 0 A_JumpIfInventory("TimeBombPause", 1, "UseFail")
        TNT1 A 0 A_PlaySound("heretic/artiuse")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"SPUse")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"SPUse")
	DMUse:
		TNT1 A 0 A_FireCustomMissile("TimeBombOfTheAncients",0,0,0,-24,0,0)
		goto PostUse
	SPUse:
		TNT1 A 0 A_FireCustomMissile("TimeBombOfTheAncientsSP",0,0,0,-24,0,0)
		goto PostUse
	  PostUse:
        TNT1 A 1 ACS_NamedExecuteWithResult("SamsaraDecorate", 23, 15)
        fail
      UseFail:
        TNT1 A 1
        fail
    }
}

actor PermaProxyBomb : CustomInventory // Turns out it didn't give frags to the user. Damn.
{
    tag "Mine of the Watchers"
    +FLOATBOB
    Inventory.Amount 1
    Inventory.MaxAmount 1
    +INVBAR
    +INVENTORY.UNDROPPABLE
    Inventory.Icon "ARTIPBMB"
    Inventory.PickupSound "heretic/itemget"
    Inventory.UseSound "heretic/artiuse"
    Inventory.PickupMessage "You picked up the Mine of the Watchers!"
	Inventory.RestrictedTo "CorvusPlayer"
    States
    {
      Spawn:
        FBMB MNOPQ 8 Bright
        Loop

      Use:
        TNT1 A 1 A_JumpIfInventory("CorvusCanProxy",1,1)
		Goto UseFail
		TNT1 A 0 A_PlaySound("heretic/artiuse")
		TNT1 A 0 A_JumpIfInventory("SPModeOn",1, "UseCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1, "UseCoop")
		TNT1 A 1 A_SpawnItemEx("ProxyOfTheAncients2",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_TRANSFERPOINTERS|SXF_TRANSFERAMBUSHFLAG) //A_FireCustomMissile("C7ProxyMine",0,0,0,-24,CMF_TRACKOWNER,0) 
         //ACS_NamedExecuteAlways("SamsaraClientDecorate",0,27)
        Goto PostUse

      UseCoop:
        TNT1 A 1 A_SpawnItemEx("ProxyOfTheAncients",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_TRANSFERPOINTERS|SXF_TRANSFERAMBUSHFLAG)
        Goto PostUse
		
	  PostUse:
        TNT1 A 1 ACS_NamedExecuteWithResult("SamsaraDecorate", 44, 44)
        fail		
		
	 UseFail:
		TNT1 A 1 A_Print("You cannot use the Mine of the Watchers yet.", 2, "CMPSHERE")
		fail
    }
}

actor TimeBombOfTheAncients
{
    DamageType "Explosive"
    Speed 24
    Radius 10
    Height 8
    RenderStyle Translucent
    Alpha 0.4
    // Health 5
    Obituary "$SAMSARA_CORVUS_OB_BOMB"
    //DeathSound "weapons/phoenixhit"
    PROJECTILE
    +FLOATBOB
    +NOBLOOD
    -SOLID
    -SHOOTABLE
    -NOBLOCKMAP
    +DOOMBOUNCE
    -NOLIFTDROP
    +NOGRAVITY
    -NOTELEPORT
    +MOVEWITHSECTOR
    -EXPLODEONWATER
    +CANBOUNCEWATER
    +FORCERADIUSDMG
    States
    {
      Spawn:	
        TNT1 A 1 A_Gravity
        goto Death
      Death:
        TNT1 A 0 A_GiveToTarget("TimeBombCount")
        TNT1 A 0 A_Stop
        FBMB ABCDE 14
        TNT1 A 0 A_SetTranslucent(0.9,1)
        TNT1 A 0 A_ChangeFlag("FLOATBOB",0)
        TNT1 A 0 A_NoGravity
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("weapons/phoenixhit")
        TNT1 A 0 A_SetTranslucent(0.9,1)
        TNT1 A 0 A_Explode(128,128)
        TNT1 A 0 A_TakeFromTarget("TimeBombCount", 1)
        FBMB FGHIJK 4 BRIGHT
        Stop
    }
}

actor TimeBombOfTheAncientsSP : TimeBombOfTheAncients
{
        Alpha 0.75
}

actor RemoteOfTheAncients
{
    DamageType "Explosive"
    Speed 24
    Radius 10
    Height 8
    RenderStyle Translucent
    Alpha 0.4
    // Health 5
    Obituary "$SAMSARA_CORVUS_OB_BOMB"
    //DeathSound "weapons/phoenixhit"
    PROJECTILE
    +FLOATBOB
    +NOBLOOD
    -SOLID
    -SHOOTABLE
    -NOBLOCKMAP
    +DOOMBOUNCE
    -NOLIFTDROP
    +NOGRAVITY
    -NOTELEPORT
    +MOVEWITHSECTOR
    -EXPLODEONWATER
    +CANBOUNCEWATER
    +FORCERADIUSDMG
    States
    {
      Spawn:
        TNT1 A 1 A_Gravity
        goto Death

      Death:
        TNT1 A 0 A_Stop
        FBRE BBBAAACCCAAA 1 A_JumpIfInTargetInventory("CorvusRemoteDetonate",1,"Explode")
        Loop

      Explode:
        TNT1 A 0 A_SetTranslucent(0.9,1)
        TNT1 A 0 A_ChangeFlag("FLOATBOB",0)
        TNT1 A 0 A_NoGravity
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("weapons/phoenixhit")
        TNT1 A 0 A_SetTranslucent(0.9,1)
        TNT1 A 0 A_Explode(128,128)
        FBMB FGHIJK 4 BRIGHT A_SetTranslucent(0.9,1)
        Stop
    }
}

actor RemoteOfTheAncientsSP : RemoteOfTheAncients
{
        Alpha 0.75
}

actor ProxyOfTheAncients
{
    DamageType "Explosive"
    Speed 0
    Radius 10
    Height 8
    RenderStyle Translucent
    Alpha 0.4
    // Health 5
    Obituary "$SAMSARA_CORVUS_OB_BOMB"
	MeleeRange 64
	Monster
    //DeathSound "weapons/phoenixhit"
    //PROJECTILE
	-COUNTKILL
	+NOBLOCKMAP
	+NOBLOCKMONST
	+DROPOFF
	+MOVEWITHSECTOR
	-SOLID	
	+VULNERABLE
	+THRUACTORS
	-SHOOTABLE	
	+FRIENDLY
	+NOTARGET
	+NOBLOOD
	+FLOATBOB
	+LOOKALLAROUND
	+FORCERADIUSDMG
    //-EXPLODEONWATER
    //+CANBOUNCEWATER
    States
    {
      Spawn:
        TNT1 A 1 A_Gravity
        goto Spawn2

      Spawn2:
        //TNT1 A 0 A_Stop
		//TNT1 A 0 A_ClearTarget //A_RearrangePointers(AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_DEFAULT)
        FBMB QPN 14 
        Goto SpawnLoop
		
	 SpawnLoop:
		//TNT1 A 0 A_GiveToTarget("Clip", 1) //sets target as player. don't know why, all other Proxy mine items work just fine
		FBMB RSTUV 14 A_LookEx(LOF_NOSOUNDCHECK, 0, 64)
		FBMB TSRUV 14 A_LookEx(LOF_NOSOUNDCHECK, 0, 64)
		Loop

      See:
        TNT1 A 0 A_SetTranslucent(0.9,0)
        FBMB N 4 A_PlaySound("heretic/proxancient", CHAN_AUTO)
		FBMB U 4
		FBMB V 4
		FBMB V 4
        TNT1 A 0 A_Die

      Death:
        TNT1 A 0 A_SetTranslucent(0.9,1)
        TNT1 A 0 A_Fall
        TNT1 A 0 A_ChangeFlag("FLOATBOB",0)
        TNT1 A 0 A_NoGravity
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("weapons/phoenixhit")
        TNT1 A 0 A_SetTranslucent(0.9,1)
		TNT1 A 0 A_CustomMissile("ProxyOfTheAncientsExplosion",0,0,0,CMF_TRACKOWNER)
        FBMB FGHIJK 4 BRIGHT
        Stop
    }
}

Actor ProxyOfTheAncients2 : ProxyOfTheAncients
{
	-FRIENDLY
}	

actor ProxyOfTheAncientsExplosion
{
    Radius 1
    Height 1
    Speed 0
    Damage 0
    Projectile
    +FORCERADIUSDMG
    Decal Scorch
    Obituary "%o didn't see %k's proximity mine."
    States
    {
      Spawn:
        TNT1 A 1 A_Die
        Goto Death

      Death:
        TNT1 A 2 Bright A_Explode(128,128,0)
        Stop
    }
}

actor CorvusRemoteActive : inventory { -INVBAR +INVENTORY.UNDROPPABLE inventory.maxamount 1 }
actor CorvusRemoteDetonate : inventory { -INVBAR +INVENTORY.UNDROPPABLE inventory.maxamount 1 }
actor CorvusCanProxy : inventory { -INVBAR +INVENTORY.UNDROPPABLE inventory.maxamount 1 }
