actor LesserTomeCooldown : Inventory { -INVBAR +INVENTORY.UNDROPPABLE inventory.maxamount 120 }

actor PortLesserTome : CustomInventory
{
    tag "Sidhe Spellbook"
    inventory.maxamount 1
    inventory.usesound "heretic/itemuse"
    +INVBAR
    +inventory.undroppable
    //+inventory.hubpower
    Inventory.Icon "ARTITOM2"
	Inventory.RestrictedTo "CorvusPlayer"
    States
    {
	  Pickup:
		TNT1 A 0 A_JumpIf(CallACS("LesserTomeCooldownReduction")==1,1)
		TNT1 A 0 A_RailWait
		stop
	  
	  Use:
        TNT1 A 0 A_JumpIfInventory("LesserTomeCooldown",1,"UseFail")
		TNT1 A 0 A_JumpIfInventory("PowerLesserTomePerma",1,"UseFail2")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"UseFail2")
        TNT1 A 1
        TNT1 A 0 A_GiveInventory("PowerLesserTome",1)
        TNT1 A 0 A_GiveInventory("PowerLesserDamage",1)
        TNT1 A 0 A_GiveInventory("PowerLesserDefense",1)
        TNT1 A 0 ACS_NamedExecuteAlways("LesserTomeCooldown")
        TNT1 A 0 A_GiveInventory("LesserTomeCooldown",120)
        fail

      UseFail:
        TNT1 A 1
        TNT1 A 0 A_Print("$LESSERTOMECOOLDOWN")
        fail
		
      UseFail2:
        TNT1 A 1
        TNT1 A 0 A_Print("$LESSERTOMECOOLDOWN2")
        fail
    }
}

actor PowerLesserTome : Powerup //PowerWeaponLevel1.5
{
    Powerup.Duration -30
    Powerup.Color "0c 10 b8", 0.1
}

actor PowerLesserTomePerma : inventory { -INVBAR +INVENTORY.UNDROPPABLE inventory.maxamount 1 }

Actor PowerLesserDamage : PowerDamage
{
    Powerup.Duration -30
    DamageFactor "Pistol", 1.2
    DamageFactor "Mace", 1.2
    DamageFactor "Normal", 1.2
    DamageFactor "DragonClaw", 1.2
    DamageFactor "Fire", 1.2
    DamageFactor "CorvusHellstaff", 1.2
    DamageFactor "CorvusHellstaffTomed", 1.2
    DamageFactor "CorvusCrossbow", 1.2
	DamageFactor "CorvusGrimBallista", 1.2
    DamageFactor "Spectral", 1.2
    DamageFactor "FireExplosive", 1.2
}

actor PowerLesserDefense : PowerProtection
{
    Powerup.Duration -30
	DamageFactor 0.75
    DamageFactor "Kamikaze", 1.0
    DamageFactor "ZorchKamikaze", 1.0
    DamageFactor "Suicide", 1.0
    DamageFactor "Telefrag", 1.0
}	

Actor GrimAttackCount : Inventory
{
	Inventory.MaxAmount 4
	+UNDROPPABLE
	-INVBAR
}	

ACTOR "Grim Ballista" : Weapon
{
    Weapon.Selectionorder 100
    Weapon.Kickback 100
	Inventory.PickUpSound "heretic/weaponget"
    Weapon.SlotNumber 8
    Weapon.YAdjust 16
	Weapon.AmmoType "Shell"
	Weapon.AmmoUse 2
	Weapon.AmmoGive 18
    +INVENTORY.UNDROPPABLE
    +WEAPON.NOAUTOAIM
    Inventory.Pickupmessage "You got the Grim Ballista!"
    Decal CrossbowScorch
    DamageType "CorvusGrimBallista"
    Obituary "$SAMSARA_CORVUS_OB_INSTAGIB"
	Inventory.RestrictedTo "CorvusPlayer"
    Tag "Grim Ballista"
    States
    {
	Spawn:
		WGRM A -1
		Stop
    Ready:
        GBLS AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_WeaponReady
        TNT1 A 0 A_PlaySoundEx("heretic/grimhbeat","soundslot6",0,0)
        GBLS KKLLMMNNNNMMLLKK 1 A_WeaponReady
        Loop
    Deselect:
        GBLS A 1 A_Lower
        Loop
    Select:
        GBLS A 1 A_Raise
        Loop
    Fire:
		TNT1 A 0 A_JumpIfInventory("SPModeOn", 1, "FireCoop")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "FireCoop")
		TNT1 A 0 A_JumpIfInventory("PowerLesserTome", 1, "FireTomedLesser")
		TNT1 A 0 A_JumpIfInventory("PowerLesserTomePerma", 1, "FireTomedLesser")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FireTomed")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"FireTomed")
		GBLS A 0 A_JumpIfInventory("GrimAttackCount", 0, 11)
        GBLS A 3 A_GiveInventory("GrimAttackCount", 1)
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_PlayWeaponSound("heretic/grimbshot")
        TNT1 A 0 A_Recoil(5)
        TNT1 AAAA 0 A_FireCustomMissile("GrimSpittle",random(-100,100)/10.0,0,0,-2,0,random(-70,70)/10.0)
        GBLS B 12 A_FireCustomMissile("GrimBoltB", 0, 1, 0) //A_FireBullets(0,0,1,Random(1,8)*15,"SoulPuff", FBF_NORANDOM)//A_RailAttack(10000,0,0,none,none,1,0,"SoulPuff")
        GBLS C 8 A_Refire
        GBLS D 6 
        TNT1 A 0 A_PlaySoundEx("heretic/grimbload","soundslot6",0,0)
        GBLS EFG 2
        GBLS HIJ 3
        GBLS A 6 A_TakeInventory("GrimAttackCount", 4)
        Goto Ready
	FireCoop:
		TNT1 A 0 A_JumpIfInventory("PowerLesserTome", 1, "FireTomedLesserCoop")
		TNT1 A 0 A_JumpIfInventory("PowerLesserTomePerma", 1, "FireTomedLesserCoop")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"FireTomedCoop")
		TNT1 A 0 A_JumpIfInventory("PowerHereticTomePerma",1,"FireTomedCoop")
		GBLS A 0 A_JumpIfInventory("GrimAttackCount", 0, 11)
        GBLS A 3 A_GiveInventory("GrimAttackCount", 1)
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_PlayWeaponSound("heretic/grimbshot")
        TNT1 A 0 A_Recoil(5)
        TNT1 AAAA 0 A_FireCustomMissile("GrimSpittleCoop",random(-100,100)/10.0,0,0,-2,0,random(-70,70)/10.0)
        GBLS B 12 A_FireCustomMissile("GrimBolt", 0, 1, 0) //A_FireBullets(0,0,1,Random(1,8)*15,"SoulPuff", FBF_NORANDOM)//A_RailAttack(10000,0,0,none,none,1,0,"SoulPuff")
        GBLS C 8 A_Refire
        GBLS D 6 
        TNT1 A 0 A_PlaySoundEx("heretic/grimbload","soundslot6",0,0)
        GBLS EFG 2
        GBLS HIJ 3
        GBLS A 6 A_TakeInventory("GrimAttackCount", 4)
        Goto Ready	
	FireTomedLesser:
		GBLS A 0 A_JumpIfInventory("GrimAttackCount", 0, 15)	
        GBLS A 3 A_GiveInventory("GrimAttackCount", 1)
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_PlayWeaponSound("heretic/grimbshot")
        TNT1 A 0 A_Recoil(5)
        TNT1 A 0 A_FireCustomMissile("GrimGhostFX1", 3.5, 0)
		TNT1 A 0 A_FireCustomMissile("GrimGhostFX1", -3.5, 0)
		TNT1 A 0 A_FireCustomMissile("GrimGhostFX1", 7, 0)
		TNT1 A 0 A_FireCustomMissile("GrimGhostFX1", -7, 0)
		TNT1 A 0 A_FireCustomMissile("GrimGhostFX1", 3.5, 0, 4)
		TNT1 A 0 A_FireCustomMissile("GrimGhostFX1", -3.5, 0, -4)
		TNT1 A 0 A_FireCustomMissile("GrimGhostFX1", 7, 0, 4)
		TNT1 A 0 A_FireCustomMissile("GrimGhostFX1", -7, 0, -4)
        GBLS B 12 A_FireCustomMissile("GrimBoltB", 0, 1, 0)
        GBLS C 8 A_Refire
        GBLS D 6
        TNT1 A 0 A_PlaySoundEx("heretic/grimbload","soundslot6",0,0)
        GBLS EFG 2
        GBLS HIJ 3
        GBLS A 6 A_TakeInventory("GrimAttackCount", 4)
        Goto Ready	
	FireTomedLesserCoop:
		GBLS A 0 A_JumpIfInventory("GrimAttackCount", 0, 15)	
        GBLS A 3 A_GiveInventory("GrimAttackCount", 1)
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_PlayWeaponSound("heretic/grimbshot")
        TNT1 A 0 A_Recoil(5)
        TNT1 A 0 A_FireCustomMissile("GrimGhostFX1B", 3.5, 0)
		TNT1 A 0 A_FireCustomMissile("GrimGhostFX1B", -3.5, 0)
		TNT1 A 0 A_FireCustomMissile("GrimGhostFX1B", 7, 0)
		TNT1 A 0 A_FireCustomMissile("GrimGhostFX1B", -7, 0)
		TNT1 A 0 A_FireCustomMissile("GrimGhostFX1B", 3.5, 0, 4)
		TNT1 A 0 A_FireCustomMissile("GrimGhostFX1B", -3.5, 0, -4)
		TNT1 A 0 A_FireCustomMissile("GrimGhostFX1B", 7, 0, 4)
		TNT1 A 0 A_FireCustomMissile("GrimGhostFX1B", -7, 0, -4)
        GBLS B 12 A_FireCustomMissile("GrimBolt", 0, 1, 0)
        GBLS C 8 A_Refire
        GBLS D 6
        TNT1 A 0 A_PlaySoundEx("heretic/grimbload","soundslot6",0,0)
        GBLS EFG 2
        GBLS HIJ 3
        GBLS A 6 A_TakeInventory("GrimAttackCount", 4)
        Goto Ready		
	FireTomed:
		GBLS A 0 A_JumpIfInventory("GrimAttackCount", 0, 21)	
        GBLS A 3 A_GiveInventory("GrimAttackCount", 1)
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_PlayWeaponSound("heretic/grimbshot")
        TNT1 A 0 A_Recoil(5)
        TNT1 AAAAAAAAAAAAAA 0 A_FireCustomMissile("GrimSpittle2",random(-100,100)/10.0,0,0,-2,0,random(-70,70)/10.0)
        GBLS B 12 A_FireBullets(0,0,1,25,"SoulPuff", FBF_NORANDOM| FBF_USEAMMO)//A_RailAttack(10000,0,0,none,none,1,0,"SoulPuff")
        GBLS C 8 A_Refire
        GBLS D 6
        TNT1 A 0 A_PlaySoundEx("heretic/grimbload","soundslot6",0,0)
        GBLS EFG 2
        GBLS HIJ 3
        GBLS A 6 A_TakeInventory("GrimAttackCount", 4)
        Goto Ready	
	FireTomedCoop:
		GBLS A 0 A_JumpIfInventory("GrimAttackCount", 0, 21)	
        GBLS A 3 A_GiveInventory("GrimAttackCount", 1)
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_PlayWeaponSound("heretic/grimbshot")
        TNT1 A 0 A_Recoil(5)
        TNT1 AAAAAAAAAAAAAA 0 A_FireCustomMissile("GrimSpittle2Coop",random(-100,100)/10.0,0,0,-2,0,random(-70,70)/10.0)
        GBLS B 12 A_FireBullets(0,0,1,Random(1,8)*20,"SoulPuffCoop", FBF_NORANDOM| FBF_USEAMMO)//A_RailAttack(10000,0,0,none,none,1,0,"SoulPuff")
        GBLS C 8 A_Refire
        GBLS D 6
        TNT1 A 0 A_PlaySoundEx("heretic/grimbload","soundslot6",0,0)
        GBLS EFG 2
        GBLS HIJ 3
        GBLS A 6 A_TakeInventory("GrimAttackCount", 4)
        Goto Ready		
    Flash:
        TNT1 A 5 bright A_Light1
        TNT1 A 5 bright A_Light2
        TNT1 A 0 bright A_Light0
        Goto LightDone
    }
}

ACTOR GrimBolt 
{
	Radius 8
	Height 8
	Speed 45
	Damage 15
	Scale 1.25
	Projectile
	+THRUSPECIES
	Species "Player"
	Obituary "$SAMSARA_CORVUS_OB_INSTAGIB"
	DamageType "CorvusGrimBallista"
	States
	{
	Spawn:
		 FX03 K 3 A_SpawnItemEx("GrimSpittleTrail3",0,0,2,0,0,0,0,160)
		 Loop
	XDeath:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("DamnedSoul",0,0,-16,0,0,25/10.0,random(0,360),0,0)		
    Death:
    Crash:
        TNT1 A 0
        TNT1 A 0 A_PlaySoundEx("weapons/bowhit","soundslot7",0,0)
		TNT1 A 0 A_Explode(40, 40)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("GrimSpittleTrail2",0,0,0,random(-400,400)/100.00,random(-400,400)/100.00,random(-400,400)/100.00,random(0,360),180)
        GPF1 ABCDEFGHIJKLMNOPQR 1 bright A_SpawnItemEx("GrimSpittleTrail2",0,0,0,random(-400,400)/100.00,random(-400,400)/100.00,random(-400,400)/100.00,random(0,360),180)
        GPF1 STUVWXYZ 2 bright
        GPF2 ABC 2 bright
        Stop	
	}
}	

Actor GrimBoltB : GrimBolt
{
	-THRUSPECIES
	Damage (Random(45, 90))
	Speed 52
	Species "Corvus"
}	

ACTOR GrimSpittle
{
    Radius 4
    Height 4
    Speed 12
    Damage (4)
    Projectile
    Obituary "$SAMSARA_CORVUS_OB_INSTAGIB"
    +MISSILE
    +RIPPER
	DamageType "CorvusGrimBallista"
    Alpha 0.7
    Scale 0.35
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("GrimSpittleTrail",0,0,2,0,0,0,0,160)
        SOLT A 1 A_FadeOut(0.04)
        loop
    }
}

Actor GrimSpittleCoop : GrimSpittle { Damage 2 +THRUSPECIES Species "Player" }

Actor GrimSpittle2 : GrimSpittle { Damage (6) Speed 16 }

Actor GrimSpittle2Coop : GrimSpittle2 { Damage 3  +THRUSPECIES Species "Player" }

ACTOR GrimSpittleTrail
{
    Speed 12
    Projectile
    +CLIENTSIDEONLY
    +MISSILE
    -SOLID
    +NOCLIP
    Alpha 0.6
    Scale 0.2
    States
    {
    Spawn:
        SOLS AAAAAAAAABBBBBBBBBCCCCCCCCCDDDDDDDDDEEEEEEEEE 1 A_FadeOut(0.05)
        stop
    }
}

Actor SoulPuff : BulletPuff
{
    +NOINTERACTION
    +PUFFONACTORS
    +ALWAYSPUFF
    +FORCEXYBILLBOARD
    DamageType "CorvusGrimBallista"
    Alpha 0.95
    Scale 0.7
    YScale 0.5
    VSpeed 0
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("DamnedSoul",0,0,-16,0,0,25/10.0,random(0,360),0,0)
    Melee:
    Crash:
        TNT1 A 0
        TNT1 A 0 A_PlaySoundEx("weapons/bowhit","soundslot7",0,0)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("GrimSpittleTrail2",0,0,0,random(-400,400)/100.00,random(-400,400)/100.00,random(-400,400)/100.00,random(0,360),180)
        GPF1 ABCDEFGHIJKLMNOPQR 1 bright A_SpawnItemEx("GrimSpittleTrail2",0,0,0,random(-400,400)/100.00,random(-400,400)/100.00,random(-400,400)/100.00,random(0,360),180)
        GPF1 STUVWXYZ 2 bright
        GPF2 ABC 2 bright
        Stop
    }
}

actor SoulPuffCoop : SoulPuff
{
	Species "Player"
	+THRUSPECIES
	+MTHRUSPECIES
}	

Actor GrimSpittleTrail2 : GrimSpittleTrail { Scale 0.25 }
Actor GrimSpittleTrail3 : GrimSpittleTrail { Scale 1.0 }

Actor DamnedSoul
{
    Radius 2
    Height 2
    Speed 1
    Alpha 1
    Scale 0.4
    Projectile
    +CLIENTSIDEONLY
    +NOINTERACTION
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_PlaySoundEx("SoulScream","soundslot5",0,0)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("GrimSpittleTrail2",0,0,random(0,16),random(-400,400)/100.00,random(-400,400)/100.00,random(-400,400)/100.00,random(0,360),180)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("GrimSpittleTrail2",0,0,random(0,16),random(-400,400)/100.00,random(-400,400)/100.00,random(-400,400)/100.00,random(0,360),180)
        DAMD A 3 BRIGHT A_CStaffMissileSlither
    FadeOut:
        TNT1 A 0 A_FadeOut(0.025)
        DAMD A 1 BRIGHT
        TNT1 A 0 A_FadeOut(0.025)
        DAMD A 1 BRIGHT
        TNT1 A 0 A_FadeOut(0.025)
        DAMD A 1 BRIGHT A_CStaffMissileSlither
        loop
    }
}

Actor GrimGhostFX1 : MummyFX1
{
	Speed 13
	Damage (8)
	DamageType "CorvusGrimBallista"
	States
	{
		Spawn:
			FX15 H 5 Bright A_PlaySoundEx("mummy/head","soundslot5",0,0)
			FX15 J 5 Bright A_SeekerMissile(10, 20)
			FX15 K 5 Bright A_PlaySoundEx("mummy/head","soundslot5",0,0)
			FX15 J 5 Bright A_SeekerMissile(10, 20)
			Loop
		Death:
			FX15 LMNO 5 Bright
			Stop
	}
}	

Actor GrimGhostFX1B : GrimGhostFX1 { Damage 3 +THRUSPECIES Species "Player" }

// Kept for legacy & compatibility purposes
actor ArtiEgg2 : ArtiEgg
{
    tag "Morph Ovum"
    +INVENTORY.UNDROPPABLE
    Inventory.MaxAmount 16
    inventory.usesound "heretic/itemuse"
	Inventory.RestrictedTo "CorvusPlayer"
    States
    {
      Use:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopUse")
        TNT1 A 0 A_FireCustomMissile("EggFX3", -15, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX3", -7.5, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX3", 0, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX3", 7.5, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX3", 15, 0, 0, 0, 1)
        stop

      CoopUse:
        TNT1 A 0 A_FireCustomMissile("EggFX2", -15, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX2", -7.5, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX2", 0, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX2", 7.5, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX2", 15, 0, 0, 0, 1)
        stop
    }
}

actor ArtiEgg3: ArtiEgg
{
    tag "Morph Ovum"
    +INVENTORY.UNDROPPABLE
    Inventory.MaxAmount 16
    inventory.usesound "heretic/itemuse"
	Inventory.RestrictedTo "CorvusPlayer"
    States
    {
      Use:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopUse")
        TNT1 A 0 A_FireCustomMissile("EggFX4", -15, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX4", -7.5, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX4", 0, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX4", 7.5, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX4", 15, 0, 0, 0, 1)
        stop

      CoopUse:
        TNT1 A 0 A_FireCustomMissile("EggFX2", -15, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX2", -7.5, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX2", 0, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX2", 7.5, 0, 0, 0, 1)
        TNT1 A 0 A_FireCustomMissile("EggFX2", 15, 0, 0, 0, 1)
        stop
    }
}

actor EggFX2 : MorphProjectile // Yeah, no more griefing co-op allies.
{
    Radius 8
    Height 8
    Speed 18
    Damage (1)
    DamageType "Chickenator"
    MorphProjectile.MonsterClass "Chicken2"
    MorphProjectile.MorphStyle MRF_FULLHEALTH
    States
    {
      Spawn:
        EGGM ABCDE 4
        Loop

      Death:
        FX01 FFGH 3 Bright
        Stop
    }
}

actor EggFX3 : MorphProjectile // This, however, still works funderfully in DM.
{
    Radius 8
    Height 8
    Speed 18
    Damage (1)
    MorphProjectile.Duration 350 // 35 * 10 = 350
    DamageType "Chickenator"
    MorphProjectile.PlayerClass "ChickenPlayer2"
    MorphProjectile.MonsterClass "Chicken2"
    //MorphProjectile.MorphStyle MRF_FULLHEALTH
    States
    {
      Spawn:
        EGGM ABCDE 4
        Loop

      Death:
        FX01 FFGH 3 Bright
        Stop
    }
}

actor EggFX4: EggFX2
{
    MorphProjectile.Duration 245 // 35 * 7 = 245
    MorphProjectile.PlayerClass "ChickenPlayer3"
}

actor Chicken2 : Chicken replaces Chicken
{
    BloodType "SamsaraBloodyHellBlood"
    States
    {
      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraClientDecorate",0,11)
        CHKN E 6 A_Scream
		CHKN F 6 A_Feathers
		CHKN G 6
		CHKN H 6 A_NoBlocking
		CHKN IJK 6
		CHKN L -1
		Stop

      Death.ZorchPistol:

      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        CHKN D 0 A_Stop
        TNT1 A 0 A_PlaySoundEx("chex/zorchsmall","soundslot5",0,0)
        CHKN D 3 A_SpawnItem("ZorchEffectSuperTiny")
        CHKN D 3 A_SetTranslucent(0.9,0)
        CHKN DD 0 A_Feathers
        CHKN D 0 A_Scream
        CHKN D 3 A_SetTranslucent(0.8,0)
        TNT1 D 0 A_NoBlocking
        CHKN D 3 A_SetTranslucent(0.7,0)
        CHKN D 3 A_SetTranslucent(0.6,0)
        CHKN D 3 A_SetTranslucent(0.5,0)
        CHKN D 3 A_SetTranslucent(0.4,0)
        CHKN D 3 A_SetTranslucent(0.3,0)
        CHKN D 3 A_SetTranslucent(0.2,0)
        CHKN D 3 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A -1
        stop

      Ice:
      Freeze:
      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        CHKN D 0 A_UnsetSolid
        CHKN D 70 A_GenericFreezeDeath
        CHKN D 0 A_IceGuyDie
        CHKN D 1 A_IceGuyDie
        TNT1 A -1
        stop

      Death.WolfenFlamethrower:
        CHKN E 0 A_Scream
        CHKN E 0 A_GiveToTarget("KillCount",1)
        CHKN EEEEEEE 0 A_Feathers
        CHKN E 0 A_SpawnItem("WolfensteinChickenLeg")
        ACLO E -1
        stop

      DeathBrutal:
        TNT1 A 0
        TNT1 AA 0 A_SpawnItemEx("RedMistTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("BrutalTinyBone",random(-5,5),random(-5,5),random(12,40),random(-200,200)/100.00,random(-200,200)/100.00,random(300,800)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("BrutalChunkTinier",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("BrutalChunkTiny",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("RedMistIntenseTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("Switchable_FlyingBlood",random(-10,10),random(-10,10),random(12,40),random(-350,350)/100.00,random(-350,350)/100.00,random(300,700)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("BrutalOnlineSound2",0,0,0,0,0,0,0,160) // Apparently A_PlaySound doesn't work, so I have to spawn the item. Weird!
        goto "Super::Death"
        
      DeathHappyfun:
        TNT1 A 0
        TNT1 AAA 0 A_SpawnItemEx("RainbowSparkleSound",random(-10,10),random(-10,10),random(32,40),0,0,0,0,32)
        TNT1 AAA 0 A_SpawnItemEx("WhiteSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 A 0 A_SpawnItemEx("RainbowSparkHuge",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 AAAA 0 A_SpawnItemEx("RainbowSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 AA 0 A_SpawnItemEx("RainbowSparkTiny",random(-5,5),random(-5,5),random(25,30),random(-80,80)/10.00,random(-80,80)/10.0,random(-3,3),random(-180,180),160)
        goto "Super::Death"
    }
}

actor ChickenPlayer2 : ChickenPlayer
{
    Health 100 // Previously 30
    ReactionTime 0
    PainChance 255
    Radius 8
    Height 12
    Speed 1
    Gravity 0.125
    +NOSKIN
    PainSound "chicken/pain"
    DeathSound "chicken/death"
    Player.JumpZ 1
    Player.Viewheight 21
    Player.ForwardMove 0.85, 0.9//1.22, 1.22
    Player.SideMove 0.85, 0.9//1.22, 1.22
    Player.SoundClass "Chicken"
    Player.DisplayName "Chicken"
    Player.MorphWeapon " Beak "
    BloodType "SamsaraBloodyHellBlood"
    -PICKUP
    States
    {
      Spawn:
        CHKN A -1
        Stop

      See:
        CHKN ABAB 3
        Loop

      Melee:
      Missile:
        CHKN C 12
        Goto Spawn

      Pain:
        CHKN D 4 A_Feathers
        CHKN C 4 A_Pain
        Goto Spawn

      Death:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraClientDecorate",0,11)
      Death2:
        CHKN E 6 A_Scream
        CHKN F 6 A_Feathers
        CHKN G 6
        CHKN H 6 A_NoBlocking
        CHKN IJK 6
        CHKN L -1
        Stop

      Death.WolfenFlamethrower:
        CHKN E 0 A_Scream
        CHKN EEEEEEEEE 0 A_Feathers
        CHKN E 0 A_SpawnItem("WolfensteinChickenLeg")
        ACLO E -1
        stop

      Pain.MightyBoot:
        CHKN G 1 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 25, 5, 24)
        CHKN G 1 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Recoil(12)
        CHKN G 6 A_Pain
        Goto See
        
      Pain.MarathonStun:
        TNT1 A 0 A_Stop
        CHKN D 1 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 25, 5, 100)
        TNT1 A 0 A_GiveInventory("NothingSpeed")
        TNT1 A 0 A_SetBlend("White",0.9,105)
        CHKN D 1 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
        CHKN D 6 A_Pain
        Goto See

      Death.ZorchPistol:

      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_PlaySoundEx("chex/zorchsmall","soundslot5",0,0)
        CHKN D 3 A_SpawnItem("ZorchEffectSuperTiny")
        CHKN D 3 A_SetTranslucent(0.9,0)
        CHKN AAAAAAAAAA 0 A_Feathers
        TNT1 A 0 A_PlayerScream
        CHKN D 3 A_SetTranslucent(0.8,0)
        TNT1 A 0 A_NoBlocking
        CHKN D 3 A_SetTranslucent(0.7,0)
        CHKN D 3 A_SetTranslucent(0.6,0)
        CHKN D 3 A_SetTranslucent(0.5,0)
        CHKN D 3 A_SetTranslucent(0.4,0)
        CHKN D 3 A_SetTranslucent(0.3,0)
        CHKN D 3 A_SetTranslucent(0.2,0)
        CHKN D 3 A_SetTranslucent(0.1,0)
        ACLO E 1 A_SetTranslucent(1,0)
        ACLO E -1
        stop

      DeathBrutal:
        TNT1 A 0
        TNT1 AA 0 A_SpawnItemEx("RedMistTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("BrutalTinyBone",random(-5,5),random(-5,5),random(12,40),random(-200,200)/100.00,random(-200,200)/100.00,random(300,800)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("BrutalChunkTinier",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("BrutalChunkTiny",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("RedMistIntenseTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("Switchable_FlyingBlood",random(-10,10),random(-10,10),random(12,40),random(-350,350)/100.00,random(-350,350)/100.00,random(300,700)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("BrutalOnlineSound2",0,0,0,0,0,0,0,160) // Apparently A_PlaySound doesn't work, so I have to spawn the item. Weird!
        goto Death2
        
      DeathHappyfun:
        TNT1 A 0
        TNT1 AAA 0 A_SpawnItemEx("RainbowSparkleSound",random(-10,10),random(-10,10),random(32,40),0,0,0,0,32)
        TNT1 AAA 0 A_SpawnItemEx("WhiteSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 A 0 A_SpawnItemEx("RainbowSparkHuge",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 AAAA 0 A_SpawnItemEx("RainbowSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
        TNT1 AA 0 A_SpawnItemEx("RainbowSparkTiny",random(-5,5),random(-5,5),random(25,30),random(-80,80)/10.00,random(-80,80)/10.0,random(-3,3),random(-180,180),160)
        goto Death2
    }
}

actor " Beak " : Beak replaces Beak
{
+CHEATNOTWEAPON
}

actor ChickenPlayer3: ChickenPlayer2
{
    Radius 16
    Height 36
    Speed 0.75
    Player.ForwardMove 0.75, 0.75
    Player.SideMove 0.75, 0.75
}
