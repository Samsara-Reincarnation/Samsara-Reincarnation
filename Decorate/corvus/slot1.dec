actor "Gauntlets of the Necromancer" : Gauntlets
{
    Weapon.SlotNumber 1
    Weapon.SlotPriority 0
    +BLOODSPLATTER
    Weapon.SelectionOrder 2300
    +WEAPON.WIMPY_WEAPON
    +WEAPON.MELEEWEAPON
    +INVENTORY.UNDROPPABLE
    Weapon.Kickback 0
    Weapon.YAdjust 16
    Inventory.PickUpSound "heretic/weaponget"
    Weapon.UpSound "weapons/gauntletsactivate"
    Inventory.PickupMessage "You got the Gauntlets of the Necromancer!"
    Obituary "$SAMSARA_CORVUS_OB_SLOT1"
    Inventory.RestrictedTo "CorvusPlayer"
    Tag "Gauntlets of the Necromancer"
    States
    {
      Spawn:
        WGNT A -1
        Stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"ReadyTome")
        GAUN A 1 A_WeaponReady
        Loop

      ReadyTome:
        GAUN GHI 4 A_WeaponReady
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"DeselectTome")
        GAUN A 1 A_Lower
        Loop

      DeselectTome:
        GAUN G 1 A_Lower
        Loop

      Select:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"SelectTome")
        GAUN A 1 A_Raise
        Loop

      SelectTome:
        GAUN G 1 A_Raise
        Loop

      Fire:
        TNT1 A 0 A_PlayWeaponSound("weapons/gauntletsuse")
        GAUN B 4 A_JumpIfInventory("PowerHereticTome",1,"FireTome")
        GAUN C 4

      Hold:
        TNT1 A 0 A_JumpIfInventory("PowerHereticTome",1,"HoldTome")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"HoldCoop")
        GAUN DEF 4 BRIGHT A_GauntletAttack(0)
        Goto HoldEnd

      HoldCoop:
        GAUN D 0 A_TakeInventory("HereticGauntletHit", 1)
        GAUN D 0 A_Saw("", "", 2 * random(1,8), "GauntletPuff1Coop", SF_NORANDOM|SF_RANDOMLIGHTBOTH)
        GAUN D 0 A_JumpIfInventory("HereticGauntletHit", 1, 2)
        GAUN D 4 BRIGHT A_PlaySound("weapons/gauntletson", CHAN_AUTO)
        Goto HoldCoop2
        GAUN D 4 BRIGHT A_PlaySound("weapons/gauntletshit", CHAN_AUTO)
        Goto HoldCoop2
      HoldCoop2:
        GAUN E 0 A_TakeInventory("HereticGauntletHit", 1)
        GAUN E 0 A_Saw("", "", 2 * random(1,8), "GauntletPuff1Coop", SF_NORANDOM|SF_RANDOMLIGHTBOTH)
        GAUN E 0 A_JumpIfInventory("HereticGauntletHit", 1, 2)
        GAUN E 4 BRIGHT A_PlaySound("weapons/gauntletson", CHAN_AUTO)
        Goto HoldCoop3
        GAUN E 4 BRIGHT A_PlaySound("weapons/gauntletshit", CHAN_AUTO)
        Goto HoldCoop3
      HoldCoop3:
        GAUN F 0 A_TakeInventory("HereticGauntletHit", 1)
        GAUN F 0 A_Saw("", "", 2 * random(1,8), "GauntletPuff1Coop", SF_NORANDOM|SF_RANDOMLIGHTBOTH)
        GAUN F 0 A_JumpIfInventory("HereticGauntletHit", 1, 2)
        GAUN F 4 BRIGHT A_PlaySound("weapons/gauntletson", CHAN_AUTO)
        Goto HoldEnd
        GAUN F 4 BRIGHT A_PlaySound("weapons/gauntletshit", CHAN_AUTO)
        Goto HoldEnd

      HoldEnd:
        GAUN C 4 A_ReFire
        GAUN B 4 A_Light0
        Goto Ready

      FireTome:
        GAUN JK 4

      HoldTome:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"HoldTomeCoop")
        GAUN LMN 4 BRIGHT A_GauntletAttack(1)
        Goto HoldTomeEnd

      HoldTomeCoop:
        GAUN L 0 A_TakeInventory("HereticGauntletHit", 1)
        GAUN L 0 A_Saw("", "", 2 * random(1,8), "GauntletPuff2Coop", SF_NORANDOM|SF_RANDOMLIGHTBOTH, 256, 1.40625, 0, 0.5)
        GAUN L 0 A_JumpIfInventory("HereticGauntletHit", 1, 2)
        GAUN L 4 BRIGHT A_PlaySound("weapons/gauntletson", CHAN_AUTO)
        Goto HoldTomeCoop2
        GAUN L 4 BRIGHT A_PlaySound("weapons/gauntletspowhit", CHAN_AUTO)
        Goto HoldTomeCoop2
      HoldTomeCoop2:
        GAUN M 0 A_TakeInventory("HereticGauntletHit", 1)
        GAUN M 0 A_Saw("", "", 2 * random(1,8), "GauntletPuff2Coop", SF_NORANDOM|SF_RANDOMLIGHTBOTH, 256, 1.40625, 0, 0.5)
        GAUN M 0 A_JumpIfInventory("HereticGauntletHit", 1, 2)
        GAUN M 4 BRIGHT A_PlaySound("weapons/gauntletson", CHAN_AUTO)
        Goto HoldTomeCoop3
        GAUN M 4 BRIGHT A_PlaySound("weapons/gauntletspowhit", CHAN_AUTO)
        Goto HoldTomeCoop3
      HoldTomeCoop3:
        GAUN N 0 A_TakeInventory("HereticGauntletHit", 1)
        GAUN N 0 A_Saw("", "", 2 * random(1,8), "GauntletPuff2Coop", SF_NORANDOM|SF_RANDOMLIGHTBOTH, 256, 1.40625, 0, 0.5)
        GAUN N 0 A_JumpIfInventory("HereticGauntletHit", 1, 2)
        GAUN N 4 BRIGHT A_PlaySound("weapons/gauntletson", CHAN_AUTO)
        Goto HoldTomeEnd
        GAUN N 4 BRIGHT A_PlaySound("weapons/gauntletspowhit", CHAN_AUTO)
        Goto HoldTomeEnd

      HoldTomeEnd:
        GAUN K 4 A_ReFire
        GAUN J 4 A_Light0
        Goto Ready
    }
}

actor GauntletPuff1Coop : GauntletPuff1
{
    Species "Player"
    +THRUSPECIES
    +MTHRUSPECIES
    +PUFFGETSOWNER
    States
    {
        Spawn:
        Melee:
        XDeath:
            TNT1 A 0
            TNT1 A 0 A_GiveToTarget("HereticGauntletHit", 1)
        Crash:
            Goto Super::Spawn
    }
}

actor GauntletPuff2Coop : GauntletPuff2
{
    Species "Player"
    +THRUSPECIES
    +MTHRUSPECIES
    +PUFFGETSOWNER
    States
    {
        Spawn:
        Melee:
        XDeath:
            TNT1 A 0
            TNT1 A 0 A_GiveToTarget("HereticGauntletHit", 1)
        Crash:
            Goto Super::Spawn
    }
}
