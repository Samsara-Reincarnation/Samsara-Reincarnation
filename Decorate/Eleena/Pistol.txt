actor "  Ripper Disc  " : Weapon
{
  weapon.selectionorder 1800
  Weapon.SlotNumber 1
  Weapon.SlotPriority 1
  Weapon.BobStyle Smooth
  Weapon.BobRangeX 0.55
  Weapon.BobRangeY 0.55
  Weapon.BobSpeed 1.2
  Inventory.PickupSound "EradAmmo/PUP"
  weapon.ammotype ""
  weapon.ammouse 0
  Weapon.AmmoGive 0
  Obituary "%o was ripped by %k's discs."
  +WEAPON.WIMPY_WEAPON
  +INVENTORY.UNDROPPABLE
  +WEAPON.NOALERT
  Inventory.RestrictedTo "Eleena"   
  states
  {
  Spawn:
    EDAP A -1
	Stop
  Ready:
	TNT1 A 0 A_JumpIfInventory("EleenaReady",1,"IntroTaunt")
    ELED A 1 A_WeaponReady
    Loop
  Deselect:
    TNT1 A 0 A_PlaySound("Erad/wepswitch")
	TNT1 A 0 A_Lower
	ELED A 1 A_Lower
	Goto Deselect+1
  Select:
	TNT1 A 0 A_Raise
    ELED A 1 A_Raise
    Loop
  Fire:
	ELED A 0 A_JumpIfInventory("PistolModeOn",1,"CheckAmmo")
  FireDo:
	TNT1 A 0 A_FireBullets(0,0, 1, 0, "SamsaraBlankPuff", FBF_NORANDOM)
	TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_PlaySound("eleena/discfire",CHAN_WEAPON)	
    TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)
	TNT1 A 0 A_FireCustomMissile("RippingDisc",0,0,0,0)	
 	Goto A
	TNT1 A 0 A_FireCustomMissile("RippingCoopDisc",0,0,0,0)	
  A:	
	TNT1 A 0 A_GunFlash
	ELED A 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    ELED CD 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	ELED EF 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	ELED FEDC 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    Goto Ready
  AltFire:
	ELED A 0 A_JumpIfInventory("PistolModeOn",1,"CheckAltAmmo")
  AltFireDo:
	TNT1 A 0 A_JumpIfInventory("EradEnWP",1,1)
	Goto Ready
	TNT1 A 0 A_FireBullets(0,0, 1, 0, "SamsaraBlankPuff", FBF_NORANDOM)
	TNT1 A 0 A_AlertMonsters	
    TNT1 A 0 A_PlaySound("eleena/fastdisc")
	TNT1 A 0 A_GunFlash
	ELED A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)	
	TNT1 A 0 A_FireCustomMissile("RippingDisc",0,1,0,0)
	Goto B
	TNT1 A 0 A_FireCustomMissile("RippingCoopDisc",0,1,0,0)	
  B:  
	ELED C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	TNT1 A 0 A_GunFlash
	ELED A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)	
	TNT1 A 0 A_FireCustomMissile("RippingDisc",0,1,0,0)
	Goto C
	TNT1 A 0 A_FireCustomMissile("RippingCoopDisc",0,1,0,0)	
  C:  
	ELED C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	TNT1 A 0 A_GunFlash
	ELED A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)	
	TNT1 A 0 A_FireCustomMissile("RippingDisc",0,1,0,0)
	Goto D
	TNT1 A 0 A_FireCustomMissile("RippingCoopDisc",0,1,0,0)	
  D:
	ELED C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	TNT1 A 0 A_GunFlash
	ELED A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,2)	
	TNT1 A 0 A_FireCustomMissile("RippingDisc",0,1,0,0)
	Goto E
	TNT1 A 0 A_FireCustomMissile("RippingCoopDisc",0,1,0,0)	
  E:
	ELED C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	ELED D 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	ELED E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	ELED F 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	ELED E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	ELED D 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	ELED C 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    ELED A 0 A_ReFire
    Goto Ready
  Flash:
    ELED B 1 bright A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    Stop
  CheckAmmo:
	ELED A 0 A_JumpIfInventory("Clip",1,2)
	ELED A 0 A_SelectWeapon("  Claw  ")
	goto Deselect
	ELED A 0 A_TakeInventory("Clip",1, TIF_NOTAKEINFINITE)
	goto FireDo	
  CheckAltAmmo:
	ELED A 0 A_JumpIfInventory("Clip",3,2)
	ELED A 0 A_SelectWeapon("  Claw  ")
	goto Deselect
	ELED A 0 A_TakeInventory("Clip",1, TIF_NOTAKEINFINITE)
	goto AltFireDo	
  IntroTaunt:
    TNT1 A 0 A_JumpIfInventory("EleenaBallgag",1,2)
    TNT1 A 0 A_PlaySoundEx("Eleena/Enter", "SoundSlot7", 0, 0)
    TNT1 A 0 A_TakeInventory("EleenaReady", 1)
    TNT1 A 0 A_GiveInventory("EleenaTauntCooldown",5)
    TNT1 A 0 ACS_NamedExecuteAlways("EleenaTauntCooldown", 0)
	Goto Ready
  }
}

ACTOR RippingDisc
{
  Height 5
  Radius 6
  Speed 70
  Scale 0.5
  Damage (Random(10,15))
  DamageType "EradDisc"
  PROJECTILE
  +WINDTHRUST
  +BOUNCEONFLOORS
  +BOUNCEONCEILINGS
  BounceFactor 1.0
  BounceCount 6
  Obituary "%o was ripped by %k's discs."
  DeathSound "eleena/discdead"
  States
  {
  Spawn:
    EDAM A 1 Bright
    loop
  Death:
    EDAM BBCCDDEE 1 Bright
    stop
  }
}

Actor RippingCoopDisc : RippingDisc
{
  Species "Player"
  +THRUSPECIES
  Speed 80
  BounceCount 8  
}