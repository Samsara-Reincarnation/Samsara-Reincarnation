Actor Boolean : Inventory
{
	Inventory.MaxAmount 1
	+UNDROPPABLE
	-INVBAR
}

Actor TemporaryBoolean : Boolean { Inventory.InterHubAmount 0 }

Actor Counter : Inventory
{
	Inventory.MaxAmount 0x7FFFFFFF
	+UNDROPPABLE
	-INVBAR
}

Actor TemporaryCounter : Counter { Inventory.InterHubAmount 0 }

Actor Trigger : CustomInventory
{
	Inventory.MaxAmount 0
	+AUTOACTIVATE
	States
	{
	PickupStop:
		TNT1 A 0 A_RailWait
		Stop
	}
}

Actor Kingpin_TrailLimiter : TemporaryCounter { Inventory.MaxAmount 2 }

Actor Kingpin_MuzzleFlash
{
	+NOINTERACTION
	-SOLID
	Radius 4
	Height 2
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 6 Light("KINGPINMUZZLE")
			Stop
	}
}

Actor Kingpin_BulletSmoke
{
	+NOBLOCKMAP
	+NOGRAVITY
	+HITTRACER
	+BLOODLESSIMPACT
	+PUFFONACTORS
	+FORCEXYBILLBOARD
	Renderstyle Add
	Radius 4
	Height 2
	Scale 0.25
	Decal KingpinBulletHole
	DamageType "KingpinBullet"
	States
	{
		Spawn:
		Melee:
		Crash:
			TNT1 A 0
			TNT1 A 0 A_Jump(192,2)
			TNT1 A 0 A_PlaySound("Kingpin/Ricochet",CHAN_5)
			KP00 A -1 ACS_NamedExecuteWithResult("Kingpin_FX",1,10,0.75*65536)
			Stop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Kingpin_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("Kingpin_BloodGenerator",1,AAPTR_TRACER)
			Stop
	}
}

Actor Kingpin_BulletSmokeBleed : Kingpin_BulletSmoke
{
	PoisonDamageType "KingpinBullet"
	PoisonDamage 2, 5, 17
	States
	{
		Spawn:
		Melee:
		Crash:
			TNT1 A 0
			TNT1 A 0 A_Jump(192,2)
			TNT1 A 0 A_PlaySound("Kingpin/Ricochet",CHAN_5)
			KP00 A -1 ACS_NamedExecuteWithResult("Kingpin_FX",1,10,0.75*65536)
			Stop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Kingpin_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("Kingpin_BloodChunkGenerator",1,AAPTR_TRACER)
			Stop
	}
}


Actor Kingpin_Weapon : Weapon
{
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	States
	{
		Spawn:
			KP98 A -1
			Stop
		Ready:
		ReadyLoop:
			TNT1 A 1
			Loop
		Deselect:
		DeselectLoop:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			Loop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			Loop
		Fire:
			TNT1 A 1
			Goto Ready
	}
}

Actor Kingpin_Casing
{
	BounceType Doom
	BounceCount 3
	Gravity 0.75
	Scale 0.5
	Damage (0)
	Height 2
	Radius 1
	Projectile
	Speed 2
	-NOGRAVITY
	+THRUACTORS
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SetGravity(frandom(0.65,0.75))
			TNT1 A 0 A_ChangeVelocity(frandom(0.5,2.0),0,velz,CVF_RELATIVE|CVF_REPLACE)
		SpawnLoop:
			KP00 A 1
			Loop
		Death:
			KP00 A 105
			KP00 AAAAAAAAAA 2 A_Fadeout(0.1)
			Stop
	}
}

Actor Kingpin_ShellCasing : Kingpin_Casing {}
Actor Kingpin_GrenadeCasing : Kingpin_Casing {}

Actor Kingpin_Explosion
{
	+NOINTERACTION
	+NOGRAVITY
	-SOLID
	Height 4
	Radius 2
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_RadiusGive("Kingpin_ExplosionRecoil",256,RGF_PLAYERS,1)
			TNT1 A 0 A_PlaySound("Kingpin/Explosion",CHAN_5)
			TNT1 AAAAAAAA 0 A_CustomMissile("Kingpin_ExplosionTrail",0,0,frandom(0,360),CMF_AIMDIRECTION,frandom(0,360))
			TNT1 A 0 A_SpawnItemEX("Kingpin_ExplosionEffect")
			TNT1 A 0 A_SpawnItemEX("Kingpin_ExplosionSmoke")
			Stop
	}
}

Actor Kingpin_ExplosionRecoil : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 ACS_NamedExecuteWithResult("GlobalRecoil",5,192)
			Stop
	}
}

Actor Kingpin_ExplosionTrail
{
	BounceType Doom
	WallBounceFactor 0.33
	BounceFactor 0.33
	Gravity 0.33
	Damage (0)
	Height 2
	Radius 1
	Projectile
	Speed 8
	-NOGRAVITY
	+THRUACTORS
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteWithResult("Kingpin_TrailLimiter")
			TNT1 A 0 A_ScaleVelocity(frandom(0.8,1.2))
		Decide:
			KP00 "#" 0 A_JumpIfInventory("Kingpin_TrailLimiter",2,"SpawnLoop3")
			KP00 "#" 0 A_JumpIfInventory("Kingpin_TrailLimiter",1,"SpawnLoop2")
		SpawnLoop:
			TNT1 A 0 A_SpawnItemEx("Kingpin_ExplosionTrailEffect",(sqrt(velx*velx+vely*vely)/8)*2,0,(velz/8)*2,0,0,0,0,0,0,25002)
			TNT1 A 0 A_SpawnItemEx("Kingpin_ExplosionTrailEffect",(sqrt(velx*velx+vely*vely)/8)*4,0,(velz/8)*4,0,0,0,0,0,0,25001)
			TNT1 A 0 A_SpawnItemEx("Kingpin_ExplosionTrailEffect",(sqrt(velx*velx+vely*vely)/8)*6,0,(velz/8)*6,0,0,0,0,0,0,25002)
			TNT1 A 1 A_SpawnItemEx("Kingpin_ExplosionTrailEffect",0,0,0,0,0,0,0,0,0,25000)
			Loop
		SpawnLoop2:
			TNT1 A 0 A_SpawnItemEx("Kingpin_ExplosionTrailEffect",(sqrt(velx*velx+vely*vely)/8)*4,0,(velz/8)*4)
			TNT1 A 1 A_SpawnItemEx("Kingpin_ExplosionTrailEffect")
			Loop
		SpawnLoop3:
			TNT1 A 1 A_SpawnItemEx("Kingpin_ExplosionTrailEffect")
			Loop
		Death:
			TNT1 A 1
			Stop
	}
}

Actor Kingpin_ExplosionTrailEffect
{
	+NOINTERACTION
	+NOGRAVITY
	-SOLID
	+FORCEXYBILLBOARD
	Height 4
	Radius 2
	VSpeed 0.5
	Renderstyle Add
	Scale 0.05
	States
	{
		Spawn:
			KP01 ABCDEF 2 Bright A_SetScale(scaleX+0.0125)
			KP01 G 0 A_JumpIf(tid > 25001,"Quit")
			KP01 GHIJ 3 Bright A_SetScale(scaleX+0.0125)
			KP01 K 0 A_JumpIf(tid > 25000,"Quit")
			KP01 KLMN 3 Bright A_SetScale(scaleX+0.0125)
			Stop
		Quit:
			TNT1 A 1
			Stop
	}
}

Actor Kingpin_ExplosionEffect
{
	+NOINTERACTION
	+NOGRAVITY
	-SOLID
	+FORCEXYBILLBOARD
	Height 4
	Radius 2
	VSpeed 0.5
	Renderstyle Add
	States
	{
		Spawn:
			KP01 A 0 NoDelay ACS_NamedExecuteWithResult("Kingpin_FX",2,28,2.0*65536)
			KP01 ABCDEFGHIJKLMN 2 Bright
			Stop
	}
}

Actor Kingpin_ExplosionEffectSmall : Kingpin_ExplosionEffect
{
	Scale 0.5
	States
	{
		Spawn:
			KP01 A 0 NoDelay ACS_NamedExecuteWithResult("Kingpin_FX",2,28,0.5*65536)
			KP01 ABCDEFGHIJKLMN 2 Bright
			Stop
	}
}

Actor Kingpin_ExplosionNukeEffect
{
	+NOINTERACTION
	+NOGRAVITY
	-SOLID
	+FORCEXYBILLBOARD
	Height 4
	Radius 2
	Renderstyle Add
	States
	{
		Spawn:
			TNT1 A 5
			TNT1 A 0 A_ChangeVelocity(3,0,0,CVF_RELATIVE|CVF_REPLACE)
			KP01 A 0 ACS_NamedExecuteWithResult("Kingpin_FX",2,28,2.0*65536)
			KP01 ABCDEFGHIJKLMN 2 Bright
			Stop
	}
}

Actor Kingpin_ExplosionSmoke
{
	+NOINTERACTION
	+NOGRAVITY
	-SOLID
	+FORCEXYBILLBOARD
	Height 4
	Radius 2
	VSpeed 0.5
	States
	{
		Spawn:
			KP02 A 0 NoDelay ACS_NamedExecuteWithResult("Kingpin_FX",2,56,4.0*65536)
			KP02 ABCDEFGHIJKLMN 4
			Stop
	}
}

Actor Kingpin_ExplosionSmokeSmall : Kingpin_ExplosionSmoke
{
	States
	{
		Spawn:
			KP02 A 0 NoDelay ACS_NamedExecuteWithResult("Kingpin_FX",2,56,1.0*65536)
			KP02 ABCDEFGHIJKLMN 4
			Stop
	}
}

Actor Kingpin_SmokeTrail
{
	+NOINTERACTION
	+NOGRAVITY
	-SOLID
	+FORCEXYBILLBOARD
	Height 4
	Radius 2
	Scale 0.25
	Renderstyle Add
	States
	{
		Spawn:
			TNT1 A 0
			KP00 A -1 ACS_NamedExecuteWithResult("Kingpin_FX",1,10,0.75*65536)
			Stop
	}
}

Actor Kingpin_BloodGenerator : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_CheckFlag("NOBLOOD","PickupStop")
			TNT1 A 0 A_SpawnItemEx("Kingpin_BloodShot",(radius-16)*cos(CallACS("Kingpin_BloodGenerator_Return",1)),(radius-16)*sin(CallACS("Kingpin_BloodGenerator_Return",1)),CallACS("Kingpin_BloodGenerator_Return",2),frandom(0.25,0.5)*sin(CallACS("Kingpin_BloodGenerator_Return",3)),frandom(-0.5,0.5)*cos(CallACS("Kingpin_BloodGenerator_Return",3)),frandom(0.05,0.25),0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEVELOCITY)
			stop
	}
}

Actor Kingpin_BloodChunkGenerator : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_CheckFlag("NOBLOOD","PickupStop")
			TNT1 A 0 A_SpawnItemEx("Kingpin_BloodShot",(radius-16)*cos(CallACS("Kingpin_BloodGenerator_Return",1)),(radius-16)*sin(CallACS("Kingpin_BloodGenerator_Return",1)),CallACS("Kingpin_BloodGenerator_Return",2),frandom(0.25,0.5)*sin(CallACS("Kingpin_BloodGenerator_Return",3)),frandom(-0.5,0.5)*cos(CallACS("Kingpin_BloodGenerator_Return",3)),frandom(0.05,0.25),0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEVELOCITY)
			TNT1 A 0 A_SpawnItemEx("Kingpin_Gib",(radius-16)*cos(CallACS("Kingpin_BloodGenerator_Return",1)),(radius-16)*sin(CallACS("Kingpin_BloodGenerator_Return",1)),CallACS("Kingpin_BloodGenerator_Return",2),frandom(0.25,0.5)*sin(CallACS("Kingpin_BloodGenerator_Return",3)),frandom(-0.5,0.5)*cos(CallACS("Kingpin_BloodGenerator_Return",3)),frandom(0.05,0.25),0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEVELOCITY)
			stop
	}
}

Actor Kingpin_BloodGeneratorBleedAngle : Inventory { Inventory.MaxAmount 131072 }
Actor Kingpin_BloodGeneratorAngle : Inventory { Inventory.MaxAmount 131072 }
Actor Kingpin_BloodGeneratorZ : Inventory { Inventory.MaxAmount 131072 }

Actor Kingpin_BloodShot
{
	+NOINTERACTION
	+NOGRAVITY
	-SOLID
	Height 4
	Radius 2
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Kingpin_BloodDrop",frandom(-1,1),frandom(-1,1),frandom(-1,1),frandom(-1,1),frandom(-1,1),frandom(-1,1),0,SXF_TRANSFERTRANSLATION)
			TNT1 A 1 A_SpawnItemEx("Kingpin_BloodSpot",frandom(-16.0,16.0),frandom(-16.0,16.0),0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			Stop
	}
}

Actor Kingpin_BloodDrop
{
	+NOINTERACTION
	-NOGRAVITY
	-SOLID
	+THRUACTORS
	+FORCEXYBILLBOARD
	Gravity 0.8
	Height 4
	Radius 2
	Scale 0.125
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(256,"Form1","Form2","Form3","Form4","Form5","Form6","Form7","Form8","Form9")
		Form1:
			KP03 A 0
			Goto Fall
		Form2:
			KP03 B 0
			Goto Fall
		Form3:
			KP03 C 0
			Goto Fall
		Form4:
			KP03 D 0
			Goto Fall
		Form5:
			KP03 E 0
			Goto Fall
		Form6:
			KP03 F 0
			Goto Fall
		Form7:
			KP03 G 0
			Goto Fall
		Form8:
			KP03 H 0
			Goto Fall
		Form9:
			KP03 I 0
			Goto Fall
		Fall:
			"####" "#" 7
			"####" "#" 0 A_ChangeVelocity(velx,vely,frandom(-2.0,-3.0),CVF_REPLACE)
			"####" "#" 28
			"####" "##########" 2 A_Fadeout(0.1)
			Stop
	}
}

Actor Kingpin_BloodSpot
{
	+NOINTERACTION
	+THRUACTORS
	-NOGRAVITY
	-SOLID
	Renderstyle Translucent
	Gravity 1.0
	Height 4
	Radius 2
	Scale 16.0
	States
	{
		Spawn:
			KP00 A 0
			KP00 A 0 A_SetScale(frandom(4.0,16.0))
			KP00 A 1050 A_Warp(AAPTR_DEFAULT,0,0,0.25,0,WARPF_TOFLOOR|WARPF_NOCHECKPOSITION)
			KP00 AAAAAAAAAA 2 A_Fadeout(0.1)
			Stop
	}
}

Actor Kingpin_BloodPool
{
	+NOINTERACTION
	+THRUACTORS
	-NOGRAVITY
	-SOLID
	Renderstyle Translucent
	Gravity 1.0
	Height 4
	Radius 2
	Scale 1.0
	States
	{
		Spawn:
			KP00 A 0
			TNT1 A 35
			TNT1 A 0 A_Warp(AAPTR_MASTER,0,0,0.25,0,WARPF_TOFLOOR|WARPF_NOCHECKPOSITION)
			KP00 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 2 A_SetScale(scaleX*1.1)
			KP00 A 1050
			KP00 AAAAAAAAAA 2 A_Fadeout(0.1)
			Stop
	}
}

Actor Kingpin_BloodTrail
{
	+NOINTERACTION
	+NOGRAVITY
	-SOLID
	+THRUACTORS
	+FORCEXYBILLBOARD
	Height 4
	Radius 2
	Scale 0.33
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(256,"Form1","Form2","Form3","Form4","Form5","Form6","Form7","Form8","Form9")
		Form1:
			KP03 A 0
			Goto Fall
		Form2:
			KP03 B 0
			Goto Fall
		Form3:
			KP03 C 0
			Goto Fall
		Form4:
			KP03 D 0
			Goto Fall
		Form5:
			KP03 E 0
			Goto Fall
		Form6:
			KP03 F 0
			Goto Fall
		Form7:
			KP03 G 0
			Goto Fall
		Form8:
			KP03 H 0
			Goto Fall
		Form9:
			KP03 I 0
			Goto Fall
		Fall:
			"####" "#" 12
			Stop
	}
}

Actor Kingpin_Gib
{
	BounceType Hexen
	BounceSound "Kingpin/Gibs"
	WallBounceFactor 0.45
	BounceFactor 0.55
	Gravity 0.5
	Damage (0)
	Height 12
	Radius 6
	Projectile
	Speed 12
	ReactionTime 0
	-NOGRAVITY
	+BOUNCEONACTORS
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteWithResult("Kingpin_TrailLimiter")
			TNT1 A 0 A_GiveInventory("Kingpin_GibLifeSpan",350)
			TNT1 A 0 A_Jump(256,"Form1","Form2","Form3","Form4","Form5")
		Form1:
			KP00 A 0
			Goto SpawnLoop
		Form2:
			KP00 B 0 A_GiveInventory("Kingpin_GibFrame",1)
			Goto SpawnLoop
		Form3:
			KP00 C 0 A_GiveInventory("Kingpin_GibFrame",2)
			Goto SpawnLoop
		Form4:
			KP00 D 0 A_GiveInventory("Kingpin_GibFrame",3)
			Goto SpawnLoop
		Form5:
			KP00 E 0 A_GiveInventory("Kingpin_GibFrame",4)
			Goto SpawnLoop
		Decide:
			KP00 "#" 0 A_JumpIfInventory("Kingpin_TrailLimiter",2,"SpawnLoop3")
			KP00 "#" 0 A_JumpIfInventory("Kingpin_TrailLimiter",1,"SpawnLoop2")
		SpawnLoop:
			KP00 "#" 0 A_TakeInventory("Kingpin_GibLifeSpan",1)
			KP00 "#" 0 A_SpawnItemEx("Kingpin_BloodTrail",(sqrt(velx*velx+vely*vely)/12)*4,0,(velz/12)*4,0,0,0,0,SXF_TRANSFERTRANSLATION)
			KP00 "#" 0 A_SpawnItemEx("Kingpin_BloodTrail",(sqrt(velx*velx+vely*vely)/12)*8,0,(velz/12)*8,0,0,0,0,SXF_TRANSFERTRANSLATION)
			KP00 "#" 0 A_SpawnItemEx("Kingpin_BloodTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			KP00 "#" 1 A_JumpIf(abs(sqrt(velx*velx+vely*vely+velz*velz)) < 4,"Stay")
			KP00 "#" 0 A_JumpIfInventory("Kingpin_GibLifeSpan",1,"SpawnLoop")
			KP00 "#" -1 A_CountDown
			Wait
		SpawnLoop2:
			KP00 "#" 0 A_TakeInventory("Kingpin_GibLifeSpan",1)
			KP00 "#" 0 A_SpawnItemEx("Kingpin_BloodTrail",(sqrt(velx*velx+vely*vely)/12)*6,0,(velz/12)*6,0,0,0,0,SXF_TRANSFERTRANSLATION)
			KP00 "#" 0 A_SpawnItemEx("Kingpin_BloodTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			KP00 "#" 1 A_JumpIf(abs(sqrt(velx*velx+vely*vely+velz*velz)) < 4,"Stay")
			KP00 "#" 0 A_JumpIfInventory("Kingpin_GibLifeSpan",1,"SpawnLoop2")
			KP00 "#" -1 A_CountDown
			Wait
		SpawnLoop3:
			KP00 "#" 0 A_TakeInventory("Kingpin_GibLifeSpan",1)
			KP00 "#" 0 A_SpawnItemEx("Kingpin_BloodTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			KP00 "#" 1 A_JumpIf(abs(sqrt(velx*velx+vely*vely+velz*velz)) < 4,"Stay")
			KP00 "#" 0 A_JumpIfInventory("Kingpin_GibLifeSpan",1,"SpawnLoop3")
			KP00 "#" -1 A_CountDown
			Wait
		Stay: //I'm only doing this for my trail optimizer
			KP00 "#" 0 A_SpawnItemEx("Kingpin_BloodSpot",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			KP00 "#" 1 ACS_NamedExecuteWithResult("Kingpin_GibTransfer")
			Stop
		Death:
			"####" "##########" 2 A_Fadeout(0.1)
			Stop
	}
}

Actor Kingpin_GibStay
{
	Gravity 0.5
	Damage (0)
	Height 12
	Radius 6
	-NOGRAVITY
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("Kingpin_GibFrame",4,"Form5")
			TNT1 A 0 A_JumpIfInventory("Kingpin_GibFrame",3,"Form4")
			TNT1 A 0 A_JumpIfInventory("Kingpin_GibFrame",2,"Form3")
			TNT1 A 0 A_JumpIfInventory("Kingpin_GibFrame",1,"Form2")
		Form1:
			KP01 A 0
			Goto SpawnLoop
		Form2:
			KP01 B 0
			Goto SpawnLoop
		Form3:
			KP01 C 0
			Goto SpawnLoop
		Form4:
			KP01 D 0
			Goto SpawnLoop
		Form5:
			KP01 E 0
			Goto SpawnLoop
		SpawnLoop:
			KP01 "#" 1 A_TakeInventory("Kingpin_GibLifeSpan",1)
			KP01 "#" 0 A_JumpIfInventory("Kingpin_GibLifeSpan",1,"SpawnLoop")
		Death:
			"####" "##########" 2 A_Fadeout(0.1)
			Stop
	}
}

Actor Kingpin_GibLifeSpan : TemporaryCounter { Inventory.MaxAmount 350 }
Actor Kingpin_GibFrame : TemporaryCounter { Inventory.MaxAmount 4 }

Actor Kingpin_GibSpawner
{
	Height 16
	Radius 4
	+ISMONSTER
	-COUNTKILL
	-SOLID
	-SHOOTABLE
	+NOTONAUTOMAP
	+NOINTERACTION
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 ACS_NamedExecuteWithResult("Kingpin_GibSpawner")
			Stop
	}
}

Actor Kingpin_BloodPoolSpawner
{
	Height 16
	Radius 4
	+ISMONSTER
	-COUNTKILL
	-SOLID
	-SHOOTABLE
	+NOTONAUTOMAP
	+NOINTERACTION
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 ACS_NamedExecuteWithResult("Kingpin_BloodPool")
			Stop
	}
}