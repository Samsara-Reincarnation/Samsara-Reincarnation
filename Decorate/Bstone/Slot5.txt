actor "Plasma Discharge Unit": InstantSwitchWeapon
{
    Weapon.SelectionOrder 1200
    Weapon.SlotNumber 5
	Weapon.BobStyle Alpha
	Weapon.BobRangeX 0
	Weapon.BobRangeY 0.6
	Weapon.BobSpeed 1.5	
    Inventory.PickUpSound "wolfen/knifeget"
    Obituary "%k destroyed %o with the Plasma Discharge Unit"
    Inventory.PickupMessage "You got a Plasma Discharge Unit!"
    DamageType "BlakePlasma"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 8
	Weapon.AmmoType "RocketAmmo"
    +INVENTORY.UNDROPPABLE
	+NOAUTOAIM
	Inventory.RestrictedTo "BlakeStone"	
    Weapon.YAdjust 11
    Tag "Plasma Discharge Unit"
    States
    {
      Spawn:
        BWP5 A -1
        Loop

      Ready:
		TNT1 A 0 A_JumpIfInventory("BlakeAOGToken", 1, 2)
        BPDU A 1 A_WeaponReady
        loop
		BPDU F 1 A_WeaponReady
		Loop

      Fire:
		TNT1 A 0 A_JumpIfInventory("BlakeAOGToken", 1, "FireAOG")
        BPDU B 3
	  Hold:	
        TNT1 A 0 A_PlayWeaponSound("BCannon/Fire")
		TNT1 A 0 A_JumpIfInventory("BlakeAOGToken", 1, "HoldAOG")
		TNT1 A 0 A_JumpIfInventory("SPModeOn", 1, "SPHold")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopHold")
        BPDU C 4 BRIGHT A_FireCustomMissile("BCannonProjDM", 0, 1, 0, 12, 0, 8)
		BPDU D 3
		BPDU E 3 A_Refire
        Goto Ready	
	  FireAOG:	
        BPDU G 3
	  HoldAOG:	
        TNT1 A 0 A_PlayWeaponSound("BCannon/Fire")
		TNT1 A 0 A_JumpIfInventory("SPModeOn", 1, "SPHoldAOG")
		TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopHoldAOG")		
        BPDU H 4 BRIGHT A_FireCustomMissile("BCannonProjDM", 0, 1, 0, 12, 0, 8)
		BPDU I 3
		BPDU J 2 A_Refire("HoldAOG")
        Goto Ready	 
	  SPHold:
        BPDU C 4 BRIGHT A_FireCustomMissile("BCannonProj", 0, 1, 0, 12, 0, 8)
		BPDU D 3
		BPDU E 3 A_Refire
        Goto Ready		  
      CoopHold:
        BPDU C 4 BRIGHT A_FireCustomMissile("BCannonProjCoop", 0, 1, 0, 12, 0, 8)
		BPDU D 3
		BPDU E 3 A_Refire
		Goto Ready
	  SPHoldAOG:
        BPDU H 4 BRIGHT A_FireCustomMissile("BCannonProj", 0, 1, 0, 12, 0, 8)
		BPDU I 3
		BPDU J 2 A_Refire("HoldAOG")
        Goto Ready	 	  
	  CoopHoldAOG:
        BPDU H 4 BRIGHT A_FireCustomMissile("BCannonProjCoop", 0, 1, 0, 12, 0, 8)
		BPDU I 3
		BPDU J 3 A_Refire
        Goto Ready		
    }
}

Actor BCannonProj
{
	PROJECTILE
	-NOGRAVITY
	+WINDTHRUST
	+BLOODLESSIMPACT
	+FORCEXYBILLBOARD
	Damage 7
	Radius 8
	Height 8
	Scale 1.25
	Speed 16
	Decal Scorch
	obituary "%k destroyed %o with the Plasma Discharge Unit"
	DamageType "BCannonProj"
	DeathSound "BCannon/Boom"
	Gravity 0.3
	States
	{
		Spawn:
			BCAP A -1 Bright
		Loop
		Death:
			TNT1 A 0 A_ChangeFlag("NOGRAVITY", 1)
			TNT1 A 0 A_Explode(110, 64, 0)
			TNT1 A 0 A_BFGSpray("BCannonPuff", 15, 3)
			BCAP BCDEF 3 Bright
		Stop
	}
}

Actor BCannonProjDM : BCannonProj
{
	Damage 5
	States
	{
		Death:
			TNT1 A 0 A_ChangeFlag("NOGRAVITY", 1)
			TNT1 A 0 A_Explode(100, 64, 0)
			TNT1 A 0 A_BFGSpray("BCannonPuff", 6, 1)
			BCAP BCDEF 3 Bright
		Stop
	}
}

Actor BCannonProjCoop : BCannonProj
{
	Species "Player"
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+MTHRUSPECIES
	States
	{
		Death:
			TNT1 A 0 A_ChangeFlag("NOGRAVITY", 1)
			TNT1 A 0 A_Explode(100, 64, 0)
			TNT1 A 0 A_BFGSpray("BCannonPuff_2", 15, 3)
			BCAP BCDEF 3 Bright
		Stop
	}	
}

Actor BCannonPuff 
{
	+NOGRAVITY
	+PUFFGETSOWNER
	+NOINTERACTION
	RenderStyle Normal
	Scale 1.0
	DamageType "BCannonProj"	
	States
	{
		Spawn:
			BCAP BCDEF 3 Bright
		Stop
	}
}			

Actor BCannonPuff_2 : BCannonPuff { Species "Player" +THRUSPECIES +NODAMAGETHRUST +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES +DONTSPLASH }