Actor Painkiller_DemonMorph : PainkillerWeapon
{
	Weapon.SelectionOrder 100
	//Weapon.SlotNumber 7
	Obituary "%o had their soul claimed by %k."
	Inventory.Pickupmessage "Huh?"
	Tag "Demon Morph"
	+WEAPON.BFG
	+WEAPON.CHEATNOTWEAPON
	States
	{
		Ready:
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_DemonModeActive") == 1, 1)
			TNT1 A 0 A_PlaySound("Painkiller/DemonMorph/Loop",CHAN_5|CHAN_NOPAUSE,1.0,true)
		ReadyLoop:
			TNT1 A 0 A_GiveInventory("PainkillerDemonMorphSlower")
			TNT1 A 0 A_GiveInventory("PainkillerDemonMorphInvulnerability")
			TNT1 A 0 A_GiveInventory("PainkillerDemonMorphTimerFreezer")
			TNT1 A 5 A_GiveInventory("PainkillerDemonMorphScreen")
			TNT1 A 0 A_WeaponReady
			Loop
		Select:
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingShotgun",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingStake",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingRifle",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingChaingun",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingElectro",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingBolt",1)
			TNT1 A 0 A_GiveInventory("Painkiller_HoldingDemon",1)
			TNT1 A 0 A_PlaySound("Painkiller/DemonMorph/Start",CHAN_6|CHAN_NOPAUSE)
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop
		Deselect:
			TNT1 A 0 A_TakeInventory("PainkillerDemonMorphSlower")
			TNT1 A 0 A_TakeInventory("PainkillerDemonMorphTimerFreezer")
			TNT1 A 0 A_TakeInventory("PainkillerDemonMorphScreen")
			//TNT1 A 0 A_StopSound(CHAN_5)
			TNT1 A 0 A_PlaySound("Painkiller/DemonMorph/Stop",CHAN_5|CHAN_NOPAUSE)
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingDemon",1)
			loop
		Fire:
			TNT1 A 0 A_StopSound(CHAN_5|CHAN_NOPAUSE)
			TNT1 A 0 A_PlaySound("Painkiller/DemonMorph/Fire",CHAN_7|CHAN_NOPAUSE)
			TNT1 A 0 A_Quake(5,15,0,4,"")
			TNT1 A 0 A_FireCustomMissile("Painkiller_DemonMorphProjectile")
			TNT1 A 0 A_ZoomFactor(0.95)
			TNT1 A 8
			TNT1 A 0 A_ZoomFactor(0.9)
			TNT1 A 9
			TNT1 A 0 A_ZoomFactor(1.0)
			Goto ReadyLoop
	}
}

Actor PainkillerDemonMorphScreen : PowerLightAmp
{
	Powerup.Color InverseMap
}

Actor PainkillerDemonMorphTimerFreezer : PowerTimeFreezer
{
	Powerup.Duration 24
}

Actor PainkillerDemonMorphSlower : PowerSpeed
{
	Speed 0.5
}

Actor PainkillerDemonMorphInvulnerability : PowerProtection {	damagefactor "normal", 0.0 powerup.duration 17 }

Actor Painkiller_DemonMorphProjectile : FastProjectile
{
	Height 8
	Radius 4
	Speed 256
	DamageType "PainkillerDemon"
	+EXTREMEDEATH
	//+NODAMAGETHRUST
	+FORCERADIUSDMG
	+NOTIMEFREEZE
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1
			Loop
		Death:
			TNT1 A 0 A_RadiusGive("Painkiller_DemonMorphBlast",64,RGF_MONSTERS|RGF_NOSIGHT,64)
			TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST",true)
			TNT1 A 0 A_Explode(50000,64,!XF_HURTSOURCE,0,64)
			TNT1 A 2
			Stop
	}
}

Actor Painkiller_DemonMorphBlast : CustomInventory
{
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
		Spawn:
			TNT1 A 0 
			TNT1 A 1
			Stop
		Pickup:
			TNT1 A 0 ACS_NamedExecuteAlways("Painkiller_DemonMorphBlast",0,0)
			TNT1 A 1
			stop
	}
}

Actor Painkiller_DemonMorph_Player : CustomInventory
{
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
		Spawn:
			BFUG A -1
			Stop
		Pickup:
			TNT1 A 0 A_RadiusGive("Painkiller_DemonMorph_Highlight",4096,RGF_CORPSES|RGF_MISSILES|RGF_PLAYERS|RGF_MONSTERS|RGF_NOSIGHT|RGF_GIVESELF,1)
			TNT1 A 1
			Stop
	}
}
Actor Painkiller_DemonMorph_Highlight : CustomInventory
{
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
		Spawn:
			BFUG A -1
			Stop
		Pickup:
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_DemonMorph_Highlight")
			TNT1 A 1
			Stop
	}
}