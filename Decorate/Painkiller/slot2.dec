ACTOR Painkiller_Shotgun : PainkillerWeapon
{
	Weapon.SelectionOrder 900
	Weapon.SlotNumber 2
	Weapon.AmmoType1 "Painkiller_ShellAmmo"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 10
	Weapon.AmmoType2 "Painkiller_FreezerAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 5
	Obituary "%o was blasted by %k's shotgun."
	Inventory.Pickupmessage "Shotgun"
	Inventory.PickupSound "Painkiller/Shotgun/Pickup"
	Tag "Shotgun"
	States
	{
		Select:
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingStake",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingRifle",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingChaingun",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingElectro",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingBolt",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingDemon",1)
			TNT1 A 0 A_GiveInventory("Painkiller_HoldingShotgun",1)
			Goto Super::Select
		Deselect:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingShotgun",1)
			Loop
		Ready:
			PK00 ABCDEFGHI 10 A_WeaponReady
			Loop
		Fire:
			PK01 A 0 A_GunFlash
			PK01 A 0 A_PlaySound("Painkiller/Shotgun/Fire",CHAN_WEAPON)
			PK01 A 0 A_SpawnItemEx("Painkiller_Shotgun_FireSmoke",4,4,38,0,-0.1,0.1,0,SXF_NOCHECKPOSITION)
			PK01 A 0 A_SpawnItemEx("Painkiller_Shotgun_FireSmoke",4,4,38,0,0.1,0.1,0,SXF_NOCHECKPOSITION)
			PK01 A 0 A_TakeInventory("Painkiller_ShellAmmo",1,TIF_NOTAKEINFINITE)
			PK01 A 0 A_FireBullets(12,12,15,8,"Painkiller_Shotgun_Puff",FBF_NORANDOM)
			PK01 A 0 A_Quake(4,5,0,4,"")
			PK01 ABCDEFGHJKMNP 2
			Goto Ready
		Flash:
			PK03 ABCDE 1 Bright
			Stop
		AltFire:
			PK02 A 0 A_PlaySound("Painkiller/Freezer/Fire",CHAN_WEAPON)
			PK02 A 0 A_FireCustomMissile("Painkiller_Shotgun_IceShot",0,0,-5)
			PK02 A 0 A_FireCustomMissile("Painkiller_Shotgun_IceShot",0,0,5)
			PK02 A 0 A_TakeInventory("Painkiller_FreezerAmmo",1,TIF_NOTAKEINFINITE)
			PK02 ABCDEFGHIJKLMNOP 3
			Goto Ready
		Spawn:
			PK99 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_Shotgun_Icon",0,0,40)
			PK99 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_Shotgun_Icon2",0,0,40)
			Loop
	}
}

Actor Painkiller_Shotgun_Icon : Painkiller_Weapon_Icon
{
	States
	{
		Spawn:
			PK07 R 2
			Stop
	}
}

Actor Painkiller_Shotgun_Icon2 : Painkiller_Weapon_Icon
{
	States
	{
		Spawn:
			PK07 S 2
			Stop
	}
}

Actor Painkiller_Shotgun_Puff : PainkillerPuff
{
	+HITTRACER
	+ALWAYSPUFF
	+PUFFONACTORS
	+PUFFGETSOWNER
	+BLOODLESSIMPACT
	-SKYEXPLODE
	+FORCEXYBILLBOARD
	Decal "PainkillerBulletHole"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("Painkiller_ShotgunBlast")
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_BloodGenerator_Offsets")==1,1)
			TNT1 A 0 A_GiveInventory("Painkiller_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ShotgunSmoke",0,0,0,0,0,frandom(0.5,1.0),0,SXF_NOCHECKPOSITION)
			TNT1 A 2
			Stop
		Melee:
		Crash:
			//TNT1 A 0 A_PlaySound("Painkiller/Ricochet",CHAN_7,0.33)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ShotgunSmoke",0,0,0,0,0,frandom(0.5,1.0),0,SXF_NOCHECKPOSITION)
			TNT1 A 2
			Stop
	}
}

Actor Painkiller_Shotgun_IceShot
{
	Projectile
	Speed 25
	Damage (0)
	ExplosionDamage 0
	Scale 0.4
	DamageType "PainkillerIce"
	Alpha 0.8
	Renderstyle Add
	Height 16
	Radius 8
	+FORCERADIUSDMG
	+FORCEPAIN
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			PK00 J 0
			PK00 J 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
		SpawnLoop:
			//PK00 J 0 A_SpawnItemEx("Painkiller_PainkillerLaserSegment",-40,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			PK00 J 0 A_SpawnItemEx("Painkiller_Shotgun_IceParticle",-8,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			PK00 J 2 Bright A_SpawnItemEx("Painkiller_Trail",-8,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			Loop
		Death:
			PK00 J 0 A_Explode(-1,16,!XF_HURTSOURCE,0,16)
			PK00 J 2 Bright A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			Stop
	}
}

Actor Painkiller_Shotgun_IceParticle
{
	Scale 0.4
	Alpha 0.8
	Renderstyle Add
	+NOINTERACTION
	+NOGRAVITY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			PK00 J 2 Bright A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 J 2 Bright A_Fadeout(0.1) PK00 J 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			PK00 H 2 Bright A_Fadeout(0.1) PK00 H 0 A_SetScale(scalex-0.05)
			Stop
	}
}

Actor Painkiller_Shotgun_FireSmoke : Painkiller_Smoke
{
	Scale 0.05
	States
	{
		Spawn:
			TNT1 A 0
			PK00 DDDDDDDDDDDDDDDD 3 A_Fadeout(0.05)
			Stop
	}
}

Actor Painkiller_IceChunk : IceChunk { Translation "0:255=%[0,0,0]:[1.6,2.0,2.0]" +FORCEXYBILLBOARD }

Actor Painkiller_IceShatter : CustomInventory
{
	-COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
		Spawn:
			TNT1 A 0 
			TNT1 A 1
			Stop
		Pickup:
			TNT1 A 0 A_JumpIfInventory("Painkiller_NoBloodFlag",1,"Finish")
			TNT1 A 0 A_GiveInventory("Painkiller_Gibbed",1)
			TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Painkiller_Blood_Spray",0,0,height*2,frandom(-4,4),frandom(-4,4),velz+frandom(4,8),0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
			TNT1 AAAAAAAA 0 A_SpawnItemEx("Painkiller_Blood",random(-8,8),random(-8,8),random(0,44),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
			TNT1 AAAAAAAA 0 A_SpawnItemEx("Painkiller_GibGeneric",0,0,height*2,velx+frandom(-4,4),vely+frandom(-4,4),velz+frandom(4,8),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			TNT1 A 0 A_PlaySound("Painkiller/Freeze/Gib",CHAN_BODY,1.0)
		Finish:	
			TNT1 A 0 A_Burst("Painkiller_IceChunk")
			TNT1 A 0 A_BossDeath
			TNT1 A 0 A_KillMaster
			TNT1 A 1
			stop
	}
}

Actor Painkiller_ShotgunSmoke
{
	Radius 2
	Height 4
	Renderstyle Translucent
	Scale 0.1
	Alpha 0.33
	+NOINTERACTION
	+NOTRIGGER
	+THRUACTORS
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			TNT1 A 0
			PK00 E 0 A_Fadeout(0.033)	PK06 E 2 A_SetScale(scalex+0.01)
			PK00 E 0 A_Fadeout(0.033)	PK06 E 2 A_SetScale(scalex+0.01)
			PK00 E 0 A_Fadeout(0.033)	PK06 E 2 A_SetScale(scalex+0.01)
			PK00 E 0 A_Fadeout(0.033)	PK06 E 2 A_SetScale(scalex+0.01)
			PK00 E 0 A_Fadeout(0.033)	PK06 E 2 A_SetScale(scalex+0.01)
			PK00 F 0 A_Fadeout(0.033)	PK06 F 2 A_SetScale(scalex+0.01)
			PK00 F 0 A_Fadeout(0.033)	PK06 F 2 A_SetScale(scalex+0.01)
			PK00 F 0 A_Fadeout(0.033)	PK06 F 2 A_SetScale(scalex+0.01)
			PK00 F 0 A_Fadeout(0.033)	PK06 F 2 A_SetScale(scalex+0.01)
			PK00 F 0 A_Fadeout(0.033)	PK06 F 2 A_SetScale(scalex+0.01)
			Stop
	}
}