ACTOR Painkiller_Painkiller : PainkillerWeapon
{
	Weapon.SelectionOrder 1000
	Weapon.SlotNumber 0
	Obituary "%o was shredded by %k's painkiller."
	+WEAPON.NOALERT
	Inventory.Pickupmessage "Painkiller"
	Inventory.PickupSound "Painkiller/Painkiller/Pickup"
	Tag "Painkiller"
	States
	{
		Select:
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingShotgun",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingStake",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingRifle",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingChaingun",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingElectro",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingBolt",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingDemon",1)
			Goto Super::Select
		Deselect:
			TNT1 A 0 A_StopSound(CHAN_6)
			Goto Super::Deselect
		Ready:
			PK00 ABCDEFGHIJKL 10 A_WeaponReady
			Loop
		Fire:
			PK01 A 0 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK01 A 0 A_JumpIfInventory("Painkiller_PainkillerAlt",1,"StartReturn")
			PK01 A 0 A_JumpIfInventory("Painkiller_PainkillerCombo",1,"StartReturnCombo")
			PK01 A 0 A_PlaySound("Painkiller/Painkiller/Start",CHAN_5)
			PK01 A 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK01 B 0 A_Refire(1)
			Goto Ready
			PK01 B 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK01 C 0 A_Refire(1)
			Goto Ready
			PK01 C 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK01 D 0 A_Refire(1)
			Goto Ready
			PK01 D 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK01 E 0 A_Refire(1)
			Goto Ready
			PK01 E 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK01 F 0 A_Refire(1)
			Goto Ready
			PK01 F 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK01 G 0 A_Refire(1)
			Goto Ready
			PK01 G 0 A_PlaySound("Painkiller/Painkiller/Loop",CHAN_6,1.0,true)
			PK01 GHI 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			Goto FireHold
		FireHold:
			PK02 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,"FireMod")
			PK02 A 0 A_JumpIfInventory("Painkiller_PainkillerAlt",1,"IdleAltWaitCombo")
			PK02 A 0 A_JumpIfInventory("CoopModeOn",1,"FireHoldCoop")
			PK02 A 2 A_FireCustomMissile("Painkiller_PainkillerMissile")
			PK02 B 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 C 0 A_Refire(1)
			Goto StopFire
			PK02 C 2 A_FireCustomMissile("Painkiller_PainkillerMissile")
			PK02 D 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 E 0 A_Refire(1)
			Goto StopFire
			PK02 E 2 A_FireCustomMissile("Painkiller_PainkillerMissile")
			PK02 F 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 G 0 A_Refire(1)
			Goto StopFire
			PK02 G 2 A_FireCustomMissile("Painkiller_PainkillerMissile")
			PK02 H 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 I 0 A_Refire(1)
			Goto StopFire
			PK02 I 2 A_FireCustomMissile("Painkiller_PainkillerMissile")
			PK02 J 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 K 0 A_Refire("FireHold")
			Goto StopFire
		FireHoldCoop:
			PK02 A 2 A_FireCustomMissile("Painkiller_PainkillerMissileCoop")
			PK02 B 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 C 0 A_Refire(1)
			Goto StopFire
			PK02 C 2 A_FireCustomMissile("Painkiller_PainkillerMissileCoop")
			PK02 D 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 E 0 A_Refire(1)
			Goto StopFire
			PK02 E 2 A_FireCustomMissile("Painkiller_PainkillerMissileCoop")
			PK02 F 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 G 0 A_Refire(1)
			Goto StopFire
			PK02 G 2 A_FireCustomMissile("Painkiller_PainkillerMissileCoop")
			PK02 H 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 I 0 A_Refire(1)
			Goto StopFire
			PK02 I 2 A_FireCustomMissile("Painkiller_PainkillerMissileCoop")
			PK02 J 2 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 K 0 A_Refire("FireHold")
			Goto StopFire
		FireMod:
			PK02 A 0 A_JumpIfInventory("Painkiller_PainkillerAlt",1,"IdleAltWaitCombo")
			PK02 A 0 A_JumpIfInventory("CoopModeOn",1,"FireModCoop")
			PK02 A 2 A_FireCustomMissile("Painkiller_PainkillerMissileMod")
			PK02 B 1 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 C 0 A_Refire(1)
			Goto StopFire
			PK02 C 2 A_FireCustomMissile("Painkiller_PainkillerMissileMod")
			PK02 D 1 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 E 0 A_Refire(1)
			Goto StopFire
			PK02 E 2 A_FireCustomMissile("Painkiller_PainkillerMissileMod")
			PK02 F 1 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 G 0 A_Refire(1)
			Goto StopFire
			PK02 G 2 A_FireCustomMissile("Painkiller_PainkillerMissileMod")
			PK02 H 1 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 I 0 A_Refire(1)
			Goto StopFire
			PK02 I 2 A_FireCustomMissile("Painkiller_PainkillerMissileMod")
			PK02 J 1 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 K 0 A_Refire("FireHold")
			Goto StopFire
		FireModCoop:
			PK02 A 2 A_FireCustomMissile("Painkiller_PainkillerMissileModCoop")
			PK02 B 1 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 C 0 A_Refire(1)
			Goto StopFire
			PK02 C 2 A_FireCustomMissile("Painkiller_PainkillerMissileModCoop")
			PK02 D 1 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 E 0 A_Refire(1)
			Goto StopFire
			PK02 E 2 A_FireCustomMissile("Painkiller_PainkillerMissileModCoop")
			PK02 F 1 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 G 0 A_Refire(1)
			Goto StopFire
			PK02 G 2 A_FireCustomMissile("Painkiller_PainkillerMissileModCoop")
			PK02 H 1 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 I 0 A_Refire(1)
			Goto StopFire
			PK02 I 2 A_FireCustomMissile("Painkiller_PainkillerMissileModCoop")
			PK02 J 1 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK02 K 0 A_Refire("FireHold")
			Goto StopFire
		StopFire:
			PK02 A 0 A_StopSound(CHAN_6)
			PK02 A 0 A_PlaySound("Painkiller/Painkiller/Stop",CHAN_5)
			PK03 ABCDEFGHIJKLMN 3
			PK03 N 0
			Goto Ready
		AltFire:
			PK04 A 0 A_JumpIfInventory("Painkiller_WeaponModifier_Token",1,"AltFireMod")
			PK04 A 0 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK04 A 0 A_JumpIfInventory("Painkiller_PainkillerCombo",1,"StartReturnCombo")
			PK04 A 0 A_JumpIfInventory("Painkiller_PainkillerAlt",1,"StartReturn")
			PK04 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
			PK04 A 0 A_PlaySound("Painkiller/Painkiller/Shoot",CHAN_5)
			PK04 A 0 A_JumpIfInventory("CoopModeOn",1,"AltFireCoop")
			PK04 A 0 A_FireCustomMissile("Painkiller_Painkiller_Head")
			Goto AltFireEnd
		AltFireCoop:
			PK04 A 0 A_FireCustomMissile("Painkiller_Painkiller_HeadCoop")
			Goto AltFireEnd
		AltFireMod:
			PK04 A 0 A_JumpIfInventory("Painkiller_Combo",1,"Combo")
			PK04 A 0 A_JumpIfInventory("Painkiller_PainkillerCombo",1,"StartReturnCombo")
			PK04 A 0 A_JumpIfInventory("Painkiller_PainkillerAlt",1,"StartReturn")
			PK04 A 0 A_PlaySound("Painkiller/Painkiller/Shoot",CHAN_5)
			PK04 A 0 A_JumpIfInventory("CoopModeOn",1,"AltFireModCoop")
			PK04 A 0 A_FireCustomMissile("Painkiller_Painkiller_HeadMod")
			Goto AltFireEnd
		AltFireModCoop:
			PK04 A 0 A_FireCustomMissile("Painkiller_Painkiller_HeadModCoop")
			Goto AltFireEnd
		AltFireEnd:
			PK04 A 1 A_GiveInventory("Painkiller_PainkillerAlt",1)
			PK04 BCDE 1
			PK04 F 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 F 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY)
			PK04 G 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 G 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY)
			PK04 H 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 H 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 I 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 I 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 J 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 J 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 K 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 K 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 L 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 L 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 M 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 M 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 N 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 N 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 O 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 O 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 P 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 P 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 Q 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 Q 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 R 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 R 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 S 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 S 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 T 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 T 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 U 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 U 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 V 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 V 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 W 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 W 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 X 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 X 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 Y 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 Y 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 Z 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return") PK04 Z 2 A_WeaponReady(WRF_NOPRIMARY)
			Goto IdleAlt
		IdleAltWait:
			PK06 AAABBBCCCDDDEEEFFFGGGHHHIIIJJJKKKLLL 3 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Return")
			Loop
		IdleAlt:
			PK06 ABCDEFGHIJKL 10 A_WeaponReady
			Loop
		StartReturn:
			PK05 A 0 A_TakeInventory("Painkiller_PainkillerAlt",1)
			Goto IdleAltWait
		Return:
			PK05 A 0 A_TakeInventory("Painkiller_PainkillerCombo",1)
			PK05 A 0 A_TakeInventory("Painkiller_PainkillerAlt",1)
			PK05 A 0 A_TakeInventory("Painkiller_PainkillerReturned",1)
			PK05 A 0 A_PlaySound("Painkiller/Painkiller/Reload",CHAN_5)
			PK05 ABCDEFGHIJKLMNOP 2 A_WeaponReady(WRF_NOSWITCH)
			PK05 P 0
			Goto Ready
		Combo:
			PK04 A 0 A_StopSound(CHAN_6)
			PK04 A 0 A_JumpIfInventory("Painkiller_PainkillerCombo",1,"StartReturnCombo")
			PK04 A 0 A_PlaySound("Painkiller/Painkiller/Shoot",CHAN_5)
			PK04 A 0 A_JumpIfInventory("CoopModeOn",1,"ComboCoop")
			PK04 A 0 A_FireCustomMissile("Painkiller_Painkiller_Blades")
			Goto ComboEnd
		ComboCoop:
			PK04 A 0 A_FireCustomMissile("Painkiller_Painkiller_BladesCoop")
			Goto ComboEnd
		ComboEnd:
			PK04 A 1 A_GiveInventory("Painkiller_PainkillerCombo",1)
			PK04 BCDE 1
			PK04 F 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 F 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY)
			PK04 G 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 G 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY)
			PK04 H 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 H 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY)
			PK04 I 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 I 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY)
			PK04 J 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 J 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 K 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 K 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 L 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 L 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 M 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 M 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 N 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 N 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 O 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 O 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 P 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 P 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 Q 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 Q 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 R 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 R 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 S 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 S 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 T 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 T 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 U 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 U 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 V 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 V 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 W 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 W 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 X 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 X 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 Y 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 Y 2 A_WeaponReady(WRF_NOPRIMARY)
			PK04 Z 0 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo") PK04 Z 2 A_WeaponReady(WRF_NOPRIMARY)
			Goto IdleAltWaitCombo
		StartReturnCombo:
			PK05 A 0 A_TakeInventory("Painkiller_PainkillerCombo",1)
			PK05 A 0 A_TakeInventory("Painkiller_PainkillerAlt",1)
			Goto IdleAltWaitCombo			
		IdleAltCombo:
			PK06 ABCDEFGHIJKL 10 A_WeaponReady
			Loop	
		IdleAltWaitCombo:
			PK06 AAABBBCCCDDDEEEFFFGGGHHHIIIJJJKKKLLL 3 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"ReturnCombo")
			Loop
		ReturnCombo:
			PK05 A 0 A_TakeInventory("Painkiller_PainkillerCombo",1)
			PK05 A 0 A_TakeInventory("Painkiller_PainkillerReturned",1)
			PK05 A 0 A_TakeInventory("Painkiller_PainkillerAlt",1)
			PK05 A 0 A_PlaySound("Painkiller/Painkiller/Reload",CHAN_5)
			PK05 ABCDEFGHIJKLMNOP 2
			PK05 P 0
			Goto Ready
	}
}

Actor Painkiller_PainkillerMissile : PainkillerProjectile
{
	Radius 16
	Height 32
	Speed 24
	Damage (10)
	DamageType "Painkiller"
	States
	{
		Spawn:
			TNT1 AA 1
			stop
		Death:
		Crash:
			TNT1 A 1 A_PlaySound("Painkiller/Painkiller/HitWall",CHAN_BODY)
			stop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Painkiller_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 1 A_PlaySound("Painkiller/Painkiller/Hit",CHAN_BODY)
			stop
	}
}

Actor Painkiller_PainkillerMissileCoop : Painkiller_PainkillerMissile { +THRUSPECIES Species "Player" }

Actor Painkiller_PainkillerMissileMod : Painkiller_PainkillerMissile { Damage (15) }

Actor Painkiller_PainkillerMissileModCoop : Painkiller_PainkillerMissileMod { +THRUSPECIES Species "Player" }

Actor Painkiller_Painkiller_Head
{
	Projectile
	Radius 4
	Height 8
	Speed 24
	Damage (random(15,38))
	DamageType "Painkiller"
	+SKYEXPLODE
	+DONTREFLECT
	+EXPLODEONWATER
	States
	{
		Spawn:
			TNT1 A 0
			PK00 A 0 ACS_NamedExecuteWithResult("Painkiller_BeamSpawner")
			PK00 AB 1
		SpawnLoop:
			PK00 C 0 A_JumpIfInventory("Painkiller_PainkillerAlt",1,1,AAPTR_Target)
			Goto Return
			PK00 C 2
			Loop
		Stay:
			PK00 C 0 A_JumpIfInventory("Painkiller_PainkillerAlt",1,1,AAPTR_Target)
			Goto Return
			PK00 C 2 A_GiveToTarget("Painkiller_Painkiller_Rail",1)
			Loop
		Return:
			PK00 A 0 A_SpawnItemEx("Painkiller_Painkiller_Head_Return",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TNT1 A 2
			stop
		Death:
		Crash:
			TNT1 A 1 A_PlaySound("Painkiller/Painkiller/HitWall",CHAN_BODY)
			Goto Stay
		XDeath:
			TNT1 A 1 A_PlaySound("Painkiller/Painkiller/Hit",CHAN_BODY)
			Goto Return
	}
}

Actor Painkiller_Painkiller_HeadCoop : Painkiller_Painkiller_Head { +THRUSPECIES Species "Player" }

Actor Painkiller_Painkiller_HeadMod : Painkiller_Painkiller_Head
{
	Damage (Random(20,50))
	States
	{
		Spawn:
			TNT1 A 0
			PK00 A 0 ACS_NamedExecuteWithResult("Painkiller_BeamSpawner",1)
			PK00 AB 1
		SpawnLoop:
			PK00 C 0 A_JumpIfInventory("Painkiller_PainkillerAlt",1,1,AAPTR_Target)
			Goto Return
			PK00 C 2
			Loop
	}
}

Actor Painkiller_Painkiller_HeadModCoop : Painkiller_Painkiller_HeadMod { +THRUSPECIES Species "Player" }

Actor Painkiller_Painkiller_Blades : PainkillerProjectile
{
	Radius 8
	Height 16
	Speed 12
	Damage (100)
	DamageType "Painkiller"
	+RIPPER
	+THRUACTORS
	+SKYEXPLODE
	+DONTREFLECT
	+EXPLODEONWATER
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("Painkiller/Painkiller/Loop",CHAN_BODY,0.5,true)
		SpawnLoop:
			PK00 A 0 A_JumpIfInventory("Painkiller_PainkillerCombo",1,1,AAPTR_Target)
			Goto Return
			PK00 AA 1 A_Explode(100,12, 0, 1, 12)
			Loop
		Return:
			PK00 A 0 A_SpawnItemEx("Painkiller_Painkiller_Blades_Return",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TNT1 A 2
			stop
		Death:
		Crash:
			TNT1 A 1 A_PlaySound("Painkiller/Painkiller/HitWall",CHAN_BODY)
			Goto Return
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Painkiller_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 1 A_PlaySound("Painkiller/Painkiller/Hit",CHAN_BODY)
			Goto Return
	}
}

Actor Painkiller_Painkiller_BladesCoop : Painkiller_Painkiller_Blades { +THRUSPECIES Species "Player" }

Actor Painkiller_Painkiller_Blades_Return
{
	+NOGRAVITY
	+THRUACTORS
	+NOINTERACTION
	-SOLID
	+DROPOFF
	+NOCLIP
	Height 16
	Radius 8
	States
	{
		Spawn:
			TNT1 A 0
			PK00 A 0 ACS_NamedExecuteAlways("Painkiller_Painkiller_WarpBack")
		SpawnLoop:
			PK00 A 2 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Remove",AAPTR_TARGET)
			Loop
		Remove:
			TNT1 A 2
			Stop
	}
}

Actor Painkiller_Painkiller_Head_Return
{
	+NOGRAVITY
	+THRUACTORS
	+NOINTERACTION
	-SOLID
	+DROPOFF
	+NOCLIP
	Height 16
	Radius 8
	States
	{
		Spawn:
			TNT1 A 0
			//TNT1 A 0 A_TransferPointer(AAPTR_MASTER,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			PK00 C 0 ACS_NamedExecuteAlways("Painkiller_Painkiller_WarpBack")
		SpawnLoop:
			PK00 C 2 A_JumpIfInventory("Painkiller_PainkillerReturned",1,"Remove",AAPTR_TARGET)
			Loop
		Remove:
			TNT1 A 2
			Stop
	}
}

Actor Painkiller_Painkiller_BeamFire
{
	-SOLID
	+THRUACTORS
	-SHOOTABLE
	+NOINTERACTION
	+DONTHARMSPECIES
	+DONTHURTSPECIES
	Species "Player"
	Height 4
	Radius 2
	States
	{
		Spawn:
			PK00 A 0
		SpawnLoop:
			PK00 A 0 A_JumpIfInventory("Painkiller_PainkillerAlt",1,1,AAPTR_TARGET)
			Goto Remove
			TNT1 A 0 A_Warp(AAPTR_MASTER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION,1)
			TNT1 A 0 A_JumpIfInTargetLOS("SpawnLaser",20)
			TNT1 A 0 A_StopSound(CHAN_5)
			Goto SpawnLaser+2
		SpawnLaser:
			TNT1 A 0 A_PlaySound("Painkiller/Painkiller/AltLoop",CHAN_5,1.0,true)
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_Fire_Laser", 8, 0, 36)
			TNT1 A 2
			Goto SpawnLoop
		Remove:
			TNT1 A 0 A_StopSound(CHAN_5)
			TNT1 A 2
			Stop
	}
}

Actor Painkiller_Painkiller_BeamFire2 : Painkiller_Painkiller_BeamFire
{
	States
	{
		Spawn:
			PK00 A 0
		SpawnLoop:
			PK00 A 0 A_JumpIfInventory("Painkiller_PainkillerAlt",1,1,AAPTR_TARGET)
			Goto Super::Remove
			TNT1 A 0 A_Warp(AAPTR_MASTER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION,1)
			TNT1 A 0 A_JumpIfInTargetLOS("SpawnLaser",90)
			TNT1 A 0 A_StopSound(CHAN_5)
			Goto SpawnLaser+2
		SpawnLaser:
			TNT1 A 0 A_PlaySound("Painkiller/Painkiller/AltLoop",CHAN_5,1.0,true)
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_Fire_Laser", 8, 0, 36)
			TNT1 A 2
			Goto SpawnLoop
	}
}

Actor Painkiller_Painkiller_LaserEnd
{
	+BLOODLESSIMPACT
	+NOEXTREMEDEATH
	+ALWAYSPUFF
	+PUFFONACTORS
	+PUFFGETSOWNER
	+NOGRAVITY
	+NOBLOCKMAP
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 5 ACS_NamedExecuteWithResult("Painkiller_Laser",1,36,36)
			stop
	}
}

Actor Painkiller_Painkiller_Rail : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_RailAttack(15,5,0,None,None,RGF_SILENT|RGF_NORANDOMPUFFZ|RGF_EXPLICITANGLE,0,"Painkiller_Painkiller_RailPuff",CallACS("Painkiller_Laser_Offsets")/182.0 - angle,CallACS("Painkiller_Laser_Offsets",1)/182.0 - pitch)
			TNT1 A 0 A_TakeInventory("Painkiller_RailAngle",131072)
			TNT1 A 0 A_TakeInventory("Painkiller_RailPitch",131072)
			Stop
	}
}

Actor Painkiller_Painkiller_RailPuff
{
	+BLOODLESSIMPACT
	+NOEXTREMEDEATH
	+ALWAYSPUFF
	+PUFFONACTORS
	+PUFFGETSOWNER
	+NOGRAVITY
	+NOBLOCKMAP
	+HITTRACER
	DamageType "Painkiller"
	States
	{
		Spawn:
			TNT1 A 1
			Stop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Painkiller_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 0 A_PlaySound("Painkiller/Painkiller/AltHit")
			TNT1 A 5
			stop
	}
}

Actor Painkiller_Painkiller_Laser : FastProjectile
{
	+EXTREMEDEATH
	+SKYEXPLODE
	+DONTSPLASH
	+CANNOTPUSH
	+NODAMAGETHRUST	
	+THRUSPECIES
	+THRUACTORS
	+BLOODLESSIMPACT
	+HITTRACER
	Species "Player"
	Speed 256
	Radius 2
	Height 4
	DamageType "Painkiller"
	Damage (0)
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 AA 1
			Goto Death	
		Death:
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_Laser",1,36,0)
			TNT1 A 5
			stop
	}
}

Actor Painkiller_Painkiller_Laser_Damage : Painkiller_Painkiller_Laser
{
	-THRUACTORS
	-THRUSPECIES
	Damage (15)
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 AA 1
			Goto Death	
		Death:
			TNT1 A 5
			stop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Painkiller_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 0 A_PlaySound("Painkiller/Painkiller/AltHit")
			TNT1 A 5
			stop
	}
}

Actor Painkiller_PainkillerLaserSegment
{
	+NOINTERACTION
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	Radius 1
	Height 1
	Renderstyle Add
	Alpha 0.5
	States
	{
		Spawn:
			TNT1 A 0
			PK00 A 2 Bright
			stop
	}
}
