ACTOR Painkiller_Boltgun : PainkillerWeapon
{
	Weapon.SelectionOrder 700
	Weapon.SlotNumber 3
	Weapon.AmmoType1 "Cell"
	Weapon.AmmoUse1 5
	Weapon.AmmoGive1 30
	Weapon.BobRangeX 0
	Weapon.BobRangeY 1.2
	Weapon.AmmoType2 "Painkiller_HeaterAmmo"
	Weapon.AmmoUse2 10
	Weapon.AmmoGive2 20
	Obituary "%o was impaled by %k's boltgun."
	Inventory.Pickupmessage "Boltgun"
	Inventory.PickupSound "Painkiller/Boltgun/Pickup"
	Tag "Boltgun"
	States
	{
		Select:
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingShotgun",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingStake",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingRifle",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingChaingun",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingElectro",1)
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingDemon",1)
			TNT1 A 0 A_GiveInventory("Painkiller_HoldingBolt",1)
			Goto Super::Select
		Deselect:
			TNT1 A 0 A_TakeInventory("Painkiller_Zoomed",1)
			TNT1 A 0 A_ZoomFactor(1.0)
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
			TNT1 A 0 A_TakeInventory("Painkiller_HoldingBolt",1)
			loop
		Ready:
			PK00 ABCDEFGHIJK 10 A_WeaponReady(WRF_ALLOWZOOM)
			Loop
		Fire:
			PK01 A 2 A_PlaySound("Painkiller/BoltGun/BoltShoot",CHAN_5)
			PK01 B 0 A_FireCustomMissile("Painkiller_BoltProjectile",0,0,0,2)
			PK01 E 0 A_TakeInventory("Cell",1,TIF_NOTAKEINFINITE)
			PK01 B 2
			PK01 C 2 A_PlaySound("Painkiller/BoltGun/BoltShoot",CHAN_5)
			PK01 D 0 A_FireCustomMissile("Painkiller_BoltProjectile",0,0,8,1)
			PK01 D 0 A_FireCustomMissile("Painkiller_BoltProjectile",0,0,-8,1)
			PK01 E 0 A_TakeInventory("Cell",2,TIF_NOTAKEINFINITE)
			PK01 D 2
			PK01 E 2 A_PlaySound("Painkiller/BoltGun/BoltShoot",CHAN_5)
			PK01 E 0 A_FireCustomMissile("Painkiller_BoltProjectile",0,0,16)
			PK01 E 0 A_FireCustomMissile("Painkiller_BoltProjectile",0,0,-16)
			PK01 E 0 A_TakeInventory("Cell",2,TIF_NOTAKEINFINITE)
			PK01 F 2
			PK02 A 0 A_PlaySound("Painkiller/BoltGun/BoltReload",CHAN_5)
			PK02 ABCDFGHIKLMNOPQSTUVWXZ 2
			PK02 "[]" 2
			PK02 A 0
			Goto Ready
		AltFire:
			PK03 A 0 A_PlaySound("Painkiller/BoltGun/HeaterFire",CHAN_5)
			PK03 AAAAAAAAAA 0 A_FireCustomMissile("Painkiller_HeaterBomb",0,0,random(-32,32),random(-12,12),0,10)
			PK03 A 0 A_TakeInventory("Painkiller_HeaterAmmo",10,TIF_NOTAKEINFINITE)
			PK03 ABCDEFGHIJKLM 3
			PK03 NOPQRST 3 A_WeaponReady
			PK03 U 2 A_WeaponReady
			Goto Ready
		Zoom:
			PK00 A 0 A_JumpIfInventory("Painkiller_Zoomed",1,"ZoomOut")
			PK00 A 0 A_PlaySound("Painkiller/BoltGun/ZoomIn",CHAN_7)
			PK00 A 0 A_GiveInventory("Painkiller_Zoomed",1)
			PK00 A 0 A_JumpIf(CallACS("Painkiller_BoltGunScopeGraphic") == 1, 1)
			PK00 A 0 A_ZoomFactor(4.0)
			PK00 A 8
			Goto Ready
		ZoomOut:
			PK00 A 0 A_TakeInventory("Painkiller_Zoomed",1)
			PK00 A 0 A_ZoomFactor(1.0)
			PK00 A 8
			Goto Ready
		Spawn:
			PK99 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_Boltgun_Icon",0,0,40)
			PK99 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Painkiller_Boltgun_Icon2",0,0,40)
			Loop
	}
}

Actor Painkiller_Boltgun_Icon : Painkiller_Weapon_Icon
{
	States
	{
		Spawn:
			PK07 "[" 2
			Stop
	}
}

Actor Painkiller_Boltgun_Icon2 : Painkiller_Weapon_Icon
{
	States
	{
		Spawn:
			PK07 "]" 2
			Stop
	}
}

Actor Painkiller_BoltProjectile : FastProjectile
{
	+HITTRACER
	+NODAMAGETHRUST
	+NOEXTREMEDEATH
	+BLOODLESSIMPACT
	+FORCEXYBILLBOARD
	Decal "PainkillerStakeHole"
	DamageType "PainkillerStake"
	Speed 135
	Scale 0.5
	Damage (45)
	Height 4
	Radius 2
	States
	{
		Spawn:
			PK00 A 0
			//PK00 A 1
			//PK00 A 0 A_ChangeFlag("THRUACTORS",0)
			Goto SpawnLoop
		SpawnLoop:
			PK00 A 0 A_GiveInventory("Painkiller_StakeFlew",1)
			PK00 A 0 A_JumpIf(CallACS("Painkiller_ProjectilePitch")==1,1)
			PK00 A 2 A_SpawnItemEx("Painkiller_StakeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			Loop
		Death:
			PK00 B 0 A_Playsound("Painkiller/Stake/Default",CHAN_6,0.5)
			PK00 B 0 A_ChangeFlag("NOGRAVITY",true)
			PK00 B 0 A_JumpIfInventory("Painkiller_StakeFlew",1,2)
			PK00 B 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect1",0,0,0,0,0,-0.15,0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect2",0,0,0,0,0,frandom(0.15,0.5),0,SXF_NOCHECKPOSITION)
			PK00 B 105
			PK00 BBBBBBBBB 2 A_Fadeout(0.1)
			Stop
		XDeath:
			PK00 B 0 A_JumpIf(CallACS("Painkiller_StakeShatter")==2,"Remove")
			PK00 B 0 A_Playsound("Painkiller/Stake/Body",CHAN_5,0.5)
			PK00 B 0 A_JumpIf(CallACS("Painkiller_StakeShatter")==1,"SpawnAnother")
			PK00 B 0 A_JumpIfInventory("Painkiller_StakeFlew",1,2)
			PK00 B 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			PK00 B 0 A_SpawnItemEx("Painkiller_BoltDebris1",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_BoltDebris2",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_BoltDebris3",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_BoltDebris4",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_BoltDebris5",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION)
			TNT1 A 2
			Stop
		SpawnAnother:
			PK00 A 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			PK00 A 0 A_JumpIf(CallACS("Painkiller_ForceStakeBodyRemoval")==1,1)
			TNT1 A 1
			TNT1 A 0 A_CustomMissile("Painkiller_BoltIntermediate",0,0,0,CMF_AIMDIRECTION|CMF_SAVEPITCH,-pitch)
			TNT1 A 0 
			TNT1 A 5
			Stop
		Remove:
			TNT1 A 5
			Stop
	}
}

Actor Painkiller_BoltProjectileDM : Painkiller_BoltProjectile { Damage (25) } // actually 20, but that seems a little weak

Actor Painkiller_BoltIntermediate
{
	+ISMONSTER
	-COUNTKILL
	-SOLID
	+THRUACTORS
	-SHOOTABLE
	Speed 0
	Height 0
	Radius 0
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Stop
			TNT1 A 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			//TNT1 A 0 A_SpawnItemEx("Painkiller_BoltProjectilePenetrate",0,0,0,cos(pitch)*135,0,-sin(pitch)*135,0,SXF_TRANSFERPOINTERS)
			TNT1 A 0 A_CustomMissile("Painkiller_BoltProjectilePenetrate",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,-pitch)
			TNT1 A 3
			Stop
	}
}

Actor Painkiller_BoltProjectilePenetrate : Painkiller_BoltProjectile
{
	+THRUACTORS
	States
	{
		Spawn:
			PK00 A 0
			PK00 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			PK00 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_TRACER)
			PK00 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			PK00 A 0 A_JumpIf(CallACS("Painkiller_StakeRetrieveTracer")==1,1)
			PK00 A 0 A_GiveInventory("Painkiller_StakeFlew",1)
			PK00 A 0
			Goto SpawnLoop
		SpawnLoop:
			PK00 A 0 A_GiveInventory("Painkiller_StakeFlew",1)
			PK00 A 0 A_JumpIf(CallACS("Painkiller_ProjectilePitch")==1,1)
			PK00 A 2 A_SpawnItemEx("Painkiller_StakeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			Loop
		Death:
			PK00 B 0 A_Playsound("Painkiller/Stake/Default",CHAN_6,0.5)
			PK00 B 0 A_ChangeFlag("NOGRAVITY",true)
			PK00 B 0 A_JumpIfInventory("Painkiller_StakeFlew",1,6)
			PK00 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			PK00 A 0 A_SetPitch(CallACS("Painkiller_ShooterPitch")/182)
			PK00 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_TRACER)
			PK00 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			PK00 A 0 A_JumpIf(CallACS("Painkiller_StakeRetrieveTracer")==1,1)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect1",0,0,0,0,0,-0.15,0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_StakeHitWallEffect2",0,0,0,0,0,frandom(0.15,0.5),0,SXF_NOCHECKPOSITION)
			PK00 B 105
			PK00 BBBBBBBBB 2 A_Fadeout(0.1)
			Stop
		XDeath:
			PK00 B 0 A_JumpIfInventory("Painkiller_StakeFlew",1,2)
			PK00 B 0 A_SetPitch(-CallACS("Painkiller_ShooterPitch")/182)
			PK00 B 105
			PK00 BBBBBBBBB 2 A_Fadeout(0.1)
			Stop
		SpawnAnother:
			PK00 B 105
			PK00 BBBBBBBBB 2 A_Fadeout(0.1)
			Stop
	}
}

Actor Painkiller_HeaterBomb : PainkillerProjectile
{
	-NOGRAVITY
	+NODAMAGETHRUST
	+THRUSPECIES
	+FORCERADIUSDMG
	-SKYEXPLODE
	DamageType "PainkillerExplosive"
	BounceType "Doom"
	BounceCount 30
	BounceFactor 0.6
	WallBounceFactor 0.5
	BounceSound "Painkiller/BoltGun/HeaterBounce"
	Species "PainkillerGrenade"
	//+THRUACTORS
	Scale 0.5
	Gravity 0.15
	Speed 12
	Damage (0)
	Height 4
	Radius 2
	ReactionTime 87
	var int user_pitch;
	var int user_timing;
	States
	{
		Spawn:
			PK00 A 0
			PK00 A 0 A_SetGravity(frandom(0.14,0.16))
			PK00 A 0 A_ChangeVelocity(random(-2,2),0,0,CVF_RELATIVE)
			PK00 A 0 A_SetUserVar("user_pitch",-CallACS("Painkiller_ShooterPitch")/182)
			PK00 A 0 A_JumpIf(CallACS("Painkiller_SpawnGrenadeHitBox",2) == 1, 1)
			//PK00 A 0 A_JumpIf(CallACS("Painkiller_GrenadeTidToken")==1,1)
			PK00 B 1
			//PK00 A 0 A_SpawnItemEx("Painkiller_GrenadeHitBox",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			PK00 B 2
			//PK00 A 1
			//PK00 A 0 A_ChangeFlag("THRUACTORS",0)
			Goto SpawnLoop
		SpawnLoop:
			PK00 B 0 A_Jump(32,2)
			PK00 B 0 A_Countdown
			PK00 B 0 A_SetUserVar("user_timing",user_timing+1)
			//PK00 A 0 A_SetPitch(pitch+5)
			PK00 B 0 A_JumpIf(CallACS("Painkiller_ProjectilePitch")==1,1)
			//PK00 B 0 A_SpawnItemEx("Painkiller_Smoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_JumpIf(user_timing > 175,"SpawnGlow3")
			PK00 B 0 A_JumpIf(user_timing > 70,"SpawnGlow2")
			Goto SpawnGlow1
		SpawnGlow1:
			PK00 B 0 A_SpawnItemEx("Painkiller_HeaterBombGlowEffect1",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			PK00 B 2 A_SpawnItemEx("Painkiller_GrenadeTrail",8,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			Goto SpawnLoop
		SpawnGlow2:
			PK00 B 0 A_SpawnItemEx("Painkiller_HeaterBombGlowEffect2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			PK00 B 2 A_SpawnItemEx("Painkiller_GrenadeTrail",8,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			Goto SpawnLoop
		SpawnGlow3:
			PK00 B 0 A_SpawnItemEx("Painkiller_HeaterBombGlowEffect3",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			PK00 B 2 A_SpawnItemEx("Painkiller_GrenadeTrail",8,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			Goto SpawnLoop
		Launch:
			PK00 A 0 A_SetUserVar("user_pitch",-CallACS("Painkiller_ShooterPitch")/182)
			PK00 B 0 A_CustomMissile("Painkiller_HeaterBombLaunch",0,0,CallACS("Painkiller_ShooterAngle")/182,CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE,user_pitch)
			TNT1 A 2
			Stop
		Death:
			PK00 B 0 A_PlaySound("Painkiller/BoltGun/HeaterExplosion",CHAN_5)
			PK00 B 0 A_Explode(35,45)
			PK00 B 0 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect1",0,0,0,0,0,frandom(0.1,0.3),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect2",0,0,0,0,0,frandom(0.25,0.5),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect3",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25),frandom(0.1,0.3),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect2",0,0,0,frandom(-0.33,0.33),frandom(-0.33,0.33),frandom(0.25,0.5),0,SXF_NOCHECKPOSITION)
			TNT1 A 2
			Stop
	}
}

Actor Painkiller_HeaterBombDM : Painkiller_HeaterBomb
{
	States
	{
		Death:
			PK00 B 0 A_PlaySound("Painkiller/BoltGun/HeaterExplosion",CHAN_5)
			PK00 B 0 A_Explode(20,45)
			PK00 B 0 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect1",0,0,0,0,0,frandom(0.1,0.3),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect2",0,0,0,0,0,frandom(0.25,0.5),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect3",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25),frandom(0.1,0.3),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect2",0,0,0,frandom(-0.33,0.33),frandom(-0.33,0.33),frandom(0.25,0.5),0,SXF_NOCHECKPOSITION)
			TNT1 A 2
			Stop
	}
}
Actor Painkiller_HeaterBombGlowEffect1 : Painkiller_Smoke
{
	Scale 0.1
	Renderstyle Add
	Alpha 1.0
	States
	{
		Spawn:
			TNT1 A 0
			PK07 LLLL 1 A_Fadeout(0.2)
			Stop
	}
}

Actor Painkiller_HeaterBombGlowEffect2 : Painkiller_Smoke
{
	Scale 0.1
	Renderstyle Add
	Alpha 1.0
	States
	{
		Spawn:
			TNT1 A 0
			PK07 MMMM 1 A_Fadeout(0.2)
			Stop
	}
}

Actor Painkiller_HeaterBombGlowEffect3 : Painkiller_Smoke
{
	Scale 0.1
	Renderstyle Add
	Alpha 1.0
	States
	{
		Spawn:
			TNT1 A 0
			PK07 NNNN 1 A_Fadeout(0.2)
			Stop
	}
}

Actor Painkiller_HeaterBombExplodeEffect1 : Painkiller_Smoke
{
	Renderstyle Translucent
	Alpha 0.5
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SetScale(frandom(0.4,0.45))
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.05)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.05)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.05)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.05)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.05)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.05)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.05)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.05)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.05)
			Stop
	}
}

Actor Painkiller_HeaterBombExplodeEffect2 : Painkiller_Smoke
{
	Renderstyle Add
	Alpha 1.0
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SetScale(frandom(0.4,0.45))
			PK07 A 0 A_SetScale(scalex+0.025)	PK07 A 1 Bright A_Fadeout(0.1)
			PK07 A 0 A_SetScale(scalex+0.025)	PK07 A 1 Bright A_Fadeout(0.1)
			PK07 A 0 A_SetScale(scalex+0.025)	PK07 A 1 Bright A_Fadeout(0.1)
			PK07 A 0 A_SetScale(scalex+0.025)	PK07 A 1 Bright A_Fadeout(0.1)
			PK07 A 0 A_SetScale(scalex+0.025)	PK07 A 1 Bright A_Fadeout(0.1)
			PK07 A 0 A_SetScale(scalex+0.025)	PK07 A 1 Bright A_Fadeout(0.1)
			PK07 A 0 A_SetScale(scalex+0.025)	PK07 A 1 Bright A_Fadeout(0.1)
			PK07 A 0 A_SetScale(scalex+0.025)	PK07 A 1 Bright A_Fadeout(0.1)
			PK07 A 0 A_SetScale(scalex+0.025)	PK07 A 1 Bright A_Fadeout(0.1)
			Stop
	}
}

Actor Painkiller_HeaterBombExplodeEffect3 : Painkiller_Smoke
{
	Renderstyle Translucent
	Alpha 0.6
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SetScale(frandom(0.19,0.26))
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.06)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.06)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.06)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.06)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.06)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.06)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.06)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.06)
			PK07 O 0 A_SetScale(scalex+0.025)	PK07 O 1 A_Fadeout(0.06)
			Stop
	}
}

Actor Painkiller_BoltDebris1 : Painkiller_StakeDebris1 { Scale 2.0 }
Actor Painkiller_BoltDebris2 : Painkiller_BoltDebris1 {}
Actor Painkiller_BoltDebris3 : Painkiller_BoltDebris1 {}
Actor Painkiller_BoltDebris4 : Painkiller_BoltDebris1 {}
Actor Painkiller_BoltDebris5 : Painkiller_BoltDebris1 {}

Actor Painkiller_HeaterHitBox
{
	Height 8
	Radius 4
	Health 1
	Mass 50000
	+ISMONSTER
	-COUNTKILL
	//+THRUACTORS
	-SOLID
	+SHOOTABLE
	+NOBLOOD
	+NOGRAVITY
	DamageFactor "PainkillerStake", 1.0
	DamageFactor "Normal", 0.0
	Species "PainkillerGrenade"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0
		SpawnLoop:
			TNT1 A 2 A_Warp(AAPTR_TRACER,0,0,0,0,WARPF_NOCHECKPOSITION)
			Loop
		Death:
			TNT1 A 2
			Stop
		Death.PainkillerStake:
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_StakeGrenadeComboLaunch")==1,1)
			TNT1 A 2
			Stop
	}
}

Actor Painkiller_HeaterBombLaunch : FastProjectile
{
	+HITTRACER
	+NODAMAGETHRUST
	+NOEXTREMEDEATH
	//+THRUACTORS
	DamageType "PainkillerExplosive"
	Speed 135
	Scale 0.5
	Damage (0)
	Height 4
	Radius 2
	States
	{
		Spawn:
			PK00 A 0
			//PK00 A 1
			//PK00 A 0 A_ChangeFlag("THRUACTORS",0)
			Goto SpawnLoop
		SpawnLoop:
			PK00 A 0 A_GiveInventory("Painkiller_StakeFlew",1)
			PK00 A 0 A_JumpIf(CallACS("Painkiller_ProjectilePitch")==1,1)
			PK00 A 2 A_SpawnItemEx("Painkiller_StakeTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
			Loop
		Death:
			PK00 B 0 A_PlaySound("Painkiller/BoltGun/HeaterExplosion",CHAN_5)
			PK00 B 0 A_Explode(200,45)
			PK00 B 0 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect1",0,0,0,0,0,frandom(0.1,0.3),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect2",0,0,0,0,0,frandom(0.25,0.5),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect3",0,0,0,frandom(-0.25,0.25),frandom(-0.25,0.25),frandom(0.1,0.3),0,SXF_NOCHECKPOSITION)
			PK00 B 0 A_SpawnItemEx("Painkiller_HeaterBombExplodeEffect2",0,0,0,frandom(-0.33,0.33),frandom(-0.33,0.33),frandom(0.25,0.5),0,SXF_NOCHECKPOSITION)
			TNT1 A 2
			Stop
	}
}