actor " Chainsaw " : Chainsaw replaces Chainsaw // doom 64 chainsaw attacks twice as fast
{
    Weapon.Kickback 0
    Weapon.SelectionOrder 2200
    Weapon.SlotNumber 1
    Weapon.SlotPriority 0
    Decal BulletChip
    Weapon.UpSound "WHAT UPSOUND"
    Weapon.ReadySound "WHAT READYSOUND"
    DamageType "Chainsaw"
	Tag "Chainsaw"
    +INVENTORY.UNDROPPABLE
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT1"
	Inventory.RestrictedTo "DoomguyPlayer"
    States
    {
      Ready:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Ready64")
		TNT1 A 0 A_PlaySound("doomguy/sawidle", CHAN_WEAPON)
        DSAW CD 4 A_WeaponReady
        loop

      Ready64:
		TNT1 A 0 A_PlaySound("doom64guy/sawidle", CHAN_WEAPON)
        64SW CD 5 A_WeaponReady 
        Goto Ready

      Deselect:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Deselect64")
        DSAW C 1 A_Lower
        loop

      Deselect64:
        64SW C 1 A_Lower
        loop		

      Select:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Select64")
		TNT1 A 0 A_PlaySound("doomguy/sawup", CHAN_WEAPON)
        DSAW C 1 A_Raise
        wait

      Select64:
        TNT1 A 0 A_PlaySound("doom64guy/sawup", 7)
        64SW C 1 A_Raise
        wait

      Fire:
        TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Fire64")
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "FireVanilla")
        TNT1 A 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserk")
        DSAW A 0 A_JumpIfInventory("CoopModeOn", 1, 3)
        DSAW AB 4 A_Saw("doomguy/sawfull", "doomguy/sawhit", Random(1,2)*10, "DoomBulletPuff2", SF_NORANDOM)
        goto FireEnd
        DSAW AB 4 A_Saw("doomguy/sawfull", "doomguy/sawhit", Random(1,2)*10, "DoomBulletPuff2_Coop", SF_NORANDOM)
        goto FireEnd

      FireVanilla:
        TNT1 A 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserkVanilla")
        DSAW A 0 A_JumpIfInventory("CoopModeOn", 1, 3)
        DSAW AB 4 A_Saw("doomguy/sawfull", "doomguy/sawhit", 2, "DoomBulletPuff2")
        goto FireEnd
        DSAW AB 4 A_Saw("doomguy/sawfull", "doomguy/sawhit", 2, "DoomBulletPuff2_Coop")
        goto FireEnd

      Fire64:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "Fire64Vanilla")
        TNT1 A 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserk64")
        64SW A 0 A_JumpIfInventory("CoopModeOn", 1, 3)
        64SW AB 2 A_Saw("doom64guy/sawfull", "doom64guy/sawhit", Random(1,3)*5, "Doom64BulletPuff", SF_NORANDOM)
        goto FireEnd64
        64SW AB 2 A_Saw("doom64guy/sawfull", "doom64guy/sawhit", Random(1,3)*5, "Doom64BulletPuff_Coop", SF_NORANDOM)
        goto FireEnd64

      Fire64Vanilla:
        TNT1 A 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserk64Vanilla")
        64SW A 0 A_JumpIfInventory("CoopModeOn", 1, 3)
        64SW AB 2 A_Saw("doom64guy/sawfull", "doom64guy/sawhit", 2, "Doom64BulletPuff")
        goto FireEnd64
        64SW AB 2 A_Saw("doom64guy/sawfull", "doom64guy/sawhit", 2, "Doom64BulletPuff_Coop")
        goto FireEnd64
		
      FireBerserk:
        DSAW A 0 A_JumpIfInventory("CoopModeOn", 1, 3)
        DSAW AB 3 A_Saw("doomguy/sawfull", "doomguy/sawhit", Random(4,7)*10, "DoomBulletPuff2", SF_NORANDOM)
        goto FireEnd
        DSAW AB 3 A_Saw("doomguy/sawfull", "doomguy/sawhit", Random(4,7)*10, "DoomBulletPuff2_Coop", SF_NORANDOM)
        goto FireEnd

      FireBerserkVanilla:
        DSAW A 0 A_JumpIfInventory("CoopModeOn", 1, 3)
        DSAW AB 3 A_Saw("doomguy/sawfull", "doomguy/sawhit", 7, "DoomBulletPuff2")
        goto FireEnd
        DSAW AB 3 A_Saw("doomguy/sawfull", "doomguy/sawhit", 7, "DoomBulletPuff2_Coop")
        goto FireEnd

      FireBerserk64:
        64SW A 0 A_JumpIfInventory("CoopModeOn", 1, 3)
        64SW AB 2 A_Saw("doom64guy/sawfull", "doom64guy/sawhit", Random(3,6)*5, "Doom64BulletPuff", SF_NORANDOM)
        goto FireEnd64
        64SW AB 2 A_Saw("doom64guy/sawfull", "doom64guy/sawhit", Random(3,6)*5, "Doom64BulletPuff_Coop", SF_NORANDOM)
        goto FireEnd64

      FireBerserk64Vanilla:
        64SW A 0 A_JumpIfInventory("CoopModeOn", 1, 3)
        64SW AB 2 A_Saw("doom64guy/sawfull", "doom64guy/sawhit", 7, "Doom64BulletPuff")
        goto FireEnd64
        64SW AB 2 A_Saw("doom64guy/sawfull", "doom64guy/sawhit", 7, "Doom64BulletPuff_Coop")
        goto FireEnd64

      FireEnd:
        DSAW B 0 A_ReFire
        goto Ready

      FireEnd64:
        64SW B 0 A_ReFire
        goto Ready
        
      Spawn:
        WSAW A -1
        stop
    }
}

actor PowerChainsawBerserk : Powerup//PowerDamage
{
    +INVENTORY.HUBPOWER
    Powerup.Duration 0x7FFFFFFF
    //DamageFactor "Chainsaw", 10.0 // Yeah, that turned out to not work for some reason.
}
