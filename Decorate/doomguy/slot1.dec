actor " Chainsaw " : Chainsaw replaces Chainsaw // doom 64 chainsaw attacks twice as fast
{
    Weapon.Kickback 0
    Weapon.SelectionOrder 2200
    Weapon.SlotNumber 1
    Weapon.SlotPriority 0
    Decal BulletChip
    Weapon.UpSound "WHAT UPSOUND"
    Weapon.ReadySound "WHAT READYSOUND"
    DamageType "Chainsaw"
    +INVENTORY.UNDROPPABLE
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT1"
	Inventory.RestrictedTo "DoomguyPlayer"
    States
    {
      Ready:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Ready64")
		TNT1 A 0 A_PlaySound("doomguy/sawidle", CHAN_WEAPON)
        DSAW CD 4 A_WeaponReady
        loop

      Ready64:
		TNT1 A 0 A_PlaySound("doom64guy/sawidle", CHAN_WEAPON)
        64SW CD 5 A_WeaponReady 
        Goto Ready

      Deselect:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Deselect64")
        DSAW C 1 A_Lower
        loop
		
      Deselect64:
        64SW C 1 A_Lower
        loop		

      Select:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Select64")
		TNT1 A 0 A_PlaySound("doomguy/sawup", CHAN_WEAPON)
        DSAW C 1 A_Raise
        wait
		
      Select64:
        TNT1 A 0 A_PlaySound("doom64guy/sawup", 7)
        64SW C 1 A_Raise
        wait
		
      Fire:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Fire64")
		TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "FireVanilla")
        TNT1 A 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserk")
        DSAW AB 4 A_Saw("doomguy/sawfull", "doomguy/sawhit", Random(1,2)*10, "DoomBulletPuff2", SF_NORANDOM) 
        DSAW B 0 A_ReFire
        goto Ready		

      FireVanilla:
        TNT1 A 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserkVanilla")
        DSAW AB 4 A_Saw("doomguy/sawfull", "doomguy/sawhit", 2, "DoomBulletPuff2") 
        DSAW B 0 A_ReFire
        goto Ready
		
      Fire64:
		TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "Fire64Vanilla")
        TNT1 A 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserk64")
        64SW AB 2 A_Saw("doom64guy/sawfull", "doom64guy/sawhit", Random(1,2)*10, "Doom64BulletPuff", SF_NORANDOM)
        64SW B 0 A_ReFire
        goto Ready				
		
      Fire64Vanilla:
        TNT1 A 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserk64Vanilla")
        64SW AB 2 A_Saw("doom64guy/sawfull", "doom64guy/sawhit", 2, "Doom64BulletPuff")
        64SW B 0 A_ReFire
        goto Ready		
		
      FireBerserk:
        DSAW AB 3 A_Saw("doomguy/sawfull", "doomguy/sawhit", Random(4,7)*10, "DoomBulletPuff2", SF_NORANDOM) 
        DSAW B 0 A_ReFire
        goto Ready		

      FireBerserkVanilla:
        DSAW AB 3 A_Saw("doomguy/sawfull", "doomguy/sawhit", 7, "DoomBulletPuff2") 
        DSAW B 0 A_ReFire
        goto Ready
		
      FireBerserk64:
        64SW AB 2 A_Saw("doom64guy/sawfull", "doom64guy/sawhit", Random(3,6)*10, "Doom64BulletPuff", SF_NORANDOM)
        64SW B 0 A_ReFire
        goto Ready			
		
      FireBerserk64Vanilla:
        64SW AB 2 A_Saw("doom64guy/sawfull", "doom64guy/sawhit", 7, "Doom64BulletPuff")
        64SW B 0 A_ReFire
        goto Ready		

      Spawn:
        WSAW A -1
        stop
    }
}

actor PowerChainsawBerserk : Powerup//PowerDamage
{
    +INVENTORY.HUBPOWER
    Powerup.Duration 0x7FFFFFFF
    //DamageFactor "Chainsaw", 10.0 // Yeah, that turned out to not work for some reason.
}
