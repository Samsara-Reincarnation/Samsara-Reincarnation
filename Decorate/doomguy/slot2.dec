actor " Shotgun " : Shotgun // doom 64 shotgun is mostly the same. the only difference is that there is recoil
{
    Weapon.AmmoType "Shell"
    Decal BulletChip
    Weapon.SlotNumber 2
    +THRUGHOST
    +INVENTORY.UNDROPPABLE
    AttackSound ""
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT2"
	Inventory.RestrictedTo "DoomguyPlayer"
    States
    {
      Ready:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Ready64")
        DSHT A 1 A_WeaponReady
        Loop
		
      Ready64:
        64SG A 1 A_WeaponReady
        Goto Ready		

      Deselect:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Deselect64")
        DSHT A 1 A_Lower
        Loop

      Deselect64:
        64SG A 1 A_Lower
        Loop

      Select:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Select64")
        DSHT A 1 A_Raise
        Loop
		
      Select64:
        64SG A 1 A_Raise
        Loop		

      Fire:
		TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Fire64")
        DSHT A 3
        DSHT A 0 A_PlayWeaponSound("doomguy/shotgf")
		TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        TNT1 A 0 A_FireBullets (5.6, 0, 7, 7, "DoomBulletPuff2")
        goto FireAnim
		
      Fire64:
        64SG A 0 A_PlayWeaponSound("doom64guy/shotgf")
		TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire64")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire64")
        TNT1 A 0 A_FireBullets (5.6, 0, 7, 7, "Doom64BulletPuff")
        goto Animation64		
		
	  VanillaFire64:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire64Vanilla")
        TNT1 A 0 A_FireBullets (5.6, 0, 7, 5, "Doom64BulletPuff")
        goto Animation64
		
      CoopFire:
        TNT1 A 0 A_FireBullets (5.6, 0, 7, 7, "DoomBulletPuff2_Coop")
        goto FireAnim		

      CoopFireVanilla:
        TNT1 A 0 A_FireBullets (5.6, 0, 7, 5, "DoomBulletPuff2_Coop")
        goto VanillaAnim
		
      CoopFire64:
        TNT1 A 0 A_FireBullets (5.6, 0, 7, 7, "Doom64BulletPuff_Coop")
        goto Animation64		
		
      CoopFire64Vanilla:
		TNT1 A 0 A_FireBullets (5.6, 0, 7, 5, "Doom64BulletPuff_Coop")
        goto Animation64		

      FireAnim:
        DSHT A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "RageAnim")
        TNT1 A 0 A_ZoomFactor(0.995, ZOOM_INSTANT)
        DSHT A 2 bright
        DSHT A 5 bright A_ZoomFactor(1, ZOOM_INSTANT)
        DSHT BCDEE 1
        DSHT F 3
        DSHT G 3
        DSHT H 4
        DSHT G 4
        DSHT F 3
        DSHT ED 2
        DSHT C 1
        DSHT B 3 A_Refire
        DSHT A 4
        Goto Ready

      RageAnim:
        DSHT A 2 bright
        DSHT A 6 bright A_ZoomFactor(1, ZOOM_INSTANT)
        DSHT CE 2
        DSHT FG 4
        DSHT H 6
        DSHT GF 4
        DSHT EDC 2
        DSHT B 2 A_Refire
        DSHT A 4
        goto Ready

      VanillaFire:
		TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFireVanilla")
		TNT1 A 0 A_FireBullets (5.6, 0, 7, 5, "DoomBulletPuff2")
      VanillaAnim:
        DSHT A 7 bright A_GunFlash
        DSHT IJ 5
        DSHT K 4
        DSHT JI 5
        DSHT A 3
        DSHT A 7 A_ReFire
        Goto Ready

      Flash:
        DSHF A 3 bright A_Light1
        DSHF B 4 bright A_Light2
        Goto LightDone
		
      Animation64:
        64SG A 0 A_GunFlash("Flash64")
        64SG A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 4, 3, 17)
        64SG A 5
        64SG C 21
        64SG B 5
        64SG A 7
        64SG A 7 A_ReFire
        Goto Ready

      Flash64:
        64SM A 2 bright
        64SM A 2 bright
        Goto LightDone		

      Spawn:
        WSHT A -1
        Stop
    }
}
