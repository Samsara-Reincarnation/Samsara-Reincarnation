actor " Fist " : Weapon
{
    Weapon.SlotNumber 0
    Weapon.SelectionOrder 3700
    Weapon.Kickback 100
    +INVENTORY.UNDROPPABLE
    +WEAPON.WIMPY_WEAPON
    +WEAPON.MELEEWEAPON
    +WEAPON.NOALERT
    Obituary "$SAMSARA_DOOMGUY_OB_FIST"
	Inventory.RestrictedTo "DoomguyPlayer"
	Tag "Brass Knuckles"
    States
    {
      Ready:
        TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Ready64")
        DPUN A 1 A_WeaponReady
        loop

      Ready64:
        64FS A 1 A_WeaponReady
        Goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Deselect64")
        DPUN A 1 A_Lower
        loop

      Deselect64:
        64FS A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Select64")
        DPUN A 1 A_Raise
        loop

      Select64:
        64FS A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Fire64")
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        DPUN L 1 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed", 1, "Rage")
        DPUN BCD 1
        goto Fire2

      Fire64:
        //64FS E 0 A_JumpIfInventory("PunchDrunkZerk", 1, "StartBerserk64")
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire64")
        64FS E 1 A_JumpIfInventory("PowerChainsawBerserk", 1, "StartBerserk64")
        64FS FG 1
        64FS B 2
        Goto Fire2

      StartBerserk64:
        64FS E 4
        Goto Fire2

      VanillaFire:
        DPUN M 4
        DPUN E 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "VanillaFireBerserk")
        DPUN E 0 A_JumpIfInventory("CoopModeOn", 1, 2)
        DPUN E 0 A_CustomPunch(random(1, 10)*2, 1, 0, "DoomPunchPuff")
        goto FireEnd
        DPUN E 0 A_CustomPunch(random(1, 10)*2, 1, 0, "DoomPunchPuff_Coop")
        goto FireEnd

      Rage:
        DPUN LC 2
        goto Fire2

      Fire2:
        TNT1 A 0 A_JumpIfInventory("Doom64Mode", 1, "Fire64_2")
        //DPUN E 0 A_JumpIfInventory("PunchDrunkZerk", 1, "FirePDBerserk")
        DPUN E 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserk")
        DPUN E 0 A_JumpIfInventory("CoopModeOn", 1, 2)
        DPUN E 0 A_CustomPunch(random(2, 10)*5, 1, 0, "DoomPunchPuff")
        goto FireEnd
        DPUN E 0 A_CustomPunch(random(2, 10)*5, 1, 0, "DoomPunchPuff_Coop")
        goto FireEnd

      Fire64_2:
        //64FS E 0 A_JumpIfInventory("PunchDrunkZerk", 1, "FirePDBerserk64")
        64FS E 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserk64")
        64FS E 0 A_JumpIfInventory("CoopModeOn", 1, 2)
        64FS E 0 A_CustomPunch(random(2, 10)*5, 1, 0, "Doom64PunchPuff")
        goto FireEnd64
        64FS E 0 A_CustomPunch(random(2, 10)*5, 1, 0, "Doom64PunchPuff_Coop")
        goto FireEnd64

      VanillaFire64:
        //64FS E 0 A_JumpIfInventory("PunchDrunkZerk", 1, "FirePDBerserk64")
        64FS B 5 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserk64")
        64FS E 0 A_JumpIfInventory("CoopModeOn", 1, 2)
        64FS E 0 A_CustomPunch(random(1, 10)*2, 1, 0, "Doom64PunchPuff")
        goto FireEnd64
        64FS E 0 A_CustomPunch(random(1, 10)*2, 1, 0, "Doom64PunchPuff_Coop")
        goto FireEnd64

      FireBerserk:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFireBerserk")
        DPUN E 0 A_JumpIfInventory("CoopModeOn", 1, 2)
        DPUN E 0 A_CustomPunch(random(20, 50)*5, 1, 0, "DoomPunchPuff2") // Technically should be random(1, 10)*20, but oh well.
        goto FireEnd
        DPUN E 0 A_CustomPunch(random(20, 50)*5, 1, 0, "DoomPunchPuff2_Coop")
        goto FireEnd

      FirePDBerserk:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFirePDBerserk")
        DPUN E 0 A_JumpIfInventory("CoopModeOn", 1, 2)
        DPUN E 0 A_CustomPunch(random(1, 10)*8, 1, 0, "DoomPunchPuff2") // Punchdrunk triples fist damage already.
        goto FireEnd
        DPUN E 0 A_CustomPunch(random(1, 10)*8, 1, 0, "DoomPunchPuff2_Coop")
        goto FireEnd

      VanillaFireBerserk:
        DPUN E 0 A_JumpIfInventory("CoopModeOn", 1, 2)
        DPUN E 0 A_CustomPunch(random(1, 10)*20, 1, 0, "DoomPunchPuff2")
        goto FireEnd
        DPUN E 0 A_CustomPunch(random(1, 10)*20, 1, 0, "DoomPunchPuff2_Coop")
        goto FireEnd

      VanillaFirePDBerserk:
        DPUN E 0 A_JumpIfInventory("CoopModeOn", 1, 2)
        DPUN E 0 A_CustomPunch(random(10, 20)*4, 1, 0, "DoomPunchPuff2")
        goto FireEnd
        DPUN E 0 A_CustomPunch(random(10, 20)*4, 1, 0, "DoomPunchPuff2_Coop")
        goto FireEnd

      FireBerserk64:
        64FS E 1 A_JumpIfInventory("VanillaDoom", 1, "VanillaFireBerserk64")
        64FS FGB 1
        64FS B 0 A_JumpIfInventory("CoopModeOn", 1, 2)
        64FS B 0 A_CustomPunch(random(20, 50)*5, 1, 0, "Doom64PunchPuff2")
        goto FireEndBerserk
        64FS B 0 A_CustomPunch(random(20, 50)*5, 1, 0, "Doom64PunchPuff2_Coop")
        goto FireEndBerserk

      FirePDBerserk64:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFireBerserk64")
        64FS E 0 A_JumpIfInventory("CoopModeOn", 1, 2)
        64FS E 0 A_CustomPunch(random(10, 20)*4, 1, 0, "Doom64PunchPuff2")
        goto FireEndBerserk
        64FS E 0 A_CustomPunch(random(10, 20)*4, 1, 0, "Doom64PunchPuff2_Coop")
        goto FireEndBerserk

	 VanillaFireBerserk64:
        64FS B 5
        64FS B 0 A_JumpIfInventory("CoopModeOn", 1, 2)
        64FS B 0 A_CustomPunch(random(1, 10)*20, 1, 0, "Doom64PunchPuff2")
        goto FireEndBerserk
        64FS B 0 A_CustomPunch(random(1, 10)*20, 1, 0, "Doom64PunchPuff2_Coop")
        goto FireEndBerserk

      FireEnd:
        DPUN E 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaEnd")
        DPUN E 2 A_JumpIfInventory("SamsaraHasDoubleFiringSpeed", 1, "RageEnd")
        DPUN FGHI 2
        DPUN JKL 1
        DPUN A 5 A_ReFire
        goto Ready

      FireEnd64:
        64FS H 1 A_JumpIfInventory("VanillaDoom", 1, "VanillaEnd64")
        64FS CIDJ 1
        64FS DICHB 2
        64FS GFE 1 A_ReFire
        goto Ready

      VanillaEnd64:
        64FS CDC 5
        64FS B 5 A_ReFire
        goto Ready

      FireEndBerserk:
        64FS H 1 A_JumpIfInventory("VanillaDoom", 1, "VanillaEndBerserk64")
        64FS CIDJDI 1
        64FS CHB 2
        64FS GFE 1 A_ReFire
        goto Ready

	  VanillaEndBerserk64:
        64FS C 4
        64FS D 5
        64FS C 4
        64FS B 5 A_ReFire
        goto Ready

      VanillaEnd:
        DPUN N 4
        DPUN O 5
        DPUN N 4
        DPUN M 5 A_ReFire
        Goto Ready

      RageEnd:
        DPUN EFGHIJL 2
        DPUN A 4 A_ReFire
        goto Ready
    }
}

actor DoomPunchPuff : BulletPuff
{
    DamageType "DoomDamageTypeFist"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doomguy/wallpunch2")
        DPFF CD 4
        Stop

      Melee:
        TNT1 A 0 A_PlaySound("doomguy/punch")
        stop
    }
}

actor DoomPunchPuff_Coop : DoomPunchPuff { Species "Player" +THRUSPECIES +MTHRUSPECIES }

actor DoomPunchPuff2 : BulletPuff
{
    DamageType "DoomDamageTypeFist"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doomguy/wallpunch")
        DPFF CD 4
        stop

      Melee:
        TNT1 A 0 A_PlaySound("doomguy/punch2")
        stop
    }
}

actor DoomPunchPuff2_Coop : DoomPunchPuff2 { Species "Player" +THRUSPECIES +MTHRUSPECIES }

actor Doom64PunchPuff : Doom64BulletPuff
{
    DamageType "DoomFist"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doom64guy/wallpunch2")
        64PF CDEF 4
        Stop

      Melee:
        TNT1 A 0 A_PlaySound("doom64guy/punch")
        stop
    }
}

actor Doom64PunchPuff_Coop : Doom64PunchPuff { Species "Player" +THRUSPECIES +MTHRUSPECIES }

actor Doom64PunchPuff2 : Doom64BulletPuff
{
    DamageType "DoomFist"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0 A_PlaySound("doom64guy/wallpunch")
        64PF CDEF 4
        stop

      Melee:
        TNT1 A 0 A_PlaySound("doom64guy/punch2")
        stop
    }
}

actor Doom64PunchPuff2_Coop : Doom64PunchPuff2 { Species "Player" +THRUSPECIES +MTHRUSPECIES }