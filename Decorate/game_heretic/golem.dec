actor Mummy2 : SamsaraMonsterHumanoidMediumTotalGib replaces Mummy
{
	Game Heretic
	SpawnID 4
	Health 80
	Radius 22
	Height 62
	Mass 75
	Speed 12
	Painchance 128
	Monster
	+FLOORCLIP
	+NOTDMATCH
    +NOICEDEATH
	SeeSound "mummy/sight"
	AttackSound "mummy/attack1"
	PainSound "mummy/pain"
	DeathSound "mummy/death"
	ActiveSound "mummy/active"
	HitObituary "$OB_MUMMY"
	DropItem "GoldWandAmmo2", 84
    Tag "Golem"
	var int user_scalex;
	var int user_scaley;
    States
    {
	Spawn:
		MUMM AB 10 A_Look
		Loop
	See:
		MUMM ABCD 4 A_Chase
		Loop
	Melee:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		MUMM E 6 A_FaceTarget
		MUMM F 6 A_CustomMeleeAttack(random[MummyAttack](1,8)*2, "mummy/attack2", "mummy/attack")
		MUMM G 6
		Goto See
	Pain:
		MUMM H 4
		MUMM H 4 A_Pain
		Goto See
	Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        MUMM H 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        MUMM H 16 A_Pain
        Goto See
	Pain.Shock:
	Pain.Tesla:
		CBGS A 4 A_Pain
		CBGS A 4
		TNT1 A -1 A_Jump(256,"See")
		wait
	Death:
        TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		MUMM I 5
		MUMM J 5 A_Scream
		MUMM K 5 A_SpawnItemEx("MummySoul", 0,0,10, 0,0,1)
		MUMM L 5
		MUMM M 5 A_NoBlocking
		MUMM NO 5
		MUMM P -1
		Stop
	Death.CalebFire:
	Death.LifeLeech:
		TNT1 A 0 A_Jump(218, 2)
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
		TNT1 A 0 A_SpawnItemEx("BloodBurningCorpse_Generic", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 5 A_NoBlocking
		stop
	Death.PainkillerStake:
		TNT1 A 0 ACS_NamedExecuteAlways("Painkiller_MonsterKill")
		TNT1 A 0 A_JumpIf(CallACS("Painkiller_IsItGibbed")==1,"PainkillerGib")
		TNT1 A 0 A_GiveInventory("Painkiller_RunStake",1)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_FaceTarget
	StakeDeath:
		MUMM H 2
		Loop
    }
}

actor MummyGhost2 : Mummy2 replaces MummyGhost
{
	Game Heretic
	SpawnID 8
	+SHADOW
	+GHOST
	RenderStyle Translucent
	Alpha 0.4
	+NOTDMATCH
    +NOICEDEATH
}

actor MummyLeader2 : Mummy2 replaces MummyLeader
{
    +NOTDMATCH
    +NOICEDEATH
    DamageFactor "CorvusCrossbow", 1.15
    DamageFactor "DragonClaw", 1.5
    PainChance "MightyBoot", 255
    DropItem "GoldWandAmmo2", 84
	Tag "Nitrogolem"
    States
    {
	Missile:
		MUMM X 5 A_FaceTarget
		MUMM Y 5 Bright A_FaceTarget
		MUMM X 5 A_FaceTarget
		MUMM Y 5 Bright A_FaceTarget
		MUMM X 5 A_FaceTarget
		MUMM Y 5 Bright A_CustomComboAttack("MummyFX1", 32, random[MummyAttack2](1,8)*2, "mummy/attack2")
		Goto See
    }
}

actor MummyFX2 : MummyFX1 replaces MummyFX1
{
    DamageType "HereNitrogolem"
    +THRUGHOST
}

actor MummyLeaderGhost2 : MummyLeader2 replaces MummyLeaderGhost
{
    +NOTDMATCH
    +NOICEDEATH
	Game Heretic
	SpawnID 9
	Species "MummyLeaderGhost"
	+SHADOW
	+GHOST
	RenderStyle Translucent
	Alpha 0.4
}