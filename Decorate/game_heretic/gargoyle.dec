// WIP actor; needs Heretic functions converted to Decorate

actor HereticImp2 : SamsaraMonsterBeastSmall replaces HereticImp
{
	Game Heretic
	SpawnID 5
	Health 40
	Radius 16
	Height 36
	Mass 50
	Speed 10
	Painchance 200
	Monster
	+FLOAT
	+NOGRAVITY
	+SPAWNFLOAT
	+DONTOVERLAP
	+MISSILEMORE
	+NOTDMATCH
    +NOICEDEATH
	SeeSound "himp/sight"
	AttackSound "himp/attack"
	PainSound "himp/pain"
	DeathSound "himp/death"
	ActiveSound "himp/active"
	Obituary "$OB_HERETICIMP"
	HitObituary "$OB_HERETICIMPHIT"
	Tag "Gargoyle"
    var int user_scalex;
	var int user_scaley;
    States
    {
	Spawn:
		IMPX ABCB 10 A_Look
		Loop
	See:
		IMPX AABBCCBB 3 A_Chase
		Loop
	Melee:
		IMPX DE 6 A_FaceTarget
		IMPX F 6 A_CustomMeleeAttack(random[ImpMeAttack](5,12), "himp/attack", "himp/attack")
		Goto See
	Missile:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		IMPX A 10 A_FaceTarget
		IMPX B 0 A_Jump(64, 2)
		IMPX B 6 A_SkullAttack(12)
		IMPX CBAB 6
		Goto Missile+3
	Pain:
		IMPX G 3
		IMPX G 3 A_Pain
		Goto See
    Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        IMPX G 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        IMPX G 16 A_Pain
        Goto See
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 0 A_ChangeFlag("SOLID",0)
		TNT1 A 0 A_ChangeFlag("FLOORCLIP",1)
		IMPX G 4
		IMPX H 5
		Wait
	Death.PainkillerStake:
		TNT1 A 0 ACS_NamedExecuteAlways("Painkiller_MonsterKill")
		TNT1 A 0 A_JumpIf(CallACS("Painkiller_IsItGibbed")==1,"PainkillerGib")
		TNT1 A 0 A_GiveInventory("Painkiller_RunStake",1)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_FaceTarget
	StakeDeath:
		IMPX G 2
		Loop
	XDeath:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 0 A_ChangeFlag("SOLID",0)
		TNT1 A 0 A_ChangeFlag("FLOORCLIP",1)
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",1)
		TNT1 A 0 A_SetSpecial("666")
		IMPX S 5 //A_ImpXDeath1
		IMPX TU 5
		IMPX V 5 A_Gravity
		IMPX W 5 
		Wait
	Crash:
		IMPX I 7 //A_ImpExplode
		IMPX J 7 A_Scream
		IMPX K 7
		IMPX L -1
		Stop
    Crash.ZorchPistol:
    Crash.Zorch:
    Crash.ZorchKamikaze:
    Crash.PhasingZorch:
    Crash.ZorchSpectral:
        TNT1 A 5 A_SetTranslucent(0,0)
        TNT1 A 5 A_NoBlocking
        TNT1 A -1
        stop
	XCrash:
		IMPX X 7
		IMPX Y 7
		IMPX Z -1
		Stop
    }
}

actor HereticImpLeader2 : HereticImp2 replaces HereticImpLeader
{
	Game Heretic
	SpawnID 7
	Species "HereticImpLeader"
	Health 80
	-MISSILEMORE
	AttackSound "himp/leaderattack"
	var int user_scalex;
	var int user_scaley;
    States
    {
	Melee:
		Stop
	Missile:
		IMPX DE 6 A_FaceTarget
		IMPX F 6 A_CustomComboAttack("HereticImpBall", 32, random[ImpMsAttack2](5,12), "himp/leaderattack")
		Goto See
    }
}

actor HereticImpBall2 : HereticImpBall replaces HereticImpBall
{
    +FIREDAMAGE
    +THRUGHOST
}

actor HereticImpChunk3 : HereticImpChunk1 replaces HereticImpChunk1
{
    +CLIENTSIDEONLY
}

actor HereticImpChunk4 : HereticImpChunk2 replaces HereticImpChunk2
{
    +CLIENTSIDEONLY
}