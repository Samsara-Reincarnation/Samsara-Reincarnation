//=================
// DOOMGUY: Imp
// The only ally with renamed sprites/sounds, so as not to conflict with mods that replace the Imp sprites/sounds.
// Which is actually kind of depressing, if you're a fan of the other iwads. None of them have stuff like that.
actor FriendlyDoomImp : DoomImp2
{
    Health 100
    SeeSound "friendlyimp/sight"
    PainSound "friendlyimp/pain"
    DeathSound "friendlyimp/death"
    ActiveSound "friendlyimp/active"
    HitObituary "%o was somehow killed by a friendly Imp."
    Obituary "%o was somehow killed by a friendly Imp."
    
    Species "Player"
    
    -SOLID
    +FRIENDLY
    
    States
    {
      Spawn:
        TRO2 AB 10 A_Look
        loop
        
      See:
        TRO2 AABBCCDD 3 A_Chase
        Loop
        
      Melee:
      Missile:
        TRO2 EF 8 A_FaceTarget
        TRO2 G 6 A_CustomComboAttack("FriendlyDoomImpBall", 32, 3 * random(1, 8), "friendlyimp/melee")//A_TroopAttack
        Goto See
        
      Pain:
        TRO2 H 2
        TRO2 H 2 A_Pain
        Goto See
        
      Death:
        TRO2 I 8
        TRO2 J 8 A_Scream
        TRO2 K 6
        TRO2 L 6 A_NoBlocking
        TRO2 M -1
        Stop
        
      XDeath:
        TRO2 N 5
        TRO2 O 5 A_XScream
        TRO2 P 5
        TRO2 Q 5 A_NoBlocking
        TRO2 RST 5
        TRO2 U -1
        Stop
        
      Raise:
        stop
    }
}

actor FriendlyDoomImpBall : DoomImpBall2
{
    +THRUSPECIES
    
    Species "Player"
    
    SeeSound "friendlyimp/attack"
    DeathSound "friendlyimp/shotx"
    
    States
    {
      Spawn:
        DBL1 AB 4
        Loop
        
      Death:
        DBL1 CDE 6
        Stop
    }
}

//=================
// CORVUS: Nitrogolem
// The best name ever. Suck it, Gez!
actor FriendlyNitrogolem : MummyLeader2
{
    Health 100
    HitObituary "%o was somehow killed by a friendly Nitrogolem."
    Obituary "%o was somehow killed by a friendly Nitrogolem."
    
    -SOLID
    +FRIENDLY
    Species "Player"
    
    States
    {
      Missile:
        MUMM X 5 A_FaceTarget
        MUMM Y 5 Bright A_FaceTarget
        MUMM X 5 A_FaceTarget
        //MUMM Y 5 Bright A_FaceTarget - So as not to be so damned useless.
        //MUMM X 5 A_FaceTarget
        MUMM Y 5 Bright A_CustomComboAttack("FriendlyNitrogolemShot", 32, random[MummyAttack2](1,8)*2, "mummy/attack2")
        Goto See
    }
}

actor FriendlyNitrogolemShot : MummyFX1
{
    +THRUSPECIES
    Species "Player"
}

//=================
// CHEX WARRIOR: Bipedal Flemoid.
// Oddly enough, never shot balls of slime despite having more orifices to shoot from than the armored one.
actor FriendlyFlemoid : FlemoidusBipedicus2
{
    Health 100
    SeeSound "friendlyflemoid/sight"
    AttackSound "friendlyflemoid/attack"
    PainSound "friendlyflemoid/pain"
    DeathSound "friendlyflemoid/death"
    ActiveSound "friendlyflemoid/active"
    
    -SOLID
    +FRIENDLY
    Species "Player"
    
    Obituary "%o was somehow slimed by a friendly flemoid."
    
    States
    {
      Spawn:
        BPFL AB 10 A_Look
        Loop
        
      See:
        BPFL AABBCCDD 3 A_Chase
        Loop
        
      Pain:
        BPFL A 3 A_SpawnDebris("FlemoidGib",1)
        BPFL A 3 A_Pain
        Goto See
        
      Raise:
        stop
        
      Pain.ZorchPistol:
        
      Pain.Zorch:
        BPFL G 3
        BPFL G 3 A_Pain
        Goto See
        
      Death.DukeExplosive:
      Death.DukeSpectral:
        
      Death.DukeDamage:
        
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death
        
      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death
        
      XDeath:
      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("flemdie")
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnDebris("FlemoidGib",1)
        SPOD A 10
        SPOD ABC 7
        SPOD D -1
        stop
        
      Death.ZorchPistol:
        
      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        BPFL H 5
        BPFL I 5 A_Scream
        BPFL J 5 A_NoBlocking
        BPFL K 5
        BPFL L -1
        Stop
        
      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Ice
        
      Ice:
      Death.HexenIce:
        
      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        BPFL A 0 A_UnsetSolid
        BPFL A 70 A_GenericFreezeDeath
        BPFL A 0 A_IceGuyDie
        BPFL A 1 A_IceGuyDie
        TNT1 A -1
        stop
        
      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        BPFL A 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        TNT1 A 0 A_SpawnDebris("FlemoidGib",1)
        BPFL A 16 A_Pain
        Goto See
        
      Melee:
      Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        BPFL EF 6 A_FaceTarget // Previously 8 each
        BPFL E 6 A_CustomComboAttack("FriendlyFlemBall", 32, random[troopattack](1,8)*3, "friendlyflemoid/shotx", "flem")
        Goto See
    }
}

actor FriendlyFlemBall : BipedicusBallV3
{
    +THRUSPECIES
    Species "Player"
    SeeSound "friendlyflemoid/flem"
    DeathSound "friendlyflemoid/shotx"
    States
    {
      Spawn:
        FLB1 AB 4
        Loop
        
      Death:
        FLB1 CDE 6
        Stop
    }
}

//=================
// B.J. BLAZKOWICZ: Nazi Guard
// Pretty sure "friendly Nazi" is an oxymoron. Nazis were bad, folks.
ACTOR FriendlyNazi
{
    Health 100
    Radius 16
    Height 50
    Speed 8
    PainChance 170
    Scale 1.25
    Monster
    +FLOORCLIP
    +FRIENDLY
    -SOLID
    SeeSound "friendlynazi/sight"
    PainSound "friendlynazi/pain"
    DeathSound "friendlynazi/death"
    ActiveSound "friendlynazi/active"
    AttackSound "friendlynazi/attack"
    Obituary "%o was somehow killed by a friendly Nazi."
    Dropitem "Clip"
    Species "Player"
    
    States
    {
      Spawn:
        NGRD A 10 A_Look
        Loop
        
      See:
        NGRD BBCCDDEE 3 A_Chase
        Loop
        
      Missile:
        NGRD H 5 A_FaceTarget
        NGRD H 5 A_FaceTarget
        TNT1 A 0 A_PlaySound("friendlynazi/attack")
        NGRD I 5 Bright A_CustomMissile("FriendlyNaziBullet",32,0,0,8)
        //NGRD H 1 A_CPosRefire
        Goto See
        
      Pain:
        NGRD F 3
        NGRD F 3 A_Pain
        Goto See
        
      Death:
        NGRD I 5
        NGRD J 5 A_Scream
        NGRD K 5 A_NoBlocking
        NGRD LM 5
        NGRD N -1
        Stop
    }
}

actor FriendlyNaziBullet : WolfenBullet2
{
    Damage (Random(1,3)*5)
    +THRUSPECIES
    Species "Player"
}

//=================
// PARIAS: Afrit
// Squawk.
actor FriendlyAfrit : FireDemon2
{
    Health 100
    
    Species "Player"
    -SOLID
    +FRIENDLY
    -INVULNERABLE
    
    States
    {
      Spawn:
      See:
        FDMN ABC 5 Bright A_FireDChase
        Loop
        
      Missile:
        FDMN K 3 Bright A_FaceTarget
        TNT1 A 0 A_PlaySound("FireDemonAttack")
        FDMN K 5 Bright A_CustomMissile("FriendlyFireDemonMissile")
        TNT1 A 0 A_PlaySound("FireDemonAttack")
        FDMN K 5 Bright A_CustomMissile("FriendlyFireDemonMissile")
        TNT1 A 0 A_PlaySound("FireDemonAttack")
        FDMN K 5 Bright A_CustomMissile("FriendlyFireDemonMissile")
        Goto Chase
    }
}

actor FriendlyFireDemonMissile : FireDemonMissile
{
    +THRUSPECIES
    Species "Player"
}

//=================
// DUKE NUKEM: Assault Trooper.
// I don't have anything witty to say here.
actor FriendlyAlienTrooper
{
    Health 100
    Radius 16
    Height 50
    Speed 6
    PainChance 64
    Scale 0.65
    MONSTER
    -SOLID
    +FRIENDLY
    Species "Player"
    Obituary "%o was somehow killed by a friendly Assault Trooper."
    SeeSound "dukealien/sight"
    PainSound "dukealien/pain"
    DeathSound "dukealien/death"
    ActiveSound "dukealien/active"
    states
    {
      Spawn:
        ALTR A 10 A_Look
        ALTR B 10 A_Look
        loop

      See:
        ALTR AAABBBCCCDDD 2 A_Chase
        loop

      Missile:
        TNT1 A 0 A_Jump(64,"Missile2")
        ALTR E 7 A_FaceTarget
        TNT1 A 0 A_PlayWeaponSound("dukealien/attack")
        ALTR F 6 bright A_CustomMissile("AlienTrooperShot",32,-4,random(-8,8))
        ALTR E 7
        TNT1 A 0 A_PlayWeaponSound("dukealien/attack")
        ALTR F 6 bright A_CustomMissile("AlienTrooperShot",32,-4,random(-8,8))
        goto See

      Missile2:
        ALTR G 5 A_FaceTarget
        TNT1 A 0 A_PlayWeaponSound("dukealien/attack")
        ALTR H 4 bright A_CustomMissile("AlienTrooperShot",28,-4,random(-2,2))
        ALTR G 4 A_FaceTarget
        TNT1 A 0 A_PlayWeaponSound("dukealien/attack")
        ALTR H 4 bright A_CustomMissile("AlienTrooperShot",28,-4,random(-2,2))
        ALTR G 4 A_FaceTarget
        TNT1 A 0 A_PlayWeaponSound("dukealien/attack")
        ALTR H 4 bright A_CustomMissile("AlienTrooperShot",28,-4,random(-2,2))
        ALTR G 5 A_FaceTarget
        goto See

      Pain:
        ALTR A 2 A_Pain
        //ALTR A 0 A_Jump(128,"TeleportAwaaaay") // Removed because they had an annoying tendency to teleport in areas they shouldn't be.
        goto See

      Death:
        ALTR K 6 A_Scream
        ALTR L 6 A_NoBlocking
        ALTR MNO 6
        ALTR O -1
        stop

      XDeath:
        TNT1 A 0 A_XScream
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_PlaySound("duke/enemygib")
        ACLO E 5 A_NoBlocking
        ACLO E -1
        stop
    }
}

actor AlienTrooperShot
{
    Speed 30
    Damage (7)
    Radius 12
    Height 8
    Scale 0.5
    Species "Player"
    PROJECTILE
    +THRUSPECIES
    States
    {
      Spawn:
        ALTR S 0 A_SpawnItemEx("AlienShotTrail",-30,0,0,0,0,0,0,128,0)
        ALTR S 0 A_SpawnItemEx("AlienShotTrail",-25,0,0,0,0,0,0,128,0)
        ALTR S 0 A_SpawnItemEx("AlienShotTrail",-20,0,0,0,0,0,0,128,0)
        ALTR S 0 A_SpawnItemEx("AlienShotTrail",-15,0,0,0,0,0,0,128,0)
        ALTR S 0 A_SpawnItemEx("AlienShotTrail",-10,0,0,0,0,0,0,128,0)
        ALTR S 1 BRIGHT A_SpawnItemEx("AlienShotTrail",-5,0,0,0,0,0,0,128,0)
        loop

      Death:
        ACLO E 1
        stop
    }
}

actor AlienShotTrail
{
    Speed 0
    Damage 0
    Radius 2
    Height 2
    Scale 0.5
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        ALTR S 2 BRIGHT
        stop
    }
}

//=================
// SECURITY OFFICER: Phfor Trooper.
// These guys are probably the closest to the actual Rebels.
ACTOR FriendlyPfhorTrooper
{
    Obituary "%o was somehow killed by a friendly Pfhor Trooper."
    Gravity 0.15
    Health 100
    Radius 16
    Height 50
    Mass 100
    Speed 8
    PainChance 200
    MinMissileChance 120
    Scale 0.45
    BloodColor "Yellow"
    SeeSound "pfhor/see"
    DeathSound "pfhor/death"
    ActiveSound "pfhor/active"
    Monster
    Species "Player"
    +MISSILEMORE
    //+MISSILEEVENMORE
    +FLOORCLIP
    +FRIENDLY
    -SOLID
    States
    {
      Spawn:
        PTRO E 1 A_Look
        LOOP

      See:
        PTRO AABBCCDD 3 A_Chase
        LOOP

      Missile: // No grenade attack, since it's far too easy to hurt the player with it.
        PTRO E 2 A_FaceTarget
        TNT1 A 0 A_PlaySound("pfhor/shoot")
        PTRO F 2 Bright A_CustomMissile("PfhorTrooperBullet",32,0,random(-4,4))
        PTRO E 2 A_FaceTarget
        TNT1 A 0 A_PlaySound("pfhor/shoot")
        PTRO F 2 Bright A_CustomMissile("PfhorTrooperBullet",32,0,random(-4,4))
        PTRO E 2 A_FaceTarget
        TNT1 A 0 A_PlaySound("pfhor/shoot")
        PTRO F 2 Bright A_CustomMissile("PfhorTrooperBullet",32,0,random(-4,4))
        TNT1 A 0 A_Jump(64,"Missile")
        Goto See

      Pain:
        PTRO E 2
        Goto See

      Death:
        TNT1 A 0 ThrustThingZ(0, random(4,8), 0, 0)
        TNT1 A 0 A_Recoil(random(1,3))
        PTRO G 15
        PTRO H 5 A_Scream
        PTRO I 5
        PTRO J 5 A_NoBlocking
        PTRO K -1
        Stop

      Death.HexGreenFire: // Hah hah yeah right.

      Burn:
        TNT1 A 0 ThrustThingZ(0, random(4,8), 0, 0)
        TNT1 A 0 A_Recoil(random(1,3))
        TNT1 A 0 A_PlaySound("pfhor/burn")
        TNT1 A 0 A_UnsetSolid
        SOPL U 5 Bright

      Burn2:
        SOPL U 5 Bright A_CheckFloor("Burn3")
        loop

      Burn3:
        SOPL VWX 7 Bright
        SOPL Y -1 A_NoBlocking
        stop
    }
}

ACTOR PfhorTrooperBullet : MarathonARBulletCoop
{
    Damage (5)
}

// Ranger Grunt
ACTOR FriendlyGrunt
{
  Tag "Grunt Ally"
  Health 30
  GibHealth 35
  Radius 20
  Height 56
  Mass 99999999
  Speed 8
  PainChance 255
  Monster
  Species "Enemies"  
  Scale 0.28
  +FLOORCLIP
  +FRIENDLY
  SeeSound "qgrunt/sight"
  AttackSound "quakeweps/shotgunfire"
  PainSound "qgrunt/pain"
  DeathSound "qgrunt/death"
  ActiveSound "qgrunt/active"
  Obituary "%o was shot by a Grunt... somehow"
  States
  {
	Spawn:
		GNT1 ABCDEFGH 4 A_Look
		Loop
	See:
		GNT2 ABCDEFGH 4 A_Chase
		Loop
	Missile:
		GNT3 A 6 A_FaceTarget
		GNT3 BCD 4 A_FaceTarget
		GNT3 E 3 A_CustomBulletAttack(22.5,0,4,random(2,2)*1,"QuakeBulletPuff",0)
		GNT3 FGHI 4
		Goto see
	Pain:
		GNT3 A 0 A_Jump(256,"Pain1","Pain2","Pain3")
	Pain1:
		GNT4 A 0 A_ChangeFlag("NOPAIN", 1)
		GNT4 A 4 A_Pain
		GNT4 BCDEF 4
		GNT4 F 0 A_ChangeFlag("NOPAIN", 0)
		Goto see
	Pain2:
		GNT5 A 0 A_ChangeFlag("NOPAIN", 1)
		GNT5 A 4 A_Pain
		GNT5 BCDEFGHIJKLMN 4
		GNT5 N 0 A_ChangeFlag("NOPAIN", 0)
		Goto see
	Pain3:
		GNT6 A 0 A_ChangeFlag("NOPAIN", 1)
		GNT6 A 4 A_Pain
		GNT6 BCDEFGHIJKLM 4
		GNT6 M 0 A_ChangeFlag("NOPAIN", 0)
		Goto see
	Death:
		TNT1 A 0 A_GiveToTarget("KillCount",1)  
		TNT1 A 0 A_ChangeFlag("NOPAIN", 1)
		TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreDeathEffect")				
		GNT7 A 0 A_Jump(256,"Death1","Death2")
	Death1:
		GNT7 A 4
		GNT7 B 4 A_Scream
		GNT7 C 4 A_Fall
		GNT7 DEFGHI 4
		GNT7 J -1
		Stop
	Death2:
		GNT8 A 4		
		GNT8 B 4 A_Scream
		GNT8 C 4 A_Fall
		GNT8 DEFGHIJ 4
		GNT8 K -1
		Stop
	XDeath:
		TNT1 A 0 A_GiveToTarget("KillCount",1)  
		TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreXDeathEffect")				
		QZUM A 0 A_SpawnDebris("Quake_GIB12")
		QZUM A 0 A_SpawnDebris("Quake_GIB2")
		QZUM A 0 A_SpawnDebris("Quake_GIB3")
		QZUM A 0 A_SpawnItemEx("FriendlyQuake_GruntHead", 0, 0, 0, 0, 0, 10)
		TNT1 A 5 A_PlaySound("ranger/gib2")
		Stop
	Pain.LifeLeech:
		TNT1 A 0 A_GiveToTarget("SerpentHealth", random(1,2))
		TNT1 A 0 A_GiveToTarget("HealthBonus",1)
		Goto Pain		
	Pain.Vodoo:
		TNT1 A 0 A_GiveToTarget("Voodoo", 1)
		Goto Pain
	Death.HLLaser:
	Death.HLExplosive:
	Death.HLRPGExplosive:	
		TNT1 A 0 A_JumpIfHealthLower(-20,1)
		goto Death
	Death.HLEgon:
	Death.FreemanGibs:
		TNT1 A 0 A_SpawnItem("HLGenericGibSpawner")
		Goto XDeath
	Death.Dynamite:
	Death.LifeLeech:
	Death.CalebNapalm:
	Death.Shock:	  
	Death.Vodoo:
		TNT1 A 0 A_JumpIfHealthLower(-20,"XDeathForCaleb")
	Death.Tesla:
	Death.CalebFire:	
	Death.CalebBullet:	
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
		Goto Death
	XDeathForCaleb:
		TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_SpawnItemEx("CalebGibSpawner")
		TNT1 A 0 A_Jump(96,"XDeath")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1",1)
		Goto XDeath		
	Death.EradExplosive:
	Death.EradNova:
	Death.EradShock:
	Death.EradExplosive2:	
		TNT1 A 0 A_JumpIfHealthLower(-5,"XDeathForCatBitch")
	Death.EradDisc:
	Death.EradPiercingDart:
	Death.EradGreenFire:
	Death.EradCoopGreenFire:
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("EleenaDMTaunt1", 1)
		Goto Death
	XDeathForCatBitch:
		TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_Jump(96,"XDeath")
		TNT1 A 0 A_GiveToTarget("EleenaGibTaunt",1)
		Goto XDeath
	Death.DukeDamage:
	Death.DukePistol:
		TNT1 A 0 A_Jump(218,"Death")
		TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		goto Death
	Death.MightyBoot:
		TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
		TNT1 A 0 A_Jump(64,"Death")
		TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
		goto Death
	Death.DukeExplosive:
		TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
	DukeGibStart:
		TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_SpawnDebris("DukeGibs1")
		TNT1 A 0 A_SpawnDebris("DukeGibs2")
		TNT1 A 0 A_SpawnDebris("DukeGibs3")
		TNT1 A 0 A_SpawnDebris("DukeGibs4")
		TNT1 A 0 A_SpawnDebris("DukeGibs5")
		TNT1 A 0 A_SpawnDebris("DukeGibs6")
		TNT1 A 0 A_PlaySound("duke/enemygib")
		TNT1 A 0 A_Jump(96,"XDeath")
		TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
	Death.PSRAPower:
	Death.PSRAPowerCoop:
	Death.PSExplosive:
	Death.PSExplosiveCoop:
		TNT1 A 0 A_JumpIfHealthLower(-30, "XDeathForJon")
	Death.PSBullet:
	Death.PSFire:	
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("JonDMTaunt1", 1)
		Goto Death
	XDeathForJon:
		TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_SpawnItemEx("PSGibSpawner")
		TNT1 A 0 A_Jump(96,"XDeath")
		TNT1 A 0 A_GiveToTarget("JonDMTaunt1",1)
		Goto XDeath		
	Death.LeonardDamage2:
	Death.LeonardDamage3:
		TNT1 A 0 A_JumpIfHealthLower(-20, "XDeathForLeonard")
	Death.LeonardDamage0:
	Death.LeonardDamage1:	
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("LeonardDMTaunt1", 1)
		Goto Death
	XDeathForLeonard:
		TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_SpawnItemEx("RRGibSpawner")
		TNT1 A 0 A_Jump(96,"XDeath")
		TNT1 A 0 A_GiveToTarget("LeonardGibTaunt",1)
		Goto XDeath				
	Death.SWFist:
		TNT1 A 0 A_GiveToTarget("SWLoWangBloody", 1)
		TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_SpawnItemEx("SWFisted")
		TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
		goto Death	
	Death.SWKatana:
		TNT1 A 0 A_GiveToTarget("SWLoWangBloody", 1)
		TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_SpawnItemEx("SWKatanad")
		TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
		goto Death	
	Death.SWShuriken:
		TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_SpawnItemEx("SWShurikend")
		TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
		goto Death
	Death.SWSticky:
	Death.SWGrenade:
	Death.SWNuke:
	Death.SWRocket:
	Death.SWRailgun:
	Death.SWRailgunCoop:
	Death.SWNukeCoop:
	Death.SWFlame:
	Death.SWFlame2:
	Death.SWFlame2Coop:
	Death.SWFlame3:	
		TNT1 A 0 A_JumpIfHealthLower(-3,"XDeathForWang")
	Death.SWBullet:	  
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
		goto Death
	XDeathForWang:
		TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_SpawnItemEx("SWHumanGibSpawner")	
		TNT1 A 0 A_Jump(218, "XDeath")	
		TNT1 A 0 A_GiveToTarget("LoWangDMTaunt2",1)
		goto XDeath
	Death.ZorchPistol:
	Death.Zorch:
	Death.ZorchKamikaze:
	Death.PhasingZorch:
	Death.ZorchSpectral:
		"####" "#" 0 A_GiveToTarget("KillCount",1)
		"####" "#" 0 A_Stop
		"####" "#" 0 A_ChangeFlag("DONTGIB",1)
		"####" "#" 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
		"####" "#" 3 A_SpawnItem("ZorchEffectSmall")
		"####" "#" 0 A_GiveInventory("EnemyZorched",1)
		"####" "#" 3 A_SetTranslucent(0.9,0)
		"####" "#" 0 A_Scream
		"####" "#" 3 A_SetTranslucent(0.8,0)
		"####" "#" 0 A_NoBlocking
		"####" "#" 3 A_SetTranslucent(0.7,0)
		"####" "#" 3 A_SetTranslucent(0.6,0)
		"####" "#" 3 A_SetTranslucent(0.5,0)
		"####" "#" 3 A_SetTranslucent(0.4,0)
		"####" "#" 3 A_SetTranslucent(0.3,0)
		"####" "#" 3 A_SetTranslucent(0.2,0)
		"####" "#" 3 A_SetTranslucent(0.1,0) 
		TNT1 A 1 A_SetTranslucent(1,0)
		TNT1 A -1
		stop
	Death.DukeIce:
		"####" "#" 0 A_PlaySound("duke/enemyfreeze")
		"####" "#" 0 A_GiveInventory("FreezethrowerWeakness")
		"####" "#" 0 A_Jump(218,"Ice")
		"####" "#" 0 A_GiveToTarget("DukeKillTaunt",1)
	Ice:
	Death.HexenIce:
	Death.WTMeleeIce:
	Death.WTRangedIce:
	Death.Ice:
		"####" "#" 0 A_GiveToTarget("KillCount",1)
		"####" "#" 0 A_NoBlocking
		"####" "#" 0 A_GenericFreezeDeath
		"####" "#" 70 A_UnsetSolid
		"####" "#" 0 A_IceGuyDie
		"####" "#" 1 A_IceGuyDie
		TNT1 A 0
		stop
	Death.RottFire:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0 A_SpawnItem("BurningSkeleton")
		TNT1 A 5 A_NoBlocking		
		  Stop	
	Death.RottExplosive:
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0 A_SpawnItemEx("RottGibbedMan",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE)
		TNT1 A 1
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_GiveToTarget("LudicrousGibsItem", 1)
		TNT1 AA 0
		TNT1 A 5 A_NoBlocking
		Stop
	Death.RottDisintegrate:
		TNT1 A 0 A_ChangeFlag("SOLID", 0)
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0 A_Scream
		TNT1 A 5 A_SpawnItem("RGodModeDeath")
		stop	
	Disintegrate:
		"####" "#" 0 A_GiveToTarget("KillCount",1)
		"####" "#" 0 A_NoBlocking
		"####" "#" 0 A_ChangeFlag("DONTGIB",1)
		"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
		"####" "#" 0 A_Scream
		"####" "#" 0 A_ChangeFlag("SOLID",FALSE)
		"####" "#" 0 A_ChangeFlag("SHOOTABLE",FALSE)
		"####" "#" 0 A_ChangeFlag("NOGRAVITY",TRUE)
		"####" "#" 0 A_ChangeFlag("FLOORCLIP",TRUE)
		"####" "#" 0 A_Stop
		"####" "#" 3 A_SpawnItem("DisintEffectSmall",0,10)
		"####" "#" 3 A_SetTranslucent(0.9,0)
		"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
		"####" "#" 3 A_SetTranslucent(0.8,0)
		"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
		"####" "#" 3 A_SetTranslucent(0.7,0)
		"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
		"####" "#" 3 A_SetTranslucent(0.6,0)
		"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
		"####" "#" 3 A_SetTranslucent(0.5,0)
		"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
		"####" "#" 2 A_SetTranslucent(0.4,0)
		"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
		"####" "#" 2 A_SetTranslucent(0.3,0)
		"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
		"####" "#" 2 A_SetTranslucent(0.2,0)
		"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
		"####" "#" 2 A_SetTranslucent(0.1,0)
		stop
	Death.Nuke:
		"####" "#" 0 A_ChangeFlag("SOLID", 0)
		"####" "#" 0 A_GiveToTarget("KillCount",1)
		"####" "#" 0 A_Scream
		"####" "#" 0 A_NoBlocking		
		"####" "#" 0 A_SpawnItem("WTNukeDeathSmall")
		"####" "#################" 3 A_Fadeout(0.05)
		stop
  }
}

actor FriendlyQuake_GruntHead : Quake_GIB1
{
  States
  {
  Death:
    QD11 B -1 A_Gravity
    stop
  }
}

actor FriendlyQuake_GruntHead2 : Quake_GIB0 {}

ACTOR FriendlyGruntHexen : FriendlyGrunt
{
Translation "187:202=81:95"
}

//////////D64Guy
actor FriendlyDoomImp64 : DoomImp2
{
    Health 100
    Scale 0.70
    Height 64
    BloodType "Doom64Blood"
    SeeSound "friendlyimp64/sight"
    PainSound "friendlyimp64/pain"
    DeathSound "friendlyimp64/death"
    ActiveSound "friendlyimp64/active"
    MeleeSound "friendlyimp64/melee"
    MeleeDamage 3
    MissileType FriendlyDoomImp64Ball
    HitObituary "%o was somehow killed by a friendly Imp."
    Obituary "%o was somehow killed by a friendly Imp."
    Monster
    Species "Player"
    -SOLID
    +FRIENDLY    
    States
    {
      Spawn:
        64TR AB 12 A_Look
        loop
      See:
        64TR AABBCCDD 4 A_Chase
        Loop
      Melee:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        64TR EF 9 A_FaceTarget
        64TR G 8 A_MeleeAttack
        Goto See
      Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        64TR IJ 9 A_FaceTarget
        64TR K 8 A_MissileAttack
        Goto See
      Pain:
        64TR H 2
        64TR H 2 A_Pain
        Goto See        
      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        64TR L 10
        64TR M 10 A_Scream
        64TR N 8
        64TR O 8 A_NoBlocking
        64TR P -1
        Stop
      Death.DukeExplosive:
      Death.DukeSpectral:
        TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
        goto Death.DukeDamage
      DukeGibStart:
        64TR H 1
        TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_PlaySound("duke/enemygib")
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
      XDeath:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        64TR Q 6 A_PlaySound("friendlyimp64/gibbed", CHAN_VOICE)
        64TR R 6
        64TR S 6
        64TR T 6 A_NoBlocking
        64TR UVW 6
        64TR X -1
        Stop
      Death.DukeDamage:
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death
      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death
      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        64TR H 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        64TR H 16 A_Pain
        Goto See
      Pain.MarathonStun:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_GiveInventory("NothingSpeed")
        TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
        64TR H 5 A_FaceTarget
        TNT1 A 0 A_Gravity
        64TR H 30 A_Pain
        64TR H 70
        Goto See
      Death.ZorchPistol:
      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        64TR H 0 A_Stop
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        64TR H 3 A_SpawnItem("ZorchEffectSmall")
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        64TR H 3 A_SetTranslucent(0.9,0)
        64TR H 0 A_Scream
        64TR H 3 A_SetTranslucent(0.8,0)
        64TR H 0 A_NoBlocking
        64TR H 3 A_SetTranslucent(0.7,0)
        64TR H 3 A_SetTranslucent(0.6,0)
        64TR H 3 A_SetTranslucent(0.5,0)
        64TR H 3 A_SetTranslucent(0.4,0)
        64TR H 3 A_SetTranslucent(0.3,0)
        64TR H 3 A_SetTranslucent(0.2,0)
        64TR H 3 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A -1
        stop
      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
      Ice:
      Death.HexenIce:
      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        TNT1 A 0 A_NoBlocking
        //TNT1 A 0 A_SpawnItem("ImpFrozen")
        64TR H 0 A_GenericFreezeDeath
        64TR H 70 A_UnsetSolid
        64TR H 0 A_IceGuyDie
        64TR H 1 A_IceGuyDie
        TNT1 A -1
        stop
      Crush:
        64GB A 0 A_PlaySound("friendlyimp64/crush")
        64GB A -1
        Stop
    }
}

actor FriendlyDoomImp64Ball : DoomImpBall2
{
    +BLOODLESSIMPACT
    +THRUSPECIES
    Species "Player"
    Speed 8
    FastSpeed 16
    RenderStyle Normal
    Scale 0.75
    SeeSound "friendlyimp64/attack"
    DeathSound "friendlyimp64/shotx"
    States
    {
      Spawn:
        64TP ABC 5 Bright
        Loop
        
      Death:
        64TP DEFGHI 1 Bright
        Stop
    }
}

//////////IP FREELY
ACTOR FriendlyLowGuard
{
   Health 100
   Radius 17
   Height 55
   Speed 6
   Mass 5000
   PainChance 200
   MONSTER   
   +FRIENDLY
   -SOLID
   SCALE 0.60
   SeeSound "lowguard"
   PainSound "lowguard/pain"
   DeathSound "lowguard/death"
   Obituary "%o was killed by a Low Guard."
   Species "Player"   
   States   
   {
   Spawn:
       LWGS A 10 A_Look
       Loop
   See:
       LWGS BCDE 4 A_Chase
       Loop
   Missile:
       LWGS M 8 A_FaceTarget
       LWGS N 8 A_FaceTarget
	   TNT1 A 0 A_PlaySound("lowguard/fire")
       LWGS O 8 Bright A_CustomMissile("FriendlyLGBullet",32,0,0,8)
       LWGS N 8
       Goto See
   Pain:
       NULL A 0 a_jump(133,4)
       LWGS p 3
       LWGS p 4 A_Pain
       Goto See
       LWGS q 3
       LWGS q 4 A_Pain
       Goto See
   Death:
       LWGS H 5
       LWGS I 6 A_Scream
       LWGS J 7 A_Fall
       LWGS K 8
       LWGS L -1
       Stop
   XDeath:
	   TNT1 A 0 A_SpawnItem("RottGibbedMan")
	   TNT1 A 5 A_NoBlocking
	   TNT1 A 5
	   Stop
	Burn:
	Death.RottFire:
	   ske7 A 0 A_playsound("gen/burned") 
	   ske7 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 bright
	   skex ABCDEFGHIJKL 2 bright
	   skex M 0 A_playsound("gen/skel") 
	   skex MNOPQRSTUV 3 bright
	   skex V -1 a_fall    
	   Stop	
   Death.RottDisintegrate:
	   TNT1 A 0 A_ChangeFlag("SOLID", 0)
	   TNT1 A 0 A_Scream
	   TNT1 A 0 A_SpawnItem("RGodModeDeath")
	   stop	  
   Disintegrate:
       TNT1 A 0 A_GiveInventory("EnemyZorched",1)
       TNT1 A 0 A_NoBlocking
       TNT1 A 0 A_ChangeFlag("DONTGIB",1)
       TNT1 A 0 A_SpawnItemEx("LowGuardDisintegrate",0,0,5,0,0,0,0,32)
       ACLO E 1 A_PlayerScream
       ACLO E -1
       stop
   }
}

actor LowGuardDisintegrate : ZombiemanDisintegrate 
{
   States
   {
      Spawn:
        TNT1 A 0
        LGWS Q 3 A_SpawnItem("DisintEffectSmall",0,10)
        LGWS Q 3 A_SetTranslucent(0.9,0)
        LGWS Q 3 A_SetTranslucent(0.8,0)
        LGWS Q 3 A_SetTranslucent(0.7,0)
        LGWS Q 3 A_SetTranslucent(0.6,0)
        LGWS Q 3 A_SetTranslucent(0.5,0)
        LGWS Q 2 A_SetTranslucent(0.4,0)
        LGWS Q 2 A_SetTranslucent(0.3,0)
        LGWS Q 2 A_SetTranslucent(0.2,0)
        LGWS Q 2 A_SetTranslucent(0.1,0)
        Stop
   }
}

actor FriendlyLGBullet : FastProjectile
{
    Speed 300
    Damage (Random(1,3)*5)
    +THRUSPECIES
    +NOTIMEFREEZE
	+BLOODSPLATTER	
    Species "Player"
	States
	{	
      Spawn:
        TNT1 A 1
        loop
      Death:
        TNT1 A 1 A_SpawnItem("RottPuff")
        stop
      XDeath:
        TNT1 A 1
        stop
	}
}

/////BSTONE
ACTOR FriendlySectorPatrol
{
    Health 100
	Speed 7
	Radius 17
	Height 54
	Mass 10000
	Painchance 255
	MONSTER 
	+FRIENDLY
	-SOLID
	+DONTGIB
	SeeSound "sectorpatrol/sight"
	PainSound "sectorpatrol/pain"
	DeathSound "sectorpatrol/death"
	Obituary "%o was killed by a Sector Patrol guard."
    Scale 1.10
    Species "Player"	
	States
	{
		Spawn:
			BST2 A 10 A_Look
			Loop
		See:
		    BST2 BBCCDDEE 3 A_Chase
			Loop
		Pain:
		    BST2 F 3
			BST2 F 3 A_Pain
			Goto See
		Missile:
			BST2 M 5 A_FaceTarget
			BST2 N 5 A_FaceTarget
			TNT1 A 0 A_PlaySound("sectorpatrol/fire")
			BST2 O 5 Bright A_CustomMissile("FriendlyPatrolBullet",32,0,0,8)
			Goto See
		Death:
            TNT1 A 0 A_GiveToTarget("KillCount",1)		
			BST2 G 5 
			BST2 H 5 A_Scream
			BST2 I 5 A_NoBlocking
			BST2 JK 5  
			BST2 L -1
			Stop
	}
}

Actor FriendlyPatrolBullet : WolfenBullet2 { Damage (Random(1,3)*5) +THRUSPECIES Species "Player" }