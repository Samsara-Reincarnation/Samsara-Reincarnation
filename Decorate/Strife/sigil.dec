actor " Sigil " : Weapon
{
	Weapon.Kickback 100
	+WEAPON.NOALERT
	+INVENTORY.UNDROPPABLE
	Inventory.PickupSound "strife/sigilcharge"
	Inventory.PickupMessage "" //"You picked up the Sigil."
	Weapon.SelectionOrder 100
	Weapon.SlotNumber 8
	Obituary "%k \cisent\c- %o \cito the Heaven."
    Inventory.RestrictedTo "StrifeguyPlayer"
    Tag "Sigil"
	States
	  {
	   Spawn:
		  TNT1 A 0 Bright
		  TNT1 A 1 Bright A_SpawnItemEx("SamsaraSlotSevenPickup")
		  Stop
	   Ready:
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 5, "Ready5")
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 4, "Ready4")
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 3, "Ready3")
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 2, "Ready2")
		  SIGL A 1 A_WeaponReady
		  Loop
	   Ready2:
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 5, "Ready5")
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 4, "Ready4")
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 3, "Ready3")
		  SIGL B 1 A_WeaponReady
		  Loop
	   Ready3:
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 5, "Ready5")
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 4, "Ready4")
		  SIGL C 1 A_WeaponReady
		  Loop
	   Ready4:
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 5, "Ready5")
		  SIGL D 1 A_WeaponReady
		  Loop
	   Ready5:
		  SIGL E 1 A_WeaponReady
		  Loop
	   Deselect:
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 5, "Deselect5")
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 4, "Deselect4")
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 3, "Deselect3")
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 2, "Deselect2")
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 1, "Deselect1")
	   Deselect1:
		  SIGL A 1 A_Lower
		  Loop
	   Deselect2:
		  SIGL B 1 A_Lower
		  Loop
	   Deselect3:
		  SIGL C 1 A_Lower
		  Loop
	   Deselect4:
		  SIGL D 1 A_Lower
		  Loop
	   Deselect5:
		  SIGL E 1 A_Lower
		  Loop	
	   Select:
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 5, "Select5")
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 4, "Select4")
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 3, "Select3")
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 2, "Select2")
		  TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 1, "Select1")
	   Select1:
		  SIGL A 1 A_Raise
		  Loop
	   Select2:
		  SIGL B 1 A_Raise
		  Loop
	   Select3:
		  SIGL C 1 A_Raise
		  Loop
	   Select4:
		  SIGL D 1 A_Raise
		  Loop
	   Select5:
		  SIGL E 1 A_Raise
		  Loop
	  Fire:
	     "####" "#" 0 A_PlaySound("strife/sigilcharge", CHAN_WEAPON)
		 "####" "#" 18 Bright A_Light2
		 "####" "#" 3 Bright A_GunFlash
		 "####" "#" 0 A_AlertMonsters
		 "####" "#" 0 A_PlaySound("strife/sigilcharge", CHAN_WEAPON)
		 "####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred", 5, "Fire5")
		 "####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred", 4, "Fire4")
		 "####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred", 3, "Fire3")
		 "####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred", 2, "Fire2")
		 "####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred", 1, "Fire1")
	  Fire1:
		  "####" "#" 0 DamageThing(2)
		  "####" "#" 0 A_JumpIfInventory("CoopModeOn",1,2)
		  "####" "#" 15 A_FireCustomMissile("StrifeSigilLightningSpot", 0, 0)
		  Goto Ready
		  "####" "#" 15 A_FireCustomMissile("StrifeSigilLightningSpot2", 0, 0)
		  Goto Ready	  
	   Fire2:
		  "####" "#" 0 DamageThing(4)
		  "####" "#" 0 A_JumpIfInventory("CoopModeOn",1,2)	  
		  "####" "#" 15 A_FireCustomMissile("StrifeSpectralLightningH1", 0, 0)
		  Goto Ready
		  "####" "#" 15 A_FireCustomMissile("StrifeSpectralLightningH1_C", 0, 0)
		  Goto Ready	  
	   Fire3:
	      "####" "#" 0 DamageThing(6)
		  "####" "#" 0 A_GiveInventory("Sigil3AttackCounter", 20)
		  "####" "#" 0 A_SetAngle(angle-90)
	   Fire3Loop:
		  "####" "#" 0 A_SetAngle(angle+9)
		  "####" "#" 0 A_JumpIfInventory("CoopModeOn",1,2)	  	  
		  "####" "#" 0 A_FireCustomMissile("StrifeSpectralLightningBall1", 0, 0)
		  Goto ContinueFileLoop
		  "####" "#" 0 A_FireCustomMissile("StrifeSpectralLightningBall1_C", 0, 0)	  
	   ContinueFileLoop:
		  "####" "#" 0 A_TakeInventory("Sigil3AttackCounter", 1)
		  "####" "#" 0 A_JumpIfInventory("Sigil3AttackCounter", 1, "Fire3Loop")
		  "####" "#" 15 A_SetAngle(angle-90)
		  Goto Ready
	   Fire4:
	      "####" "#" 0 DamageThing(8)
		  "####" "#" 0 A_JumpIfInventory("CoopModeOn",1,6)	  
		  "####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV", -15, 0)
		  "####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV", 15, 0)
	  	  "####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV", -7.5, 0)
		  "####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV", 7.5, 0)
		  "####" "#" 15 A_FireCustomMissile("StrifeSigilLightningBigV", 0, 0)
		  Goto Ready
		  "####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV2", -15, 0)
		  "####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV2", 15, 0)
		  "####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV2", -7.5, 0)
		  "####" "#" 0 A_FireCustomMissile("StrifeSigilLightningBigV2", 7.5, 0)
		  "####" "#" 15 A_FireCustomMissile("StrifeSigilLightningBigV2", 0, 0)
		  Goto Ready	  
	   Fire5:
	      "####" "#" 0 DamageThing(10)
		  "####" "#" 0 Bright A_JumpIfInventory("CoopModeOn",1,2)	
		  "####" "#" 10 Bright A_FireCustomMissile("StrifeSpectralLightningBigBall")
		  Goto ContinueFire5
		  "####" "#" 10 Bright A_FireCustomMissile("StrifeSpectralLightningBigBall2")	
	   ContinueFire5:
		  "####" "#" 5
		  Goto Ready
	   AltFire:
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred",0,"AltFire1")
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred",4,"AltFire5")
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred",3,"AltFire4")
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred",2,"AltFire3")
		"####" "#" 0 A_JumpIfInventory("StrifeSigilPiecePreferred",1,"AltFire2")
		Goto Ready
	   AltFire1:
		"####" "#" 0 A_TakeInventory("StrifeSigilPiecePreferred",5)
		"####" "#" 0 A_GiveInventory("StrifeSigilPiecePreferred",1)
		"####" "#" 0 A_Print("Sigil Mode: Lightning Strike",1.0)
		"####" "#" 15
		Goto Ready
	   AltFire2:
	     "####" "#" 0 A_JumpIfInventory("StrifeSigilPiece",2,1)
		 Goto AltFire1
		 "####" "#" 0 A_GiveInventory("StrifeSigilPiecePreferred",1)
		 "####" "#" 0 A_Print("Sigil Mode: Lightning Bolt",1.0)
		 "####" "#" 15
		 Goto Ready
	   AltFire3:
		 "####" "#" 0 A_JumpIfInventory("StrifeSigilPiece",3,1)
		 Goto AltFire1
	     "####" "#" 0 A_GiveInventory("StrifeSigilPiecePreferred",1)
		 "####" "#" 0 A_Print("Sigil Mode: Lightning Wall",1.0)
		 "####" "#" 15
		 Goto Ready
	   AltFire4:
		 "####" "#" 0 A_JumpIfInventory("StrifeSigilPiece",4,1)
		 Goto AltFire1
		 "####" "#" 0 A_GiveInventory("StrifeSigilPiecePreferred",1)
		 "####" "#" 0 A_Print("Sigil Mode: Homing Lightning",1.0)
		 "####" "#" 15
		 Goto Ready
	   AltFire5:
		 "####" "#" 0 A_JumpIfInventory("StrifeSigilPiece",5,1)
		 Goto AltFire1
	     "####" "#" 0 A_GiveInventory("StrifeSigilPiecePreferred",1)
		 "####" "#" 0 A_Print("Sigil Mode: Lightning Storm",1.0)
		 "####" "#" 15
		 Goto Ready
	   Flash:
		  SIGF A 4 Bright A_Light2
		  SIGF B 6 Bright A_LightInverse
		  SIGF C 4 Bright A_Light1
		  SIGF C 0 Bright A_Light0
		  Stop
	  }
}

Actor StrifeSigilPiece : Inventory { Inventory.Amount 1 Inventory.MaxAmount 5 Inventory.InterHubAmount 5 }
Actor StrifeSigilPiecePreferred : Inventory { Inventory.Amount 1 Inventory.MaxAmount 5 Inventory.InterHubAmount 5 }
Actor Sigil3AttackCounter : Inventory { Inventory.Amount 1 Inventory.MaxAmount 20 }

Actor StrifeSigil1Pickup : CustomInventory //replaces Sigil1
{
   Inventory.PickupMessage "$TXT_SIGIL"
   Inventory.PickupSound "weapons/sigilcharge"
   +FLOORCLIP
   States
   {
   Pickup:
      TNT1 A 0 A_JumpIfInventory(" Sigil ", 1, "GivePieceLoop")
      TNT1 A 0 A_TakeInventory("StrifeSigilPiece", 5)
      TNT1 A 0 A_GiveInventory(" Sigil ", 1)
      TNT1 A 0 A_Jump(256, "GivePieceLoop")
      Stop
   Drop:
      TNT1 A 0 A_GiveInventory(" Sigil ", 1)
      TNT1 A 0 A_GiveInventory("StrifeSigilPiece", 1)
      Stop
   Spawn:
      SIGP A -1
      Stop
   GivePieceLoop:
      TNT1 A 0
      Stop
   }
}

Actor StrifeSigil2Pickup : StrifeSigil1Pickup //replaces Sigil2
{
   States
   {
   Spawn:
      SIGP B -1
      Stop
   GivePieceLoop:
      TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 2, 2)
      TNT1 A 0 A_GiveInventory("StrifeSigilPiece", 1)
      Loop
      TNT1 A 0
      Stop
   }
}

Actor StrifeSigil3Pickup : StrifeSigil1Pickup //replaces Sigil3
{
   States
   {
   Spawn:
      SIGP C -1
      Stop
   GivePieceLoop:
      TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 3, 2)
      TNT1 A 0 A_GiveInventory("StrifeSigilPiece", 1)
      Loop
      TNT1 A 0
      Stop
   }
}

Actor StrifeSigil4Pickup : StrifeSigil1Pickup //replaces Sigil4
{
   States
   {
   Spawn:
      SIGP D -1
      Stop
   GivePieceLoop:
      TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 4, 2)
      TNT1 A 0 A_GiveInventory("StrifeSigilPiece", 1)
      Loop
      TNT1 A 0
      Stop
   }
}

Actor StrifeSigil5Pickup : StrifeSigil1Pickup //replaces Sigil5
{
   States
   {
   Spawn:
      SIGP E -1
      Stop
   GivePieceLoop:
      TNT1 A 0 A_JumpIfInventory("StrifeSigilPiece", 5, 2)
      TNT1 A 0 A_GiveInventory("StrifeSigilPiece", 1)
      Loop
      TNT1 A 0
      Stop
   }
}

Actor StrifeSigilLightningSpot : SpectralLightningSpot
{
	SeeSound "strife/sigil" 
	DeathSound "strife/sigilhit"
   +SPECTRAL //This flag is needed, otherwise the SpectralLightningV1/V2
/* States    //actors won't damage the spectres. Wierd.
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_TransferPointer(AAPTR_TARGET, AAPTR_DEFAULT, AAPTR_PLAYER_GETTARGET, AAPTR_TRACER)
      TNT1 A 0 A_CheckFlag("SHOOTABLE", 3, AAPTR_TRACER)
      TNT1 A 0 A_Warp(AAPTR_TARGET) //Try to stick to the player's feet.
      ZAP5 A 4 Bright A_ChangeVelocity(28,0,0, CVF_RELATIVE|CVF_REPLACE)
      Goto Super::Spawn+1
      ZAP5 A 4 Bright A_Warp(AAPTR_TRACER, 0,0,0, 0, WARPF_NOCHECKPOSITION|WARPF_STOP|WARPF_TOFLOOR)
      Goto Super::Spawn+1
   }*/
}

Actor StrifeSigilLightningSpot2 : StrifeSigilLightningSpot { +THRUSPECIES +DONTHARMSPECIES +DONTHARMCLASS +MTHRUSPECIES Species "Player" }
Actor StrifeSpectralLightningH1 : SpectralLightningH1 { Damage 20 SeeSound "strife/sigil" DeathSound "strife/sigilhit" }
Actor StrifeSpectralLightningH1_C : StrifeSpectralLightningH1 { Species "Player" +THRUSPECIES +DONTHARMSPECIES +DONTHARMCLASS +MTHRUSPECIES }
Actor StrifeSpectralLightningBall1 : SpectralLightningBall1 { Damage 5 SeeSound "strife/sigil" DeathSound "strife/sigilhit" }
Actor StrifeSpectralLightningBall1_C : StrifeSpectralLightningBall1 { Species "Player" +THRUSPECIES +DONTHARMSPECIES +DONTHARMCLASS +MTHRUSPECIES }

Actor StrifeSigilLightningBigV : SpectralLightningBigV1 
{
   SeeSound "strife/sigil" 
   DeathSound "strife/sigilhit"
   Damage 25
   +SPECTRAL
   States
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_TransferPointer(AAPTR_TARGET, AAPTR_DEFAULT, AAPTR_PLAYER_GETTARGET, AAPTR_TRACER)
      TNT1 A 0 A_CheckFlag("SHOOTABLE", 2, AAPTR_TRACER)
      ZOT2 A 4 Bright A_ChangeVelocity(28,0,0, CVF_RELATIVE)
      Goto Super::Spawn+1
      ZOT2 A 4 Bright
      Goto Super::Spawn+1
   }
}

Actor StrifeSigilLightningBigV2 : StrifeSigilLightningBigV { +THRUSPECIES +DONTHARMSPECIES +DONTHARMCLASS +MTHRUSPECIES Species "Player" } 

actor StrifeSpectralLightningH
{
  +FORCEXYBILLBOARD
  +NOTELEPORT
  +ACTIVATEIMPACT
  +ACTIVATEPCROSS
  +SPECTRAL
  +FORCERADIUSDMG  
  MaxStepHeight 4
  RenderStyle Add
  Speed 30
  Radius 8
  Height 16
  Damage (random(1,4)*35)
  Projectile
  DeathSound "strife/sigilhit"
  Decal "BaronScorch"
  Obituary "%k \cisent\c- %o \cito the Heaven."
  States
  {
  Spawn:
    ZAP6 A 4 Bright
    ZAP6 BC 4 Bright A_SpawnItemEx("StrifeSpectralLightningHTail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
    Loop
  Death:
    AHIT B 3 A_Explode(32, 32)
    AHIT ABCDEFE 3
    AHIT DCB 2
    AHIT A 1
    Stop
  }
}

actor StrifeSpectralLightningH2 : StrifeSpectralLightningH { +THRUSPECIES Species "Player" +DONTHARMCLASS +DONTHARMSPECIES +MTHRUSPECIES }

actor StrifeSpectralLightningHTail
{
  +FORCEXYBILLBOARD
  +NOGRAVITY
  +CLIENTSIDEONLY  
  Renderstyle Add
  States
  {
  Spawn:
    ZAP6 ABC 5 Bright
    Stop
  }
}

actor StrifeSpectralLightningBigBall
{
  +NOTELEPORT
  +ACTIVATEIMPACT
  +ACTIVATEPCROSS
  +SPECTRAL
  +FORCEXYBILLBOARD
  Speed 18
  Radius 16
  Height 32
  Damage (random(1,4)*100)
  Projectile
  MaxStepHeight 4
  RenderStyle Add
  SeeSound "strife/sigil"
  DeathSound "strife/sigilhit"
  Decal "BaronScorch"
  Obituary "%k \cisent\c- %o \cito the Heaven."
  States
  {
  Spawn:
    ZAP7 A 0 Bright A_CustomMissile("StrifeSpectralLightningH",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 A 0 Bright A_CustomMissile("StrifeSpectralLightningH",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
    ZAP7 A 4 Bright A_CustomMissile("StrifeSpectralLightningH",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 B 0 Bright A_CustomMissile("StrifeSpectralLightningH",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 B 0 Bright A_CustomMissile("StrifeSpectralLightningH",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 B 4 Bright A_CustomMissile("StrifeSpectralLightningH",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 C 0 Bright A_CustomMissile("StrifeSpectralLightningH",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 C 0 Bright A_CustomMissile("StrifeSpectralLightningH",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
    ZAP7 C 4 Bright A_CustomMissile("StrifeSpectralLightningH",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 D 0 Bright A_CustomMissile("StrifeSpectralLightningH",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 D 0 Bright A_CustomMissile("StrifeSpectralLightningH",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 D 4 Bright A_CustomMissile("StrifeSpectralLightningH",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 E 0 Bright A_CustomMissile("StrifeSpectralLightningH",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 E 0 Bright A_CustomMissile("StrifeSpectralLightningH",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 E 4 Bright A_CustomMissile("StrifeSpectralLightningH",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
    Loop
  Death:
    AHIT BCDEFE 3
    AHIT DCB 2
    AHIT A 1
    Stop
  }
}

actor StrifeSpectralLightningBigBall2 : StrifeSpectralLightningBigBall
{
  +THRUSPECIES
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +MTHRUSPECIES
  Species "Player"
  States
  {
  Spawn:
    ZAP7 A 0 Bright A_CustomMissile("StrifeSpectralLightningH2",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 A 0 Bright A_CustomMissile("StrifeSpectralLightningH2",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
    ZAP7 A 4 Bright A_CustomMissile("StrifeSpectralLightningH2",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 B 0 Bright A_CustomMissile("StrifeSpectralLightningH2",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 B 0 Bright A_CustomMissile("StrifeSpectralLightningH2",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 B 4 Bright A_CustomMissile("StrifeSpectralLightningH2",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 C 0 Bright A_CustomMissile("StrifeSpectralLightningH2",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 C 0 Bright A_CustomMissile("StrifeSpectralLightningH2",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
    ZAP7 C 4 Bright A_CustomMissile("StrifeSpectralLightningH2",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 D 0 Bright A_CustomMissile("StrifeSpectralLightningH2",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 D 0 Bright A_CustomMissile("StrifeSpectralLightningH2",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 D 4 Bright A_CustomMissile("StrifeSpectralLightningH2",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 E 0 Bright A_CustomMissile("StrifeSpectralLightningH2",8,0,-90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 E 0 Bright A_CustomMissile("StrifeSpectralLightningH2",8,0,90,CMF_TRACKOWNER|CMF_AIMDIRECTION)
	ZAP7 E 4 Bright A_CustomMissile("StrifeSpectralLightningH2",8,0,0,CMF_TRACKOWNER|CMF_AIMDIRECTION)
    Loop
  Death:
    AHIT BCDEFE 3
    AHIT DCB 2
    AHIT A 1
    Stop
  }
}

Actor StrifeSigilPieceCheck1 : Inventory { Inventory.Amount 1 Inventory.MaxAmount 1 }
Actor StrifeSigilPieceCheck2 : Inventory { Inventory.Amount 1 Inventory.MaxAmount 1 }
Actor StrifeSigilPieceCheck3 : Inventory { Inventory.Amount 1 Inventory.MaxAmount 1 }
Actor StrifeSigilPieceCheck4 : Inventory { Inventory.Amount 1 Inventory.MaxAmount 1 }
Actor StrifeSigilPieceCheck5 : Inventory { Inventory.Amount 1 Inventory.MaxAmount 1 }