Actor DeusEx_DartsLoaded : Boolean {}
Actor DeusEx_TranqDartsLoaded : Boolean {}
Actor DeusEx_FlareDartsLoaded : Boolean {}

Actor DeusEx_MiniXBowMagazine : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.MaxAmount 8 // was 4
}

Actor DeusEx_MiniCrossbow : DeusExWeapon
{
	Weapon.SelectionOrder 1600
	Weapon.SlotNumber 1
	Weapon.SlotPriority 2
	Weapon.AmmoType1 "DeusEx_MiniXBowMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 16
	Tag "Mini Crossbow"
	Weapon.UpSound "DeusEx/Crossbow/Select"
	Inventory.PickupMessage "You found a Mini Crossbow."
	Obituary "%o was punctured with darts by %k."
	States
	{
		Spawn:
			DX03 A -1
			Stop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop
	    Ready:
			DX00 B 0 A_GiveInventory("DeusEx_MiniXBowEquipped", 1)
			DX00 BCDEFGHIJKL 2
			DX00 L 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
		ReadyLoop:
			DX00 M 1 A_WeaponReady(WRF_ALLOWRELOAD) DX00 M 0 A_Jump(3, "Idle1", "Idle2", "Idle3")
			Loop
		Idle1:
			DX01 "UVWYZ[\]" 6 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Idle2:
			DX02 ABCDEF 6 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Idle3:
			DX02 GHIJKLMNOP 6 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Fire:
			DX00 P 0 A_JumpIfInventory("DeusEx_MiniXBowMagazine", 1, "Fire2")
			Goto FireReload
		Fire2:
			DX00 N 0 A_JumpIfInventory("DeusEx_FlareDartsLoaded", 1, "FireFlare")
			DX00 N 0 A_JumpIfInventory("DeusEx_DartsLoaded", 1, "FireTranq")
			DX00 N 0 A_JumpIfInventory("DeusEx_TranqDartsLoaded", 1, "FireDarts")
			Goto FireTranq
		FireDarts:
			DX00 N 0 A_JumpIfInventory("CoopModeOn",1,2)
			DX00 N 2 A_FireCustomMissile("DeusEx_Dart", Random(-2,2), 1, 8, -3, 0, Random(-1,1))
			Goto FireEnd
			DX00 N 2 A_FireCustomMissile("DeusEx_DartCoop", Random(-2,2), 1, 8, -3, 0, Random(-1,1))
			Goto FireEnd
		FireTranq:
			DX00 N 0 A_JumpIfInventory("CoopModeOn",1,2)
			DX00 N 2 A_FireCustomMissile("DeusEx_TranqDart", Random(-2,2), 1, 8, -3, 0, Random(-1,1))
			Goto FireEnd
			DX00 N 2 A_FireCustomMissile("DeusEx_TranqDartCoop", Random(-2,2), 1, 8, -3, 0, Random(-1,1))
			Goto FireEnd
		FireFlare:
			DX00 N 0 A_JumpIfInventory("CoopModeOn",1,2)
			DX00 N 2 A_FireCustomMissile("DeusEx_FlareDart", Random(-2,2), 1, 8, -3, 0, Random(-1,1))
			Goto FireEnd
			DX00 N 2 A_FireCustomMissile("DeusEx_FlareDartCoop", Random(-2,2), 1, 8, -3, 0, Random(-1,1))
			Goto FireEnd
		FireEnd:
			DX00 O 0 A_PlaySound("DeusEx/Stealth/Fire", CHAN_WEAPON)
			DX01 D 0 A_JumpIfInventory("DeusEx_PistolSkill", 4, "FireEnd4")
			DX01 D 0 A_JumpIfInventory("DeusEx_PistolSkill", 3, "FireEnd3")
			DX01 D 0 A_JumpIfInventory("DeusEx_PistolSkill", 2, "FireEnd2")
			DX01 D 0 A_JumpIfInventory("DeusEx_PistolSkill", 1, "FireEnd1")
			DX00 "OPQRSTUVWXYZ[\" 2
			Goto ReadyLoop
		FireEnd1:
			DX00 OPQRSTUVWX 2
			DX00 "YZ[\" 1
			Goto ReadyLoop
		FireEnd2:
			DX00 OPQRSTU 2
			DX00 "VWXYZ[\" 1
			Goto ReadyLoop
		FireEnd3:
			DX00 OPQR 2
			DX00 "STUVWXYZ[\" 1
			Goto ReadyLoop
		FireEnd4:
			DX00 "OPQRSTUVWXYZ[\" 1
			Goto ReadyLoop
		FireReload:
			DX00 M 0 A_PlaySound("DeusEx/DryFire", CHAN_WEAPON)
			DX00 MMM 1
			DX00 M 1 A_ClearRefire
			Goto Reload
		Reload:
			DX01 A 0 A_JumpIfInventory("DeusEx_MiniXBowMagazine", 0, "ReadyLoop")
			DX01 A 0 A_JumpIfInventory("Shell", 1, "ReloadSuccess")
			Goto ReadyLoop
		ReloadSuccess:
			DX01 A 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX01 A 0 A_PlaySound("DeusEx/Crossbow/Reload1", CHAN_WEAPON)
			DX01 ABC 2 A_WeaponReady(WRF_NOFIRE)
			DX01 D 0 A_JumpIfInventory("DeusEx_PistolSkill", 4, "ReloadAmmo")
			DX01 D 0 A_JumpIfInventory("DeusEx_PistolSkill", 3, 9)
			DX01 D 0 A_JumpIfInventory("DeusEx_PistolSkill", 2, 5)
			DX01 D 0 A_JumpIfInventory("DeusEx_PistolSkill", 1, 3)
			DX01 DEFGHIJDEFGHIJ 4 A_WeaponReady(WRF_NOFIRE)
			Goto ReloadAmmo
		ReloadAmmo:
			TNT1 A 0 A_JumpIfInventory("DeusEx_MiniXBowMagazine",0,"ReloadFinish")
			TNT1 A 0 A_JumpIfInventory("Shell",1,1)
			goto ReloadFinish
			TNT1 A 0 A_GiveInventory("DeusEx_MiniXBowMagazine",1)
			TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
			loop
		ReloadFinish:
			DX01 K 0 A_PlaySound("DeusEx/Crossbow/Reload2", CHAN_WEAPON)
			DX01 KLMN 2
			Goto ReadyLoop
		Deselect:
			DX01 O 0 A_TakeInventory("DeusEx_MiniXBowEquipped", 1)
			DX01 O 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX01 OPQRS 2
			Goto Super::InstantDeselect
		AltFire:
			TNT1 A 0 A_JumpIfInventory("DeusEx_MiniXBowMagazine",1,1)
			Goto Reload // If no ammo is in the mag, reload first
			DX01 A 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX01 A 0 A_PlaySound("DeusEx/Crossbow/Reload1", CHAN_WEAPON)
			DX01 ABC 2
			DX01 D 0 A_JumpIfInventory("DeusEx_PistolSkill", 4, 17)
			DX01 D 0 A_JumpIfInventory("DeusEx_PistolSkill", 3, 9)
			DX01 D 0 A_JumpIfInventory("DeusEx_PistolSkill", 2, 5)
			DX01 D 0 A_JumpIfInventory("DeusEx_PistolSkill", 1, 3)
			DX01 DEFGHIJDEFGHIJ 4 A_WeaponReady(WRF_NOFIRE)
			DX01 A 0 A_JumpIfInventory("DeusEx_FlareDartsLoaded", 0, "CheckTranq")
			DX01 A 0 A_JumpIfInventory("DeusEx_TranqDartsLoaded", 0, "CheckFlare")
			DX01 A 0 A_JumpIfInventory("Shell", 1, "ChangeToTranq")
			Goto ReadyLoop
		CheckTranq:
			//DX01 A 0 A_JumpIfInventory("DeusEx_DartsLoaded", 0, "CheckDarts")
			DX01 A 0 A_JumpIfInventory("Shell", 1, "ChangeToDarts")
			Goto ReadyLoop
		CheckDarts:
			//DX01 A 0 A_JumpIfInventory("DeusEx_DartsLoaded", 0, "CheckDarts")
			DX01 A 0 A_JumpIfInventory("Shell", 1, "ChangeToDarts")
			Goto ReadyLoop
		CheckFlare:
			//DX01 A 0 A_JumpIfInventory("DeusEx_DartsLoaded", 0, "CheckDarts")
			DX01 A 0 A_JumpIfInventory("Shell", 1, "ChangeToFlare")
			Goto ReadyLoop
		ChangeToTranq:
			TNT1 A 0 A_TakeInventory("DeusEx_DartsLoaded", 999)
			TNT1 A 0 A_TakeInventory("DeusEx_FlareDartsLoaded", 999)
			TNT1 A 0 A_GiveInventory("DeusEx_TranqDartsLoaded", 1)
			TNT1 A 0 A_Print("Mini Crossbow now has Darts Loaded.", 2)
			Goto AltfireEnd
		ChangeToDarts:
			TNT1 A 0 A_TakeInventory("DeusEx_TranqDartsLoaded", 999)
			TNT1 A 0 A_TakeInventory("DeusEx_FlareDartsLoaded", 999)
			TNT1 A 0 A_GiveInventory("DeusEx_DartsLoaded", 1)
			TNT1 A 0 A_Print("Mini Crossbow now has Tranquilizer Darts Loaded.", 2)
			Goto AltfireEnd	
		ChangeToFlare:
			TNT1 A 0 A_TakeInventory("DeusEx_TranqDartsLoaded", 999)
			TNT1 A 0 A_TakeInventory("DeusEx_DartsLoaded", 999)
			TNT1 A 0 A_GiveInventory("DeusEx_FlareDartsLoaded", 1)
			TNT1 A 0 A_Print("Mini Crossbow now has Flare Darts Loaded.", 2)
			Goto AltfireEnd
		AltFireEnd:
			DX01 K 0 A_PlaySound("DeusEx/Crossbow/Reload2", CHAN_WEAPON)
			DX01 KLMN 2
			DX01 O 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
	}
}

Actor DeusEx_Dart : DeusExBullet
{
	Speed 75
	Scale 0.5
	Damage(CallACS("DeusEx_WeaponSkillDamage", 1, 20))
	DamageType "DeusExPistol"
	RenderStyle Normal
	States
	{
		Spawn:
			TNT1 A 0 NODELAY A_SetUserVar(user_damage, CallACS("DeusEx_WeaponSkillDamage", 1, 20))
			TNT1 A 1
			Goto Spawn2
		Death:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_AlertMonsters(64)
			DX00 A 1 //A_SpawnItemEx("BulletPuff",0,0,0,0,0,1,0,0)
			Goto DeathLoop
		DeathLoop:
			DX00 A 1 A_JumpIfInTargetInventory("DeusExClass", 1, 1)
			Loop
			DX00 A 1 A_JumpIfCloser(48, "TakeShell")
			Loop
		TakeShell:
			TNT1 A 0 A_PlaySound("DeusEx/WeaponPickup", CHAN_BODY)
			TNT1 A 2 A_GiveInventory("Shell", 1, AAPTR_TARGET)
			Stop
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 20)),8,!XF_HURTSOURCE,0,8) //was 15
			stop
		XDeath:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 20)),8,!XF_HURTSOURCE,0,8)
			stop
	}
}

Actor DeusEx_DartCoop : DeusEx_Dart { Species "Player" +THRUSPECIES }

Actor DeusEx_TranqDart : DeusEx_Dart
{
	DamageType "DeusExKnockedOut"
	Damage (CallACS("DeusEx_WeaponSkillDamage", 1, 8))
	PoisonDamageType "DeusExPoisonEffect"
	PoisonDamage 8, 16, 70 //can't scale up poison damage =(
	+ADDITIVEPOISONDURATION
	States
	{
		Spawn:
			TNT1 A 0 //NODELAY A_SetUserVar(user_damage, CallACS("DeusEx_WeaponSkillDamage", 1, 10))
			TNT1 A 1
			Goto Spawn2
		Death:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_AlertMonsters(64)
			DX00 A 1 //A_SpawnItemEx("BulletPuff",0,0,0,0,0,1,0,0)
			Goto DeathLoop
		DeathLoop:
			DX00 A 1 A_JumpIfInTargetInventory("DeusExClass", 1, 1)
			Loop
			DX00 A 1 A_JumpIfCloser(48, "TakeShell")
			Loop
		TakeShell:
			TNT1 A 0 A_PlaySound("DeusEx/WeaponPickup", CHAN_BODY)
			TNT1 A 2 A_GiveInventory("Shell", 1, AAPTR_TARGET)
			Stop
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 8)),8,!XF_HURTSOURCE,0,8) //was 5
			stop
		XDeath:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 8)),8,!XF_HURTSOURCE,0,8) // was 5
			stop
	}
}

Actor DeusEx_TranqDartCoop : DeusEx_TranqDart { Species "Player" +THRUSPECIES }

Actor DeusEx_FlareDart : DeusEx_Dart
{
	DamageType "DeusExPistolFire"
	Damage (CallACS("DeusEx_WeaponSkillDamage", 1, 5))
	PoisonDamageType "DeusExPistolFire"
	PoisonDamage 5, 5, 12 //can't scale up poison damage =(
	ReactionTime 60
	+ADDITIVEPOISONDURATION
	States
	{
		Spawn:
			TNT1 A 0 //NODELAY A_SetUserVar(user_damage, CallACS("DeusEx_WeaponSkillDamage", 1, 10))
			TNT1 A 1
			Goto Spawn2
		Death:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_AlertMonsters(64)
			DX00 A 1 BRIGHT //A_SpawnItemEx("BulletPuff",0,0,0,0,0,1,0,0)
			Goto DeathLoop
		DeathLoop:
			//DX00 A 0 A_GiveInventory("DeusEx_FlareDartSubTimer", 1)
			//DX00 A 0 A_JumpIfInventory("DeusEx_FlareDartSubTimer", 0, "GiveTimer")
			DX00 A 1 BRIGHT A_JumpIfInTargetInventory("DeusExClass", 1, 1)
			Loop
			//DX00 A 0 A_GiveInventory("DeusEx_FlareDartSubTimer", 1)
			//DX00 A 0 A_JumpIfInventory("DeusEx_FlareDartSubTimer", 0, "GiveTimer")
			DX00 A 1 BRIGHT A_JumpIfCloser(48, "TakeShell")
			Loop
		GiveTimer:
			DX00 A 0 A_TakeInventory("DeusEx_FlareDartSubTimer", 35)
			DX00 A 0 A_GiveInventory("DeusEx_FlareDartTimer", 1)
			DX00 A 0 A_JumpIfInventory("DeusEx_FlareDartTimer", 0, "Fade")
			Goto DeathLoop
		Fade:
			DX00 AAAAA 1 BRIGHT A_FadeOut(0.2)
			Stop
		TakeShell:
			TNT1 A 0 A_PlaySound("DeusEx/WeaponPickup", CHAN_BODY)
			TNT1 A 2 A_GiveInventory("Shell", 1, AAPTR_TARGET)
			Stop
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 5)),8,!XF_HURTSOURCE,0,8) //was 5
			stop
		XDeath:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 5)),8,!XF_HURTSOURCE,0,8) // was 5
			stop
	}
}

Actor DeusEx_FlareDartCoop : DeusEx_FlareDart { Species "Player" +THRUSPECIES }

Actor DeusEx_XBowSpread : Ammo { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 40 Inventory.InterHubAmount 0 Ammo.BackPackMaxAmount 40 }

Actor DeusEx_FlareDartTimer : Inventory { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 20 Inventory.InterHubAmount 0  }
Actor DeusEx_FlareDartSubTimer : Inventory { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 35 Inventory.InterHubAmount 0  }