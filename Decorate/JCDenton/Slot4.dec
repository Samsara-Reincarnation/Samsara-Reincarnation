Actor DeusEx_AssaultGunMagazine : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.MaxAmount 30
}

Actor DeusEx_762Loaded : Boolean {}
Actor DeusEx_20mmHELoaded : Boolean {}
Actor DeusEx_AssaultFireAlt : Boolean {}

Actor DeusEx_AssaultRifle : DeusExWeapon
{
	Weapon.SelectionOrder 1600
	Weapon.SlotNumber 4
	Weapon.SlotPriority 1
	Weapon.AmmoType1 "DeusEx_AssaultGunMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Clip"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 30
	Tag "Assault Rifle"
	Weapon.UpSound "DeusEx/AssaultGun/Select"
	Inventory.PickupMessage "You found an Assault Rifle."
	Obituary "%o was perforated with %k's Assault Rifle."
	States
	{
		Spawn:
			DX03 A -1
			Stop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop
	    Ready:
			DX00 B 0 A_GiveInventory("DeusEx_AssGunEquipped", 1)
			DX00 BCDEFG 2
			DX00 G 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
		ReadyLoop:
			DX00 H 1 A_WeaponReady(WRF_ALLOWRELOAD) DX00 H 0 A_TakeInventory("DeusEx_AssaultGunSpread", 1) DX00 H 0 A_Jump(2, "IdleAnim")
			Loop
		IdleAnim:
			DX01 IJKLMNOP 6 A_WeaponReady(WRF_ALLOWRELOAD)
			DX01 QRSTUVWX 6 A_WeaponReady(WRF_ALLOWRELOAD)
			DX01 "YZ[\]" 6 A_WeaponReady(WRF_ALLOWRELOAD)
			DX02 AB 6 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Fire:
			DX00 H 0 A_JumpIfInventory("DeusEx_20mmHELoaded", 1, "Fire20mmCannister")
			DX00 H 0 A_JumpIfInventory("DeusEx_762Loaded", 1, "FireBullets")
		FireBullets:
			DX00 H 0 A_JumpIfInventory("DeusEx_AssaultGunMagazine", 1, "Fire2")
			Goto FireReload	
		Fire2:
			TNT1 A 0 ACS_NamedExecuteAlways("DeusExRecoil", 0, 1)
			DX00 H 0 A_Jump(256, "Fire3")
			Goto Fire3
		Fire3:
			DX00 I 2 BRIGHT A_PlaySound("DeusEx/AssaultGun/Fire", CHAN_WEAPON)
			DX00 J 0 BRIGHT A_SpawnItemEx("DeusEx_ShellCasing1", 8, -16, 39, Random(1,3), 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 J 1 BRIGHT A_GiveInventory("SamsaraDeusExAssaultGunAttackHandler")
			Goto Fire4
		Fire4:
			DX00 H 0 A_JumpIfInventory("DeusEx_AssaultGunMagazine", 1, 1)
			Goto FireReload		
			TNT1 A 0 ACS_NamedExecuteAlways("DeusExRecoil", 0, 1)
			DX00 K 2 BRIGHT //A_PlaySound("DeusEx/AssaultGun/Fire", CHAN_WEAPON)
			DX00 M 0 BRIGHT A_SpawnItemEx("DeusEx_ShellCasing1", 8, -16, 39, Random(1,3), 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 M 0 BRIGHT A_GiveInventory("DeusEx_AssaultGunSpread", 2)
			DX00 L 1 BRIGHT A_GiveInventory("SamsaraDeusExAssaultGunAttackHandler")
			Goto Fire5
		Fire5:
			DX00 H 0 A_JumpIfInventory("DeusEx_AssaultGunMagazine", 1, 1)
			Goto FireReload
			TNT1 A 0 ACS_NamedExecuteAlways("DeusExRecoil", 0, 1)
			DX00 M 2 BRIGHT A_PlaySound("DeusEx/AssaultGun/Fire", CHAN_WEAPON)
			DX00 P 0 BRIGHT A_SpawnItemEx("DeusEx_ShellCasing1", 8, -16, 39, Random(1,3), 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 P 0 BRIGHT A_GiveInventory("DeusEx_AssaultGunSpread", 2)
			DX00 N 1 BRIGHT A_GiveInventory("SamsaraDeusExAssaultGunAttackHandler")
			Goto Fire6
		Fire6:
			DX00 H 0 A_JumpIfInventory("DeusEx_AssaultGunMagazine", 1, 1)
			Goto FireReload
			TNT1 A 0 ACS_NamedExecuteAlways("DeusExRecoil", 0, 1)
			DX00 O 2 BRIGHT //A_PlaySound("DeusEx/AssaultGun/Fire", CHAN_WEAPON)
			DX00 P 0 BRIGHT A_GiveInventory("DeusEx_AssaultGunSpread", 2)
			DX00 P 0 BRIGHT A_SpawnItemEx("DeusEx_ShellCasing1", 8, -16, 39, Random(1,3), 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 P 1 BRIGHT A_GiveInventory("SamsaraDeusExAssaultGunAttackHandler")
			Goto Fire7
		Fire7:
			DX00 H 0 A_JumpIfInventory("DeusEx_AssaultGunMagazine", 1, 1)
			Goto FireReload
			TNT1 A 0 ACS_NamedExecuteAlways("DeusExRecoil", 0, 1)
			DX00 I 2 BRIGHT A_PlaySound("DeusEx/AssaultGun/Fire", CHAN_WEAPON)
			DX00 J 0 BRIGHT A_GiveInventory("DeusEx_AssaultGunSpread", 2)
			DX00 J 0 BRIGHT A_SpawnItemEx("DeusEx_ShellCasing1", 8, -16, 39, Random(1,3), 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 J 1 BRIGHT A_GiveInventory("SamsaraDeusExAssaultGunAttackHandler")
			DX00 H 1 A_Refire("Holding")
			Goto FireEnd
		Holding:
			DX00 H 0 A_JumpIfInventory("DeusEx_AssaultGunMagazine", 1, 1)
			Goto FireReload
			TNT1 A 0 ACS_NamedExecuteAlways("DeusExRecoil", 0, 1)
			DX00 H 0 A_PlaySound("DeusEx/AssaultGun/Fire", CHAN_WEAPON)
			DX00 H 0 BRIGHT A_GiveInventory("DeusEx_AssaultGunSpread", 5)
			DX00 H 0 BRIGHT A_SpawnItemEx("DeusEx_ShellCasing1", 8, -16, 39, Random(1,3), 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 H 0 A_Jump(256, "Holding1","Holding2", "Holding3","Holding4")
			Goto Holding1
		Holding1:
			DX00 I 2 BRIGHT //A_PlaySound("DeusEx/AssaultGun/Fire", CHAN_WEAPON)
			DX00 J 1 BRIGHT A_GiveInventory("SamsaraDeusExAssaultGunAttackHandler")
			DX00 H 1 A_Refire("Holding")
			Goto FireEnd
		Holding2:
			DX00 K 2 BRIGHT //A_PlaySound("DeusEx/AssaultGun/Fire", CHAN_WEAPON)
			DX00 L 1 BRIGHT A_GiveInventory("SamsaraDeusExAssaultGunAttackHandler")
			DX00 H 1 A_Refire("Holding")
			Goto FireEnd
		Holding3:
			DX00 M 2 BRIGHT //A_PlaySound("DeusEx/AssaultGun/Fire", CHAN_WEAPON)
			DX00 N 1 BRIGHT A_GiveInventory("SamsaraDeusExAssaultGunAttackHandler")
			DX00 H 1 A_Refire("Holding")
			Goto FireEnd
		Holding4:
			DX00 O 2 BRIGHT //A_PlaySound("DeusEx/AssaultGun/Fire", CHAN_WEAPON)
			DX00 P 1 BRIGHT A_GiveInventory("SamsaraDeusExAssaultGunAttackHandler")
			DX00 H 1 A_Refire("Holding")
			Goto FireEnd
		FireEnd:
			DX00 HH 1
			Goto ReadyLoop
		Fire20mmCannister:
			DX00 H 0 A_JumpIfInventory("RocketAmmo", 1, "FireCannister2")
			Goto FireChange
		FireCannister2:
			DX00 I 2 BRIGHT A_PlaySound("DeusEx/AssaultGun/Fire2", CHAN_WEAPON)
			DX00 J 0 BRIGHT A_AlertMonsters(900)
			DX00 J 0 BRIGHT A_JumpIfInventory("CoopModeOn",1,2)
			DX00 J 0 BRIGHT A_FireCustomMissile("DeusEx_AssaultGunGrenade", 0, 0, 16, 0, 0, 5)
			Goto FireCannisterEnd
			DX00 J 0 BRIGHT A_FireCustomMissile("DeusEx_AssaultGunGrenadeCoop", 0, 0, 16, 0, 0, 5)
			Goto FireCannisterEnd
		FireCannisterEnd:
			DX00 J 1 BRIGHT A_TakeInventory("RocketAmmo", 1, TIF_NOTAKEINFINITE)
			Goto RechargeGrenade
		RechargeGrenade:
			DX00 H 0 A_JumpIfInventory("RocketAmmo", 1, 1)
			Goto Altfire
			DX00 HHHH 8
			DX00 HH 8
			Goto ReadyLoop
		FireReload:
			DX00 H 0 A_PlaySound("DeusEx/DryFire", CHAN_WEAPON)
			DX00 HHH 1
			DX00 H 1 A_ClearRefire
			Goto Reload
		FireChange:
			DX00 H 0 A_PlaySound("DeusEx/DryFire", CHAN_WEAPON)
			DX00 HHH 1
			DX00 H 1 A_ClearRefire
			Goto AltFire
		Reload:
			DX00 R 0 A_JumpIfInventory("DeusEx_AssaultGunMagazine", 0, "ReadyLoop")
			DX00 R 0 A_JumpIfInventory("Clip", 1, "ReloadSuccess")
		ReloadSuccess:	
			DX00 R 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 R 0 A_PlaySound("DeusEx/AssaultGun/Reload1", CHAN_WEAPON)
			DX00 RSTUVWX 2 A_WeaponReady(WRF_NOFIRE)
			DX00 Y 0 A_JumpIfInventory("DeusEx_RifleSkill", 4, 11)
			DX00 Y 0 A_JumpIfInventory("DeusEx_RifleSkill", 3, 7)
			DX00 Y 0 A_JumpIfInventory("DeusEx_RifleSkill", 2, 3)
			DX00 Y 0 A_JumpIfInventory("DeusEx_RifleSkill", 1, 1)					
			DX00 "YZ[\YZ[\Y" 4 A_WeaponReady(WRF_NOFIRE)
			Goto ReloadAmmo
		ReloadAmmo:
			TNT1 A 0 A_JumpIfInventory("DeusEx_AssaultGunMagazine",0,"ReloadFinish")
			TNT1 A 0 A_JumpIfInventory("Clip",1,1)
			goto ReloadFinish
			TNT1 A 0 A_GiveInventory("DeusEx_AssaultGunMagazine",1)
			TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
			loop 
		ReloadFinish:
			DX00 "]" 2 A_PlaySound("DeusEx/AssaultGun/Reload2", CHAN_WEAPON)
			DX00 "}" 0 A_TakeInventory("DeusEx_Reloading", 1)
			DX01 AB 3
			Goto ReadyLoop
		Deselect:
			DX01 C 0 A_TakeInventory("DeusEx_AssaultGunSpread", 100)
			DX01 C 0 A_TakeInventory("DeusEx_AssGunEquipped", 1)
			DX01 C 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX01 CDEFG 2
			Goto Super::InstantDeselect
		Altfire:
			DX00 H 0 A_JumpIfInventory("DeusEx_20mmHELoaded", 1, "LoadBullets")
			DX00 H 0 A_JumpIfInventory("RocketAmmo", 1, "AltfireSuccess")
			Goto ReadyLoop
		AltFireSuccess:
			DX00 R 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 R 0 A_PlaySound("DeusEx/AssaultGun/Reload1", CHAN_WEAPON)
			DX00 RSTUVWX 2
			DX00 Y 0 A_JumpIfInventory("DeusEx_RifleSkill", 4, 11)
			DX00 Y 0 A_JumpIfInventory("DeusEx_RifleSkill", 3, 7)
			DX00 Y 0 A_JumpIfInventory("DeusEx_RifleSkill", 2, 3)
			DX00 Y 0 A_JumpIfInventory("DeusEx_RifleSkill", 1, 1)			
			DX00 "YZ[\YZ[\Y" 4
			DX00 Y 0 A_JumpIfInventory("DeusEx_20mmHELoaded", 1, "ChangeToBullets")
			Goto Changeto20mm
		Changeto20mm:
			TNT1 A 0 A_TakeInventory("DeusEx_762Loaded", 999)
			TNT1 A 0 A_GiveInventory("DeusEx_20mmHELoaded", 1)
			TNT1 A 0 A_Print("Assault Rifle has 20mm HE Grenades loaded.", 2)
			Goto AltfireEnd
		AltFireEnd:
			DX00 "]" 2 A_PlaySound("DeusEx/AssaultGun/Reload2", CHAN_WEAPON)
			DX00 "}" 0 A_TakeInventory("DeusEx_Reloading", 1)
			DX01 AB 3
			Goto ReadyLoop
		LoadBullets:
			DX00 R 0 A_JumpIfInventory("DeusEx_AssaultGunMagazine", 1, 1)
			Goto Reload
			DX00 R 0 A_JumpIfInventory("Clip", 1, "AltFireSuccess")
			Goto ReadyLoop
		ChangetoBullets:
			TNT1 A 0 A_TakeInventory("DeusEx_20mmHELoaded", 999)
			TNT1 A 0 A_GiveInventory("DeusEx_762Loaded", 1)
			TNT1 A 0 A_Print("Assault Rifle has 7.62x51mm Rounds loaded.", 2)
			Goto AltfireEnd
	}
}

Actor DeusEx_AssaultGunBullet : DeusExBullet
{
	Damage (CallACS("DeusEx_WeaponSkillDamage", 3, 4)) // was 3
	DamageType "DeusExRifleShot"
	States
	{
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 3, 4)),8,!XF_HURTSOURCE,0,8) // was 3
			stop
		XDeath:
			//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 3, 4)),8,!XF_HURTSOURCE,0,8)
			stop
	}
}

Actor DeusEx_AssaultGunBulletCoop : DeusEx_AssaultGunBullet { Species "Player" +THRUSPECIES }

Actor DeusEx_AssaultGunGrenade
{
	Projectile 
	-NOGRAVITY
	+FORCERADIUSDMG
	Damage (0)
	Radius 3
	Height 3
	Speed 20
	Gravity 0.15
	Scale 0.25
	DamageType "DeusExRifleExploded"
	States
	{
		Spawn:
			DX00 A 1 BRIGHT
			Loop
		Death:
			TNT1 A 0 A_PlaySound("DeusEx/AssaultGun/20mmExplode", CHAN_BODY)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ExplosionMid", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ShockWaveXBig", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ShockWaveYBig", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 0 A_AlertMonsters(2000)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 3, 30), 102)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 3, 30), 204)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 3, 30), 306, 0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 3, 30), 408, 0)
			TNT1 A 2 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 3, 30), 512, 0)
			Stop
	}
}

Actor DeusEx_AssaultGunGrenadeCoop : DeusEx_AssaultGunGrenade { +THRUSPECIES Species "Player" }
Actor DeusEx_AssaultGunSpread : Ammo { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 64 Inventory.InterHubAmount 0 Ammo.BackPackAmount 0 Ammo.BackPackMaxAmount 64 }

Actor SamsaraDeusExAssaultGunAttackHandler : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0
			TNT1 A 0 A_AlertMonsters(850)
			TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
			TNT1 A 0 A_TakeInventory("DeusEx_AssaultGunMagazine",1,TIF_NOTAKEINFINITE)
			TNT1 A 0 A_FireCustomMissile("DeusEx_AssaultGunBullet", CallACS("DeusEx_Decorate", 5, 2, 2)*frandom(-1.0,1.0), 0, 4, 0, 0, CallACS("DeusEx_Decorate", 5, 2, 2)*frandom(-0.5,0.5))
			Stop
		PickupAttackCoop:
			TNT1 A 0 A_FireCustomMissile("DeusEx_AssaultGunBulletCoop", CallACS("DeusEx_Decorate", 5, 2, 2)*frandom(-1.0,1.0), 0, 4, 0, 0, CallACS("DeusEx_Decorate", 5, 2, 2)*frandom(-0.5,0.5))
			Stop
	}
}