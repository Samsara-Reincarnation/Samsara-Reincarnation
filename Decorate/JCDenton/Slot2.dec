Actor DeusEx_ShotgunMagazine : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.MaxAmount 5 // was 4 
}

Actor DeusEx_SawedOffSpread : Ammo { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 90 Ammo.BackPackAmount 0 Inventory.InterHubAmount 0 Ammo.BackPackMaxAmount 90  }

Actor DeusEx_BuckShotLoadedForSawedOff : Boolean {}
Actor DeusEx_SabotLoadedForSawedOff : Boolean {}

Actor DeusEx_Shotgun : DeusExWeapon
{
	Weapon.SelectionOrder 1600
	Weapon.SlotNumber 2
	Weapon.SlotPriority 1
	Weapon.AmmoType1 "DeusEx_ShotgunMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 8
	Tag "Sawed-Off Shotgun"
	Weapon.UpSound "DeusEx/Shotgun/Select"
	Inventory.PickupMessage "You found a Sawed-Off Shotgun."
	Obituary "%o was shotgunned by %k."
	States
	{
		Spawn:
			DX03 A -1
			Stop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop
	    Ready:
			DX00 B 0 A_GiveInventory("DeusEx_ShotgunEquipped", 1)
			DX00 BCDEFGHIJKL 3
			DX00 L 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
		ReadyLoop:
			DX00 A 1 A_WeaponReady(WRF_ALLOWRELOAD) DX00 A 0 A_TakeInventory("DeusEx_SawedOffSpread", 1) DX00 A 0 A_Jump(4, "IdleAnim")
			Loop
		IdleAnim:
			DX01 "TUVWXYZ[\]" 6 A_WeaponReady(WRF_ALLOWRELOAD)
			DX02 ABCDEF 6 A_WeaponReady(WRF_ALLOWRELOAD)
			Goto ReadyLoop
		Fire:
			DX00 P 0 A_JumpIfInventory("DeusEx_ShotgunMagazine", 1, "Fire2")
			Goto FireReload
		Fire2:
			DX00 N 0 A_JumpIfInventory("DeusEx_BuckShotLoadedForSawedOff", 1, "FireBuckShot")
			DX00 N 0 A_JumpIfInventory("DeusEx_SabotLoadedForSawedOff", 1, "FireSabot")
			Goto FireBuckShot
		FireBuckshot:
			TNT1 A 0 ACS_NamedExecuteAlways("DeusExRecoil", 0, 2)
			DX00 M 0 BRIGHT A_PlaySound("DeusEx/Shotgun/Fire", CHAN_WEAPON)
			DX00 M 0 BRIGHT A_GiveInventory("DeusEx_SawedOffSpread", 14)
			DX00 M 0 BRIGHT A_AlertMonsters(1100)
			DX00 M 0 BRIGHT A_JumpIfInventory("CoopModeOn",1,"FireBuckshotCoop")
			DX00 MMMM 0 BRIGHT A_FireCustomMissile("DeusEx_ShotgunBullet", Frandom(-2.2,2.2)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-1.0,1.0), 0, 4, 0, 0, Frandom(-1.8,1.8)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-0.5,0.5))
			DX00 M 4 BRIGHT A_FireCustomMissile("DeusEx_ShotgunBullet", Frandom(-2.2,2.2)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-1.0,1.0), 1, 4, 0, 0, Frandom(-1.8,1.8)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-0.5,0.5))
			Goto FireEnd
		FireBuckshotCoop:
			DX00 MMMM 0 BRIGHT A_FireCustomMissile("DeusEx_ShotgunBulletCoop", Frandom(-2.2,2.2)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-1.0,1.0), 0, 4, 0, 0, Frandom(-1.8,1.8)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-0.5,0.5))
			DX00 M 4 BRIGHT A_FireCustomMissile("DeusEx_ShotgunBulletCoop", Frandom(-2.2,2.2)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-1.0,1.0), 1, 4, 0, 0, Frandom(-1.8,1.8)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-0.5,0.5))
			Goto FireEnd
		FireSabot:
			TNT1 A 0 ACS_NamedExecuteAlways("DeusExRecoil", 0, 2)
			DX00 M 0 BRIGHT A_PlaySound("DeusEx/Shotgun/Fire", CHAN_WEAPON)
			DX00 M 0 BRIGHT A_GiveInventory("DeusEx_SawedOffSpread", 14)
			DX00 M 0 BRIGHT A_AlertMonsters(1100)
			DX00 M 0 BRIGHT A_JumpIfInventory("CoopModeOn",1,"FireSabotCoop")
			DX00 MMMM 0 BRIGHT A_FireCustomMissile("DeusEx_SabotBullet", Frandom(-2.2,2.2)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-1.0,1.0), 0, 4, 0, 0, Frandom(-1.8,1.8)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-0.5,0.5))
			DX00 M 4 BRIGHT A_FireCustomMissile("DeusEx_SabotBullet", Frandom(-2.2,2.2)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-1.0,1.0), 1, 4, 0, 0, Frandom(-1.8,1.8)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-0.5,0.5))
			Goto FireEnd
		FireSabotCoop:
			DX00 MMMM 0 BRIGHT A_FireCustomMissile("DeusEx_SabotBulletCoop", Frandom(-2.2,2.2)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-1.0,1.0), 0, 4, 0, 0, Frandom(-1.8,1.8)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-0.5,0.5))
			DX00 M 4 BRIGHT A_FireCustomMissile("DeusEx_SabotBulletCoop", Frandom(-2.2,2.2)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-1.0,1.0), 1, 4, 0, 0, Frandom(-1.8,1.8)+CallACS("DeusEx_Decorate", 5, 0, 6)*frandom(-0.5,0.5))
			Goto FireEnd
		FireEnd:
			DX00 NO 4 BRIGHT 
			DX00 PQR 3
			DX00 S 2 A_PlaySound("DeusEx/Shotgun/Select", CHAN_WEAPON)
			DX00 T 2
			DX00 U 3 A_SpawnItemEx("DeusEx_ShellCasing3", 8, -16, 39, Random(1,3), 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX00 VWXY 3
			DX00 Z 3 A_Refire
			Goto ReadyLoop
		FireReload:
			DX01 S 0 A_PlaySound("DeusEx/DryFire", CHAN_WEAPON)
			DX01 SSS 1
			DX01 S 1 A_ClearRefire
			Goto Reload
		Reload:
			DX01 A 0 A_JumpIfInventory("DeusEx_ShotgunMagazine", 0, "ReadyLoop")
			DX01 A 0 A_JumpIfInventory("Shell", 1, "ReloadSuccess")
			Goto ReadyLoop
		ReloadSuccess:
			DX00 "\" 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 "\]" 3
			DX01 AB 3
			DX01 C 0 A_PlaySound("DeusEx/AssShot/Reload2", CHAN_WEAPON)
			DX01 C 0 A_JumpIfInventory("DeusEx_PistolSkill", 4, 16)
			DX01 C 0 A_JumpIfInventory("DeusEx_PistolSkill", 3, 11)
			DX01 C 0 A_JumpIfInventory("DeusEx_PistolSkill", 2, 6)
			DX01 C 0 A_JumpIfInventory("DeusEx_PistolSkill", 1, 2)
			DX01 CDEFGHCDEFGHCDEFGH 2 A_WeaponReady(WRF_NOFIRE)
			Goto ReloadAmmo
		ReloadAmmo:
			TNT1 A 0 A_JumpIfInventory("DeusEx_ShotgunMagazine",0,"ReloadFinish")
			TNT1 A 0 A_JumpIfInventory("Shell",1,1)
			goto ReloadFinish
			TNT1 A 0 A_GiveInventory("DeusEx_ShotgunMagazine",1)
			TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
			loop
		ReloadFinish:
			DX01 H 0 A_PlaySound("DeusEx/Shotgun/Reload", CHAN_WEAPON)
			DX01 I 0 A_TakeInventory("DeusEx_Reloading", 1)
			DX01 IJKL 3
			Goto ReadyLoop
		Deselect:
			DX01 M 0 A_TakeInventory("DeusEx_SawedOffSpread", 90)
			DX01 M 0 A_TakeInventory("DeusEx_ShotgunEquipped", 1)
			DX01 M 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX01 MNOPQ 3
			Goto Super::InstantDeselect
		AltFire:
			//DX00 H 0 A_JumpIfInventory("DeusEx_SabotLoadedForSawedOff", 1, "LoadBuckShot")
			DX00 H 0 A_JumpIfInventory("DeusEx_ShotgunMagazine", 1, "Altfire1Success")
			Goto Reload
		AltFire1Success:
			DX00 "\" 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 "\]" 3
			DX01 AB 3
			DX01 C 0 A_PlaySound("DeusEx/AssShot/Reload2", CHAN_WEAPON)
			DX01 C 0 A_JumpIfInventory("DeusEx_PistolSkill", 4, 16)
			DX01 C 0 A_JumpIfInventory("DeusEx_PistolSkill", 3, 11)
			DX01 C 0 A_JumpIfInventory("DeusEx_PistolSkill", 2, 6)
			DX01 C 0 A_JumpIfInventory("DeusEx_PistolSkill", 1, 2)
			DX01 CDEFGHCDEFGHCDEFGH 2
			DX01 H 0 A_JumpIfinventory("DeusEx_SabotLoadedForSawedOff", 1, "ChangeToBuckshot")
			Goto ChangeToSabot
		ChangeToSabot:
			TNT1 A 0 A_TakeInventory("DeusEx_BuckShotLoadedForSawedOff", 999)
			TNT1 A 0 A_GiveInventory("DeusEx_SabotLoadedForSawedOff", 1)
			TNT1 A 0 A_Print("Sawed-Off Shotgun now has Sabot rounds loaded")
			Goto AltfireFinish
		AltFireFinish:
			DX01 H 0 A_PlaySound("DeusEx/Shotgun/Reload", CHAN_WEAPON)
			DX01 I 0 A_TakeInventory("DeusEx_Reloading", 1)
			DX01 IJKL 3
			Goto ReadyLoop
		ChangeToBuckshot:
			TNT1 A 0 A_TakeInventory("DeusEx_SabotLoadedForSawedOff", 999)
			TNT1 A 0 A_GiveInventory("DeusEx_BuckShotLoadedForSawedOff", 1)
			TNT1 A 0 A_Print("Sawed-Off Shotgun now has 12 Gauge Buckshot loaded")
			Goto AltfireFinish
	}
}

Actor DeusEx_ShotgunBullet : DeusExBullet
{
	Damage (CallACS("DeusEx_WeaponSkillDamage", 1, 7)) // was 5
	DamageType "DeusExPistol"
	States
	{
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 7)),8,!XF_HURTSOURCE,0,8)
			stop
		XDeath:
			//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 1, 7)),8,!XF_HURTSOURCE,0,8)
			stop
	}
}

Actor DeusEx_ShotgunBulletCoop : DeusEx_ShotgunBullet { +THRUSPECIES Species "Player" }

Actor DeusEx_SabotBullet : DeusExBullet
{
	DamageType "DeusExSabotPistol"
	Damage (0)
	States
	{
		Death:
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/SmallExplod", CHAN_BODY)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ExplosionSmall", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 1 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 1, 21),32,!XF_HURTSOURCE,0,32)
			stop
		XDeath:
			//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 0 A_PlaySound("DeusEx/SmallExplod", CHAN_BODY)
			TNT1 A 0 A_SpawnItemEX("DeusEx_ExplosionSmall", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 1 A_Explode(CallACS("DeusEx_WeaponSkillDamage", 1, 21),32,!XF_HURTSOURCE,0,32)
			stop
	}
}

Actor DeusEx_SabotBulletCoop : DeusEx_SabotBullet { +THRUSPECIES +DONTHARMSPECIES +MTHRUSPECIES Species "Player" }

Actor DeusEx_SniperMagazine : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.MaxAmount 6
}

Actor DeusEx_SniperRifle : DeusExWeapon 
{
	Weapon.SelectionOrder 1600
	Weapon.SlotNumber 2
	Weapon.SlotPriority 2
	Weapon.AmmoType1 "DeusEx_SniperMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 8
	Tag "Sniper Rifle"
	Weapon.UpSound "DeusEx/Sniper/Select"
	Inventory.PickupMessage "You found a Sniper Rifle."
	Obituary "%o was sniped by %k."
	States
	{
		Spawn:
			DX03 A -1
			Stop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
			loop
	    Ready:
			DX00 B 0 A_GiveInventory("DeusEx_SniperEquipped", 1)
			DX00 BCDEFGHIJ 2
			DX00 J 0 A_TakeInventory("DeusEx_Reloading", 1)
			Goto ReadyLoop
		ReadyLoop:
			DX01 D 0 A_JumpIfInventory("DeusEx_Zoomed", 1, "ReadyZoomed")
			DX01 D 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM) DX01 D 0 A_Jump(3, "Idle1", "Idle2", "Idle3")
			Loop
		ReadyZoomed:
			TNT1 A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			Loop
		Idle1:
			DX01 DEFGHIJ 6 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			Goto ReadyLoop
		Idle2:
			DX01 KLMNOPQRSTUV 6 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			Goto ReadyLoop
		Idle3:
			DX01 "VWXYZ[\]" 6 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			Goto ReadyLoop
		Reload:
			DX01 A 0 A_JumpIfInventory("DeusEx_SniperMagazine", 0, "ReadyLoop")
			DX01 A 0 A_JumpIfInventory("Shell", 1, "ReloadSuccess")
			Goto ReadyLoop
		ReloadSuccess:
			DX00 Q 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 Q 0 A_PlaySound("DeusEx/Sniper/Reload1", CHAN_WEAPON)
			DX00 Q 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
			DX00 Q 0 A_TakeInventory("DeusEx_Zoomed", 999)
			DX00 Q 3
			DX00 RSTUVW 3
			DX00 X 0 A_JumpIfInventory("DeusEx_RifleSkill", 4, 16)
			DX00 X 0 A_JumpIfInventory("DeusEx_RifleSkill", 3, 13)
			DX00 X 0 A_JumpIfInventory("DeusEx_RifleSkill", 2, 8)
			DX00 X 0 A_JumpIfInventory("DeusEx_RifleSkill", 1, 4)
			DX00 "XYZ[\XYZ[\XYZ[\XYZ" 3
			Goto ReloadAmmo
		ReloadAmmo:
			TNT1 A 0 A_JumpIfInventory("DeusEx_SniperMagazine",0,"ReloadFinish")
			TNT1 A 0 A_JumpIfInventory("Shell",1,1)
			goto ReloadFinish
			TNT1 A 0 A_GiveInventory("DeusEx_SniperMagazine",1)
			TNT1 A 0 A_TakeInventory("Shell",1,TIF_NOTAKEINFINITE)
			loop
		ReloadFinish:
			DX00 "]" 3 A_PlaySound("DeuxEx/Sniper/Reload2", CHAN_WEAPON)
			DX00 Q 0 A_TakeInventory("DeusEx_Reloading", 1)
			DX01 ABC 3
			Goto ReadyLoop
		Fire:
			DX00 P 0 A_JumpIfInventory("DeusEx_SniperMagazine", 1, "Fire2")
			Goto FireReload
		Fire2:
			TNT1 A 0 ACS_NamedExecuteAlways("DeusExRecoil", 0, 3)
			DX02 A 0 A_JumpIfInventory("DeusEx_Zoomed", 1, "FireZoomed")
			DX00 K 0 A_PlaySound("DeusEx/Sniper/Fire", CHAN_WEAPON, 1.1)
			DX00 K 0 A_JumpIfInventory("CoopModeOn",1,2)
			DX00 K 5 A_FireCustomMissile("DeusEx_SniperBullet", FRandom(-2.2, 2.2), 1, 5, 2)
			Goto Fire2End
			DX00 K 5 A_FireCustomMissile("DeusEx_SniperBulletCoop", FRandom(-2.2, 2.2), 1, 5, 2)
			Goto Fire2End
		Fire2End:
			DX00 L 0 A_AlertMonsters(3500)
			DX00 LM 4
			DX00 N 5
			DX00 N 0 A_PlaySound("DeusEx/Sniper/Select", CHAN_WEAPON)
			DX01 MNO 3
			DX01 P 0 A_SpawnItemEx("DeusEx_ShellCasing2", 8, -16, 39, Random(1,3), 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			DX01 PQRS 4
			DX01 TU 5
			Goto ReadyLoop
		FireZoomed:
			TNT1 K 0 A_PlaySound("DeusEx/Sniper/Fire", CHAN_WEAPON, 1.1)
			TNT1 A 0 A_FireCustomMissile("DeusEx_FakeTracer", 0, 0, 0, 2)
			TNT1 K 0 A_JumpIfInventory("CoopModeOn",1,2)
			TNT1 K 5 A_FireCustomMissile("DeusEx_SniperBullet", 0, 1, 0, 5)
			Goto FireZoomedEnd
			TNT1 K 5 A_FireCustomMissile("DeusEx_SniperBulletCoop", 0, 1, 0, 5)
			Goto FireZoomedEnd
		FireZoomedEnd:
			TNT1 L 0 A_AlertMonsters(3500)
			TNT1 LM 4
			TNT1 N 5
			TNT1 N 0 A_PlaySound("DeusEx/Sniper/Select", CHAN_WEAPON)
			TNT1 MNO 3
			TNT1 P 0 A_SpawnItemEx("DeusEx_ShellCasing2", 8, -16, 39, Random(1,3), 0, 3, -90, SXF_NOCHECKPOSITION|SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH)
			TNT1 PQRS 4
			TNT1 TU 5
			Goto ReadyLoop
		FireReload:
			DX01 D 0 A_PlaySound("DeusEx/DryFire", CHAN_WEAPON)
			DX01 DDD 1
			DX01 D 1 A_ClearRefire
			Goto Reload
		Deselect:
			DX00 O 0 A_TakeInventory("DeusEx_SniperEquipped", 1)
			DX00 O 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
			DX00 O 0 A_TakeInventory("DeusEx_Zoomed", 999)
			DX00 O 0 A_GiveInventory("DeusEx_Reloading", 1)
			DX00 O 4
			Goto Super::InstantDeselect
		Altfire:
		Zoom:
			DX02 A 0 A_JumpIfInventory("DeusEx_Zoomed", 1, "ZoomOut")
		ZoomIn:
			DX02 AB 3
			DX02 C 0 A_GiveInventory("DeusEx_Zoomed", 1)
			DX02 C 0 ACS_NamedExecuteAlways("DeusEx_ScopeZoom")
			DX02 C 3 A_ZoomFactor(5.0, ZOOM_INSTANT)
			Goto ReadyLoop
		ZoomOut:
			DX02 C 3 A_ZoomFactor(1.0, ZOOM_INSTANT)
			DX02 B 0 A_TakeInventory("DeusEx_Zoomed", 999)
			DX02 BA 3
			Goto ReadyLoop
	}
}

Actor DeusEx_SniperBullet : DeusExBullet
{
	Damage (CallACS("DeusEx_WeaponSkillDamage", 3, 30)) // was 25. buffed for rarity and to keep it viable alongside other sniper weapons
	DamageType "DeusExRifleShot"
	States
	{
		Crash:
			TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitRobot", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 3, 30)),8,!XF_HURTSOURCE,0,8)
			stop
		XDeath:
			//TNT1 A 0 A_PlaySound("DeusEx/Bullet/HitFlesh", CHAN_BODY)
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 3, 30)),8,!XF_HURTSOURCE,0,8)
			stop
	}
}

Actor DeusEx_SniperBulletCoop : DeusEx_SniperBullet { +THRUSPECIES Species "Player" }

Actor DeusEx_SniperPuff : DeusEx_BulletPuff
{
	DamageType "DeusExRifleShot"
	+ALWAYSPUFF
	+PUFFGETSOWNER
	+PUFFONACTORS
	+FORCERADIUSDMG
	+HITTRACER
	+NODAMAGETHRUST
	States
	{
		Crash:
		Melee:
		XDeath:
			TNT1 A 1 A_Explode(CallACS("DeusEx_DamageModifiers",CallACS("DeusEx_WeaponSkillDamage", 3, 30)),8,!XF_HURTSOURCE,0,8)
			Goto Super::Spawn
	}
}

Actor DeusEx_SniperPuffCoop : DeusEx_SniperPuff { +THRUSPECIES +MTHRUSPECIES Species "Player" }

Actor DeusEx_FakeTracer : DeusExBullet
{
	+THRUACTORS
	Speed 150
	Damage (0)
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1
			Stop
	}
}