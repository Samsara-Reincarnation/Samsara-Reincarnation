actor CanDualShotties: Boolean {}
actor LevelLimiter: Boolean {}
actor InvasionDualShottyCheck: Boolean {}
actor UsingShotguns: Boolean {}
actor UsingDualShotguns: Boolean {}

actor "WSTE-M5 Combat Shotgun": Weapon
{
    Weapon.BobRangeX 0
    Weapon.BobStyle Smooth
    Weapon.SelectionOrder 400
    Weapon.SlotNumber 2
    Decal BulletChip
    Inventory.PickupMessage "You picked up a WSTE-M5 Combat Shotgun!"
    Weapon.AmmoType1 "WastemShell"
    Weapon.AmmoGive1 0
    Weapon.AmmoUse1 2
    Weapon.AmmoType2 "Shell"
    Weapon.AmmoGive2 8
    Weapon.AmmoUse2 0
    +NOALERT
    +INVENTORY.UNDROPPABLE
    +AMMO_OPTIONAL
	Inventory.RestrictedTo "MarathonPlayer"
    Tag "WSTE-M5 Combat Shotgun"
    States
    {
      Spawn:
        SHTY A -1
        STOP

      Ready:
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"Ready2")
        MSHT A 1 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,"Ready")
      SingleReload:
        TNT1 A 0 A_JumpIfInventory("Shell",2,"SingleReloadAnim")
        goto Ready

      SingleReloadAnim:
        MSHT F 4
        goto SingleReloadAmmo

      SingleReloadAnimFinish:
        MSHT G 4 A_PlaySound("marathon/shotgun/reload")
        MSHT H 4
        MSHT IJ 5
        MSHT A 3
        goto Ready

      SingleReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,"SingleReloadAnimFinish")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        goto SingleReloadAnimFinish
        TNT1 A 0 A_TakeInventory("Shell",2,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_GiveInventory("WastemShell",2)
        loop

      Deselect:
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"Deselect2")
        TNT1 A 0 A_Lower
        MSHT A 1 A_Lower
        LOOP

      Select:
        TNT1 A 0 A_TakeInventory("UsingFists",1)
        TNT1 A 0 A_TakeInventory("UsingPistols",1)
        TNT1 A 0 A_TakeInventory("UsingDualPistols",1)
        TNT1 A 0 A_TakeInventory("UsingSMG",1)
        TNT1 A 0 A_TakeInventory("UsingPacifier",1)
        //TNT1 A 0 A_TakeInventory("UsingShotguns",1)
        TNT1 A 0 A_TakeInventory("UsingDualShotguns",1)
        TNT1 A 0 A_TakeInventory("UsingFusionPistol",1)
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle",1)
        TNT1 A 0 A_TakeInventory("UsingSpanker",1)
        TNT1 A 0 A_TakeInventory("UsingToasty",1)
        TNT1 A 0 A_TakeInventory("UsingWMC",1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon",1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2",1)
        //TNT1 A 0 A_TakeInventory("UsingMarathonInstagib",1)

        TNT1 A 0 A_GiveInventory("UsingShotguns",1)
        TNT1 A 0 A_Raise
        MSHT A 1 A_Raise
        LOOP

      Fire:
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"LFire")
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,1)
        goto SingleReload
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"NothingFire")
        MSHT A 2 A_AlertMonsters
        TNT1 A 0 A_TakeInventory("WastemShell")
        TNT1 A 0 A_PlayWeaponSound("marathon/shotgun/fire")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopFire")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        TNT1 AAAAAAAAAA 0 A_FireCustomMissile("MarathonShotgunBullet",random(-4,4),0,0,0,0,random(-4,4))
      FireContinue:
        MSHT A 1
        MSHT A 1 A_Light2
        MSHT B 4 Bright A_Light1 
        MSHT C 5 Bright A_Light0
        MSHT D 5 Bright
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,1)
        goto SingleReload
        TNT1 A 0 A_ReFire
        goto Ready

      CoopFire:
        TNT1 AAAAAAAAAA 0 A_FireCustomMissile("MarathonShotgunBulletCoop",random(-4,4),0,0,0,0,random(-4,4))
        goto FireContinue

      AltFire:
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"RFire")
        TNT1 A 0 A_JumpIfInventory("CanDualShotties",1,1)
        goto Ready
        SHT3 BDFHJLNOPQ 1 A_GiveInventory("UsingDualShotguns")
        TNT1 A 0 A_GunFlash
        SHT3 R 1 A_GiveInventory("SynthFireActive",1)
        goto Ready2

      NothingFire:
        TNT1 A 0
        goto Ready
        
        // Dual shotgun modes
        
      Deselect2:
        TNT1 A 0 A_TakeInventory("SynthFireActive",1)
        TNT1 A 0 A_TakeInventory("SynthFireLeft",1)
        TNT1 A 0 A_TakeInventory("SynthFireRight",1)

      DS:
        TNT1 A 0 A_Lower
        RSHT A 1 A_Lower
        Goto DS

      Ready2:
        RSHT A 1 A_GunFlash
        TNT1 A 0 A_GiveInventory("SynthFireActive",1)
        // Left Shotgun
      LIdle:
        RSHT A 1 A_WeaponReady(12)
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,1)
        goto LReload
        TNT1 A 0 A_JumpIfInventory("SynthFireLeft",1,"LFire")
        loop

      LReload:
        TNT1 A 0 A_JumpIfInventory("Shell",2,"LReloadAnim")
        goto LIdle

      LReloadAnim:
        RSHT F 4
        goto LReloadAmmo

      LReloadAnimFinish:
        RSHT G 4 A_PlaySound("marathon/shotgun/reload")
        RSHT H 4
        RSHT IJ 5
        RSHT A 3
        goto LIdle

      LReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,"LReloadAnimFinish")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        goto LReloadAnimFinish
        TNT1 A 0 A_TakeInventory("Shell",2,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_GiveInventory("WastemShell",2)
        loop

      LFire:
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,1)
        goto LReload
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"NothingFire")
        RSHT A 2 A_AlertMonsters
        TNT1 A 0 A_TakeInventory("WastemShell")
        TNT1 A 0 A_PlayWeaponSound("marathon/shotgun/fire")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"LCoopFire")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"LCoopFire")
        TNT1 AAAAAAAAAA 0 A_FireCustomMissile("MarathonShotgunBullet",random(-4,4),0,0,0,0,random(-4,4))
      LFireContinue:
        RSHT A 1
        RSHT A 1 A_Light2
        RSHT B 4 Bright A_Light1 
        RSHT C 5 Bright A_Light0
        RSHT D 5 Bright
        TNT1 A 0 A_JumpIfInventory("WastemShell",0,1)
        goto LReload
        TNT1 A 0 A_JumpIfInventory("SynthFireLeft",1,"LFire")
        goto LIdle

      LCoopFire:
        TNT1 AAAAAAAAAA 0 A_FireCustomMissile("MarathonShotgunBulletCoop",random(-4,4),0,0,0,0,random(-4,4))
        goto LFireContinue

        // Right Shotgun
      Flash:
        SHT2 A 8
      RIdle:
        SHT2 A 1
        TNT1 A 0 A_JumpIfInventory("WastemShellRight",0,1)
        goto RReload
        TNT1 A 0 A_JumpIfInventory("SynthFireRight",1,"RFire")
        loop

      RReload:
        TNT1 A 0 A_JumpIfInventory("Shell",2,"RReloadAnim")
        goto RIdle

      RReloadAnim:
        SHT2 F 4
        goto RReloadAmmo

      RReloadAnimFinish:
        SHT2 G 4 A_PlaySound("marathon/shotgun/reload")
        SHT2 H 4
        SHT2 IJ 5
        SHT2 A 3
        goto RIdle

      RReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("WastemShellRight",0,"RReloadAnimFinish")
        TNT1 A 0 A_JumpIfInventory("Shell",2,1)
        goto RReloadAnimFinish
        TNT1 A 0 A_TakeInventory("Shell",2,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_GiveInventory("WastemShellRight",2)
        loop

      RFire:
        TNT1 A 0 A_JumpIfInventory("WastemShellRight",0,1)
        goto RReload
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"RIdle")
        SHT2 A 2 A_AlertMonsters
        TNT1 A 0 A_TakeInventory("WastemShellRight")
        TNT1 A 0 A_PlayWeaponSound("marathon/shotgun/fire")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"RCoopFire")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"RCoopFire")
        TNT1 AAAAAAAAAA 0 A_FireCustomMissile("MarathonShotgunBullet",random(-4,4),0,0,0,0,random(-4,4))
      RFireContinue:
        SHT2 A 1
        SHT2 A 1 A_Light2
        SHT2 B 4 Bright A_Light1 
        SHT2 C 5 Bright A_Light0
        SHT2 D 5 Bright
        TNT1 A 0 A_JumpIfInventory("WastemShellRight",0,1)
        goto RReload
        TNT1 A 0 A_JumpIfInventory("SynthFireRight",1,"RFire")
        goto RIdle

      RCoopFire:
        TNT1 AAAAAAAAAA 0 A_FireCustomMissile("MarathonShotgunBulletCoop",random(-4,4),0,0,0,0,random(-4,4))
        goto RFireContinue
    }
}

actor WastemShell : Ammo
{
    Inventory.MaxAmount 2
    +INVENTORY.IGNORESKILL
}

actor WastemShellRight : Ammo
{
    Inventory.MaxAmount 2
    +INVENTORY.IGNORESKILL
}

actor MarathonShotgunBullet: FastProjectile
{
    Speed 100
    Damage (random(9,12))
    Radius 8
    Height 8
    Projectile
    Obituary "$SAMSARA_MARATHON_OB_SLOT2"
    +BLOODSPLATTER
    +NOTIMEFREEZE
    +NOEXTREMEDEATH
    +THRUGHOST
    Alpha 1.0
    Decal Bulletchip
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Crash:
        TNT1 A 0 A_PlaySound("puff/ding")
      Death:
        TNT1 A 1 A_SpawnItem("MarathonPuff")
        stop

      XDeath:
        TNT1 A 1 A_PlaySound("puff/hit")
        stop
    }
}

actor MarathonShotgunBulletCoop: MarathonShotgunBullet
{
    Radius 1
    Height 1
    Speed 80
    Damage (random(10,12))
    +THRUSPECIES
    Species "Player"
}

Actor MarathonShotgunPickedUpX : MapCoordinate {}
Actor MarathonShotgunPickedUpY : MapCoordinate {}
Actor MarathonShotgunPickedUpZ : MapCoordinate {}