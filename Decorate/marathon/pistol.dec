actor UsingPistols: Boolean {}
actor UsingDualPistols: Boolean {}
actor CanDualPistols: Boolean {}

actor ".44 Magnum Mega Class A1" : Weapon// replaces Pistol
{
    Weapon.BobRangeX 0
    Weapon.BobStyle Smooth
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 1900
    Decal BulletChip
    Inventory.PickupMessage "You picked up a .44 Magnum Mega Class A1!"
    Weapon.AmmoType "MagnumBullet"
    Weapon.AmmoGive 0
    Weapon.AmmoUse 1
    +AMMO_OPTIONAL
    +NOALERT
    +INVENTORY.UNDROPPABLE
	Inventory.RestrictedTo "MarathonPlayer"
    Tag ".44 Magnum Mega Class A1"
    States
    {
      Spawn:
        PSTL A -1
        STOP

      Ready:
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"Ready2")
        PIST A 1 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",1,"Ready")
      SingleReload:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"SingleReloadCheck")
      SingleReloadAnim:
        PIST F 11
        goto SingleReloadAmmo

      SingleReloadAnimFinish:
        TNT1 A 0 A_PlaySound("marathon/pistol/reload")
        PIST GH 11
        goto Ready
        
      SingleReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"SingleReloadAmmo2")
      SingleReloadAmmo1:
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"SingleReloadAnimFinish")
        TNT1 A 0 A_GiveInventory("MagnumBullet")
        loop

      SingleReloadAmmo2:
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"SingleReloadAnimFinish")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto SingleReloadAnimFinish
        TNT1 A 0 A_TakeInventory("Clip",1)
        TNT1 A 0 A_GiveInventory("MagnumBullet")
        loop

      SingleReloadCheck:
        TNT1 A 0 A_JumpIfInventory("Clip",1,"SingleReloadAnim")
        goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"Deselect2")
        TNT1 A 0 A_Lower
        PIST A 1 A_Lower
        LOOP

      Select:
        TNT1 A 0 A_TakeInventory("UsingFists",1)
        //TNT1 A 0 A_TakeInventory("UsingPistols",1)
        TNT1 A 0 A_TakeInventory("UsingDualPistols",1)
        TNT1 A 0 A_TakeInventory("UsingSMG",1)
        TNT1 A 0 A_TakeInventory("UsingPacifier",1)
        TNT1 A 0 A_TakeInventory("UsingShotguns",1)
        TNT1 A 0 A_TakeInventory("UsingDualShotguns",1)
        TNT1 A 0 A_TakeInventory("UsingFusionPistol",1)
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle",1)
        TNT1 A 0 A_TakeInventory("UsingSpanker",1)
        TNT1 A 0 A_TakeInventory("UsingToasty",1)
        TNT1 A 0 A_TakeInventory("UsingWMC",1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon",1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2",1)
        //TNT1 A 0 A_TakeInventory("UsingMarathonInstagib",1)

        TNT1 A 0 A_GiveInventory("UsingPistols",1)
        TNT1 A 0 A_Raise
        PIST A 1 A_Raise
        LOOP

      Fire:
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"LFire")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",1,1)
        goto SingleReload
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_TakeInventory("MagnumBullet",1,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_PlayWeaponSound("marathon/pistol/fire")
        TNT1 A 0 A_FireCustomMissile("44mmCasingSpawner",0,0,8,0)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"FireCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"FireCoop")
        TNT1 A 0 A_FireCustomMissile("MarathonMagnumBullet",random(-1,1),0,0,0,0,random(-1,1))//A_FireBullets(1,1,-1,20+random(0,8),"MarathonPuff")
      FireContinue:
        PIST A 3 A_Light2
        PIST B 3 Bright A_Light1
        PIST C 3 Bright A_Light0
        PIST D 3
        PIST E 4
        TNT1 A 0 A_ReFire
        goto Ready

      FireCoop:
        TNT1 A 0 A_FireCustomMissile("MarathonMagnumBulletCoop",random(-1,1),0,0,0,0,random(-1,1))//A_FireBullets(1,1,-1,20+random(0,8),"MarathonPuff")
        goto FireContinue

      AltFire:
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"RFire")
        TNT1 A 0 A_JumpIfInventory("CanDualPistols",1,1)
        goto Ready
        PIS3 ABCDEFGHIJ 1 A_GiveInventory("UsingDualPistols")
        TNT1 A 0 A_GunFlash
        PIS3 K 1 A_GiveInventory("SynthFireActive",1)
        goto Ready2

      Deselect2:
        TNT1 A 0 A_TakeInventory("SynthFireActive",1)
        TNT1 A 0 A_TakeInventory("SynthFireLeft",1)
        TNT1 A 0 A_TakeInventory("SynthFireRight",1)
        TNT1 A 0 A_TakeInventory("LeftReload",1)
        TNT1 A 0 A_TakeInventory("RightReload",1)

      DS:
        TNT1 A 0 A_Lower
        PIS1 A 1 A_Lower
        Goto DS

      Ready2:
        PIS1 A 1 A_GunFlash
        TNT1 A 0 A_GiveInventory("SynthFireActive",1)
        // Left Magnum
      LIdle:
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LInvisible")
        PIS1 A 1 A_WeaponReady(12)
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",1,1)
        goto LReload
        TNT1 A 0 A_JumpIfInventory("SynthFireLeft",1,"LFire")
        loop

      LInvisible:
        TNT1 A 33
        TNT1 A 0 A_TakeInventory("RightReload",1)
        Goto LIdle

      LReload:
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LInvisible")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"LReloadCheck")
      LReloadAnim:
        TNT1 A 0 A_Light0
        PIS1 F 11 A_GiveInventory("LeftReload")
        goto LReloadAmmo

      LReloadAnimFinish:
        TNT1 A 0 A_PlaySound("marathon/pistol/reload")
        PIS1 GH 11
        goto LIdle

      LReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"LReloadAmmo2")
      LReloadAmmo1:
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"LReloadAnimFinish")
        TNT1 A 0 A_GiveInventory("MagnumBullet")
        loop

      LReloadAmmo2:
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"LReloadAnimFinish")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto LReloadAnimFinish
        TNT1 A 0 A_TakeInventory("Clip",1)
        TNT1 A 0 A_GiveInventory("MagnumBullet")
        loop

      LReloadCheck:
        TNT1 A 0 A_JumpIfInventory("Clip",1,"LReloadAnim")
        goto LIdle

      LFire:
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LInvisible")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",1,1)
        goto LReload
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_TakeInventory("MagnumBullet",1,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_PlayWeaponSound("marathon/pistol/fire")
        TNT1 A 0 A_FireCustomMissile("44mmCasingSpawner",0,0,8,0)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"LFireCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"LFireCoop")
        TNT1 A 0 A_FireCustomMissile("MarathonMagnumBullet",random(-1,1),0,0,0,0,random(-1,1))//A_FireBullets(1,1,-1,20+random(0,8),"MarathonPuff")
      LFireContinue:
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LInvisible")
        PIS1 A 3 A_Light2
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LInvisible")
        PIS1 B 3 Bright A_Light1
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LInvisible")
        PIS1 C 3 Bright A_Light0
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LInvisible")
        PIS1 D 3 
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LInvisible")
        PIS1 E 4 
        TNT1 A 0 A_JumpIfInventory("SynthFireLeft",1,"LFire")
        goto LIdle

      LFireCoop:
        TNT1 A 0 A_FireCustomMissile("MarathonMagnumBulletCoop",random(-1,1),0,0,0,0,random(-1,1))//A_FireBullets(1,1,-1,20+random(0,8),"MarathonPuff")
        goto LFireContinue

        // Right Magnum
      Flash:
        PIS2 A 8
      RIdle:
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RInvisible")
        PIS2 A 1
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",1,1)
        goto RReload
        TNT1 A 0 A_JumpIfInventory("SynthFireRight",1,"RFire")
        loop

      RInvisible:
        TNT1 A 33
        TNT1 A 0 A_TakeInventory("LeftReload",1)
        Goto RIdle

      RReload:
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RInvisible")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"RReloadCheck")
      RReloadAnim:
        TNT1 A 0 A_Light0
        PIS2 F 11 A_GiveInventory("RightReload")
        goto RReloadAmmo

      RReloadAnimFinish:
        TNT1 A 0 A_PlaySound("marathon/pistol/reload")
        PIS2 GH 11
        goto RIdle

      RReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"RReloadAmmo2")
      RReloadAmmo1:
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",0,"RReloadAnimFinish")
        TNT1 A 0 A_GiveInventory("MagnumBulletRight")
        loop

      RReloadAmmo2:
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",0,"RReloadAnimFinish")
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto RReloadAnimFinish
        TNT1 A 0 A_TakeInventory("Clip",1)
        TNT1 A 0 A_GiveInventory("MagnumBulletRight")
        loop

      RReloadCheck:
        TNT1 A 0 A_JumpIfInventory("Clip",1,"RReloadAnim")
        goto RIdle

      RFire:
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RInvisible")
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",1,1)
        goto RReload
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_TakeInventory("MagnumBulletRight",1,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_PlayWeaponSound("marathon/pistol/fire")
        TNT1 A 0 A_FireCustomMissile("44mmCasingSpawner2",0,0,-8,0)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"RFireCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"RFireCoop")
        TNT1 A 0 A_FireCustomMissile("MarathonMagnumBullet",random(-1,1),0,0,0,0,random(-1,1))//A_FireBullets(1,1,-1,20+random(0,8),"MarathonPuff")
      RFireContinue:
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RInvisible")
        PIS2 A 3 A_Light2
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RInvisible")
        PIS2 B 3 Bright A_Light1
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RInvisible")
        PIS2 C 3 Bright A_Light0
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RInvisible")
        PIS2 D 3
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RInvisible")
        PIS2 E 4
        TNT1 A 0 A_JumpIfInventory("SynthFireRight",1,"RFire")
        goto RIdle

      RFireCoop:
        TNT1 A 0 A_FireCustomMissile("MarathonMagnumBulletCoop",random(-1,1),0,0,0,0,random(-1,1))//A_FireBullets(1,1,-1,20+random(0,8),"MarathonPuff")
        goto RFireContinue
    }
}

actor 44mmCasingSpawner
{
    Radius 1
    Height 1
    Speed 25
    PROJECTILE
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 1
        TNT1 A 0 A_Die
        Goto Death

      Death:
        TNT1 A 0 A_CustomMissile("44mmCasing",1,5,random(-80,-110),2,random(30,40))
        Stop
    }
}

actor 44mmCasingSpawner2 : 44mmCasingSpawner
{
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 1
        TNT1 A 0 A_Die
        Goto Death

      Death:
        TNT1 A 0 A_CustomMissile("44mmCasing",1,-5,random(80,110),2,random(30,40))
        Stop
    }
}

actor 44mmCasing
{ 
    Scale 0.25
    Radius 3
    Height 3
    Speed 7
    Mass 5
    BounceCount 1
    +THRUACTORS +DROPOFF +MISSILE +NOTELEPORT +FLOORCLIP
    +FORCEXYBILLBOARD +DOOMBOUNCE
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        PBLT ABCDEF 1
        Loop

      Death:
        TNT1 A 1
        Stop
    }
}

actor MagnumBullet : Ammo
{
    Inventory.MaxAmount 8
    +INVENTORY.IGNORESKILL
}

actor MagnumBulletRight : Ammo
{
    Inventory.MaxAmount 8
    +INVENTORY.IGNORESKILL
}


actor MarathonMagnumBullet : FastProjectile
{
    Damage (random(10,18))
    Radius 8
    Height 8
    Speed 100
    Projectile
    Obituary "$SAMSARA_MARATHON_OB_PISTOL"
    +BLOODSPLATTER
    +NOTIMEFREEZE
    +NOEXTREMEDEATH
    +SKYEXPLODE
    DamageType "MarathonPistol"
    Alpha 1.0
    Decal Bulletchip
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Crash:
        TNT1 A 0 A_PlaySound("puff/ding")
      Death:
        TNT1 A 1 A_SpawnItem("MarathonPuff")
        stop

      XDeath:
        TNT1 A 1 A_PlaySound("puff/hit")
        stop
    }
}

actor MarathonMagnumBulletCoop : MarathonMagnumBullet
{
    Damage (random(20,28))
    Radius 1
    Height 1
    +THRUSPECIES
    Speed 80
    Species "Player"
}

