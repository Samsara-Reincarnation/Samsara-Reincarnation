actor UsingPistols: Boolean {}
actor UsingDualPistols: Boolean {}
actor CanDualPistols: Boolean {}

actor ".44 Magnum Mega Class A1" : Weapon// replaces Pistol
{
    Weapon.BobRangeX 0
    Weapon.BobStyle Smooth
    Weapon.SlotNumber 1
    Weapon.SelectionOrder 1900
    Decal BulletChip
    Inventory.PickupMessage "You picked up a .44 Magnum Mega Class A1!"
    Weapon.AmmoType "MagnumBullet"
    Weapon.AmmoGive 0
    Weapon.AmmoUse 1
    +AMMO_OPTIONAL
    +NOALERT
    +INVENTORY.UNDROPPABLE
	Inventory.RestrictedTo "MarathonPlayer"
    Tag ".44 Magnum Mega Class A1"
    States
    {
      Spawn:
        PSTL A -1
        stop

      // Single
      Ready:
        TNT1 A 0 A_TakeInventory("SamsaraLevelSwitch_DualRaising")
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"ReadyDual")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyModernReloadable")
      ReadyNonReloadable:
        PIST A 1 A_WeaponReady
      ReadyAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyAmmoCheckReloadable")
        goto Ready

      ReadyModernReloadable:
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"ReadyNonReloadable")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ReadyModernReloadableReserve")
      ReadyModernReloadableCanReload:
        PIST A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyAmmoCheck

      ReadyModernReloadableReserve:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"ReadyModernReloadableCanReload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"ReadyModernReloadableCanReload")
        goto ReadyNonReloadable

      ReadyAmmoCheckReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"Ready")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",1,"Ready")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto Reload
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Reload")
        goto Ready

      Deselect:
        TNT1 A 0 A_TakeInventory("SamsaraLevelSwitch_DualWielding")
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"DeselectDual")
        TNT1 A 0 A_Lower
        PIST A 1 A_Lower
        loop

      Select:
        TNT1 A 0 A_TakeInventory("UsingFists")
        //TNT1 A 0 A_TakeInventory("UsingPistols")
        //TNT1 A 0 A_TakeInventory("UsingDualPistols")
        TNT1 A 0 A_TakeInventory("UsingSMG")
        TNT1 A 0 A_TakeInventory("UsingPacifier")
        TNT1 A 0 A_TakeInventory("UsingShotguns")
        TNT1 A 0 A_TakeInventory("UsingDualShotguns")
        TNT1 A 0 A_TakeInventory("UsingFusionPistol")
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle")
        TNT1 A 0 A_TakeInventory("UsingSpanker")
        TNT1 A 0 A_TakeInventory("UsingToasty")
        TNT1 A 0 A_TakeInventory("UsingWMC")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2")
        //TNT1 A 0 A_TakeInventory("UsingMarathonInstagib")

        TNT1 A 0 A_GiveInventory("UsingPistols")
        TNT1 A 0 A_JumpIfInventory("SamsaraLevelSwitch_DualWielding",1,"SelectDual")
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"SelectDual")
        TNT1 A 0 A_TakeInventory("UsingDualPistols")
        TNT1 A 0 A_Raise
        PIST A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"LFire")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
      FireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto FireActual
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireActual")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"FireActual")
        goto Ready

      FireReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireActual")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",1,"FireActual")
      FireReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto Reload
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"Reload")
        goto Ready

      FireActual:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile("44mmCasingSpawner",0,false,8)
        TNT1 A 0 A_GiveInventory("SamsaraMarathonMagnumPrimaryAttackHandler")
      FireFinish:
        PIST A 3 A_Light2
        PIST B 3 Bright A_Light1
        PIST C 3 Bright A_Light0
        PIST D 3
        PIST E 4
        TNT1 A 0 A_ReFire
        goto Ready

      Reload:
        PIST F 11
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReloadAmmo")
      ReloadFinish:
        TNT1 A 0 A_PlaySound("marathon/pistol/reload")
        PIST GH 11
        goto Ready

      ReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"ReloadAmmoReserve")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"ReloadFinish")
        TNT1 A 0 A_GiveInventory("MagnumBullet")
        loop

      ReloadAmmoReserve:
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"ReloadFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto ReloadFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Clip",1)
        TNT1 A 0 A_GiveInventory("MagnumBullet")
        goto ReloadAmmo

      // Dual Init
      AltFire:
        TNT1 A 0 A_JumpIfInventory("SynthFireActive",1,"RFire")
        TNT1 A 0 A_JumpIfInventory("CanDualPistols",1,1)
        goto Ready
        TNT1 A 0 A_GiveInventory("UsingDualPistols")
        PIS3 A 1 A_GiveInventory("SamsaraLevelSwitch_DualWielding")
        PIS3 BCDEFGHIJ 1
        TNT1 A 0 A_GiveInventory("SynthFireActive")
        PIS3 K 1 A_GunFlash("Flash",GFF_NOEXTCHANGE)
        goto ReadyDual

      // Dual
      ReadyDual:
        TNT1 A 0 A_GiveInventory("SynthFireActive")
        PIS1 A 1 A_GunFlash("Flash",GFF_NOEXTCHANGE)
      // Left Magnum
      LReady:
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LInvisible")
        PIS1 A 1 A_WeaponReady(WRF_NOFIRE)
      LReadyAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"LReadyAmmoCheckReloadable")
      LReadyFireCheck:
        TNT1 A 0 A_JumpIfInventory("SynthFireLeft",1,"LFire")
        goto LReady

      LReadyAmmoCheckReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"LReadyFireCheck")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",1,"LReadyFireCheck")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto LReload
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"LReload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"LReload")
        goto LReadyFireCheck

      LInvisible:
        TNT1 AAA 11
        TNT1 A 0 A_TakeInventory("RightReload")
        goto LReady

      DeselectDual:
        TNT1 A 0 A_TakeInventory("SamsaraLevelSwitch_DualWielding")
        TNT1 A 0 A_TakeInventory("SynthFireActive")
      DeselectDual2:
        TNT1 A 0 A_TakeInventory("LeftReload")
        TNT1 A 0 A_TakeInventory("RightReload")
        TNT1 A 0 A_Lower
        PIS1 A 1 A_Lower
        loop

      SelectDual:
        TNT1 A 0 A_GiveInventory("UsingDualPistols")
        TNT1 A 0 A_GiveInventory("SamsaraLevelSwitch_DualRaising")
        TNT1 A 0 A_GiveInventory("SynthFireActive")
        TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
        TNT1 A 0 A_Raise
        PIS1 A 1 A_Raise
        goto Select

      LFire:
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LInvisible")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"LFireModernReloadable")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"LFireClassicReloadable")
      LFireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto LFireActual
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"LFireActual")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"LFireActual")
        goto LReady

      LFireClassicReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"LFireActual")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",1,"LFireActual")
      LFireClassicReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto LReload
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"LReload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"LReload")
        goto LReady

      LFireModernReloadable:
        TNT1 A 0 A_JumpIfInventory("SynthFireReload",1,1)
        goto LFireClassicReloadable
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"LFireClassicReloadable")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"LFireModernReloadableReserve")
        goto LReload

      LFireModernReloadableReserve:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"LReload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"LReload")
        goto LFireClassicReloadable

      LFireActual:
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraDualFireFlash")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile("44mmCasingSpawner",0,false,8)
        TNT1 A 0 A_GiveInventory("SamsaraMarathonMagnumPrimaryAttackHandler")
      LFireContinue:
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LInvisible")
        PIS1 A 3 A_Light2
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LInvisible")
        PIS1 B 3 Bright A_Light1
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LInvisible")
        PIS1 C 3 Bright A_Light0
        PIS1 D 3 A_JumpIfInventory("RightReload",1,"LInvisible")
        PIS1 E 4 A_JumpIfInventory("RightReload",1,"LInvisible")
        TNT1 A 0 A_JumpIfInventory("SynthFireLeft",1,"LFire")
        goto LReady

      LReload:
        TNT1 A 0 A_JumpIfInventory("RightReload",1,"LInvisible")
        TNT1 A 0 A_Light0
        PIS1 F 11 A_GiveInventory("LeftReload")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"LReloadAmmo")
      LReloadFinish:
        TNT1 A 0 A_PlaySound("marathon/pistol/reload")
        PIS1 GH 11
        goto LReady

      LReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"LReloadAmmoReserve")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"LReloadFinish")
        TNT1 A 0 A_GiveInventory("MagnumBullet")
        loop

      LReloadAmmoReserve:
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",0,"LReloadFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto LReloadFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Clip",1)
        TNT1 A 0 A_GiveInventory("MagnumBullet")
        goto LReloadAmmo

      // Right Magnum
      Flash:
        PIS2 A 1
      RReady:
        PIS2 A 1 A_JumpIfInventory("LeftReload",1,"RInvisible")
        TNT1 A 0 A_JumpIfInventory("SamsaraLevelSwitch_DualRaising",1,"RReady")
      RReadyAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"RReadyAmmoCheckReloadable")
      RReadyFireCheck:
        TNT1 A 0 A_JumpIfInventory("SynthFireRight",1,"RFire")
        goto RReady

      RReadyAmmoCheckReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"RReadyFireCheck")
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",1,"RReadyFireCheck")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto RReload
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"RReload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"RReload")
        goto RReadyFireCheck

      RInvisible:
        TNT1 AAA 11
        TNT1 A 0 A_TakeInventory("LeftReload")
        goto RReady

      RFire:
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RInvisible")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"RFireModernReloadable")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"RFireClassicReloadable")
      RFireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto RFireActual
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"RFireActual")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"RFireActual")
        goto RReady

      RFireClassicReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"RFireActual")
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",1,"RFireActual")
      RFireClassicReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto RReload
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"RReload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"RReload")
        goto RReady

      RFireModernReloadable:
        TNT1 A 0 A_JumpIfInventory("SynthFireReload",1,1)
        goto RFireClassicReloadable
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",0,"RFireClassicReloadable")
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"RFireModernReloadableReserve")
        goto RReload

      RFireModernReloadableReserve:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"RReload")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"RReload")
        goto RFireClassicReloadable

      RFireActual:
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraDualFireFlash")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile("44mmCasingSpawner2",0,false,-8)
        TNT1 A 0 A_GiveInventory("SamsaraMarathonMagnumSecondaryAttackHandler")
      RFireContinue:
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RInvisible")
        PIS2 A 3 A_Light2
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RInvisible")
        PIS2 B 3 Bright A_Light1
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RInvisible")
        PIS2 C 3 Bright A_Light0
        PIS2 D 3 A_JumpIfInventory("LeftReload",1,"RInvisible")
        PIS2 E 4 A_JumpIfInventory("LeftReload",1,"RInvisible")
        TNT1 A 0 A_JumpIfInventory("SynthFireRight",1,"RFire")
        goto RReady

      RReload:
        TNT1 A 0 A_JumpIfInventory("LeftReload",1,"RInvisible")
        TNT1 A 0 A_Light0
        PIS2 F 11 A_GiveInventory("RightReload")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"RReloadAmmo")
      RReloadFinish:
        TNT1 A 0 A_PlaySound("marathon/pistol/reload")
        PIS2 GH 11
        goto RReady

      RReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"RReloadAmmoReserve")
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",0,"RReloadFinish")
        TNT1 A 0 A_GiveInventory("MagnumBulletRight")
        loop

      RReloadAmmoReserve:
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",0,"RReloadFinish")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Clip",1,1)
        goto RReloadFinish
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Clip",1)
        TNT1 A 0 A_GiveInventory("MagnumBulletRight")
        goto RReloadAmmo
    }
}

actor SamsaraMarathonMagnumPrimaryAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,1)
        goto PickupAttack
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Clip",1,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("MagnumBullet",1,"PickupTakeAmmoReloadable")
        stop

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("marathon/pistol/fire",CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(0,0,1,0,"SamsaraBlankPuff",FBF_NORANDOM|FBF_NOFLASH)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("MarathonMagnumBullet",random(-1,1),false,0,0,0,random(-1,1))
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("MarathonMagnumBulletCoop",random(-1,1),false,0,0,0,random(-1,1))
        stop

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("Clip",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("MagnumBullet",1)
        goto PickupAttack
    }
}

actor SamsaraMarathonMagnumSecondaryAttackHandler : SamsaraMarathonMagnumPrimaryAttackHandler
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
        goto PickupCheckAmmoNonReloadable

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("MagnumBulletRight",1,"PickupTakeAmmoReloadable")
        stop

      // Ammo taking stuff
      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("MagnumBulletRight",1)
        goto PickupAttack
    }
}

actor 44mmCasingSpawner
{
    Radius 1
    Height 1
    Speed 25
    PROJECTILE
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 1
        TNT1 A 0 A_Die
        Goto Death

      Death:
        TNT1 A 0 A_CustomMissile("44mmCasing",1,5,random(-80,-110),2,random(30,40))
        Stop
    }
}

actor 44mmCasingSpawner2 : 44mmCasingSpawner
{
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        TNT1 A 1
        TNT1 A 0 A_Die
        Goto Death

      Death:
        TNT1 A 0 A_CustomMissile("44mmCasing",1,-5,random(80,110),2,random(30,40))
        Stop
    }
}

actor 44mmCasing
{ 
    Scale 0.25
    Radius 3
    Height 3
    Speed 7
    Mass 5
    BounceCount 1
    +THRUACTORS +DROPOFF +MISSILE +NOTELEPORT +FLOORCLIP
    +FORCEXYBILLBOARD +DOOMBOUNCE
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        PBLT ABCDEF 1
        Loop

      Death:
        TNT1 A 1
        Stop
    }
}

actor MagnumBullet : Ammo
{
    Inventory.MaxAmount 8
    +INVENTORY.IGNORESKILL
}

actor MagnumBulletRight : Ammo
{
    Inventory.MaxAmount 8
    +INVENTORY.IGNORESKILL
}


actor MarathonMagnumBullet : FastProjectile
{
    Damage (random(10,18))
    Radius 8
    Height 8
    Speed 100
    Projectile
    Obituary "$SAMSARA_MARATHON_OB_PISTOL"
    +BLOODSPLATTER
    +NOTIMEFREEZE
    +NOEXTREMEDEATH
    +SKYEXPLODE
    DamageType "MarathonPistol"
    Alpha 1.0
    Decal Bulletchip
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Crash:
        TNT1 A 0 A_PlaySound("puff/ding")
      Death:
        TNT1 A 1 A_SpawnItem("MarathonPuff")
        stop

      XDeath:
        TNT1 A 1 A_PlaySound("puff/hit")
        stop
    }
}

actor MarathonMagnumBulletCoop : MarathonMagnumBullet
{
    Damage (random(20,28))
    Radius 1
    Height 1
    +THRUSPECIES
    Speed 80
    Species "Player"
}

