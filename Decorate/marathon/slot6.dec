actor UsingToasty: Boolean {}
Actor ToztCounter: Counter { Inventory.MaxAmount 7 }

actor "TOZT-7 Napalm Unit" : Weapon// replaces BFG9000
{
    Weapon.BobRangeX 0
    Weapon.BobStyle Smooth
    Weapon.SelectionOrder 700
    Weapon.SlotNumber 6
    Inventory.PickupMessage "You picked up a TOZT-7 Backpack Napalm Unit!"
    Weapon.AmmoType "NapalmInTank"
    Weapon.AmmoGive 0
    Weapon.AmmoUse 1
    Weapon.AmmoType2 "Cell"
    Weapon.AmmoGive2 110
    Weapon.AmmoUse2 0	
    +AMMO_OPTIONAL
    +NOALERT
    +INVENTORY.UNDROPPABLE
	Inventory.RestrictedTo "MarathonPlayer"
    Tag "TOZT-7 Napalm Unit"
    States
    {
      Spawn:
        THRW A -1
        stop

      Ready:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",0,"ReadyModernReloadable")
      ReadyNonReloadable:
        FLMT A 1 A_WeaponReady
      ReadyAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"ReadyAmmoCheckReloadable")
        goto Ready

      ReadyModernReloadable:
        TNT1 A 0 A_JumpIfInventory("NapalmInTank",0,"ReadyNonReloadable")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto ReadyNonReloadable
        FLMT A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        goto ReadyAmmoCheck

      ReadyAmmoCheckReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"Ready")
        TNT1 A 0 A_JumpIfInventory("NapalmInTank",1,"Ready")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"Reload")
        goto Ready

      Deselect:
        FLMT A 1 A_Lower
        loop

      Select: // 30 tics in Marathon to ready
        TNT1 A 0 A_TakeInventory("UsingFists")
        TNT1 A 0 A_TakeInventory("UsingPistols")
        TNT1 A 0 A_TakeInventory("UsingDualPistols")
        TNT1 A 0 A_TakeInventory("UsingSMG")
        TNT1 A 0 A_TakeInventory("UsingPacifier")
        TNT1 A 0 A_TakeInventory("UsingShotguns")
        TNT1 A 0 A_TakeInventory("UsingDualShotguns")
        TNT1 A 0 A_TakeInventory("UsingFusionPistol")
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle")
        TNT1 A 0 A_TakeInventory("UsingSpanker")
        //TNT1 A 0 A_TakeInventory("UsingToasty")
        TNT1 A 0 A_TakeInventory("UsingWMC")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon")
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2")
        //TNT1 A 0 A_TakeInventory("UsingMarathonInstagib")

        TNT1 A 0 A_GiveInventory("UsingToasty")
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"SelectReloadAmmoCheck")
      Select2:
        FLMT A 1 A_Raise
        loop

      SelectReloadAmmoCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"Select2")
        TNT1 A 0 A_JumpIfInventory("NapalmInTank",1,"Select2")
      SelectReloadAmmo:
        TNT1 A 0 A_JumpIfInventory("NapalmInTank",0,"Select2")
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Select2
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,2)
        TNT1 A 0 A_TakeInventory("Cell",1)
        TNT1 A 0 A_GiveInventory("NapalmInTank")
        loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"FireReloadable")
      FireNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"FireActual")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"FireActual")
        goto Ready

      FireReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"FireActual")
        TNT1 A 0 A_JumpIfInventory("NapalmInTank",1,"FireActual")
      FireReloadableDryCheck:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,2)
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"Reload")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"Reload")
        goto Ready

      FireActual:
        TNT1 A 0 A_JumpIfInventory("SamsaraCurrentWaterLevel",0,"Ready")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GiveInventory("SamsaraMarathonFlamethrowerAttackHandler")
      FireFinish:
        FLMT B 1 Bright
        TNT1 A 0 A_ReFire
        goto Ready

      Reload:
        FLMT A 1 Offset(0,38)
        FLMT A 1 Offset(0,44)
        FLMT A 1 Offset(0,50)
        FLMT A 1 Offset(0,56)
        FLMT A 1 Offset(0,62)
        FLMT A 1 Offset(0,68)
        FLMT A 1 Offset(0,74)
        FLMT A 1 Offset(0,80)
        FLMT A 1 Offset(0,86)
        FLMT A 1 Offset(0,92)
        FLMT A 1 Offset(0,98)
        FLMT A 1 Offset(0,104)
        FLMT A 1 Offset(0,110)
        FLMT A 1 Offset(0,116)
        FLMT A 1 Offset(0,122)
        FLMT A 18 Offset(0,128)
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"SelectReloadAmmo")
        goto Select2
    }
}

actor SamsaraMarathonFlamethrowerAttackHandler : Trigger
{
    States
    {
      // Ammo check stuff
      Pickup:
        TNT1 A 0 A_JumpIfInventory("SamsaraReloadMode",1,"PickupCheckAmmoReloadable")
      PickupCheckAmmoNonReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",1,"PickupTakeAmmoNonReloadable")
        TNT1 A 0 A_JumpIfInventory("Cell",1,"PickupTakeAmmoNonReloadable")
        stop

      PickupCheckAmmoReloadable:
        TNT1 A 0 A_JumpIfInventory("SamsaraHasInfiniteAmmo",0,"PickupAttack")
        TNT1 A 0 A_JumpIfInventory("NapalmInTank",1,"PickupTakeAmmoReloadable")
        stop

      // Attack stuff
      PickupAttack:
        TNT1 A 0 A_PlaySound("marathon/flamethrower/fire",CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"PickupAttackCoop")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"PickupAttackCoop")
      PickupAttackDM:
        TNT1 A 0 A_FireCustomMissile("MaraFlameShot",0,false,0,3)
      PickupFinish:
        TNT1 A 0 A_Recoil(0.1)
        stop

      PickupAttackCoop:
        TNT1 A 0 A_FireCustomMissile("MaraFlameShot2",0,false,0,3)
        goto PickupFinish

      // Ammo taking stuff
      PickupTakeAmmoNonReloadable:
        TNT1 A 0 A_TakeInventory("Cell",1,TIF_NOTAKEINFINITE)
        goto PickupAttack

      PickupTakeAmmoReloadable:
        TNT1 A 0 A_TakeInventory("NapalmInTank",1)
        goto PickupAttack
    }
}

actor NapalmInTank : Ammo
{
    +IGNORESKILL
    Inventory.MaxAmount 210
}

actor MaraFlameShot
{
    Radius 6
    Height 8
    Speed 50
    Damage (random(6,10))
    DamageType Fire
    Scale 0.85
    Projectile
    Obituary "$SAMSARA_MARATHON_OB_SLOT6"
    +RANDOMIZE
    +RIPPER
    +NOBOSSRIP
    +BLOODLESSIMPACT
    States
    {
      Spawn:
        FLME A 1 BRIGHT
        FLME B 1 BRIGHT A_GiveInventory("ToztCounter",1)
        FLME C 1 BRIGHT A_GiveInventory("ToztCounter",1)
        FLME D 1 BRIGHT A_GiveInventory("ToztCounter",1)
        FLME E 1 BRIGHT A_GiveInventory("ToztCounter",1)
        FLME F 1 BRIGHT A_GiveInventory("ToztCounter",1)
        FLME G 1 BRIGHT A_GiveInventory("ToztCounter",1)
        Stop

      Death:
        TNT1 A 0 A_JumpIfInventory("ToztCounter",6,"Death6")
        TNT1 A 0 A_JumpIfInventory("ToztCounter",5,"Death5")
        TNT1 A 0 A_JumpIfInventory("ToztCounter",4,"Death4")
        TNT1 A 0 A_JumpIfInventory("ToztCounter",3,"Death3")
        TNT1 A 0 A_JumpIfInventory("ToztCounter",2,"Death2")
        TNT1 A 0 A_JumpIfInventory("ToztCounter",1,"Death1")
        FLME A 1 Bright
      Death1:
        FLME B 1 Bright
      Death2:
        FLME C 1 Bright
      Death3:
        FLME D 1 Bright
      Death4:
        FLME E 1 Bright
      Death5:
        FLME F 1 Bright
      Death6:
        FLME G 1 Bright
        Stop
    }
}

actor MaraFlameShot2 : MaraFlameshot
{
    Species "Player"
    +THRUSPECIES
    Damage (random(8,12))
}
