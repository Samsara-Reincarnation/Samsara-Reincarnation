Actor Kingpin_Flamethrower : Kingpin_Weapon
{
	Weapon.SlotNumber 8
	Weapon.SelectionOrder 300
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 30
	Weapon.AmmoType1 "Cell"
	Obituary "%o was roasted in %k's torch"
	Inventory.Pickupmessage "Copped a flamethrower."
	Tag "Flamethrower"
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	States
	{
		Ready:
			TNT1 A 0
			KP00 ABCDEF 3
			Goto ReadyLoop
		ReadyLoop:
			KP00 O 0 A_GunFlash("ReadyLoopFlash",GFF_NOEXTCHANGE)
			KP00 OPQRSTUVWXYZ 5 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		ReadyLoopFlash:
			KP90 OPQRSTUVWXYZ 5 Bright
			Stop
		Deselect:
			KP00 "[" 0 A_GunFlash("DeselectFlash",GFF_NOEXTCHANGE)
			KP00 "[\]" 3
			KP01 ABC 3
			TNT1 A 0 A_GunFlash("StopFlash",GFF_NOEXTCHANGE)
			Goto DeselectLoop
		DeselectFlash:
			KP90 "[\]" 3 Bright
			KP91 ABC 3 Bright
			Stop
		Fire:
			"####" "#" 3 A_GunFlash("StopFlash",GFF_NOEXTCHANGE)
			KP00 G 0 A_GiveInventory("Kingpin_Flamethrower_Hold")
			KP00 G 0 ACS_NamedExecuteWithResult("Kingpin_Flamethrower")
			KP00 G 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 H 0 A_Refire(1)
			Goto FireResolve
			KP00 H 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 I 0 A_Refire(1)
			Goto FireResolve
			KP00 I 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 J 0 A_Refire(1)
			Goto FireResolve
			KP00 J 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 K 0 A_Refire(1)
			Goto FireResolve
			KP00 K 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 L 0 A_Refire(1)
			Goto FireResolve
			KP00 L 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 M 0 A_Refire(1)
			Goto FireResolve
			KP00 M 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 N 0 A_Refire(1)
			Goto FireResolve
			KP00 N 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 G 0 A_Refire("Fire")
			Goto FireResolve
		FireResolve:
			KP00 G 3 A_TakeInventory("Kingpin_Flamethrower_Hold")
			KP00 O 0
			Goto ReadyLoop
		DryFire:
			KP01 M 0 A_PlaySound("Kingpin/DryFire",CHAN_WEAPON)
			KP01 M 15 A_WeaponReady(WRF_NOFIRE)
			Goto ReadyLoop
		StopFlash:
			TNT1 A 1
			Stop
	}
}

Actor Kingpin_Flamethrower_Hold : Boolean {}

Actor Kingpin_Flamethrower_FireTrigger : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_FireCustomMissile("Kingpin_FlamethrowerFireStream",0,0,6,4)
			TNT1 A 0 A_FireCustomMissile("Kingpin_FlamethrowerFire",0,0,6,4)
			Stop
	}
}

Actor Kingpin_FlamethrowerFire
{
	Gravity 0.33
	Damage (10)
	Height 4
	Radius 2
	Projectile
	Speed 128
	+THRUACTORS
	+BLOODLESSIMPACT
	+NOTRIGGER
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1	
		Death:
			TNT1 A 0 A_Stop
			TNT1 A 1 A_SpawnItemEx("Kingpin_FlamethrowerFireEffect",0,0,0,frandom(-1,1),frandom(-1,1),frandom(-1,1))
			Stop
	}
}

Actor Kingpin_FlamethrowerFireEffect
{
	+NOINTERACTION
	+NOGRAVITY
	-SOLID
	+FORCEXYBILLBOARD
	Height 4
	Radius 2
	VSpeed 0.5
	Renderstyle Add
	Scale 1.0
	States
	{
		Spawn:
			KP01 A 0 NoDelay ACS_NamedExecuteWithResult("Kingpin_FX",2,28,1.0*65536)
			KP01 ABCDEFGHIJKLMN 2 Bright
			Stop
	}
}

Actor Kingpin_FlamethrowerFireStream : FastProjectile
{
	Gravity 0.33
	Damage (0)
	Height 4
	Radius 2
	Projectile
	Speed 128
	Renderstyle Add
	MissileType "Kingpin_FlamethrowerStream"
	MissileHeight 0
	+THRUACTORS
	+BLOODLESSIMPACT
	+NOTRIGGER
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1	
		Death:
			TNT1 A 1 A_Stop
			Stop
	}
}

Actor Kingpin_FlamethrowerStream
{
	+NOINTERACTION
	+NOGRAVITY
	-SOLID
	+FORCEXYBILLBOARD
	Height 4
	Radius 2
	Scale 0.25
	Renderstyle Add
	States
	{
		Spawn:
			TNT1 A 0
			KP04 C 1
			Stop
	}
}