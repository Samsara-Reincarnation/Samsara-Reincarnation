Actor Kingpin_Flamethrower : Kingpin_Weapon
{
	Weapon.SlotNumber 8
	Weapon.SelectionOrder 300
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 30
	Weapon.AmmoType1 "Cell"
	Obituary "%o was roasted in %k's torch"
	Inventory.Pickupmessage "Copped a flamethrower."
	Tag "Flamethrower"
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	States
	{
		Ready:
			KP00 A 0 A_GunFlash("ReadyFlash",GFF_NOEXTCHANGE)
			KP00 ABCDEF 3
			Goto ReadyLoop
		ReadyFlash:
			KP90 ABCDEF 3
			Goto ReadyLoop
		ReadyLoop:
			KP00 O 0 A_PlaySound("Kingpin/Flamethrower/Twitch",CHAN_5,1.0,1)
			KP00 O 0 A_GunFlash("ReadyLoopFlash",GFF_NOEXTCHANGE)
			KP00 OPQRSTUVWXYZ 5 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		ReadyLoopFlash:
			KP91 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 1 Bright
			KP92 "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 1 Bright
			KP93 AB 1 Bright
			Stop
		Deselect:
			KP00 "[" 0 A_StopSound(CHAN_5)
			KP00 "[" 0 A_GunFlash("DeselectFlash",GFF_NOEXTCHANGE)
			KP00 "[\]" 3
			KP01 ABC 3
			TNT1 A 0 A_GunFlash("StopFlash",GFF_NOEXTCHANGE)
			Goto DeselectLoop
		DeselectFlash:
			KP94 ABCDE 3 Bright
			Stop
		Fire:
			"####" "#" 3 A_GunFlash("StopFlash",GFF_NOEXTCHANGE)
			KP00 G 0 A_StopSound(CHAN_5)
			KP00 G 0 A_GiveInventory("Kingpin_Flamethrower_Hold")
			KP00 G 0 ACS_NamedExecuteWithResult("Kingpin_Flamethrower")
		FireLoop:
			KP00 G 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 H 0 A_Refire(1)
			Goto FireResolve
			KP00 H 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 I 0 A_Refire(1)
			Goto FireResolve
			KP00 I 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 J 0 A_Refire(1)
			Goto FireResolve
			KP00 J 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 K 0 A_Refire(1)
			Goto FireResolve
			KP00 K 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 L 0 A_Refire(1)
			Goto FireResolve
			KP00 L 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 M 0 A_Refire(1)
			Goto FireResolve
			KP00 M 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 N 0 A_Refire(1)
			Goto FireResolve
			KP00 N 3 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			KP00 G 0 A_Refire("FireLoop")
			Goto FireResolve
		FireResolve:
			KP00 G 3 A_TakeInventory("Kingpin_Flamethrower_Hold")
			KP00 O 0 A_PlaySound("Kingpin/Flamethrower/End",CHAN_AUTO)
			KP00 O 0
			Goto ReadyLoop
		DryFire:
			KP01 M 0 A_PlaySound("Kingpin/DryFire",CHAN_WEAPON)
			KP01 M 15 A_WeaponReady(WRF_NOFIRE)
			Goto ReadyLoop
		StopFlash:
			TNT1 A 1
			Stop
	}
}

Actor Kingpin_Flamethrower_Hold : Boolean {}

Actor Kingpin_Flamethrower_FireTrigger : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_FireCustomMissile("Kingpin_FlamethrowerFireStream",0,0,6,2)
			TNT1 A 0 A_FireCustomMissile("Kingpin_FlamethrowerFire",0,0,16,4)
			Stop
	}
}

Actor Kingpin_FlamethrowerFire
{
	Gravity 0.33
	Damage (10)
	Height 4
	Radius 2
	Projectile
	Speed 24
	Scale 0.5
	+THRUACTORS
	+BLOODLESSIMPACT
	+NOTRIGGER
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1	
		Death:
			TNT1 A 0 A_SetArg(0,1+CallACS("Kingpin_ReturnUpgrade",21))
			TNT1 A 0 A_SetScale(scaleX+(0.25*(args[0]-1)))
			TNT1 A 0 A_ChangeVelocity(12*(sqrt(velx*velx+vely*vely)/24)*args[0],0,12*(velz/24)*args[0],CVF_RELATIVE|CVF_REPLACE)
			TNT1 A 1 A_SpawnItemEx("Kingpin_FlamethrowerFireEffect",0,0,0,velx+frandom(-1,1),vely+frandom(-1,1),velz+frandom(-1,1),0,SXF_ABSOLUTEVELOCITY|SXF_TRANSFERSCALE|SXF_TRANSFERPOINTERS)
			Stop
	}
}

Actor Kingpin_FlamethrowerFireEffect
{
	+NOINTERACTION
	+NOGRAVITY
	-SOLID
	+FORCEXYBILLBOARD
	Height 4
	Radius 2
	VSpeed 0.5
	Renderstyle Add
	Scale 0.5
	States
	{
		Spawn:
			KP01 A 0 NoDelay ACS_NamedExecuteWithResult("Kingpin_FX",2,28,1.0*65536)
			KP01 ABCDEFGHIJKLMN 2 Bright A_GiveInventory("Kingpin_FlamethrowerIgniteShooter")
			Stop
	}
}

Actor Kingpin_FlamethrowerFireStream
{
	Damage (0)
	Height 4
	Radius 2
	Speed 48
	Scale 0.1
	+BLOODLESSIMPACT
	+NOTRIGGER
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SetArg(0,x)
			TNT1 A 0 A_SetArg(1,y)
			TNT1 A 0 A_SetArg(2,z)
			TNT1 A 1
			TNT1 A 0 A_SetArg(3,CallACS("Kingpin_FlamethrowerDistance",args[0],args[1],args[2]))
			TNT1 A 0 A_GiveInventory("Kingpin_StreamDistance",args[3])
			TNT1 A 0 A_SetArg(3,args[3]*-2)
		SpawnTrailLoop:
			TNT1 A 0 A_JumpIfInventory("Kingpin_StreamDistance",4,1)
			Goto Death
			TNT1 A 0 A_SetScale(scaleX*1.1)
			TNT1 AA 0 A_SpawnItemEx("Kingpin_FlamethrowerStream",args[3]*(sqrt(velx*velx+vely*vely)/48),0,args[3]*(velz/48),0,0,0,0,SXF_TRANSFERSCALE)
			TNT1 A 0 A_SetArg(3,args[3]+4)
			TNT1 A 0 A_TakeInventory("Kingpin_StreamDistance",4)
			Loop
		Death:
			TNT1 A 1
			Stop
	}
}

Actor Kingpin_FlamethrowerStream
{
	+NOINTERACTION
	+NOGRAVITY
	-SOLID
	+FORCEXYBILLBOARD
	+NOBLOCKMAP
	Height 4
	Radius 1
	Renderstyle Add
	States
	{
		Spawn:
			TNT1 A 0
			KP04 C 1
			Stop
	}
}

Actor Kingpin_FlamethrowerIgniteShooter : Trigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_SetAngle(5*random(0,6))
			TNT1 A 0 A_CustomMissile("Kingpin_FlamethrowerIgniteTracer",-16,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("Kingpin_FlamethrowerIgniteTracer",-16,0,45,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("Kingpin_FlamethrowerIgniteTracer",-16,0,90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("Kingpin_FlamethrowerIgniteTracer",-16,0,135,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("Kingpin_FlamethrowerIgniteTracer",-16,0,180,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("Kingpin_FlamethrowerIgniteTracer",-16,0,225,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("Kingpin_FlamethrowerIgniteTracer",-16,0,270,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			TNT1 A 0 A_CustomMissile("Kingpin_FlamethrowerIgniteTracer",-16,0,315,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			stop
	}
}

Actor Kingpin_FlamethrowerIgniteTracer
{
	Height 64
	Radius 32
	Speed 32
	Projectile
	+HITTRACER
    +DONTBLAST
    +DONTREFLECT
	States
	{
		Spawn:
			TNT1 A 2 A_TransferPointer(AAPTR_Target,AAPTR_Default,AAPTR_Target,AAPTR_Target)
			Stop
		Death:
			TNT1 A 1 A_TransferPointer(AAPTR_Target,AAPTR_Default,AAPTR_Target,AAPTR_Target)
			Stop
		Crash:
		XDeath:
			TNT1 A 0 A_TransferPointer(AAPTR_Target,AAPTR_Default,AAPTR_Target,AAPTR_Target)
			TNT1 A 2 ACS_NamedExecuteWithResult("Kingpin_FlamethrowerTracer")
			Stop
	}
}

Actor Kingpin_FlamethrowerIgnition : Counter { Inventory.MaxAmount 50 }
Actor Kingpin_FlamethrowerStacker : Counter { Inventory.MaxAmount 32 }
Actor Kingpin_FlamethrowerHitTarget : Boolean {}
Actor Kingpin_FlamethrowerBurnt : Boolean {}

Actor Kingpin_FlamethrowerBurner
{
	Height 4
	Radius 2
	Speed 0
	Damage (0)
	Projectile
	DamageType "RTCWFire"
	+THRUACTORS
	+NODAMAGETHRUST
	+FORCEPAIN
    +DONTBLAST
    +DONTREFLECT
	var int user_radius;
	var int user_height;
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteWithResult("Kingpin_BurnHandler")
		Death:
			TNT1 A 0 A_SetUserVar("user_radius",CallACS("Kingpin_Decorate",9))
			TNT1 A 0 A_SetUserVar("user_height",CallACS("Kingpin_Decorate",9,1))
			TNT1 A 0 A_Explode(CallACS("Kingpin_Decorate",10),16,!XF_HURTSOURCE,0,16)
			TNT1 A 0 A_ChangeFlag("PAINLESS",1)
			TNT1 A 0 A_ChangeFlag("FORCEPAIN",0)
		SpawnLoop:
			TNT1 A 0 A_CheckFlag("SHOOTABLE",1,AAPTR_Tracer)
			Goto Remove
			TNT1 A 0 A_CheckFlag("CORPSE","Remove",AAPTR_Tracer)
			TNT1 A 0 A_SpawnItemEx("Kingpin_FlamethrowerFireVisual",random(-user_radius/2,user_radius/2),random(-user_radius/2,user_radius/2),random(0,user_height),0,0,0,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_Explode(CallACS("Kingpin_Decorate",10),16,!XF_HURTSOURCE,0,16)
			TNT1 A 1 A_Warp(AAPTR_Tracer,0,0,0,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_CheckFlag("SHOOTABLE",1,AAPTR_Tracer)
			Goto Remove
			TNT1 A 0 A_CheckFlag("CORPSE","Remove",AAPTR_Tracer)
			TNT1 A 0 A_Explode(CallACS("Kingpin_Decorate",10),16,!XF_HURTSOURCE,0,16)
			TNT1 A 1 A_Warp(AAPTR_Tracer,0,0,0,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_CheckFlag("SHOOTABLE",1,AAPTR_Tracer)
			Goto Remove
			TNT1 A 0 A_CheckFlag("CORPSE","Remove",AAPTR_Tracer)
			TNT1 A 0 A_Explode(CallACS("Kingpin_Decorate",10),16,!XF_HURTSOURCE,0,16)
			TNT1 A 1 A_Warp(AAPTR_Tracer,0,0,0,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_CheckFlag("SHOOTABLE",1,AAPTR_Tracer)
			Goto Remove
			TNT1 A 0 A_CheckFlag("CORPSE","Remove",AAPTR_Tracer)
			TNT1 A 0 A_Explode(CallACS("Kingpin_Decorate",10),16,!XF_HURTSOURCE,0,16)
			TNT1 A 1 A_Warp(AAPTR_Tracer,0,0,0,0,WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
			Loop
		Remove:
			TNT1 A 0 A_TakeInventory("Kingpin_FlamethrowerIgnition",65536,0,AAPTR_Tracer)
			TNT1 A 0 A_TakeInventory("Kingpin_FlamethrowerHitTarget",1,0,AAPTR_Tracer)
			TNT1 A 0 A_TakeInventory("Kingpin_FlamethrowerBurnt",1,0,AAPTR_Tracer)
			Stop
	}
}

Actor Kingpin_FlamethrowerFireVisual
{
	+NOINTERACTION
	+NOGRAVITY
	-SOLID
	+FORCEXYBILLBOARD
	Height 4
	Radius 2
	VSpeed 0.5
	Renderstyle Add
	Scale 0.1
	States
	{
		Spawn:
			KP01 A 0 NoDelay ACS_NamedExecuteWithResult("Kingpin_FX",2,28,0.25*65536)
			KP01 ABCDEFGHIJKLMN 2 Bright
			Stop
	}
}

Actor Kingpin_StreamDistance : Counter {}
Actor Kingpin_Flamethrower_Range : Boolean {}