Actor HLBoolean : Inventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 1
	+INVENTORY.IGNORESKILL
	+INVENTORY.UNDROPPABLE
	-INVENTORY.INVBAR
}

Actor HLCounter : Inventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 255
	+INVENTORY.IGNORESKILL
	+INVENTORY.UNDROPPABLE
	-INVENTORY.INVBAR
}

Actor HLAmmo : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.Amount 1
	Inventory.MaxAmount 1
}

Actor HLWeapon : Weapon
{
	+INVENTORY.UNDROPPABLE
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.ALT_AMMO_OPTIONAL
	+WEAPON.NOALERT //Only weapons with reload-functionality use this flag
}

Actor HLOpposingForceFireEnhancer : PowerDoubleFiringSpeed 
{
	Powerup.Duration 5
}

actor HLBossSee: Boolean {}
actor HLBossSightMusicQueue : CustomInventory
{
	  Inventory.MaxAmount 0
	  +AUTOACTIVATE
	  States
	  {
	  Pickup:
		TNT1 A 0 A_JumpIfInventory("HalflifeClass", 1, 1)
		Goto Nada
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraOST_HalfLifeScriptedMusic", 0, 5)
		stop
	  Nada:
		TNT1 A 0
		stop
	  }
}

Actor HLReloadTrigger : HLBoolean {}
Actor HLGordonDied : HLBoolean {}
Actor HLHEVIsTalking : HLBoolean {}
Actor HLHEVPickupID : HLCounter {}
Actor HLHEVDamageType : HLCounter {}
Actor HLSpeedTokens : HLCounter {}

ACTOR HLBulletPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	-ALLOWPARTICLES
	+RANDOMIZE
	+NOEXTREMEDEATH
	+CANNOTPUSH
	+PUFFONACTORS
	+BLOODLESSIMPACT
	RenderStyle Add
	Alpha 0.5
	//VSpeed 0
	Mass 5
	DamageType "HLBullet"
	scale 0.4
	Decal HalfLifeBulletChip
	States
	{
		Spawn:
		Crash:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("halflife/ricochet",CHAN_BODY)
			TNT1 A 0 A_Jump(128,1,2,3,4,5,6,7,8,9,10,11,12,13,14)		
			TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx(HLBulletDust,FRandom(0,12),0,FRandom(-8,15),FRandom(0,0.5),0,FRandom(-1,-0.1),Random(1,360))
			TNT1 A 0
			TNT1 A 0 A_Jump(128,2)
			TNT1 A 0 A_SpawnItemEx("HLPuffSpark",0,0,0,FRandom(1,4),0,Random(0,3),Random(-90,90))
			TNT1 A 0 A_Jump(128,2)
			TNT1 A 0 A_SpawnItemEx("HLPuffSpark",0,0,0,FRandom(1,4),0,Random(0,3),Random(-90,90))
			TNT1 A 0 A_SpawnItemEx("HLPuffSpark",0,0,0,FRandom(1,4),0,Random(0,3),Random(-90,90))
			TNT1 A 5
			stop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("HL_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("HL_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 5
			stop
	}
}

Actor HLBulletPuffCoop : HLBulletPuff { Species "Player" +MTHRUSPECIES }

Actor HLBulletDust
{
	+CLIENTSIDEONLY
	-SOLID
	+NOBLOCKMAP
	+NOINTERACTION
	-NOGRAVITY
	renderstyle shaded
	stencilcolor "black"
	Scale 1.5
	Radius 0.5
	Height 1
	Gravity 0.66
	States
	{
	Spawn:
		P001 ABCDEFGH 0 A_Jump(64,"Fade")
		Loop
	Fade:
		"----" A 5
		"----" A 1 A_FadeOut
		Wait
	}
}

Actor HLPuffSpark
{
	PROJECTILE
	-NOGRAVITY
	+DOOMBOUNCE
	-NOINTERACTION
	+CLIENTSIDEONLY
	Radius 1
	Height 1
	Speed 5
	Scale 0.05
	RenderStyle Shaded
	StencilColor "af af 61"
	states
	{
	Spawn:
		TNT1 A 0
		HL00 A 3 Bright A_SetScale(scalex*FRandom(1,1.25))
		HL00 A 1 Bright A_SetScale(scalex-0.005)
		TNT1 A 0 A_JumpIf(scalex<=0,"Death")
		Goto Spawn+2
	Death:
		TNT1 A 5
		stop
	}
}

Actor HLBullet : FastProjectile
{
	Speed 100
	Damage (0)
	Radius 1
	Height 2
	PROJECTILE
	+NOTIMEFREEZE
	+HITTRACER
	+NOEXTREMEDEATH
	+CANNOTPUSH
	+NODAMAGETHRUST
	+FORCERADIUSDMG
	+BLOODLESSIMPACT
	//+CLIENTSIDEONLY
	DamageType "HLBullet"
	RenderStyle Shaded
	StencilColor "af af 61"
	Decal HalfLifeBulletChip
	states
	{
	Spawn:
		TNT1 A 1
		Goto SpawnLoop
	SpawnLoop:
		HL00 A 1 Bright
		loop
	Crash:
	Death:
		TNT1 A 0	
		TNT1 A 1 A_SpawnItemEx("HLBulletPuff",0,0,0,0,0,1,0,0)//SXF_CLIENTSIDE) ;_;
		stop
	XDeath:
		TNT1 A 0 ACS_NamedExecuteWithResult("HL_BloodGenerator_Offsets")
		TNT1 A 0 A_GiveInventory("HL_BloodGenerator",1,AAPTR_TRACER)
		TNT1 A 5
		stop
	}
}

Actor HLBulletCoop : HLBullet {	+THRUSPECIES Species "Player" }

Actor HLBrassShellCaser
{
	PROJECTILE
	+CLIENTSIDEONLY
	Radius 1
	Height 1
	Speed 25
	states
	{
	Spawn:
		TNT1 A 1
		goto Death
	Death:
		TNT1 A 1 A_CustomMissile("HLBrassShell",1,12,random(-100,-90),CMF_AIMDIRECTION,random(20,30))
		stop
	}
}

Actor HLBrassShell
{
	Scale 0.15
    Speed 5
    Mass 8
    Radius 3
    Height 3
	Bouncecount 2
	+MISSILE
    +CLIENTSIDEONLY
    +THRUACTORS
	+DROPOFF
	+NOTELEPORT
	+FLOORCLIP
	-SOLID
	+DOOMBOUNCE
    States
    {
    Spawn:
		HWX1 ABCDEFGHI 1
        loop
    Death:
		HWX1 A 0 A_JumpIf(waterlevel != 0,"Done")
		HWX1 A 0 A_PlaySound("halflife/brass_shell",CHAN_BODY)
		HWX1 A 0 ThrustThingZ(0,10,0,0)
		goto Done
	Done:
		HWX1 A 500
		HWX1 A 1 A_FadeOut
		Goto Done+1
    }
}


Actor HLExplosionBoom
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOBLOCKMAP
	-SOLID
	+NOGRAVITY
	Radius 4
	Height 8
	scale 2.5
	Renderstyle add
	Alpha 0.8
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 Radius_Quake(3,20,0,4,0)
		TNT1 A 0 A_PlaySound("halflife/explosion_boom",CHAN_VOICE)
		HWX3 A 2 bright A_SpawnItemEx("HLExplosionDebris",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
		HWX3 B 2 bright
		TNT1 AAA 0 A_CustomMissile("HLExplosionSparkSpawner",random(0,8),random(-4,4),random(0,359),CMF_AIMDIRECTION,random(60,40))
		HWX3 CDEFGHI 2 bright A_SpawnItemEx("HLExplosionSmoke",random(-4,4),random(-4,4),random(0,8),random(0,20)/10,random(0,20)/10,random(0,50)/10,0,SXF_CLIENTSIDE)
		HWX3 JKLMNO 2 bright
		stop
	}
}

Actor HLMegaExplosionBoom
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOBLOCKMAP
	-SOLID
	+NOGRAVITY
	Radius 4
	Height 8
	scale 3.0
	Renderstyle add
	Alpha 0.8
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 Radius_Quake(6,28,0,8,0)
		TNT1 A 0 A_PlaySound("halflife/explosion_boom",CHAN_VOICE)
		HWX3 A 2 bright A_SpawnItemEx("HLExplosionDebris",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
		HWX3 B 2 bright
		TNT1 AAAAA 0 A_CustomMissile("HLExplosionSparkSpawner",random(0,8),random(-4,4),random(0,359),CMF_AIMDIRECTION,random(60,40))
		HWX3 CDEFGHIJKLMNO 2 bright A_SpawnItemEx("HLExplosionSmoke",random(-4,4),random(-4,4),random(0,8),random(0,20)/10,random(0,20)/10,random(0,50)/10,0,SXF_CLIENTSIDE)
		stop
	}
}

Actor HLSmallExplosionBoom : HLExplosionBoom { Scale 1.0 }

Actor HLExplosionDebris
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOBLOCKMAP
	-SOLID
	+NOGRAVITY
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_PlaySound("halflife/explosion_debris",CHAN_VOICE)
		stop
	}
}

Actor HLExplosionSparkSpawner
{
	PROJECTILE
	-NOGRAVITY
	+CLIENTSIDEONLY
	Gravity 0.6
    Radius 2
    Height 4
    Mass 15
    Speed 8
	ReactionTime 4
	states
	{
	Spawn:
		TNT1 A 8 A_SpawnItemEx("HLExplosionSpark",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		TNT1 A 0 A_CountDown
		loop
	Death:
		TNT1 A 1
		stop
	}
}

Actor HLExplosionSpark
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOBLOCKMAP
	-SOLID
	+NOGRAVITY
	renderstyle add
	states
	{
	Spawn:
		HWX5 ABCDEFGHIJKLM 1 bright
		stop
	}
}

Actor HLExplosionSmoke
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOBLOCKMAP
	-SOLID
	+NOGRAVITY
	renderstyle add
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 8
		HWX2 ABCDEFGHIJKLM 3 bright
		stop
	}
}

Actor HLMuzzleFlash
{
 +FORCEXYBILLBOARD
 +NOINTERACTION
 +CLIENTSIDEONLY
 -SOLID
 +NOCLIP
 Speed 0
 RenderStyle Add
 Alpha 1.0
 States
 {
  Spawn:
   TNT1 A 0
   HLWF AB 2 Bright
   Stop
 }
}

//===============
// FREEMAN: Barney.
// The idiotic security guard
Actor FriendlyBarney
{
	Health 100
	Radius 16
	Height 50
	Speed 6
	PainChance 128
	MONSTER
	+FLOORCLIP
	+FRIENDLY
	-SOLID
	Species "Player"	
	Seesound "halflife/barney_see"
	ActiveSound "halflife/barney_active"
	PainSound "halflife/barney_pain"
	DeathSound "halflife/barney_death"
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256, "Spawn1", "Spawn2", "Spawn3", "Spawn4")
	Spawn1:
		TNT1 A 0 A_Jump(254,2)
		TNT1 A 0 A_Playsound("HLBarney/Question")
		TNT1 A 0		
		HL00 ABCDEFGHIJKLMNOP 3 A_Look
		Goto Spawn
	Spawn2:
		TNT1 A 0 A_Jump(254,2)
		TNT1 A 0 A_Playsound("HLBarney/Question")
		TNT1 A 0		
		HL01 ABCDEFGHIJKLMNOPQRSTUVWX 3 A_Look
		Goto Spawn
	Spawn3:
		TNT1 A 0 A_Jump(254,2)
		TNT1 A 0 A_Playsound("HLBarney/Question")
		TNT1 A 0
		HL02 ABCDEFGHIJKLMNO 3 A_Look
		Goto Spawn
	Spawn4:
		TNT1 A 0 A_Jump(254,2)
		TNT1 A 0 A_Playsound("HLBarney/Question")
		TNT1 A 0		
		HL03 ABCDEFGHIJKLMNO 3 A_Look
		Goto Spawn
	See:
		HL04 ABCDEFGHIJKLMNOP 3 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_Jump(148,2)
		TNT1 A 0 A_Playsound("HLBarney/Attack")
		TNT1 A 0
		TNT1 A 0 A_FaceTarget
		HL07 ABCDEFGH 3 A_FaceTarget
	FireGun:
		TNT1 A 0 A_JumpIfInventory("HLBarneyClip", 1, 1)
		Goto BReload
		TNT1 A 0 A_Jump(128, "FireGun2")
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_PlaySound("halflife/barney_shoot")
		TNT1 A 0 A_CustomMissile("HLBarneyBullet",32,0,random(-4,4))
		TNT1 A 0 A_SpawnItemEx("HLMuzzleFlash_Enemy",20,4,42,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_TakeInventory("HLBarneyClip", 1)
		HL09 ABCDEFGH 3
		TNT1 A 0 A_JumpIfTargetInLOS("FireGun", 0, JLOSF_DEADNOJUMP)
	FireGun2:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_PlaySound("halflife/barney_shoot")
		TNT1 A 0 A_CustomMissile("HLBarneyBullet",32,0,random(-4,4))
		TNT1 A 0 A_SpawnItemEx("HLMuzzleFlash_Enemy",20,4,42,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_TakeInventory("HLBarneyClip", 1)
		HL10 ABCDEFG 3
		TNT1 A 0 A_JumpIfTargetInLOS("FireGun", 0, JLOSF_DEADNOJUMP)
	DisarmGun:
		HL08 ABCDEFGHIJ 3
		Goto See
	BReload:
		HL11 ABCDEFGHIJK 3 A_FaceTarget
		TNT1 A 0 A_GiveInventory("HLBarneyClip", 17)
		TNT1 A 0 A_JumpIfTargetInLOS("FireGun", 0, JLOSF_DEADNOJUMP)
		Goto See
	Pain:
		TNT1 A 0 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_Jump(256,"Death1","Death2","Death3","Death4","Death5","Death6")
	Death1:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		HL12 ABCDEFGHIJKLMN 3
		HL12 O -1
		stop
	Death2:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		HL13 ABCDEFG 3
		HL13 H -1
		stop
	Death3:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		HL14 ABCDEF 3
		HL14 G -1
		stop
	Death4:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		HL15 ABCDEFGHIJKLMNOPQR 3
		HL15 S -1
		stop
	Death5:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		HL16 ABCDEFGHIJKLMN 3
		HL16 O -1
		stop
	Death6:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		HL17 ABCDEFGHIJ 3
		HL17 K -1
		stop
	Death.HLLaser:
	Death.HLExplosive:
	Death.HLRPGExplosive:	
		TNT1 A 0 A_JumpIfHealthLower(-20,1)
		goto Death
	Death.HLEgon:
	Death.FreemanGibs:
	XDeath:	
		TNT1 A 0 A_GiveToTarget("KillCount",1)
		TNT1 A 0 A_SpawnItem("HLGenericGibSpawner")
		TNT1 A 5 A_NoBlocking
		Stop	
	}
}

Actor HLBarneyBullet : HLBulletCoop { Damage (random(10,15)) }