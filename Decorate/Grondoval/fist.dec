Actor WTSwordUpgrade : WTBoolean {}

Actor WTFistActive : WTBoolean {}
Actor WTFistAttack : WTBoolean {}
Actor Witchaven_Dagger : WTWeapon
{
	Weapon.Slotnumber 0
	Weapon.SelectionOrder 4000
	Weapon.BobStyle Alpha
	Weapon.BobRangeX 0.2
	Weapon.BobRangeY 0.6
	Weapon.BobSpeed 0.8	
	+WEAPON.MELEEWEAPON
	+WEAPON.NOALERT
	+WEAPON.WIMPY_WEAPON
	+INVENTORY.UNDROPPABLE
	Tag "Dagger"
	States
	{
	Spawn:
		NULL A 0
		stop
	Select:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		goto Select+1
	Deselect:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		goto Deselect+1
	Ready:
	Draw:
		WW02 ABCDEFGHIJKL 3
		TNT1 A 0 A_GunFlash
	WIdle:
		WW02 M 1 A_WeaponReady
		loop
		
	Fire:
		WW02 A 1 A_Jump(256,"SAttack","SWAttack")
		goto WIdle
	WGrondovalIsKill:
		TNT1 A 3
		goto WIdle
		
	// Stab Attack	
	SAttack:
		WW02 N 3
		WW02 OPQR 3
		WW02 A 0 A_PlayWeaponSound("ggen/swing2")
		WW02 S 3 A_FireCustomMissile("WTDaggerProjectileWeak",0,0,0,8)
		WW02 RQP 3
		WW02 M 3 Offset(-2,30)
		WW02 M 0 Offset(0,32)
		goto WIdle
		
	// Swing attack
	SWAttack:
		WW02 T 0 A_PlayWeaponSound("ggen/swing2")
		WW02 TU 4
		WW02 V 4 A_FireCustomMissile("WTDaggerProjectile",0,0,0,8)
		TNT1 A 3
		WW02 T 0 A_PlayWeaponSound("ggen/swing2")
		WW02 UT 4
		goto WIdle
	}
}

Actor WTDaggerProjectile : WTMeleeProjectile
{
	Speed 64
	+FORCEPAIN
	Damage (random(30,45)) // all damage values, except the fireballs, icicles, arrows, and magic spells, are there Witch 1 values *3. Enchantments do double what the normal attacks do.
	Obituary "$SAMSARA_WITCHAVEN_OB_FISTS"
}

Actor WTDaggerProjectileWeak : WTMeleeProjectile
{
	Speed 64
	+FORCEPAIN
	Damage (random(15,24))
	Obituary "$SAMSARA_WITCHAVEN_OB_FISTS"
}

Actor WTMegaDaggerProjectile : WTMegaMeleeProjectile
{
	Speed 64
	+FORCEPAIN
	Damage (random(60,90))
	Obituary "$SAMSARA_WITCHAVEN_OB_FISTS"
}

Actor WTMegaDaggerProjectileWeak : WTMegaMeleeProjectile
{
	Speed 64
	+FORCEPAIN
	Damage (random(30,48))
	Obituary "$SAMSARA_WITCHAVEN_OB_FISTS"
}