Actor WTSwordUpgrade : WTBoolean {}

Actor WTFistActive : WTBoolean {}
Actor WTFistAttack : WTBoolean {}
Actor Witchaven_Fists : WTWeapon
{
	Weapon.Slotnumber 0
	Weapon.SelectionOrder 4000
	Weapon.BobStyle Alpha
	Weapon.BobRangeX 0.2
	Weapon.BobRangeY 0.6
	Weapon.BobSpeed 0.8	
	+WEAPON.MELEEWEAPON
	+WEAPON.NOALERT
	+WEAPON.WIMPY_WEAPON
	+INVENTORY.UNDROPPABLE
	Tag "Ye Fists!"
	States
	{
	Spawn:
		NULL A 0
		stop
	Select:
		TNT1 A 0 A_GiveInventory("WTFistActive",1)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		goto Select+1
	Deselect:
		TNT1 A 0 A_TakeInventory("WTFistActive",1)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		goto Deselect+1
	Ready:
		TNT1 A 0 A_GunFlash
	WIdle:
		WW00 A 0 A_TakeInventory("WTFistAttack",1)
		WW00 A 0 //A_JumpIfHealthLower(1,"WGrondovalIsKill")
		WW00 A 1 A_WeaponReady
		loop
		
	Altfire:
		WW00 A 1 A_ChangeFlag("PICKUP",0)
		TNT1 A 0 ACS_NamedExecuteAlways("SamsaraWitchaven",0,1) //Throws potion if you got
	MegaAltNormal:
		WW00 A 18
		WW00 A 0 A_ChangeFlag("PICKUP",1)
		goto WIdle
		
	Fire:
		WW00 A 0 A_GiveInventory("WTFistAttack",1)
		WW00 A 1 A_Jump(256,"RAttack","LAttack")
		goto WIdle
	WGrondovalIsKill:
		TNT1 A 3
		goto WIdle
		
	// Right fist	
	RAttack:
		WW00 A 1
		WW00 BCDE 2 A_JumpIfHealthLower(1,"WGrondovalIsKill")
		WW00 A 0 A_PlayWeaponSound("gfist/swing")
		WW00 F 4 A_FireCustomMissile("WTFistProjectile",0,0,0,8)
		WW00 EDCBA 3 //A_JumpIfHealthLower(1,"WGrondovalIsKill")
		goto WIdle
		
	// Left fist
	LAttack:
		WW01 A 1
		WW01 BCDE 2 A_JumpIfHealthLower(1,"WGrondovalIsKill")
		WW01 A 0 A_PlayWeaponSound("gfist/swing")
		WW01 F 4 A_FireCustomMissile("WTFistProjectile",0,0,0,8)
		WW01 EDCBA 3 //A_JumpIfHealthLower(1,"WGrondovalIsKill")
		goto WIdle
		
	// Shows left fist only if not attacking and not being dead
	Flash:
	LShow:
		WW01 A 0 A_JumpIfInventory("WTFistActive",1,1)
		stop
		WW01 A 0 A_JumpIfHealthLower(1,"LWait")
		WW01 A 0 A_JumpIfInventory("WTFistAttack",1,"LWait")
		WW01 A 1
		loop
	LWait:
		TNT1 A 1
		goto LShow
	}
}

Actor WTFistProjectile : WTMeleeProjectile
{
	Speed 64
	Damage (random(10,15)) // 3 to 15? ew, nah
	Obituary "$SAMSARA_WITCHAVEN_OB_FISTS"
	States
	{
	Crash:
		TNT1 A 1 A_PlaySoundEx("gfist/hithard","body",0,0)
		stop
	Death: // In Witchaven, you hurt yourself by punching walls
		TNT1 A 0 A_PlaySoundEx("gfist/hithard","body",0,0)
		TNT1 A 1 A_GiveToTarget("WTFistsHurt",1)
		stop
	XDeath:
		TNT1 A 0 A_GiveInventory("WTGoreSpawner",1,AAPTR_TRACER)
		TNT1 A 1 A_PlaySoundEx("gfist/hit","body",0,0)
		stop
	}
}

Actor WTFistsHurt : WTACSTrigger
{
	Obituary "test lel"
	HitObituary "test lel"
	states
	{
	Spawn:
		NULL A 0
		stop
	//Pickup:
	//	stop
	Use:
		TNT1 A 1 A_Explode((1),7,1)//ACS_NamedExecuteAlways("SamsaraWitchaven",0,2) //Drains 1 HP
		stop
	}
}

Actor WTDaggerProjectile : WTMeleeProjectile
{
	Speed 64
	+FORCEPAIN
	Damage (random(30,45)) // all damage values, except the fireballs, icicles, arrows, and magic spells, are there Witch 1 values *3. Enchantments do double what the normal attacks do.
	Obituary "$SAMSARA_WITCHAVEN_OB_FISTS"
}

Actor WTDaggerProjectileWeak : WTMeleeProjectile
{
	Speed 64
	+FORCEPAIN
	Damage (random(15,24))
	Obituary "$SAMSARA_WITCHAVEN_OB_FISTS"
}

Actor WTMegaDaggerProjectile : WTMegaMeleeProjectile
{
	Speed 64
	+FORCEPAIN
	Damage (random(60,90))
	Obituary "$SAMSARA_WITCHAVEN_OB_FISTS"
}

Actor WTMegaDaggerProjectileWeak : WTMegaMeleeProjectile
{
	Speed 64
	+FORCEPAIN
	Damage (random(30,48))
	Obituary "$SAMSARA_WITCHAVEN_OB_FISTS"
}