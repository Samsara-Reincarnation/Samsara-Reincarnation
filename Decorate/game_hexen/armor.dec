actor HexenArmor1 : BasicArmorBonus replaces AmuletOfWarding
{
    RenderStyle Add
    Alpha 0.875
    +Floatbob
    Armor.Savepercent 33.335
    Armor.MaxSaveAmount 200
    Armor.Saveamount 25
    Inventory.PickupMessage "$SAMSARA_ARMORHEXEN"
    Inventory.PickupSound "armor1get"
    -ALWAYSPICKUP
    States
    {
      Spawn:
        RMR4 A 0
        RMR4 A 35 bright ACS_NamedExecuteAlways("SamsaraClientDecorate", 0, 4, 100, 90)
        wait
    }
}

actor HexenArmor5 : CustomInventory replaces ArtiBoostArmor
{
    +Floatbob
    Inventory.PickupMessage "$SAMSARA_POWERUP_ARMOR"
    Inventory.PickupSound "nosoundforyoubitch"
    States
    {
      Spawn:
        BRAC ABCDEFGH 4 Bright
        Loop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("MarathonClass",1,"PickupMarathon")
        TNT1 A 1 A_JumpIfInventory("CorvusClass",1,"PickupHeretic")
		TNT1 A 0 A_JumpIfInventory("HexenClass",1,"PickupHexen")
		TNT1 A 0 A_JumpIfInventory("StrifeClass",1,"PickupStrife")
		TNT1 A 0 A_JumpIfInventory("DukeClass",1,"PickupDuke")
        TNT1 A 0 A_PlaySound("FighterHammerHitWall")
        TNT1 A 1 A_GiveInventory("HexenArmor5B")
        stop

      PickupMarathon:
        TNT1 A 0 A_SetBlend("White",0.5,5)
        TNT1 A 0 A_PlaySound("FighterHammerHitWall")
        TNT1 A 1 A_GiveInventory("HexenArmor5B")
        stop

      PickupHeretic:
        TNT1 A 0 A_JumpIfInventory("HexenArmor5A",0,"PickupInvFail")
        TNT1 A 0 A_PlaySound("heretic/itemget")
        TNT1 A 1 A_GiveInventory("HexenArmor5A")
        stop
		
	  PickupStrife:
		TNT1 A 0 A_JumpIfInventory("HexenArmor5A",0,"PickupInvFail")
		TNT1 A 0 A_PlaySound("strife/itemget")
        TNT1 A 1 A_GiveInventory("HexenArmor5A")
        stop		

	  PickupHexen:
		TNT1 A 0 A_JumpIfInventory("HexenArmor5A",0,"PickupInvFail")
		TNT1 A 0 A_PlaySound("hexen/artiget")
        TNT1 A 1 A_GiveInventory("HexenArmor5A")
        stop		
		
	  PickupDuke:
        TNT1 A 0 A_PlaySound("duke/kickhit")
        TNT1 A 1 A_GiveInventory("HexenArmor5B")
        stop		

      PickupInvFail:
        BRAC A 4 BRIGHT
        fail		
    }
}

actor HexenArmor5A : CustomInventory
{
    tag "Dragonskin Bracers"
    +INVBAR
    Inventory.Icon "ARTIBRAC"
    Inventory.DefMaxAmount
    States
    {
      Use:
        TNT1 A 0 A_GiveInventory("HexenArmor5B",50)
        stop
    }
}

actor HexenArmor5B : BasicArmorBonus
{
    Armor.Savepercent 33.335
    Armor.MaxSaveAmount 300
    Armor.Saveamount 50
}

actor HexenArmor2 : HexenArmor1 replaces FalconShield { }
actor HexenArmor3 : HexenArmor1 replaces MeshArmor { }
actor HexenArmor4 : HexenArmor1 replaces PlatinumHelm { }