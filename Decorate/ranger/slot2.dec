actor "Double Shotgun": InstantSwitchWeapon
{
	Weapon.BobRangeX 0
	Weapon.BobRangeY 0.75
	Weapon.BobSpeed 1.4
	Weapon.BobStyle Alpha   
    Weapon.Kickback 100
    Weapon.SelectionOrder 6
    Weapon.AmmoUse 1
    Weapon.AmmoGive 10
    Weapon.AmmoType "Shell"
	Inventory.RestrictedTo "QuakePlayer"
    +UNDROPPABLE
    +THRUGHOST
    +NOALERT
    
    Weapon.SisterWeapon "Spectral DSG"
    Inventory.Icon "QDBSZ0"
    
    Inventory.PickupSound "quakeweps/weaponpickup"
    Inventory.PickupMessage "You got the double-barreled shotgun"
    Obituary "$SAMSARA_RANGER_OB_SLOT2"
    
    //Decal BulletChip
    Tag "Double Shotgun"
    States
    {
      Select:
		TNT1 A 0 A_GiveInventory("Ranger_GunSelected", 1)
		TNT1 A 0 A_TakeInventory("Ranger_AxeSelected", 1)
		TNT1 A 0 A_TakeInventory("Ranger_ExplosiveSelected", 1)
		TNT1 A 0 A_TakeInventory("Ranger_NailSelected", 1)
		TNT1 A 0 A_TakeInventory("Ranger_PlasmaSelected", 1)
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
        TNT1 A 1 A_Raise
        loop
	  
	  Deselect:
		TNT1 A 0 A_TakeInventory("Ranger_GunSelected", 1)
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        loop
	  
	  Ready:
        QDBS A 1 A_WeaponReady
        Loop
        
      Fire:
        QDBS A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 4, 3, 17)  
        QDBS A 0 A_PlaySound("quakeweps/ssgfire", CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("QuakeInvisTimer", 1, 2)
        TNT1 A 0 A_AlertMonsters
//		QDBS A 0 A_JumpIfInventory("QuadDamagePower", 1, "QuadFire")
        QDBS A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        QDBS A 0 A_FireBullets(7, 5, 14, 5, "QuakeBulletPuff", FBF_USEAMMO | FBF_NORANDOM)
        goto Fire2
        
      CoopFire:
        QDBS A 0 A_FireBullets(7, 5, 14, 5, "QuakeBulletPuffCoop", FBF_USEAMMO | FBF_NORANDOM)
        goto Fire2
		
/*	  QuadFire:
        QDBS A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopQuadFire")
        QDBS A 0 A_FireBullets(7, 5, 14, 4, "QuakeQuadPuff", FBF_USEAMMO | FBF_NORANDOM)
        goto Fire2	 */ 
        
        // 25 tics
      Fire2:
        QDBS B 1 bright
        QDBS C 1 bright
        QDBS D 2 bright
        QDBS E 2
        QDBS E 5
        QDBS F 4
        QDBS F 3
        QDBS G 3
        QDBS G 2
        QDBS A 2
        QDBS A 0 A_ReFire
        Goto Ready
        
      Spawn:
        QDBS Z -1
        Stop
    }
}

actor "Spectral DSG": "Double Shotgun"
{
    Weapon.AmmoType ""
    Weapon.AmmoUse  0
    Weapon.AmmoGive 0

    Weapon.SisterWeapon "Double Shotgun"
    +POWERED_UP
    
    States
    {
      Fire:
        TNT1 A 0 A_JumpIfInventory("QuakeInvisTimer", 1, 2)
        TNT1 A 0 A_AlertMonsters

        QDBS A 0 ACS_NamedExecuteAlways("SamsaraRecoil", 0, 4, 3, 17)  
        QDBS A 0 ACS_NamedExecuteAlways("SamsaraSigil", 0, 8)

        QDBS A 0 A_PlaySound("quakeweps/ssgfire", CHAN_WEAPON)
        QDBS A 0 A_PlaySound("weapons/sigil", 6)
        QDBS A 0 A_FireBullets(7, 5, 14, 4, "QuakeSpectralPuff", FBF_NORANDOM)
        goto Fire2
    }
}
