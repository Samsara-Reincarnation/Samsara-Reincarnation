actor "Axe": InstantSwitchWeapon
{
	Weapon.BobRangeX 0
	Weapon.BobRangeY 0.75
	Weapon.BobSpeed 1.4
	Weapon.BobStyle Alpha   
    Weapon.SelectionOrder 8
    Weapon.Kickback 100
	Inventory.RestrictedTo "QuakePlayer"
    +NOALERT
    +WEAPON.MELEEWEAPON
    +UNDROPPABLE

    Inventory.Icon "TNT1A0"
    Obituary "$SAMSARA_RANGER_OB_FIST"
    Tag "Axe"
    States
    {
      Select:
		TNT1 A 0 A_GiveInventory("Ranger_AxeSelected", 1)
		TNT1 A 0 A_TakeInventory("Ranger_GunSelected", 1)
        TNT1 A 0 A_TakeInventory("Ranger_ExplosiveSelected", 1)
		TNT1 A 0 A_TakeInventory("Ranger_NailSelected", 1)
		TNT1 A 0 A_TakeInventory("Ranger_PlasmaSelected", 1)
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
        TNT1 A 1 A_Raise
        loop

      Deselect:
		TNT1 A 0 A_TakeInventory("Ranger_AxeSelected", 1)
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        loop

      Ready:
        QAXE A 1 A_WeaponReady
        Loop

      // 18 tics
      Fire:
        QAXE A 0 A_PlaySound("quakeweps/axe", CHAN_WEAPON)
        QAXE B 4 A_Jump(128, "Axe2")
        QAXE C 2
        QAXE D 0 A_JumpIfInventory("CoopModeOn",1,2)
        QAXE D 2 A_CustomPunch(20, 1, 0, "QuakeAxePuff")
        goto FireEnd
        QAXE D 2 A_CustomPunch(20, 1, 0, "QuakeAxePuffCoop")
        goto FireEnd
      FireEnd:
        QAXE E 4
        goto AxeEnd

      Axe2:
        QAXE F 4
        QAXE G 2
        QAXE H 0 A_JumpIfInventory("CoopModeOn",1,2)
        QAXE H 2 A_CustomPunch(20, 1, 0, "QuakeAxePuff")
        goto Axe2End
        QAXE H 2 A_CustomPunch(20, 1, 0, "QuakeAxePuffCoop")
        goto Axe2End
      Axe2End:
        QAXE I 4
        goto AxeEnd

      AxeEnd:
        QAXE A 6
        goto Ready
    }
}

actor "Spectral Axe": Axe
{
    Weapon.SisterWeapon "Axe"
    +POWERED_UP
    
    States
    {
      Fire:
        QAXE A 0 A_PlaySound("weapons/sigil", 6)
        QAXE A 0 A_PlaySound("quakeweps/axe", CHAN_WEAPON)
        QAXE B 4 A_Jump(128, "Axe2")
        QAXE C 2
        QAXE D 0 A_JumpIfInventory("CoopModeOn",1,2)
        QAXE D 2 A_CustomPunch(20, 1, 0, "SpectralAxePuff")
        goto FireEnd
        QAXE D 2 A_CustomPunch(20, 1, 0, "SpectralAxePuffCoop")
        goto FireEnd

      FireEnd:
        QAXE E 4
        goto AxeEnd

      Axe2:
        QAXE F 4
        QAXE G 2
        QAXE H 0 A_JumpIfInventory("CoopModeOn",1,2)
        QAXE H 2 A_CustomPunch(20, 1, 0, "SpectralAxePuff")
        goto Axe2End
        QAXE H 2 A_CustomPunch(20, 1, 0, "SpectralAxePuffCoop")
        goto Axe2End
      Axe2End:
        QAXE I 4
        goto AxeEnd

      AxeEnd:
        QAXE A 6
        goto Ready
    }
}

actor QuakeAxePuff: QuakeBulletPuff
{
    +PUFFONACTORS
    DamageType "QuakeExplosive"
    AttackSound "quakeweps/axewall"
    SeeSound "quakeweps/axehit"
    MeleeSound "quakeweps/axehit"
    States
    {
      Spawn:
      Crash:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("quakeweps/axewall")
        goto Super::Spawn

      Melee:
        TNT1 A 1 A_PlaySound("quakeweps/axehit")
        stop
    }
}

actor QuakeAxePuffCoop : QuakeAxePuff { Species "Player" +THRUSPECIES +MTHRUSPECIES }

actor SpectralAxePuff: QuakeAxePuff
{
    DamageType "Spectral"
}

actor SpectralAxePuffCoop : SpectralAxePuff { Species "Player" +THRUSPECIES +MTHRUSPECIES }


Actor Ranger_AxeSelected : Inventory
{
	Inventory.MaxAmount 1
	Inventory.InterHubAmount 0
	+INVENTORY.IGNORESKILL
	+INVENTORY.UNDROPPABLE
	-INVENTORY.INVBAR
}
